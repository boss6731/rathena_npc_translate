//============================================================
//= rAthenaCN [RWC2012附魔]漢化腳本 
//============================================================

//===== rAthena Script =======================================
//= RWC 2012 Enchants
//===== By: =========Translation:============================= 

//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Adds slots and enchantments to 2012 RWC Memory accessories.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Euphy]
//============================================================ 

prontera,147,61,3	script	打洞工匠#pron	87,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "您攜帶的物品種類太多, 請稍做處理.";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "您的負重不足, 請稍做處理.";
		close;
	}
	set .@part, EQI_ACC_L;
	if (!getequipisequiped(.@part)) {
		mes "[打洞工匠]";
		mes "我可以在RWC紀念裝飾品上打洞開啟一個卡片插槽.";
		next;
		mes "[打洞工匠]";
		mes "您的右邊裝飾品位置是空的.";
		close;
	}
	mes "[打洞工匠]";
	mes "我可以把RWC紀念裝備的第一個封閉卡槽打洞成可以用的卡槽. 當然, 只能是在 ^ff0000原始裝備^000000 上操作, 也就是說還沒進行過打洞的RWC裝備.";
	next;
	set .@equip_id, getequipid(.@part);
	if (.@equip_id != 2966 && .@equip_id != 2968) {
		mes "[打洞工匠]";
		mes "我發現您的右邊裝飾品不是RWC紀念裝飾品. 恕我無能為力了. ";
		close;
	}
	mes "[打洞工匠]";
	mes "您也應該知道裝備開洞是極其危險的工作. ^ff0000成功的機會約50%.^000000 您還敢不敢繼續?";
	next;
	if(select("- 取消:- 繼續!") == 1) {
		mes "[打洞工匠]";
		mes "那真可惜了.";
		close;
	}
	if (.@equip_id == 2966) {
		set .@slotted, 2967; //RWC_2012_Ring_
		set .@name$,"RWC 2012 Memorial Ring";
		set .@str$,"ring";
	} else if (.@equip_id == 2968) {
		set .@slotted, 2969; //RWC_2012_Pendant_
		set .@name$,"RWC 2012 Memorial Pendant";
		set .@str$,"pendant";
	} else {
		mes "[打洞工匠]";
		mes "我識別不出您右邊裝飾品位置上的物品, 總之這個裝飾品不是我能改造的.";
		close;
	}
	if (getequipcardid(.@part,3) > 0) {
		mes "[打洞工匠]";
		mes "這個物品已經開啟一個卡片插槽了. 依據規定我無法再次對它進行改造了.";
		close;
	}
	delequip .@part;
	if (rand(1,10) > 5) {
		getitem .@slotted,1;
		specialeffect2 EF_REPAIRWEAPON;
		mes "[打洞工匠]";
		mes "耶! 搞定! 您的 "+.@name$+" 已經成功開啟一個卡片插槽了. 快試試看吧!";
		close;
	} else {
		specialeffect2 EF_LORD;
		mes "[打洞工匠]";
		mes "呃啊... 真倒黴 "+.@str$+"... 承受不了損壞了. 我很抱歉.";
		close;
	}
}

prontera,147,59,3	script	戈爾登伯格#pron	878,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "您攜帶的物品種類太多, 請稍做處理.";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "您的負重不足, 請稍做處理.";
		close;
	}
	mes "[戈爾登伯格]";
	mes "您好! 我有方法開啟RWC紀念裝備的隱藏力量.";
	next;
	set .@part, EQI_ACC_L;
	if (!getequipisequiped(.@part)) {
		mes "[戈爾登伯格]";
		mes "您沒有裝備任何裝飾品在您的右邊飾品位置.";
		close;
	}
	set .@equip_id, getequipid(.@part);
	if (.@equip_id < 2966 || .@equip_id > 2969) {
		mes "[戈爾登伯格]";
		mes "我發現您的右邊裝飾品不是RWC紀念裝飾品. 恕我無能為力了. ";
		close;
	}
	set .@select, select("- 不感興趣.:- 請激活我的RWC潛能.:- 重置附魔.")-1;
	if (.@select == 0) {
		mes "[戈爾登伯格]";
		mes "好吧, 再見...";
		close;
	}
	set .@equip_refine, getequiprefinerycnt(.@part);
	setarray .@equip_card[0], getequipcardid(.@part,0),getequipcardid(.@part,1),getequipcardid(.@part,2),getequipcardid(.@part,3);
	if (.@select == 1) {
		switch(getequipid(.@part)) {
		case 2966:
			setarray .@option[0],2,2,4,4;
			break;
		case 2967:
			setarray .@option[0],0,2,4,4;
			break;
		case 2968:
			setarray .@option[0],1,1,3,3;
			break;
		case 2969:
			setarray .@option[0],0,1,3,3;
			break;
		default:
			mes "[戈爾登伯格]";
			mes "對不起, 我無法對您正裝備的裝飾品進行附魔.";
			mes "如果您把RWC飾品在了左邊的位置的話, 請將它們佩戴到右邊裝飾品欄.";
			close;
		}
		for(set .@i,3; .@i>=0; set .@i,.@i-1) {
			if (.@equip_card[.@i] == 0) {
				set .@slot, .@i;
				set .@op_type, .@option[.@i];
				break;
			}
		}
		switch(.@op_type) {
		case 4:
			mes "[戈爾登伯格]";
			mes "您想偏重於哪方面的附魔效果?";
			next;
			setarray .@enchant_select[0],1,2,3,4;
			set .@i, select("- 取消:- 近戰:- ATK (%):- Max HP:- HP")-2;
			break;
		case 3:
			setarray .@enchant_select[0],5,6,7;
			set .@i, select("- 取消:- MATK (%):- 施法:- SP")-2;
			break;
		case 2:
			mes "[戈爾登伯格]";
			mes "^ff0000要事先申明下! 附魔有 75% 的成功率. 如果失敗的話, 這件裝飾品就會被損毀.^000000 您想偏重於哪方面的附魔效果?";
			next;
			setarray .@enchant_select[0],8,9,10,11,12,13,14;
			set .@i, select("- 取消:- STR:- AGI:- VIT:- INT:- DEX:- LUK:- SP")-2;
			break;
		case 1:
			mes "[戈爾登伯格]";
			mes "^ff0000要事先申明下! 附魔有 75% 的成功率. 如果失敗的話, 這件裝飾品就會被損毀.^000000 您想偏重於哪方面的附魔效果?";
			next;
			setarray .@enchant_select[0],8,9,10,11,12,13,15,16;
			set .@i, select("- 取消:- STR:- AGI:- VIT:- INT:- DEX:- LUK:- MHP:- HP")-2;
			break;
		case 0:
			mes "[戈爾登伯格]";
			mes "您的裝飾品已經附魔了太多次數了, 我無法繼續對它進行附魔.";
			close;
		}
		if (.@i == -1) {
			mes "[戈爾登伯格]";
			mes "好吧, 再見.";
			close;
		}
		mes "[戈爾登伯格]";
		mes "附魔最終結果具備隨機性. ^ff0000一旦附魔完成, 附魔結界將不能被刪除.^000000 是否繼續?";
		next;
		if(select("- 不要了.:- 請繼續.") == 1) {
			mes "[戈爾登伯格]";
			mes "好吧, 再見...";
			close;
		}
		set .@enchant_type, .@enchant_select[.@i];
		if (!getequipisequiped(.@part)) {
			mes "[戈爾登伯格]";
			mes "在我附魔的時候, 請不要解除這件裝備, 行不行?";
			close;
		}
		switch(.@enchant_type) {
		case 1:
			setarray .@enc[0],4811,4810,4809; //Fighting_Spirit1,Fighting_Spirit2,Fighting_Spirit3
			break;
		case 2:
			setarray .@enc[0],4819,4766,4767; //Atk1,Atk2,Atk3
			break;
		case 3:
			setarray .@enc[0],4861,4862,4867; //MHP1,MHP2,MHP3
			break;
		case 4:
			setarray .@enc[0],4795,4796,4797; //HP100,HP200,HP300
			break;
		case 5:
			setarray .@enc[0],4760,4761,4806; //Matk1,Matk2,Matk3
			break;
		case 6:
			setarray .@enc[0],4815,4814,4813; //Spell1,Spell2,Spell3
			break;
		case 7:
			setarray .@enc[0],4870,4800,4871; //SP25,SP50,SP75
			break;
		case 8:
			setarray .@enc[0],4700,4701,4702; //Strength1,Strength2,Strength3
			break;
		case 9:
			setarray .@enc[0],4730,4731,4732; //Agility1,Agility2,Agility3
			break;
		case 10:
			setarray .@enc[0],4740,4741,4742; //Vitality1,Vitality2,Vitality3
			break;
		case 11:
			setarray .@enc[0],4710,4711,4712; //Inteligence1,Inteligence2,Inteligence3
			break;
		case 12:
			setarray .@enc[0],4720,4721,4722; //Dexterity1,Dexterity2,Dexterity3
			break;
		case 13:
			setarray .@enc[0],4750,4751,4752; //Luck1,Luck2,Luck3
			break;
		case 14:
			setarray .@enc[0],4870,4800,4871; //SP25,SP50,SP75
			break;
		case 15:
			setarray .@enc[0],4861,4862,4867; //MHP1,MHP2,MHP3
			break;
		case 16:
			setarray .@enc[0],4795,4796,4797; //HP100,HP200,HP300
			break;
		default:
			mes "[戈爾登伯格]";
			mes "We have got a problem, let me check it up.";
			close;
		}

		if (.@enchant_type < 8)
			set .@i, rand(1,300); // 0% break chance.
		else
			set .@i, rand(1,400); // 25% break chance.

		     if (.@i < 151) set .@enchant, .@enc[0];
		else if (.@i < 251) set .@enchant, .@enc[1];
		else if (.@i < 301) set .@enchant, .@enc[2];
		else set .@enchant,9;

		set .@equip_card[.@slot], .@enchant;
		if (.@slot == 2 && .@enchant == 0) {
			set .@equip_card[3],0;
		} else if (.@slot == 1 && .@enchant == 0) {
			set .@equip_card[2],0;
			set .@equip_card[3],0;
		} else if (.@slot == 0 && .@enchant == 0) {
			set .@equip_card[1],0;
			set .@equip_card[2],0;
			set .@equip_card[3],0;
		}

		delequip .@part;
		if (.@enchant == 9) {
			mes "[戈爾登伯格]";
			mes "該死!";
 			mes "這個飾品還是太脆了, 經受不了這麼折騰. 太抱歉了.";
			specialeffect2 EF_LORD;
			close;
		}
		if (.@enchant == 0) { // Should never happen.
			mes "[戈爾登伯格]";
			mes "噢... 看上去附魔狀態非常不穩定. 這引發了所有附魔消失. 很遺憾, 請再嘗試一下!";
		} else {
			mes "[戈爾登伯格]";
			mes "太棒了!";
			mes "附魔完成! 附魔被鑲嵌在第.^990000"+(.@slot+1)+"^000000洞上.";
			specialeffect2 EF_REPAIRWEAPON;
		}

//		GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] .@equip_card[3]
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];

		close;
	} else if (.@select == 2) {
		mes "[戈爾登伯格]";
		mes "我僅會重置附魔效果, 不會影響卡片. 是否繼續?";
		next;
		if(select("- 取消.:- 是的, 繼續.") == 1) {
			mes "[戈爾登伯格]";
			mes "需要時再來找我吧.";
			close;
		}
		if (countitem(6665) == 0) {
			mes "[戈爾登伯格]";
			mes "抱歉. 但你沒有的RWC初始化優惠券. 你能不能查看下背包?";
			close;
		}
		if (.@equip_card[3] == 0) {
			mes "[戈爾登伯格]";
			mes "嗯... 這個飾品沒有附魔. 不需要我重置! 請確認下.";
			close;
		}
		specialeffect2 EF_REPAIRWEAPON;
		mes "[戈爾登伯格]";
		mes "您飾品上的附魔效果將被重置.";
		delitem 6665,1; //RWC_Inicializer
		delequip .@part;
	
//		GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] .@equip_card[3]
		for(set .@i,0; .@i<4; set .@i,.@i+1) {
			if (.@equip_card[.@i] >= 4700) // Armor Enchant System
				set .@equip_card[.@i],0;
		}
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];
		
		close;
	}
}
