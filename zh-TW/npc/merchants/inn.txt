//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================


//===== rAthena Script =======================================
//= Inn Npcs
//===== By: ==================================================
//= Darkchild (1.1)
//= Playtester (1.2)
//===== Current Version: =====================================
//= 2.9a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Inn Npcs, Save and Heal
//============================================================
//= variables:
//= arg0 - name of npc
//= arg1 - name of the inn
//= arg2 - map to save at
//= arg3, arg4 - x and y cord. to save at
//= @cost - cost of renting a room
//===== Additional Comments: =================================
//= 1.1 Blind Effect By Kobra_k88 (Taken from his old script)
//= I added it to all of them though [Darkchild]
//= 1.1a Minor bug fixes and optimizations. Switched from @variables 
//= to arguments.[kobra_k88]
//= 1.2 Rewrote inn script [Playtester]
//= 1.3 Added (finally) Rachel Inn Maid. Official warp
//=     and save points[erKURITA]
//= 1.4 Added Hugel inn [erKurita]
//= 1.5 Added "end;" after warping the player. [L0ne_W0lf]
//=	- Small corrections to the prontera Inns.
//=	- Removed the Cancel dialog.
//= 1.6 More fixes, changed progression of "Rest"ing.
//=	- Corrected Morroc NPCs. [L0ne_W0lf]
//= 1.7 Corrected Payon NPC. [L0ne_W0lf] 
//= 1.8 Corrected Geffen NPC. [L0ne_W0lf] 
//= 1.9 Added the Inn for Al De baran. [L0ne_W0lf]
//= 2.0 Added Inn for the Town of Veins. [L0ne_W0lf]
//= 2.1 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.1a Corrected a little Typo error. (bugreport:536 & 537) [Samuray22]
//= 2.2 Small update to Vein's Inn Master. [L0ne_W0lf]
//= 2.3 Added Lighthalzen Inn from Lighthalzen town file. [L0ne_W0lf]
//= 2.4 Fixed Inn Employee#Ahlma warping to same destination as save point, bugreport: 191.
//= 2.5 Added Moskovia Gostinitsa (Inn) from jA
//= 2.6 Updated Alberta inn to official. [L0ne_W0lf]
//= 2.7 Removed Morroc Inns as per episode 12.1. [L0ne_W0lf]
//= 2.8 Removed Moscovia Inn; Its in quests/quest_moscovia.txt [Kisuka]
//= 2.9 Added switch menus, updated to match AEGIS scripts. [Kisuka]
//= 2.9a Added 'npcskill' command. [Euphy]
//============================================================


//======================== Prontera ===================================
// West Side Inn -------------------
prt_in,244,135,2	script	旅館 服務生#Sammy	53,{

	callfunc "F_InnMaid","[員工薩米]","普隆德拉","prt_in",238,130;
	warp "prt_in",247,104; end;
}
// East Side Inn ------------------
prt_in,61,141,2	script	旅館 服務生#Ahlma	53,{

	callfunc "F_InnMaid","[員工艾瑪]","普隆德拉","prt_in",64,136;
	warp "prt_in",60,166; end;
}

//======================== Alberta ====================================
alberta_in,32,142,3	script	旅館 服務生#Jennie	53,{

	callfunc "F_InnMaid","[員工珍妮]","'漁人旅館'!","alberta_in",26,142;
	warp "alberta_in",18,188; end;
}

//====================== Geffen ======================================
geffen_in,70,64,5	script	旅館 服務生#Cena	53,{

	callfunc "F_InnMaid","[員工塞納]","吉芬唯一的酒館.","geffen_in",70,59;
	warp "geffen_in",31,31; end;
}

//======================= Payon ======================================
payon_in01,132,62,5	script	旅館 服務生#Ahee	53,{

	callfunc "F_InnMaid","[員工阿喜]","斐揚旅館","payon_in01",136,61;
	warp "payon_in01",132,11; end;
}

//======================== Morocc ====================================
// North East --------------------------
/*
morocc_in,147,141,3	script	旅館 服務生#Hasna	53,{

	callfunc "F_InnMaid","[員工漢斯娜]","夢羅克旅館","morocc_in",142,140;
	warp "morocc_in",173,135; end;
}
// South --------------------------------
morocc_in,80,100,5	script	旅館 服務生#Manar	53,{

	callfunc "F_InnMaid","[員工馬納爾]","夢羅克旅館","morocc_in",78,95;
	warp "morocc_in",79,123; end;
}
*/

//====================== Aldebaran ===================================
aldeba_in,92,58,5	script	旅館服務生#Rilim	53,{

	callfunc "F_InnMaid","[Rilim]","艾爾帕蘭旅館","aldeba_in",92,50;
	warp "aldeba_in",92,112; end;
}

//======================== Rachel ====================================
ra_in01,376,69,4	script	旅館主人#Annie	931,{

	callfunc "F_InnMaid","[安妮]","拉赫旅館","ra_in01",375,58;
	warp "ra_in01",384,128; end;
}

//====================== Lighthalzen =================================
lhz_in02,230,284,4	script	飯店 職員#01	86,{
	mes "[飯店 職員]";
	mes "歡迎光臨";
	mes "裡希塔樂鎮,";
	mes "這裡可以找到";
	mes "最好的住宿";
	mes "與最好的服務.";
	next;
	switch(select("儲存重生點:休息 - 5,000 zeny:取消")) {
	case 1:
		savepoint "lhz_in02",209,275;
		mes "[飯店 職員]";
		mes "感謝你,";
		mes "你的重生點";
		mes "已儲存在";
		mes "這裡.";
		close;
	case 2:
		if(Zeny < 5000) {
			mes "[飯店 職員]";
			mes "很抱歉,";
			mes "你似乎不足 5,000 zeny";
			mes "請在確認一下是否有足夠的金錢.";
			close;
		}
		set Zeny,Zeny - 5000;
		mes "[飯店 職員]";
		mes "謝謝您~";
		mes "希望您喜歡這裡";
		mes "能在裡希塔樂鎮";
		mes "多停留一會.";
		close2;
		warp "lhz_in02",219,150;
		percentheal 100,100;
		npcskill "AL_BLESSING",10,99,99;
		end;
	case 3:
		mes "[飯店 職員]";
		mes "謝謝你並祝您";
		mes "有個愉快的一天.";
		close;
	}
}

//======================== Hugel ====================================
hu_in01,246,107,3	script	旅館服務生#Receptionist	53,{

	callfunc "F_InnMaid","[接待員]","毀葛旅館","hu_in01",263,95;
	warp "hu_in01",267,5; end;
}

//======================== Veins ====================================
ve_in,157,219,5	script	旅館主人#Receptionist	709,{
	mes "[[旅館主人]]";
	mes "你好~";
	mes "歡迎光臨";
	mes "菲音斯";
	mes "最舒適的旅館~";
	next;
	switch(select("儲存重生點:休息一會 -> 5000 zeny:離開")) {
	case 1:
		mes "[[旅館主人]]";
		mes "你的重生點";
		mes "已儲存在這裡.";
		mes "希望你能好好享受在城裡多停留一會.";
		savepoint "ve_in",157,209;
		close;
	case 2:
		mes "[[旅館主人]]";
		if(Zeny < 5000){
			mes "很抱歉,";
			mes "你似乎沒有足夠的,";
			mes "金錢辦理住宿.";
			mes "服務費用是 5,000 zeny 喔.";
			close;
		}
		mes "歡迎住宿~";
		close2;
		set Zeny,Zeny - 5000;
		percentheal 100,100;
		warp "ve_in",184,228;
		end;
	case 3:
		mes "[[旅館主人]]";
		mes "期待您再次光臨.";
		close;
	}
}

//======================= Inn Function ==============================
function	script	F_InnMaid	{
	.@npc_name$ = getarg(0);

	mes .@npc_name$;
	mes "歡迎光臨";
	mes getarg(1) + ".";
	mes "有什麼需要我幫忙的呢?";
	next;
	switch(select("儲存點:休息一會 -> 5000 zeny:取消")) {
	case 1:
		mes .@npc_name$;
		mes "你的重生點";
		mes "已儲存.";
		mes "感謝您,";
		mes "歡迎再次光臨.";
		savepoint getarg(2),getarg(3),getarg(4);
		close;
	case 2:
		mes .@npc_name$;
		if(Zeny < 5000){
			mes "很抱歉, 服務費用是 5,000 zeny. 請確認你有足夠的金錢在過來, 好嗎?";
			close;
		}
		mes "感謝您.";
		mes "希望您能";
		mes "好好休息~";
		close2;
		set Zeny,Zeny - 5000;
		percentheal 100,100;
		return;
	case 3:
		close;
	}
}
