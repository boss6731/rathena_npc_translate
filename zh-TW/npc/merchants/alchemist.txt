//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================

//===== rAthena Script =======================================
//= Alchemist Shop
//===== By: ================================================== 
//= rAthena Dev Team
//===== Current Version: ===================================== 
//= 1.8
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Merchants for Alchemist Materials and Manuals.
//===== Additional Comments: ================================= 
//= 1.1 fixed Medicine Bowl issue, thanx 2 MasterOfMuppets
//= 1.2 Reddozen's fixes of typos. added optional Elemental
//=     Potion Guide. [Lupus]
//= 1.3 Deleted Elemental Potions Guide due to original quest [Lupus]
//= 1.4 Alchemists can now purchase 2000 Medicine Bowls at once. [SinSloth]
//= 1.5 Updated to Aegis 10.3 Standards. [L0ne_W0lf]
//= 1.6 Corrected canceling purchase. [L0ne_W0lf]
//= 1.7 Updated to match AEGIS script. [Masao]
//= 1.8 Moved Craft Book Merchant to Renewal path. [Euphy]
//============================================================ 

alde_alche,24,188,3	script	行會 販賣員	740,{
	if (checkweight(1201,1) == 0) {
		mes "- 等一下! -";
		mes "- 現在你身上帶著 -";
		mes "- 太多東西了! -";
		mes "- 你先去卡普拉倉庫 -";
		mes "- 寄存一些東西再來吧! -";
		close;
	}
	mes "[吉佛 阿爾 夏普]";
	mes "歡迎來到";
	mes "鍊金術師公會. ";
	mes "有什麼我能幫助您的嗎? ";
	next;
	switch(select("- 購買材料:- 購買說明書:- 退出")) {
	case 1:
		mes "[吉佛 阿爾 夏普]";
		mes "您想要點什麼? ";
		next;
		if (select("藥缽 - 8 Zeny:取消. ") == 1) {
			mes "[吉佛 阿爾 夏普]";
			mes "您想要買多少個? ";
			mes "如果不想買就請輸入 '0'";
			next;
			while(1) {
				input .@input,0,2001;
				if (.@input == 0) {
					mes "[吉佛 阿爾 夏普]";
					mes "交易取消. ";
					mes "歡迎下次再來. ";
					close;
				}
				else if (.@input < 1 || .@input > 2000) {
					mes "[吉佛 阿爾 夏普]";
					mes "您輸入的數量";
					mes "必須小於2000. ";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 8;
			set .@item_weight,.@input * 10;
			if (Zeny < .@sell) {
				mes "[吉佛 阿爾 夏普]";
				mes "您似乎";
				mes "沒有足夠的zeny. ";
				mes "請先確認";
				mes "您所帶的zeny數量. ";
				close;
			}
			if ((MaxWeight - Weight) < .@item_weight) {
				mes "[吉佛 阿爾 夏普]";
				mes "看來您似乎";
				mes "帶不下這些東西. ";
				mes "請檢查您的";
				mes "隨身物品數量. ";
				close;
			}
			set Zeny, Zeny-.@sell;
			getitem 7134,.@input; //Medicine_Bowl
			mes "[吉佛 阿爾 夏普]";
			mes "謝謝. ";
			mes "歡迎再來. ";
			close;
		}
		mes "[吉佛 阿爾 夏普]";
		mes "好吧, ";
		mes "如果您想";
		mes "購買任何和鍊金術師";
		mes "有關的物品";
		mes "記得來找我, 好嗎? ";
		close;
	case 2:
		mes "[吉佛 阿爾 夏普]";
		mes "您想要點什麼? ";
		mes "說明書價格一般為 100,000 zeny.  不過有部分特殊的說明書更貴一些. ";
		next;
		switch(select("- 藥水製作說明書:- 酒精製作說明書:- 火煙瓶製作說明書:- 鹽酸瓶製作說明書:- 植物瓶製作說明書:- 刺錘瓶製作說明書:- 護貝藥製作說明書:- 纖細藥水製作說明書:- 取消")) {
		case 1:
			callsub S_SellManual,7144,100000; //Normal_Potion_Book
			break;
		case 2:
			callsub S_SellManual,7127,100000; //Alcol_Create_Book
			break;
		case 3:
			callsub S_SellManual,7128,100000; //FireBottle_Create_Book
			break;
		case 4:
			callsub S_SellManual,7129,100000; //Acid_Create_Book
			break;
		case 5:
			callsub S_SellManual,7130,100000; //Plant_Create_Book
			break;
		case 6:
			callsub S_SellManual,7131,100000; //Mine_Create_Book
			break;
		case 7:
			callsub S_SellManual,7132,100000; //Coating_Create_Book
			break;
		case 8:
			callsub S_SellManual,7133,240000; //Slim_Potion_Create_Book
			break;
		case 9:
			mes "[吉佛 阿爾 夏普]";
			mes "好吧……";
			mes "如果您想要買";
			mes "任何的製作說明書";
			mes "請再來. ";
			close;
		}
	case 3:
		mes "[吉佛 阿爾 夏普]";
		mes "好吧, ";
		mes "祝您愉快. ";
		close;
	}

S_SellManual:
	.@item_id = getarg(0);
	.@zeny_req = getarg(1);

	if (Zeny < .@zeny_req) {
		mes "[吉佛 阿爾 夏普]";
		mes "您似乎沒有";
		mes "足夠的 Zeny. ";
		mes "請檢查您是否有";
		mes "足夠的 Zeny. ";
		close;
	}
	mes "[吉佛 阿爾 夏普]";
	mes getitemname(.@item_id) +"?";
	mes "總共是 "+ .@zeny_req +" zeny.";
	next;
	if (select("購買:退出") == 1) {
		Zeny = Zeny - .@zeny_req;
		getitem .@item_id,1;
		mes "[吉佛 阿爾 夏普]";
		mes "謝謝您的";
		mes "惠顧. ";
		close;
	}
	mes "[吉佛 阿爾 夏普]";
	mes "歡迎";
	mes "下次再來. ";
	close;
}
