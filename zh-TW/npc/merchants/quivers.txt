//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================


//===== rAthena Script =======================================
//= Arrow Quiver Event
//===== By: ==================================================
//= Muad_Dib (Prometheus Project); L0ne_W0lf
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Turns arrows into Arrow Quivers.
//= Breakdown of Subroutine "S_BuyQuiver"
//= arg(0): Type of Arrow to be packaged (item ID).
//= arg(1): How many of each 'getarg(0)' arrow per quiver.
//= arg(2): The cost of making a 'getarg(0)' quiver.
//= arg(3): The quiver given by the NPC (item ID).
//===== Additional Comments: =================================
//= 07/06/05 : Added 1st Version. [Muad_Dib]
//= Converted to rAthena format by Dr.Evil
//= added prize to quest - 500 Zeny
//= Fixed a few spelling errors. [Nexon]
//= 1.1 Added Holy Arrow Quivers [Playtester]
//= 1.2 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//=     Removed "Holy Arrows" from the list.
//============================================================

payon_in01,5,134,5	script	發明家 欽斯	89,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "[發明家 欽斯]";
		mes "嘿, 你現在的負重太重了";
		mes "你為什麼不儲存些到卡普拉那? 你把東西去整理一下再來,我們可以談一談,好嗎?";
		close;
	}
	mes "[發明家 欽斯]";
	mes "我是天才發明家欽斯";
	mes "毫不誇張的說,我絕對是";
	mes "^663300本世紀最偉大的發明家^000000.";
	mes "別用這麼懷疑的眼神看著我";
	next;
	mes "[發明家 欽斯]";
	mes "我這一次發明了一個";
	mes "必將載入史冊的物品!";
	mes "那就是... ^663300魔法箭袋^000000 !! ";
	mes "弓箭的革命!";
	emotion ET_BEST;
	next;
	mes "[發明家 欽斯]";
	mes "我已經對這個神奇的魔法箭袋進行了多年的研究,日夜工作,現在我終於知道了如何濃縮魔法箭袋的秘訣,神奇的魔法箭袋能讓你減輕身上的負重,可以攜帶更多的箭矢!";
	next;
	mes "[發明家 欽斯]";
	mes "你想要嘗試一下我的魔法箭袋嗎?我毫不懷疑,像你這樣的人能欣賞我的天才!";
	next;
	switch(select("箭袋:鐵箭袋:鋼鐵箭袋:神之金屬箭袋:火箭袋:銀箭袋:風靈箭袋:地靈箭袋:水靈箭袋:影子箭袋:無形箭袋:鐵鏽箭袋")) {
	case 1: callsub S_BuyQuiver,1750,500,500,12004;
	case 2: callsub S_BuyQuiver,1770,500,500,12005;
	case 3: callsub S_BuyQuiver,1753,500,500,12006;
	case 4: callsub S_BuyQuiver,1765,500,500,12007;
	case 5: callsub S_BuyQuiver,1752,500,500,12008;
	case 6: callsub S_BuyQuiver,1751,500,500,12009;
	case 7: callsub S_BuyQuiver,1755,500,500,12010;
	case 8: callsub S_BuyQuiver,1756,500,500,12011;
	case 9: callsub S_BuyQuiver,1754,500,500,12012;
	case 10: callsub S_BuyQuiver,1767,500,500,12013;
	case 11: callsub S_BuyQuiver,1757,500,500,12014;
	case 12: callsub S_BuyQuiver,1762,500,500,12015;
	}
	//This doesn't seem to be accessable...
	//mes "[發明家 欽斯]";
	//mes "Is there";
	//mes "anything you want?";
	//close;

S_BuyQuiver:
	.@item_id = getarg(0);
	.@amount = getarg(1);
	.@zeny_req = getarg(2);

	if (countitem(.@item_id) >= .@amount) {
		mes "[發明家 欽斯]";
		mes "好極了!";
		mes "你攜帶了什麼箭矢? 我會給你一個箭袋,可以攜帶 "+ .@amount +" of your "+ getitemname(.@item_id) +"!不過需要手續費 ^FF3131"+ .@zeny_req +" Zeny^000000.";
		next;
		switch(select("我想儘可能多的換成箭袋:只換一個箭袋:取消")) {
		case 1:
			.@arrows = countitem(.@item_id);
			set .@quiver,.@arrows / .@amount;
			set .@arrows_used,.@quiver * .@amount;
			.@arrow_zeny01 = .@quiver * .@zeny_req;
			mes "你一共有";
			mes "箭矢數量: ^3131FF"+.@arrows+"^000000 支";
			mes "如果全部兌換的話,";
			mes "最多可以換箭袋";
			mes "箭袋數量: ^3131FF"+.@quiver+"^000000 個";
			mes "手續費: ^3131FF"+.@arrow_zeny01+" Zeny^000000";
			next;
			mes "[發明家 欽斯]";
			mes "你確定";
			mes "要將身上的所有";
			mes "箭矢換成";
			mes "箭袋嗎?";
			next;
			if (select("是的:取消") == 1) {
				if (.@arrow_zeny01 < Zeny) {
					mes "[發明家 欽斯]";
					mes "這是你的箭袋,請收好!";
					mes "另外請記住 ^FF0000你身上的負重量超過最大負重限額達到90%的話,就不能使用箭袋^000000.";
					set Zeny, Zeny-.@arrow_zeny01;
					delitem .@item_id,.@arrows_used;// Arrow
					getitem getarg(3),.@quiver; //Quiver
					next;
					mes "[發明家 欽斯]";
					mes "所以...";
					mes "只要保持";
					mes "自己的背包";
					mes "不要太滿,";
					mes "就應該不會有問題!";
					mes "放心吧.";
					close;
				}
				else {
					mes "[發明家 欽斯]";
					mes "很抱歉,你沒有足夠的金錢來完成交易!";
					mes "為了發明箭袋,我傾盡了所有......";
					close;
				}
			}
			mes "[發明家 欽斯]";
			mes "你居然改變注意了!";
			mes "就差一步";
			mes "你就能享受這世上最犀利的發明給你帶來的便利...";
			close;
		case 2:
			if (Zeny > .@zeny_req) {
				mes "[發明家 欽斯]";
				mes "這是你的箭袋,請收好!";
				mes "另外請記住 ^FF0000你身上的負重量超過最大負重限額達到90%的話,就不能使用箭袋^000000.";
				Zeny = Zeny -.@zeny_req;
				delitem .@item_id,.@amount;// Arrow
				getitem getarg(3),1; //Quiver
				next;
				mes "[發明家 欽斯]";
				mes "所以...";
				mes "只要保持";
				mes "自己的背包";
				mes "不要太滿,";
				mes "就應該不會有問題!";
				mes "放心吧.";
				close;
			}
			else {
				mes "[發明家 欽斯]";
				mes "你甚至沒";
				mes "有 "+ .@zeny_req +" Zeny?";
				mes "很抱歉,我沒有想到你會那麼...";
				mes "^333333貧困^000000.";
				mes "哎...";
				close;
			}
		case 3:
			mes "[發明家 欽斯]";
			mes "什麼...?";
			mes "你看不出來這個發明可以永遠改變箭矢的歷史?! 未來就是現在!";
			close;
		}
	}
	else {
		mes "[發明家 欽斯]";
		mes "你可以可以最多放500支箭在箭筒裡. 這是我用特殊秘方製作的,";
		mes "所以使用箭袋的重量要比直接拿箭矢輕的多.";
		next;
		mes "[發明家 欽斯]";
		mes "這是一個科學的奇蹟! 你可以帶500支箭和500Zeny 來和我交換一個箭袋,來體驗一下吧";
		close;
	}
}
