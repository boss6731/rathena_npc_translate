//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================


//===== rAthena Script =======================================
//= Coin Merchants
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= [Official Conversion]
//= Coin redemption NPCs.
//===== Additional Comments: =================================
//= 1.0 First version 
//= 1.1 Fixed checkweight. [Gepard]
//= 1.2 Optimized and merged into one file. [Euphy]
//============================================================

// Manuk & Splendide
//============================================================
-	script	::merchant_13_2	-1,{
	set .@n$, "["+strnpcinfo(1)+"]";
	mes .@n$;
	if (isequipped(2782) && ep13_2_rhea == 100) {
		if (!checkweight(1201,1)) {
			mes "您攜帶了太多的物品.";
			mes "請稍微處理一下再來?";
			close;
		}
		mes "你好.";
		mes "你需要什麼?";
		next;
		if (strnpcinfo(1) == "馬努克商人") {
			setarray .@items[2],12342,12343,12348;
			set .@Book,11019; //Manuk_Selling_Item
			set .@coin,6080; //Manuk_Coin
			set .@i, select("- 查看物品介紹:- 馬努克的機會:- 馬努克的勇氣:- 馬努克的信心:- 取消");
		}
		else {
			setarray .@items[2],12344,12345,12349;
			set .@Book,11018; //Splendide_Selling_Item
			set .@coin,6081; //Splendide_Coin
			set .@i, select("- 查看物品介紹:- 購買 Pinguicula's 果醬:- 購買 Luciola's 蜂蜜果醬:- 購買山茱萸之淚:- 取消");
		}
		mes .@n$;
		if (.@i == 1) {
			mes "這是物品介紹.";
			close2;
			readbook .@Book,1;
			end;
		}
		else if (.@i < 5) {
			mes "我可以買給你 3 "+getitemname(.@items[.@i])+" 個換 ^3131FF1 硬幣^000000.";
			next;
			switch(select("- 購買:- 取消")) {
			case 1:
				if (countitem(.@coin)) {
					mes .@n$;
					mes "謝謝惠顧.";
					delitem .@coin,1;
					getitem .@items[.@i],3;
					close;
				}
				else {
					mes .@n$;
					mes "你的硬幣不夠.";
					close;
				}
			case 2:
				mes .@n$;
				break;
			}
		}
		mes "如果你改變主意了請來找我.";
		close;
	}
	else {
		if (strnpcinfo(1) == "馬努克商人") {
			mes "Rtt od d";
			mes "Qwo hd is d irr";
		}
		else {
			mes "BurWehAla";
			mes "tasnarAndu Ie Ru";
		}
		close;
	}
}
man_in01,286,16,1	duplicate(merchant_13_2)	馬努克商人	4_MAN_PIOM
spl_in01,110,326,5	duplicate(merchant_13_2)	輝煌領域商人	4_F_FAIRYKID4
