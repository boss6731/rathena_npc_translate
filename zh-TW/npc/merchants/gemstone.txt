//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================

//===== rAthena Script =======================================
//= Gemstone Trader
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Trade various colors of gemstones for other color gemstones.
//===== Additional Comments: ================================= 
//= 1.0 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//=     Any notes pertaining to the prior trader may be found
//=     in the cities/payon.txt
//= 1.1 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 1.2 Updated input with min/max values. [L0ne_W0lf]
//=     Added a checkweight. 
//= 1.3 Fixed checks. [Euphy]
//============================================================ 

payon,173,238,5	script	傑依德#pay	754,{
	if (checkweight(1201,1) == 0) {
		mes "你好像攜帶了非常多的東西. 為何不放一些東西到倉庫後再來找我呢?";
		close;
	}
	mes "[傑依德]";
	mes "您好~";
	mes "如果你拿兩個相同色彩的";
	mes "魔力礦石來給我的話,";
	mes "我會馬上幫你做成不同色彩的";
	mes "魔力礦石喔.";
	next;
	switch(select("藍色魔力礦石換為紅色魔力礦石!:紅色魔力礦石換為黃色魔力礦石!:黃色魔力礦石換為藍色魔力礦石!")) {
	case 1: callsub S_TradeGems,717,716;
	case 2: callsub S_TradeGems,716,715;
	case 3: callsub S_TradeGems,715,717;
	}

S_TradeGems:
	.@item_req = getarg(0);
	.@item_id = getarg(1);

	if (countitem(.@item_req) < 2) {
		mes "[傑依德]";
		mes "啊哈...!";
		mes "你在拿我開玩笑吧?";
		mes "如果沒有2個相同色彩的";
		mes "魔力礦石的話,";
		mes "是無法進行兌換的！";
		mes "2 "+ getitemname(.@item_req) +"s!";
		close;
	}
	else {
		.@gems = countitem(.@item_req) /2;
		mes "[傑依德]";
		mes "我想我能製作";
		mes "總共 "+ .@gems +" "+ getitemname(.@item_id) +"s";
		mes "這些 "+ getitemname(.@item_req) +"材料";
		mes "就是你帶來的.";
		mes "你想要現在就交換嗎?";
		next;
		switch(select("我要把全部礦石的都換掉.:我想兌換一部分就可以了:忽然,不想兌換了.")) {
		case 1:
			delitem .@item_req,.@gems * 2;
			getitem .@item_id,.@gems;
			break;
		case 2:
			mes "[傑依德]";
			mes "你想兌換多少個呢?";
			mes "你只能輸入不超過100的數值哈.";
			mes "如果不想兌換,";
			mes "就輸入0好了.";
			next;
			while(1) {
				input .@input,0,101;
				if (.@input == 0) {
					mes "[傑依德]";
					mes "額?一個都不兌換?";
					mes "我想.";
					mes "你應該是改變主意了...";
					close;
				}
				else if (.@input > 100) {
					mes "[傑依德]";
					mes "呃,這...";
					mes "輸入的數量";
					mes "請不要超過100個.";
					mes "請記住...";
					next;
				}
				else if (.@gems < .@input) {
					// Custom dialogue
					mes "[傑依德]";
					mes "呃...";
					mes "你沒有足夠的礦石.";
					mes "可以進行交易...";
					next;
				}
				else break;
			}
			delitem .@item_req,.@input * 2;
			getitem .@item_id,.@input;
			break;
		case 3:
			mes "[傑依德]";
			mes "好的,沒問題.";
			mes "如果需要的話隨時找我.";
			close;
		}
		mes "[傑依德]";
		mes "輕鬆搞定~";
		mes "歡迎你下次再來.";
		mes "唔,你在看什麼呢?";
		mes "唔!";
		mes "是不是有什麼東西在我臉上?";
		close;
	}
}
