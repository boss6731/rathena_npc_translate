//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================

//===== rAthena Script =======================================
//= Auction system
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.3
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Auction house releated NPCs, and warps
//===== Additional Comments: =================================
//= 1.0 First version, place holders only. [L0ne_W0lf]
//= 1.1 Moved Morroc Auction NPCs to Morroc Ruins. [L0ne_W0lf]
//= 1.2 Warp fixed. Dialog in Ruins/Lighthalzen changed. [Kisuka]
//= 1.3 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//============================================================

// Auction House warpers
//============================================================
moc_ruins,78,173,6	script	拍賣行接待#moc	98,{
	callfunc "F_AuctionWarper",1;
}

prontera,218,120,4	script	拍賣行接待#prt	117,{
	callfunc "F_AuctionWarper",2;
}

yuno,129,116,0	script	拍賣行接待#yuno	90,{
	callfunc "F_AuctionWarper",3;
}

lighthalzen,205,169,6	script	拍賣行接待#lhz	833,{
	callfunc "F_AuctionWarper",4;
}

function	script	F_AuctionWarper	{
	mes "[拍賣行接待]";
	mes "您好!尊敬的冒險家";
	mes "您想進入";
	mes "拍賣大廳嗎?";
	next;
	if (select("想去:不想") == 1) {
		if (getarg(0) == 1 || getarg(0) == 4) {
			mes "[拍賣行接待]";
			mes "太好了.那好.";
			mes "我希望您玩得開心";
			mes "祝您愉快~";
		} else {
			mes "[拍賣行接待]";
			mes "請上車.";
		}
		close2;
		switch(getarg(0)) {
		case 1:
			warp "auction_01",179,53; end;
		case 2:
			warp "auction_01",21,43; end;
		case 3:
			warp "auction_02",151,23; end;
		case 4:
			warp "auction_02",43,24; end;
		}
	}
	mes "[拍賣行接待]";
	mes "那麼好吧,";
	mes "再見.";
	mes "如果您改變了主意";
	mes "請隨時來找我";
	mes "謝謝您的支持~";
	close;
}

// Sign posts
//============================================================
-	script	拍賣行廣告牌#dum::AuctionSign	-1,{
	mes "[拍賣行廣告牌]";
	mes "拍賣物品會讓你的生活改變";
	close;
}

moc_ruins,76,176,6	duplicate(AuctionSign)	拍賣行廣告牌#moc	837
prontera,216,120,4	duplicate(AuctionSign)	拍賣行廣告牌#prt	837
yuno,131,116,0	duplicate(AuctionSign)	拍賣行廣告牌#yuno	837
lighthalzen,207,169,6	duplicate(AuctionSign)	拍賣行廣告牌#lhz	837

// Warps
//============================================================
auction_01,180,49,0	warp	auction_entrance_moc	1,1,moc_ruins,78,171
auction_01,22,37,0	warp	auction_entrance_prt	1,1,prontera,214,120
auction_02,151,17,0	warp	auction_enterance_juno	1,1,yuno,132,119
auction_02,43,17,0	warp	auction_enterance_lhz	1,1,lighthalzen,209,169

// Auction House NPCs
//============================================================
-	script	拍賣行大堂經理#dum::AuctionBroker	-1,{
	mes "[拍賣行大堂經理]";
	mes "歡迎來到拍賣大廳.";
	mes "要查看什麼貨物?";
	next;
	if (select("查看:結束") == 1) {
		mes "[拍賣行大堂經理]";
		mes "尊敬的客戶";
		mes "已經幫您打開拍賣系統";
		mes "請您自行添加添加您要賣的物品";
		mes "也可以查詢到其他客戶售賣的物品 謝謝您的支持~";
		openauction;
		close;
	}
	mes "[拍賣行大堂經理]";
	mes "謝謝您的支持.";
	mes "歡迎您下次光臨";
	mes "如有任何意見和建議";
	mes "請反饋給我們的售後服務部門";
	mes "將會第一時間給您處理您的問題~";
	close;
}

auction_01,182,68,6	duplicate(AuctionBroker)	拍賣行大堂經理#moc1	98
auction_01,182,75,0	duplicate(AuctionBroker)	拍賣行大堂經理#moc2	99
auction_01,177,75,2	duplicate(AuctionBroker)	拍賣行大堂經理#moc3	98
auction_01,177,68,4	duplicate(AuctionBroker)	拍賣行大堂經理#moc4	99
auction_01,21,74,4	duplicate(AuctionBroker)	拍賣行大堂經理#prt1	117
auction_01,27,78,4	duplicate(AuctionBroker)	拍賣行大堂經理#prt2	116
auction_01,16,78,4	duplicate(AuctionBroker)	拍賣行大堂經理#prt3	115
auction_02,158,47,6	duplicate(AuctionBroker)	拍賣行大堂經理#yuno1	90
auction_02,145,47,2	duplicate(AuctionBroker)	拍賣行大堂經理#yuno2	90
auction_02,151,54,0	duplicate(AuctionBroker)	拍賣行大堂經理#yuno3	90
auction_02,152,41,4	duplicate(AuctionBroker)	拍賣行大堂經理#yuno4	90
auction_02,57,46,2	duplicate(AuctionBroker)	拍賣行大堂經理#lhz1	874
auction_02,31,46,6	duplicate(AuctionBroker)	拍賣行大堂經理#lhz2	874
auction_02,43,65,4	duplicate(AuctionBroker)	拍賣行大堂經理#lhz3	833
