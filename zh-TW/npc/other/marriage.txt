//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================

//===== rAthena Script ======================================= 
//= Player Marriage
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.4
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Official Marriage script.
//= Added isloggedin() check, even though Aegis doesn't do it.
//= Currently does not support same-Sex marriages.
//= Old script located in: npc/custom/marriage.txt
//= - Variable in use: wedding_sign (max 1)
//= - Variable in use: $@wedding (max 1) 
//= - Variable in use: $@wed_groom$ $@wed_bride$
//===== Additional Comments: ================================= 
//= 1.0 First version. [L0ne_W0lf]
//= 1.1 Fixed a missing ")" [L0ne_W0lf]
//= 1.2 Corrected duration of Wedding status. [L0ne_W0lf]
//= 1.3 Changed permanent global variables to temporary ones. [Euphy]
//=     Added GM NPCs (commented).
//= 1.4 Added GM management function. [Euphy]
//============================================================

prt_church,97,100,4	script	快樂的瑪麗#w	1_F_LIBRARYGIRL,{
	cutin "wedding_marry01",2;
	if (Upper == 2) {
		mes "[快樂的瑪麗]";
		mes "你好~";
		mes "我叫瑪麗";
		mes "我在此為您解答";
		mes "結婚相關的問題!";
		mes "好了, ";
		mes "您有什麼想知道的麼?";
		next;
		if (select("- 我想結婚.:- 謝謝你的好意!") == 1) {
			mes "[快樂的瑪麗]";
			mes "喔, 我很抱歉, ";
			mes "不過被收養的小孩";
			mes "是無法結婚的";
			mes "現在";
			mes "你為什麼不享受下";
			mes "單純又美麗的幼年時間呢?";
			goto L_End;
		}
		mes "[快樂的瑪麗]";
		mes "喔, 當然你不是";
		mes "一個小盆與～";
		mes "不過你還是不能結婚";
		mes "這裡有太多的法律和倫理";
		mes "問題阻擋著你";
		mes "可愛的小盆與?";
		goto L_End;
	}
	mes "[快樂的瑪麗]";
	mes "結婚是兩個靈魂最";
	mes "美妙的結合";
	mes "選擇在一起享受,";
	mes "美妙的人生.";
	mes "在戀愛中每個人生而平等";
	mes "不過是否有些人在你生命中更平等?";
	next;
	switch(select("- 詢問有關結婚典禮:- 詢問有關程序:- 申請婚禮:- 我們是不可戰勝的光頭一族!")) {
	case 1:
		mes "[快樂的瑪麗]";
		mes "明智且博學的國王";
		mes "曾經組織過結婚儀式";
		mes "不過他再也不這樣做了";
		mes "現在的他正忙於";
		mes "他的國家大事! 並且,";
		mes "他還有一份排滿的日程表.";
		next;
		mes "[快樂的瑪麗]";
		mes "主教沃瑪赤, 是負責愛情的主教";
		mes "他現在是結婚典禮";
		mes "的主要司儀.";
		mes "他是我們聖米德加德王國";
		mes "最重要的寶藏.";
		next;
		mes "[快樂的瑪麗]";
		mes "當你與某人結婚的時候,";
		mes "這是關係到你一輩子的事情, 所以";
		mes "請在籤訂結婚協議時仔細考慮.";
		mes "當然, 你要知道這是個異性戀的國度";
		mes "同性之間的感情是不允許的.";
		next;
		mes "[快樂的瑪麗]";
		mes "如果你真的幸運到";
		mes "找到了一位願意與你相伴餘生";
		mes "的人的話, 那你就要開始求婚了.";
		mes "我希望每個人都能找到";
		mes "他最佳的伴侶~";
		break;

	case 2:
		mes "[快樂的瑪麗]";
		mes "結婚程序的";
		mes "第一步是完善的組織.";
		mes "新郎和新娘要想完成申請";
		mes "他們必須組成一個隊伍.";
		mes "方式方法";
		mes "與平時組隊完全相同.";
		next;
		mes "[快樂的瑪麗]";
		mes "在組隊之後,";
		mes "兩位新人要與";
		mes "主教沃瑪赤對話. 新浪要";
		mes "先與主教對話";
		mes "並且必須準確的說出新娘的名字. 否則, 典禮將會停止.";
		next;
		mes "[快樂的瑪麗]";
		mes "然後, 新娘要與";
		mes "主教對話";
		mes "告訴他新郎的名字.";
		mes "如果他們告訴主教的";
		mes "名字都正確的話, 他們就能";
		mes "開始叫喚戒指了.";
		next;
		mes "[快樂的瑪麗]";
		mes "當結婚戒指交換後";
		mes "雙方就正式成為戀人了. 當然,";
		mes "在此之前, 你還有很多機會去反悔,所以...";
		next;
		mes "[快樂的瑪麗]";
		mes "如果同時有太多";
		mes "的新人想結婚的話";
		mes "請排隊, 一對對的";
		mes "與主教交談";
		mes "沃瑪赤一次只能接待一對新人.";
		next;
		mes "[快樂的瑪麗]";
		mes "最後請確定告訴沃瑪赤";
		mes "你戀人的正式名字";
		mes "請不要浪費太多的名字. 如果你用的時間太久,";
		mes "典禮就會自動停止, 你就要重來一次了.";
		next;
		mes "[快樂的瑪麗]";
		mes "新娘需要記著";
		mes "你只有3分鐘來完成與";
		mes "主教的對話";
		mes "在這之後";
		mes "請新郎再次與主教對話以完成婚禮.";
		next;
		if (select("十分感謝你的幫助!:有什麼簡單的方式來說我新郎的名字麼?") == 1) {
			mes "[快樂的瑪麗]";
			mes "好吧, 我會幫你";
			mes "完成你的婚禮";
			mes "儘可能的順利.";
			mes "如果有什麼東西";
			mes "你還沒理解的話,";
			mes "請再向我詢問.";
			goto L_End;
		}
		mes "[快樂的瑪麗]";
		mes "最簡單的方法是將";
		mes "您愛人的名字發送給主教大人.";
		mes "主教大人會回復一條消息給你.";
		mes "然後左鍵點擊戀人的名字.";
		mes "激活對方的人物信息窗口.";
		next;
		mes "[快樂的瑪麗]";
		mes "然後按'Ctrl'和'C'";
		mes "來複製名字.";
		mes "然後你可以通過按'Insert'與'Shift'";
		mes "來粘貼你戀人的名字";
		mes "清楚了麼?";
		next;
		mes "[快樂的瑪麗]";
		mes "好的, 現在讓我們";
		mes "來試試我給你說的方法吧";
		mes "把你戀人的名字通過";
		mes "我剛才說的方法告訴我吧.";
		next;
		input .@partner$;
		mes "[快樂的瑪麗]";
		mes "好了, 當你決定";
		mes "要結婚了,";
		mes "就來找我, 並提交申請吧";
		mes "希望能再見到你, 冒險者~";
		mes "祝福你~";
		break;

	case 3:
		cutin "wedding_marry02",2;
		if (Sex) {
			mes "[快樂的瑪麗]";
			mes "你準備結婚了?";
			mes "作為個新郎, 你需要準備";
			mes "^3377FF1 結婚禮服^000000 並且支付 ^3377FF1,300,000 Zeny^000000.";
			mes "新娘的話應該要準備自己的禮服, 並且支付";
			mes "1,200,000 zeny.";
		}
		else {
			mes "[快樂的瑪麗]";
			mes "所以, 你準備結婚了?";
			mes "作為個新娘你需要準備";
			mes "1 結婚禮服並且支付";
			mes "1,200,000 zeny. 新浪必須";
			mes "穿上結婚禮服並支付 1,300,000 zeny才能結婚.";
		}
		next;
		mes "[快樂的瑪麗]";
		mes "新浪和新娘都要準備";
		mes " ^3377FF1 鑽石戒指^000000 ";
		mes "這樣你們才能交換戒指.";
		mes "你需要準備好這些東西";
		mes "這樣你才能提交你的";
		mes "結婚典禮申請.";
		next;
		mes "[快樂的瑪麗]";
		mes "未來的新郎與新娘";
		mes "都必須完成了申請";
		mes "在他們進行結婚之前";
		mes "現在, 你準備要結婚,";
		mes "並且馬上開始進行";
		mes "結婚儀式嗎?";
		next;
		if (select("- 是的:- 不") == 1) {
			if (getpartnerid()) {
				cutin "wedding_marry02",2;
				mes "[快樂的瑪麗]";
				mes "我很抱歉, 不過";
				mes "你不能申請結婚!";
				mes "我不能允許你出賣";
				mes "你的配偶, ";
				mes "同時, 一夫多妻制";
				mes "在米德加德是違法的.";
				break;
			}
			else if (wedding_sign == 1) {
				mes "[快樂的瑪麗]";
				mes "你真的已經完成";
				mes "這些申請了麼?";
				mes "唔, 去問問你的戀人是否";
				mes "也完成了同樣申請程序";
				mes "然後再去找主教交談.";
				mes "繼續完成婚禮.";
				break;
			}
			else if (BaseLevel < 45) {
				mes "[快樂的瑪麗]";
				mes "唔, 你需要變得更加強壯";
				mes "才能保護你說愛的人";
				mes "在你的等級提升之後.";
				mes "如果同時您又有幸";
				mes "找到心上人的話.";
				mes "再來找我吧";
				goto L_End;
			}
			else if (countitem(2613) < 1) {
				mes "[快樂的瑪麗]";
				mes "唔? 你是不是忘記了";
				mes "帶鑽石戒指了?";
				mes "好好去找找吧";
				mes "說不定就掉在附近";
				mes "等你找到了再來找我好麼?";
				mes "請放鬆噢.";
				goto L_End;
			}
			else if (Sex) {
				if (Zeny < 1300000) {
					mes "[快樂的瑪麗]";
					mes "我很抱歉, 不過你沒有";
					mes "1,300,000 zeny";
					mes "所有的新浪都必須";
					mes "為他的結婚典禮付錢.";
					mes "你是不是犯錯口袋了?";
					goto L_End;
				}
				else if (countitem(7170) < 1) {
					mes "[快樂的瑪麗]";
					mes "你的結婚禮服呢?";
					mes "在你結婚的全程你都必須";
					mes "穿著它!";
					mes "趕快去把你的禮服找出來";
					mes "然後我們才能開始你的結婚典禮";
					mes "懂麼?";
					goto L_End;
				}
			}
			else if (Sex == 0) {
				if (Zeny < 1200000) {
					mes "[快樂的瑪麗]";
					mes "我很抱歉, 不過所有的新娘必須";
					mes "支付1,200,000";
					mes "來繼續這結婚典禮";
					mes "或許你可以找";
					mes "你的老公來支付這筆錢";
					goto L_End;
				}
				else if (countitem(2338) < 1) {
					mes "[快樂的瑪麗]";
					mes "親, 不要忘記";
					mes "你的結婚禮服?";
					mes "快找出來,";
					mes "然後帶來給我--";
					mes "你必須穿著";
					mes "參加婚禮!";
					goto L_End;
				}
			}
			mes "[快樂的瑪麗]";
			mes "好吧, 看起來你";
			mes "已經準備好所有的東西了.";
			mes "儘管我都不知道你的戀人是誰";
			mes "讓我成為第一位為你們";
			mes "即將到來的婚禮祝賀的人吧～";
			next;
			mes "[快樂的瑪麗]";
			mes "現在, 讓我們開始";
			mes "申請吧. 請寫下你的";
			mes "的正式姓名吧.";
			next;
			while(1) {
				input .@name$;
				if (.@name$ != strcharinfo(0)) {
					mes "[快樂的瑪麗]";
					mes "唔, 你必須寫下";
					mes "你的正式姓名就";
					mes "如同顯示的那樣. ";
					mes "或許你要複製和粘貼一下?";
					mes "不管怎樣, 讓我們再試一次.";
					next;
				}
				else 
					break;
			}
			mes "[快樂的瑪麗]";
			mes "太棒了, 看起來";
			mes "我們已經完成了你的申請.";
			mes "請記住你要寫下你";
			mes "的伴侶的正式姓名";
			mes "當你和主教交談一會";
			mes "之後, 懂了嗎?";
			next;
			mes "[快樂的瑪麗]";
			mes "當你的戀人也";
			mes "完成了申請之後";
			mes "你們都需要與主教對話";
			mes "才能開始你們正確的";
			mes "結婚典禮.";
			emotion ET_THROB;
			next;
			mes "[快樂的瑪麗]";
			if (Sex) {
				mes "因為你是新郎,";
				mes "所以你要先與教主對話";
				mes "當你結束對話之後";
				mes "那就輪到你的戀人";
				mes "與教主對話了";
				mes "^ ^";
				set Zeny, Zeny - 1300000;
				delitem 7170,1; //Tuxedo
			}
			else {
				mes "因為你是新娘,";
				mes "所以你要先等你的男友";
				mes "與教主大人對話之後";
				mes "然後才輪到你與教主";
				mes "對話";
				mes "^ ^";
				set Zeny, Zeny - 1200000;
				delitem 2338,1; //Wedding_Dress
			}
			delitem 2613,1; //Diamond_Ring
			set wedding_sign,1;
			goto L_End;
		}
		mes "[快樂的瑪麗]";
		mes "不...?";
		mes "好吧, 當你準備好";
		mes "要結婚了,";
		mes "再來找我提交申請好麼?";
		mes "祝你一天過的愉快";
		break;

	case 4:
		cutin "wedding_marry02",2;
		donpcevent "一名軍人#Prontera::OnEnable";
		donpcevent "一名軍人#Geffen::OnEnable";
		donpcevent "一名軍人#Morocc::OnEnable";
		donpcevent "一名軍人#Payon::OnEnable";
		donpcevent "一名軍人#Amatsu::OnEnable";
		donpcevent "一名軍人#Gonryun::OnEnable";
		emotion ET_HUK;
		mes "[FFF團]";
		mes "^CC9933你必須精煉";
		mes "你所擁有的裝備";
		mes "使他變得更極品!^000000";
		emotion ET_ROCK, getnpcid(0, "一名軍人#Prontera");
		next;
		mes "[FFF團]";
		mes "^330099在副本裡練級";
		mes "簡直就是在浪費時間";
		mes "我自己擼擼就能";
		mes "練級了!^000000";
		emotion ET_ROCK, getnpcid(0, "一名軍人#Geffen");
		next;
		mes "[FFF團]";
		mes "^666666該死,";
		mes "我從出生開始就在不斷的";
		mes "訓練我自己, 只為了";
		mes "能夠挫出火球!^000000";
		emotion ET_ROCK, getnpcid(0, "一名軍人#Morocc");
		next;
		mes "[FFF團]";
		mes "^666600我選擇花費";
		mes "孤單的聖誕夜...";
		mes "玩玩紙牌";
		mes "玩玩填字遊戲, 擼擼睡了!^000000";
		emotion ET_ROCK, getnpcid(0, "一名軍人#Payon");
		next;
		mes "[FFF團]";
		mes "^CC9966師傅說";
		mes "山下的女人是老虎, 能吃了你的靈魂";
		mes "不過他們絕對不能帶走我的自由!^000000";
		emotion ET_ROCK, getnpcid(0, "一名軍人#Amatsu");
		next;
		mes "[FFF團]";
		mes "^669900...我們是自由的! ";
		mes "我們是一隻無法被打倒的軍隊!^000000";
		emotion ET_ROCK, getnpcid(0, "一名軍人#Gonryun");
		close2;
		cutin "wedding_marry01",255;
		emotion ET_SWEAT;
		donpcevent "一名軍人#Prontera::OnInit";
		donpcevent "一名軍人#Geffen::OnInit";
		donpcevent "一名軍人#Morocc::OnInit";
		donpcevent "一名軍人#Payon::OnInit";
		donpcevent "一名軍人#Amatsu::OnInit";
		donpcevent "一名軍人#Gonryun::OnInit";
		end;
	}
	close2;
	cutin "wedding_marry01",255;
	end;

L_End:
	close2;
	cutin "",255;
	end;
}

prt_church,97,102,0	script	一名軍人#Prontera	8W_SOLDIER,{
	mes "[FFF團]";
	mes "^CC9933你必須精煉";
	mes "你所擁有的裝備";
	mes "使他變得更極品!^000000";
	close;

OnInit:
	hideonnpc "一名軍人#Prontera";
	end;

OnEnable:
	hideoffnpc "一名軍人#Prontera";
	emotion ET_GO;
	end;
}

prt_church,98,102,0	script	一名軍人#Geffen	4_M_GEF_SOLDIER,{
	mes "[FFF團]";
	mes "^330099在副本裡練級";
	mes "簡直就是在浪費時間";
	mes "我自己擼擼就能";
	mes "練級了!^000000";
	close;

OnInit:
	hideonnpc "一名軍人#Geffen";
	end;

OnEnable:
	hideoffnpc "一名軍人#Geffen";
	emotion ET_GO;
	end;
}

prt_church,99,102,0	script	一名軍人#Morocc	4_M_MOC_SOLDIER,{
	mes "[FFF團]";
	mes "我從出生開始就在不斷的";
	mes "訓練我自己, 只為了";
	mes "能夠挫出火球!^000000";
	close;

OnInit:
	hideonnpc "一名軍人#Morocc";
	end;

OnEnable:
	hideoffnpc "一名軍人#Morocc";
	emotion ET_GO;
	end;
}

prt_church,100,102,0	script	一名軍人#Payon	4_M_PAY_SOLDIER,{
	mes "[FFF團]";
	mes "^666600我選擇";
	mes "孤單的聖誕夜...";
	mes "玩玩紙牌";
	mes "玩玩填字遊戲, 擼擼睡了!^000000";
	close;

OnInit:
	hideonnpc "一名軍人#Payon";
	end;

OnEnable:
	hideoffnpc "一名軍人#Payon";
	emotion ET_GO;
	end;
}

prt_church,101,102,0	script	一名軍人#Amatsu	8_M_JPNSOLDIER,{
	mes "[FFF團]";
	mes "^CC9966師傅說";
	mes "山下的女人是老虎, 能吃了你的靈魂";
	mes "不過他們絕對不能帶走我的自由!^000000";
	close;

OnInit:
	hideonnpc "一名軍人#Amatsu";
	end;

OnEnable:
	hideoffnpc "一名軍人#Amatsu";
	emotion ET_GO;
	end;
}

prt_church,102,102,0	script	一名軍人#Gonryun	8_M_TWSOLDIER,{
	mes "[FFF團]";
	mes "^669900...我們是自由的! ";
	mes "我們是一隻無法被打倒的軍隊!^000000";
	close;

OnInit:
	hideonnpc "一名軍人#Gonryun";
	end;

OnEnable:
	hideoffnpc "一名軍人#Gonryun";
	emotion ET_GO;
	end;
}

prt_church,100,128,4	script	牧師#w	1_M_PASTOR,{
	cutin "wedding_bomars01",2;
	if (Upper == 2) {
		mes "[沃瑪赤]";
		mes "你好啊, 孩子.";
		mes "迷路了麼? 唔.";
		mes "你知道你的爸爸和";
		mes "媽媽在哪裡麼?";
		close2;
		cutin "wedding_bomars01",255;
		end;
	}

	if (!getpartnerid()) {
		if (!$@wedding) {
			if (wedding_sign == 1) {
				getpartymember(getcharid(1));
				set .@partymembercount,$@partymembercount;
				if (.@partymembercount == 2) {
					if (Sex) {
						set $@wedding,1;
						initnpctimer;
						mes "[沃瑪赤]";
						mes "年輕的戀人啊, 請";
						mes "記住接下來的時刻";
						mes "在你餘下的生命中. 你的未來將會被寧靜所保佑";
						mes "希望你的伴侶能";
						mes "在你們生命中的每一天都與你一同成長.";
						next;
						mapannounce "prt_church","現在正在舉行結婚典禮, Mr. " + strcharinfo(0) + "...",bc_map;
						mes "[沃瑪赤]";
						mes "直到世界的終結";
						mes "我也希望你們能";
						mes "手牽著手一起走下去";
						mes "鼓勵她, 保護她";
						mes "現在, 我能知道新娘";
						mes "的名字了麼?";
						next;
						input $@wed_bride$;
						mes "[沃瑪赤]";
						mes "Mr. " + strcharinfo(0) + "...";
						mes "你能發誓在你生命中";
						mes "你會永遠珍愛並且";
						mes "關心你的新娘麼,";
						mes "Miss " + $@wed_bride$ + "?";
						next;
						select("I do.");
						set $@wed_groom$,strcharinfo(0);
						mes "[沃瑪赤]";
						mes "現在我要來問問";
						mes "新娘來說出她的";
						mes "結婚誓言.";
						mes "我們要請新娘向前一步...";
						close2;
						mapannounce "prt_church","新郎, Mr. " + strcharinfo(0) + ", 已經與他的新娘 Miss " + $wed_bride$ + "...",bc_map;
						cutin "",255;
						end;
					}
					mes "[沃瑪赤]";
					mes "我很抱歉, 不過我需要";
					mes "新娘先來和我對話";
					mes "我們才能繼續婚禮.";
					mes "這雖然是很老的規矩,";
					mes "不過規定上就是這些寫的";
					mes "並且這樣使事情變得很順利.";
					goto L_End;
				}
				mes "[沃瑪赤]";
				mes "在你結婚之前";
				mes "你必須重新與你的戀人";
				mes "組隊, 然後來找我";
				mes "這樣, 就能繼續你的.";
				mes "結婚典禮了";
				goto L_End;
			}
			mes "[沃瑪赤]";
			mes "你必須先與";
			mes "快樂的瑪麗 提交結婚申請";
			mes "然後你才能過來結婚.";
			mes "快樂的瑪麗會告訴你";
			mes "所有的結婚流程";
			mes "並且告訴你需要準備什麼.";
			goto L_End;
		}
		else if ($@wedding == 1) {
			if (wedding_sign == 1) {
				getpartymember(getcharid(1));
				set .@partymembercount,$@partymembercount;
				if (.@partymembercount == 2) {
					if (Sex == 0) {
						if (strcharinfo(0) == $@wed_bride$) {
							mes "[沃瑪赤]";
							mes "年輕的戀人啊, 請";
						        mes "記住接下來的時刻";
						        mes "在你餘下的生命中. 你的未來將會被寧靜所保佑";
						        mes "希望你的伴侶能";
						        mes "在你們生命中的每一天都與你一同成長.";
							next;
							mapannounce "prt_church","讓我們聽聽新娘, Miss "+$@wed_bride$+", 說...",bc_map;
							mes "[沃瑪赤]";
							mes "小姐 "+$@wed_bride$+"...";
							mes "你是否發誓你將把你個真心";
							mes "交給 "+$@wed_groom$+",";
							mes "相伴與他身邊, 無論";
							mes "多危險的情況都與他度過?";
							next;
							if (select("^FF0000不.^000000:I do.") == 1) {
								cutin "wedding_bomars03",2;
								mapannounce "prt_church","下一對, 去準備...",bc_map;
								mes "[沃瑪赤]";
								mes "這樣 "+$@wed_groom$;
								mes "他並不是";
								mes "你想與之結婚的人? 唔.";
								mes "我真誠的道歉";
								mes "一定是哪裡弄錯了...";
								set $@wedding,0;
								close2;
								stopnpctimer;
								cutin "",255;
								end;
							}
							mes "[沃瑪赤]";
							mes "你是否真心發誓";
							mes "你的忠誠與耐心?";
							mes "你是否願意與結婚 "+$@wed_groom$+"?";
							next;
							if (select("是, 我願意.:^FF0000不.^000000") == 1) {
								if (isloggedin(getcharid(3,$@wed_groom$))) {
									if (marriage($@wed_groom$)) {
										//Call Wedding effect
										wedding;
										//Give ring to Bride, and change to wedding sprite.
										sc_start SC_WEDDING,3600000,1;
										getitem 2635,1; //Bride_Ring
										//Give ring to Groom, and change to wedding sprite.
										attachrid(getcharid(3,$@wed_groom$));
										sc_start SC_WEDDING,3600000,1;
										getitem 2634,1; //Bridegroom_Ring
										detachrid;
										//Switch Script progression back to Bride
										attachrid(getcharid(3,$@wed_bride$));
										cutin "wedding_bomars02",2;
										mapannounce "prt_church","我現在宣布你們, "+$@wed_groom$+" 和 "+$@wed_bride$+", 結為夫婦.",bc_map;
										mes "[沃瑪赤]";
										mes "作為天賜的米德加德帝國的大主教";
										mes "我現在非常榮耀的";
										mes "宣布你們正式的在這塊";
										mes "美麗的大地上正式結為夫妻";
										mes "願你們未來的生活中";
										mes "美滿與愉悅.";
										next;
										mes "[沃瑪赤]";
										mes "最後...";
										mes "請永遠幸福,";
										mes "親愛的 "+$@wed_bride$+"...";
										set $@wed_groom$,"";
										set $@wed_bride$,"";
										set $@wedding,0;
										close2;
										stopnpctimer;
										cutin "",255;
										detachrid;
										end;
									}
								}
								cutin "wedding_bomars03",2;
								mes "[沃瑪赤]";
								mes "唔. 看上去";
								mes "你的新郎";
								mes "在儀式還沒結束就離開了";
								mes "等他回來之後";
								mes "再試一次吧.";
								goto L_End;
							}
							else {
								cutin "wedding_bomars03",2;
								mapannounce "prt_church","唉! "+$@wed_bride$+" 拒絕了 "+$@wed_groom$+"的求婚!",bc_map;
								mes "[沃瑪赤]";
								mes "唔.這看起來";
								mes "你好像改變了你的主意";
								mes "對於新郎我很抱歉";
								mes "你必須服從你的內心";
								mes "告訴你自己這樣是正確的";
								mes "好了, 滾吧!";
							}
							set $@wed_groom$,"";
							set $@wed_bride$,"";
							set $@wedding,0;
							close2;
							stopnpctimer;
							cutin "",255;
							end;
						}
						callsub S_Busy;
					}
					callsub S_Busy;
				}
				callsub S_Busy;
			}
			if (strcharinfo(0) == $@wed_bride$) {
				mes "[沃瑪赤]";
				mes "唔? 看起來似乎是";
				mes "歡樂的瑪麗";
				mes "並沒有收到你的結婚申請";
				mes "請向她提交申請";
				mes "這樣你才能";
				mes "開始結婚程序了.";
				goto L_End;
			}
			callsub S_Busy;
		}
		callsub S_Busy;
	}
	mes "[沃瑪赤]";
	mes "我希望你能保持這份歡樂";
	mes "無論未來的道路有多黑暗";
	mes "你的伴侶都一直能";
	mes "陪伴在他的身邊";
	mes "並且著眼未來";
	mes "你們的幸福也能繼續下去.";

L_End:
	close2;
	cutin  "",255;
	end;

S_Busy:
	if ($@wed_groom$ != "" && $@wed_bride$ != "") {
		mes "[沃瑪赤]";
		mes "婚禮";
		mes "小姐 "+ $@wed_bride$ +" 和";
		mes "的結婚典禮正在進行.";
		mes "請小聲.";
		goto L_End;
	}
	mes "[沃瑪赤]";
	mes "我正在為另一對";
	mes "新人組織婚禮";
	mes "請等待到你們的時候";
	mes "謝謝你的理解... ^FFFFFF ^000000";
	goto L_End;

OnStop:
	stopnpctimer;
	end;

OnReset:
	set $@wed_groom$,"";
	set $@wed_bride$,"";
	set $@wedding,0;
	end;

OnTimer180000:
	mapannounce "prt_church","你的反應挺慢的... 下一對, 請開始吧.",bc_map;
	donpcevent "Bishop#w::OnReset";
	stopnpctimer;
	end;
}

prt_church,28,178,4	script	米德加茲國王殿下	1_M_PRON_KING,{
	callfunc "F_GM_NPC";
	mes "[沃瑪赤]";
	mes "你是誰?";
	mes "你必須要有";
	mes "密碼才能夠";
	mes "藉助我的力量.";
	next;
	if (callfunc("F_GM_NPC",1854,0) < 1) {
		mes "[沃瑪赤]";
		mes "這裡";
		mes "可沒地方";
		mes "可以鬼混.";
		close2;
		warp "prt_church",101,102;
		end;
	}
	mes "[結婚開關]";
	mes "婚禮過程是否";
	mes "發生了問題?";
	mes "我可以重置牧師";
	mes "沃瑪赤 NPC.";
	next;
	switch(select("不用謝謝!:重置")) {
	case 1:
		mes "[結婚開關]";
		mes "Alright, then.";
		mes "However, if the";
		mes "Bishop Vomars";
		mes "NPC is stuck, it may";
		mes "be best to reset it.";
		close;
	case 2:
		donpcevent "牧師#w::OnStop";
		donpcevent "牧師#w::OnReset";
		mapannounce "prt_church","你的動作也太慢了...下一對, 請繼續.",bc_map;
		mes "[Wedding Switch]";
		mes "The Bishop Vomars NPC";
		mes "has now been reactivated.";
		mes "It should now be possible";
		mes "to proceed with weddings.";
		close;
	}
}

prt_church,20,179,4	script	離婚職員	1_F_LIBRARYGIRL,{
	callfunc "F_GM_NPC";
	mes "[Bad Ending]";
	mes "Uh oh...";
	mes "You know I can't";
	mes "do anything for you.";
	next;
	if (callfunc("F_GM_NPC",1854,0) < 1) {
		mes "[Bad Ending]";
		mes "Hmm...";
		mes "You really";
		mes "shouldn't be";
		mes "in this place...";
		close;
	}
	mes "[Bad Ending]";
	mes "Great, you know the";
	mes "password! Now, did you";
	mes "want me to remove the";
	mes "Wedding Ring in your";
	mes "inventory?";
	next;
	switch(select("Drop 1 Wedding Ring.:Keep it.")) {
	case 1:
		set .@ring, (Sex)?2634:2635; //Bridegroom_Ring, Bride_Ring
		if (countitem(.@ring)) {
			delitem .@ring,1;
			mes "[Bad Ending]";
			mes "It's done!";
		} else {
			mes "[Bad Ending]";
			mes "I couldn't find";
			mes "the Wedding Ring...";
			mes "Please make sure";
			mes "that it's not equipped.";
		}
		close;
	case 2:
		mes "[Bad Ending]";
		mes "You sure you want";
		mes "to keep that ring?";
		mes "Alright, but if it becomes";
		mes "a problem, you come to me.";
		close;
	}
}

prt_church,22,179,4	script	再婚職員	1_F_LIBRARYGIRL,{
	callfunc "F_GM_NPC";
	mes "[Wedding Again]";
	mes "Hmm...?";
	mes "What exactly are";
	mes "you doing here?";
	next;
	if (callfunc("F_GM_NPC",1854,0) < 1) {
		mes "[Wedding Again]";
		mes "Ahk!";
		mes "An adventurer";
		mes "like you shouldn't";
		mes "be in this place!";
		close;
	}
	if (getpartnerid()) {
		mes "[Wedding Again]";
		mes "Hmm...";
		mes "I can only create";
		mes "a Wedding Ring if the";
		mes "character is married.";
		close;
	}
	mes "[Wedding Again]";
	mes "Ah, I see that you";
	mes "know the password.";
	mes "Alright, if you somehow";
	mes "lost your Wedding Ring,";
	mes "I can make you a new one.";
	next;
	switch(select("Make new Wedding Ring.:Cancel.")) {
	case 1:
		set .@ring, (Sex)?2634:2635; //Bridegroom_Ring, Bride_Ring
		if (countitem(.@ring) || isequipped(.@ring)) {
			mes "[Wedding Again]";
			mes "Wait, wait...";
			mes "You're wearing your";
			mes "Wedding Ring. I better";
			mes "not make you another since";
			mes "you don't need more than one.";
		} else {
			getitem .@ring,1;
			mes "[Wedding Again]";
			mes "Here you go~";
			mes "It's your brand";
			mes "new Wedding Ring!";
		}
		close;
	case 2:
		mes "[Wedding Again]";
		mes "Alright. If you ever";
		mes "lose your Wedding Ring,";
		mes "come to me if you happen";
		mes "to need a new one, okay?";
		close;
	}
}
