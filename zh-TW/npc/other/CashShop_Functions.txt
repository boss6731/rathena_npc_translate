//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================

//===== rAthena Script ======================================= 
//= Cash Shop Functions
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.8
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Used for Cash shop items:
//= - Kafra Card
//= - Giant Fly Wing
//= - Neuralizer
//= - Dungeon Teleport Scroll
//= Also included are functions related to the VIP system.
//===== Additional Comments: ================================= 
//= 1.0 First version. [L0ne_W0lf]
//=     F_CashPartyCall Optimized by Trancid.
//= 1.1 Updated Dungeon Teleport Scroll. [L0ne_W0lf]
//=     Added Town Teleport Scroll Function
//=     Added Seige Teleport Scroll Function
//= 1.2 Updated the dungeon teleport scroll menu. [L0ne_w0lf]
//= 1.3 Implemented Aozi Giant Flywing Fix. (bugreport:4242) [L0ne_w0lf]
//= 1.4 Simplified Giant Flywing function using warpparty. [L0ne_w0lf]
//= 1.5 Neuralizer is no longer consumed upon failure. [Euphy]
//= 1.6 Updated Teleport functions and added Curious_Snowball. [Euphy]
//= 1.7 Added Dungeon Teleport Scroll II maps. [Euphy]
//= 1.8 Added VIP function. [Euphy]
//= 1.9 Added checkdragon() in F_CashReset. [Capuche]
//= 2.0 Moved VIP_Third_Class function into other/re/CashShop_Functions. [Capuche]
//============================================================ 

// 隨地倉庫使用券
//============================================================ 
// - 隨時隨地打開倉庫
// - 無參數
function	script	F_CashStore	{
	cutin "kafra_01",2;
	mes "[卡普拉 職員]";
	mes "您好,歡迎呼叫卡普拉公司!";
	mes "現在將按照您的請求開啟倉庫.";
	close2;
	openstorage;
	cutin "",255;
	return;
}

// 巨大蒼蠅翅膀
//============================================================ 
// - Warp party leader to random spot on the map.
// - 隨著地圖上的隊長一起瞬間移動
// - 無參數.
function	script	F_CashPartyCall	{
	warp "Random",0,0;
	if(getpartyleader(getcharid(1),2) == getcharid(0)) {
		getmapxy .@mapl$, .@xl, .@yl, UNITTYPE_PC;
		warpparty .@mapl$, .@xl, .@yl, getcharid(1), .@mapl$;
	}
	return;
}

// 重置技能棒
//============================================================ 
// - 負重為0,且老鷹、手推車須放棄
// - 無參數
function	script	F_CashReset	{
	if (Class != Job_Novice && Weight == 0 && !checkriding() && !checkfalcon() && !checkcart() && !ismounting() && !checkdragon()) {
		resetskill;
		sc_end SC_ALL;
		if (countitem(12213)) delitem 12213,1;
	}
	return;
}

// 洞穴瞬間移動 捲軸
//============================================================ 
// - Warp player to the selected dungeon.
// - Arg(0) = type of scroll.
function	script	F_CashDungeon	{
	switch(getarg(0)) {
	case 1:
		switch(select("諾可羅德:妙勒尼 廢棄礦場:"+((checkre(0))?"":"樹木裡的 異界通路")+":艾音布羅克礦山洞穴:斐揚地下廢棄村落:玩具工廠倉庫:克雷斯特漢姆 地下監獄1樓:龍之城審良宮:崑崙神仙的圍棋臺:依斯魯得島海盜地洞密穴:烏龜島密穴:鐘塔 地下3樓:鐘塔 地上3樓:古城 地下水道2樓:史芬克斯 密穴4樓:金字塔 迷宮4樓:普隆德拉 地下水道3樓:天津町 榻榻米 迷宮1樓:生體試驗 研究所1樓:哎喲泰雅 古代神殿2樓")) {
		case 1: warp "mag_dun01",125,71; end;	// 諾可羅德
		case 2: warp "mjo_dun02",80,297; end;	// 妙勒尼 廢棄礦場
		case 3: warp "um_dun02",125,122; end;	// 樹木裡的異界通路 (Pre-Renewal only)
		case 4: warp "ein_dun01",261,262; end;	// 艾音布羅克礦山洞穴
		case 5: warp "pay_dun03",155,150; end;	// 斐揚地下廢棄村落
		case 6: warp "xmas_dun01",133,130; end;	// 玩具工廠倉庫
		case 7: warp "gl_prison",140,15; end;	// 克雷斯特漢姆 地下監獄1樓
		case 8: warp "lou_dun03",165,38; end;	// 龍之城審良宮
		case 9: warp "gon_dun02",251,263; end;	// 崑崙神仙的圍棋臺
		case 10: warp "iz_dun02",350,335; end;	// 依斯魯得島海盜地洞密穴
		case 11: warp "tur_dun02",165,30; end;	// 烏龜島密穴
		case 12: warp "alde_dun03",275,180; end;	// 鐘塔 地下3樓
		case 13: warp "c_tower3",34,42; end;	// 鐘塔 地上3樓
		case 14: warp "gl_sew02",292,295; end;	// 古城 地下水道2樓
		case 15: warp "in_sphinx4",120,120; end;	// 史芬克斯 密穴4樓
		case 16: warp "moc_pryd04",195,4; end;	// 金字塔 迷宮4樓
		case 17: warp "prt_sewb3",20,175; end;	// 普隆德拉 地下水道3樓
		case 18: warp "ama_dun01",222,144; end;	// 天津町 榻榻米 迷宮1樓
		case 19: warp "lhz_dun01",19,153; end;	// 生體試驗 研究所1樓
		case 20: warp "ayo_dun02",70,240; end;	// 哎喲泰雅 古代神殿2樓
		}
	case 2: // Menu names are not official.
		switch(select("託爾火山 2f:冰洞入口:無名島入口:尼菲海姆:迷宮森林 2f:優配擂斯洞穴入口:螞蟻地獄 2f:基爾學院:達納託斯塔入口:深淵湖水入口:菲力亞大神殿入口:奧迪神殿 2f")) {
		case 1: warp "thor_v02",77,208; end;	// Thor Volcano 2F
		case 2: warp "ra_fild01",237,333; end;	// Ice Dungeon
		case 3: warp "ve_fild07",127,131; end;	// Nameless Island
		case 4: warp "niflheim",206,179; end;	// Niflheim
		case 5: warp "prt_maze02",100,174; end;	// Labyrinth Forest F2
		case 6: warp "jupe_cave",36,54; end;	// Juperos
		case 7: warp "anthell02",36,265; end;	// Ant Hell F2
		case 8: warp "yuno_fild08",70,171; end;	// Kiel Hyre's Academy
		case 9: warp "hu_fild01",140,160; end;	// Thanatos Tower
		case 10: warp "hu_fild05",168,302; end;	// Abyss Lake
		case 11: warp "ra_temple",117,173; end;	// Rachel Sanctuary
		case 12: warp "odin_tem02",257,374; end;	// Odin Temple F2
		}
	case 3:
		switch(select("生體試驗 研究所 (2樓):冰洞穴 (3樓):奧丁神殿 (3樓):託爾火山 洞穴 (3樓):深淵湖水 地下洞穴 (3樓):優配擂斯 廢墟 (2樓)")) {
		case 1: warp "lhz_dun02",145,149; end;	// 生體試驗 研究所 2樓
		case 2: warp "ice_dun03",150,176; end;	// 冰洞穴 3樓
		case 3: warp "odin_tem03",278,235; end;	// 奧丁神殿 3樓
		case 4: warp "thor_v03",144,170; end;	// 託爾火山 洞穴 3樓
		case 5: warp "abyss_03",97,104; end;	// 深淵湖水 地下洞穴 3樓
		case 6: warp "juperos_02",130,159; end;	// 優配擂斯 廢墟 2樓
		}
	}
	return;
}

// Coloured Butterly Wings
//============================================================ 
// - Warp player to different cities acording to the scroll.
// - Arg(0) = type of scroll.
function	script	F_CashCity	{
	switch(getarg(0)) {
	case 1: // Yellow - Rune-Midgart
		switch(select("普隆德拉:依斯魯得島:吉芬:斐揚:夢羅克:艾爾帕蘭:艾爾貝塔")) {
			case 1: warp "prontera",122,87; end;
			case 2: if (checkre(0)) warp "izlude",130,90; else warp "izlude",91,105; end;
			case 3: warp "geffen",128,48; end;
			case 4: warp "payon",164,123; end;
			case 5: warp "morocc",160,100; end;
			case 6: warp "alberta",117,50; end;
			case 7: warp "aldebaran",140,110; end;
		}
	case 2: // Green - Schwartzvalt
		switch(select("朱諾:裡希塔樂鎮:艾音布羅克:艾音貝赫:毀葛")) {
			case 1: warp "yuno",160,170; end;
			case 2: warp "lighthalzen",190,310; end;
			case 3: warp "einbroch",230,190; end;
			case 4: warp "einbech",187,120; end;
			case 5: warp "hugel",92,165; end;
		}
	case 3: // Red - Arunafeltz
		switch(select("拉赫:伯仁斯")) {
			case 1: warp "rachel",120,125; end;
			case 2: warp "veins",215,105; end;
		}
	case 4: // Blue - Islands
		switch(select("天津町:崑崙:哎喲泰雅:龍之城")) {
			case 1: warp "amatsu",110,140; end;
			case 2: warp "gonryun",160,115; end;
			case 3: warp "ayothaya",220,170; end;
			case 4: warp "louyang",217,95; end;
		}
	case 5: // Blue - Islands (Renewal)
		switch(select("天津町:崑崙:哎喲泰雅:龍之城:莫斯科比亞:德瓦塔:巴西利亞斯")) {
			case 1: warp "amatsu",110,140; end;
			case 2: warp "gonryun",160,115; end;
			case 3: warp "ayothaya",220,170; end;
			case 4: warp "louyang",217,95; end;
			case 5: warp "moscovia",224,195; end;
			case 6: warp "dewata",200,107; end;
			case 7: warp "brasilis",196,181; end;
		}
	}
	return;
}

// 空間移動捲軸
//============================================================ 
// - 將使用者傳送到存點或是選項裡的城市.
// - Arg(0) = type of scroll.
function	script	F_CashTele	{
	switch(getarg(0)) {
	case 1:
		switch(select("普隆德拉:吉芬:艾爾帕蘭:依斯魯得島:儲存點")) {
		case 1: warp "prontera",119,77; end;
		case 2: warp "geffen",119,39; end;
		case 3: warp "aldebaran",165,107; end;
		case 4: if (checkre(0)) warp "izlude",128,105; else warp "izlude",91,105; end;
		case 5: warp "SavePoint",0,0; end;
		}
	case 2:
		switch(select("斐揚:愛爾貝塔:夢羅克:克魔島:儲存點")) {
		case 1: warp "payon",158,55; end;
		case 2: warp "alberta",115,57; end;
		case 3: warp "morocc",158,48; end;
		case 4: warp "comodo",217,148; end;
		case 5: warp "SavePoint",0,0; end;
		}
	case 3:
		switch(select("龍之城:天津町:崑崙:哎喲泰雅:儲存點")) {
		case 1: warp "louyang",214,101; end;
		case 2: warp "amatsu",112,145; end;
		case 3: warp "gonryun",160,118; end;
		case 4: warp "ayothaya",216,175; end;
		case 5: warp "SavePoint",0,0; end;
		}
	case 4:
		switch(select("薑餅城:汶巴拉:尼芙菲姆:儲存點")) {
		case 1: warp "xmas",148,131; end;
		case 2: warp "umbala",93,154; end;
		case 3: warp "niflheim",187,189; end;
		case 4: warp "SavePoint",0,0; end;
		}
	case 5:
		switch(select("朱諾:艾音布羅克:裡希塔樂鎮:毀葛:儲存點")) {
		case 1: warp "yuno",157,124; end;
		case 2: warp "einbroch",230,192; end;
		case 3: warp "lighthalzen",158,94; end;
		case 4: warp "hugel",93,159; end;
		case 5: warp "SavePoint",0,0; end;
		}
	case 6:
		switch(select("拉赫:伯仁斯:儲存點")) {
		case 1: warp "rachel",118,132; end;
		case 2: warp "veins",214,125; end;
		case 3: warp "SavePoint",0,0; end;
		}
	}
	return;
}

// Siege Teleport Scroll
//============================================================ 
// - Warp player to selected guild castle.
// - No arguments.
function	script	F_CashSiegeTele	{
	switch(select("露依納 工會 (aldeg_cas01):露依納 工會 (aldeg_cas02):露依納 工會 (aldeg_cas03):露依納 工會 (aldeg_cas04):露依納工會 (aldeg_cas05):咘哩妥利亞 工會 (gefg_cas01):咘哩妥利亞 工會 (gefg_cas02):咘哩妥利亞 工會 (gefg_cas03):咘哩妥利亞 工會 (gefg_cas04):咘哩妥利亞 工會 (gefg_cas05):青林湖泊 工會 (payg_cas01):青林湖泊 工會 (payg_cas02):青林湖泊 工會 (payg_cas03):青林湖泊 工會 (payg_cas04):青林湖泊 工會 (payg_cas05):Kriemhild (prtg_cas01):巴基力雷恩 工會 (prtg_cas02):巴基力雷恩 工會 (prtg_cas03):巴基力雷恩 工會 (prtg_cas04):巴基力雷恩 工會 (prtg_cas05)")) {
	case 1: warp "alde_gld",48,91; end;
	case 2: warp "alde_gld",103,245; end;
	case 3: warp "alde_gld",142,87; end;
	case 4: warp "alde_gld",236,243; end;
	case 5: warp "alde_gld",269,90; end;
	case 6: warp "gef_fild13",217,75; end;
	case 7: warp "gef_fild13",307,237; end;
	case 8: warp "gef_fild13",77,297; end;
	case 9: warp "gef_fild13",190,276; end;
	case 10: warp "gef_fild13",312,91; end;
	case 11: warp "pay_gld",121,232; end;
	case 12: warp "pay_gld",297,116; end;
	case 13: warp "pay_gld",318,293; end;
	case 14: warp "pay_gld",140,164; end;
	case 15: warp "pay_gld",202,264; end;
	case 16: warp "prt_gld",141,64; end;
	case 17: warp "prt_gld",240,131; end;
	case 18: warp "prt_gld",153,133; end;
	case 19: warp "prt_gld",126,240; end;
	case 20: warp "prt_gld",195,240; end;
	}
	return;
}

// Curious Snowball
//============================================================ 
// - You can get an effect of Restore, Endure, or Wing of Butterfly.
// - No arguments.
function	script	F_Snowball	{
	cutin "rutie_snownow03",2;
	mes "[雪人]";
	mes "聖誕節快樂!";
	switch(select("恢復一些 HP/SP:強化我的身體狀態!:SP完全回復.:返回儲存點")) {
	case 1:
		mes "阿哈!";
		mes "寧靜的夜晚~";
		percentheal 70,70;
		close2;
		cutin "",255;
		break;
	case 2:
		mes "祝福這.";
		mes "神聖之夜~";
		unitskilluseid getcharid(3),"SM_ENDURE",10;
		close2;
		cutin "",255;
		break;
	case 3:
		mes "阿哈!";
		mes "白色耶誕~";
		percentheal 0,100;
		close2;
		cutin "",255;
		break;
	case 4:
		mes "鈴兒響叮噹~";
		mes "鈴兒響叮噹~ 鈴兒響叮噹!";
		close2;
		cutin "",255;
		warp "SavePoint",0,0;
		break;
	}
	end;
}

// Status reduction potion
//============================================================
// - Permanently reduces base stat <type> by <val>.
// - Returns status points equals to points needed to raise
//   that stat to original value.
// - Doesn't work if base status <type> would become lower than 1 after reduction.
// * callfunc("F_CashReduceStat",<type>{,<val>,<itemid>});
function	script	F_CashReduceStat	{
	.@type = getarg(0);
	.@amount = getarg(1, -1);
	.@itemid = getarg(2, 0);
	
	if((readparam(.@type) + .@amount) < 1) return;
	
	if(.@itemid) {
		if(countitem(.@itemid))
			delitem .@itemid,1;
		else
			return;
	}
	StatusPoint += needed_status_point(.@type, .@amount);
	statusup2 .@type,.@amount;
	return;
}
