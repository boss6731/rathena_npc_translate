//============================================================
//= rAthenaCN [飛空艇]漢化腳本 
//============================================================

//===== rAthena Script =======================================
//= The Airship System Script
//===== By: ================================================== 
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= The Airship System used in the official servers.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//============================================================ 

//============================================================ 
//= Domestic Airship Route
//= ... -> Juno -> Einbroch -> Lighthalzen -> Einbroch
//= -> Juno -> Hugel -> ...
//============================================================ 

airplane,243,73,0	script	#AirshipWarp-1	45,1,1,{
	end;

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;

OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;

OnTouch:
	switch ($@airplanelocation) {
	case 0: warp "yuno",92,260; end;
	case 1: warp "einbroch",92,278; end;
	case 2: warp "lighthalzen",302,75; end;
	case 3: warp "hugel",181,146; end;
	}
}

airplane,243,29,0	duplicate(#AirshipWarp-1)	#AirshipWarp-2	45,1,1

airplane,1,1,0	script	Domestic_Airship	-1,{
	end;
OnInit:
	initnpctimer;
	end;
OnTimer20000:
	mapannounce "airplane","目前本飛空艇正在飛向艾音布羅克中.",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane","等一下飛空艇就要抵達艾音布羅克了.",bc_map,"0x00ff00";
	end;
OnTimer60000:
	set $@airplanelocation,1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","我們已抵達艾音布羅克,下機的旅客再見了.",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane","我們現在停靠在艾音布羅克中,等一下我們的飛空艇就要出發囉.",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","飛空艇已起飛.我們的下一個目的地是裡希塔樂鎮. ",bc_map,"0x70dbdb";
	end;
OnTimer100000:
	mapannounce "airplane","目前本飛空艇正在飛向裡希塔樂鎮中.",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane","等一下飛空艇就要抵達裡希塔樂鎮了.",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	set $@airplanelocation,2;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","我們已抵達裡希塔樂鎮,下機的旅客再見了.",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane","我們現在停靠在裡希塔樂鎮中,等一下我們的飛空艇就要出發囉.",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","飛空艇已起飛.我們的下一個目的地是艾音布羅克.",bc_map,"0x00ff00";
	end;
OnTimer180000:
	mapannounce "airplane","目前本飛空艇正在飛向艾音布羅克中.",bc_map,"0x00ff00";
	end;
OnTimer210000:
	mapannounce "airplane","等一下飛空艇就要抵達艾音布羅克了.",bc_map,"0x00FF00";
	end;
OnTimer220000:
	set $@airplanelocation,1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","我們已抵達艾音布羅克,下機的旅客再見了",bc_map,"0x00ff00";
	end;
OnTimer230000:
	mapannounce "airplane","我們現在停靠在艾音布羅克中,等一下我們的飛空艇就要出發囉.",bc_map,"0x00ff00";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","飛空艇已起飛.我們的下一個目的地是朱諾.",bc_map,"0xff8200";
	end;
OnTimer260000:
	mapannounce "airplane","目前本飛空艇正在飛向朱諾中.",bc_map,"0xff8200";
	end;
OnTimer290000:
	mapannounce "airplane","等一下飛空艇就要抵達朱諾了.",bc_map,"0xff8200";
	end;
OnTimer300000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","我們已抵達朱諾,下機的旅客再見了.",bc_map,"0xff8200";
	end;
OnTimer310000:
	mapannounce "airplane","我們現在停靠在朱諾中,等一下我們的飛空艇就要出發囉.",bc_map,"0xff8200";
	end;
OnTimer320000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","飛空艇已起飛.我們的下一個目的地是毀葛.",bc_map,"0xca4bf3";
	end;
OnTimer340000:
	mapannounce "airplane","目前本飛空艇正在飛向毀葛中.",bc_map,"0xca4bf3";
	end;
OnTimer370000:
	mapannounce "airplane","等一下飛空艇就要抵達毀葛了.",bc_map,"0xca4bf3";
	end;
OnTimer380000:
	set $@airplanelocation,3;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","我們已抵達毀葛,下機的旅客再見了.",bc_map,"0xca4bf3";
	end;
OnTimer390000:
	mapannounce "airplane","我們現在停靠在毀葛中,等一下我們的飛空艇就要出發囉.",bc_map,"0xca4bf3";
	end;
OnTimer400000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","飛空艇已起飛.我們的下一個目的地是朱諾.",bc_map,"0xff8200";
	end;
OnTimer420000:
	mapannounce "airplane","目前本飛空艇正在飛向朱諾中.",bc_map,"0xff8200";
	end;
OnTimer450000:
	mapannounce "airplane","等一下飛空艇就要抵達朱諾了.",bc_map,"0xff8200";
	end;
OnTimer460000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","我們已抵達朱諾,下機的旅客再見了.",bc_map,"0xff8200";
	end;
OnTimer470000:
	mapannounce "airplane","我們現在停靠在朱諾中,等一下我們的飛空艇就要出發囉.",bc_map,"0xff8200";
	end;
OnTimer480000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","飛空艇已起飛.我們的下一個目的地是艾音布羅克.",bc_map,"0x00ff00";
	stopnpctimer;
	initnpctimer;
}

airplane,240,64,5	script	Exit#airplane1a::ExitAirplane	857,{
	end;
}
airplane,247,64,5	duplicate(ExitAirplane)	Exit#airplane1b	857
airplane,240,40,1	duplicate(ExitAirplane)	Exit#airplane2a	857
airplane,247,40,1	duplicate(ExitAirplane)	Exit#airplane2b	857

airplane,100,69,3	script	機組員#ein-1	852,{
	mes "[機組員]";
	mes "如果我們抵達了您所想去的目的地,";
	mes "就請登上前方的樓梯下機吧.";
	mes "謝謝您選擇使用我們的服務.";
	mes "祝您有個美好的一天~";
	close;
}

airplane,64,94,1	script	汶巴拉孩童#ein_p	787,{
	emotion ET_PROFUSELY_SWEAT;
	mes "[孩童]";
	if (event_umbala >= 3) {
		mes "哇哦,麻麻!";
		mes "看-看!";
		mes "我們在飛!我-我們在...";
		mes "我們在天上啊!";
	}
	else {
		mes "馬庫馬拉古!";
		mes "散帕 啾金波";
		mes "卡酷 納 基恩蒂 颼羅.";
		mes "波爾哇哈哈哈哈!";
	}
	close;
}

airplane,66,93,3	script	汶巴拉女子#ein_p	783,{
	emotion ET_THINK;
	mes "[女士]";
	if (event_umbala >= 3) {
		mes "噓,噓...";
		mes "小寶貝,注意點~";
		mes "公共場合別這麼激動";
	}
	else {
		mes "超圖!";
		mes "薩克然普 馬蹄伊!";
		mes "砂咖 格爾夫濤飛";
		mes "杭太吉 慢珠兒!";
	}
	close;
}

airplane,71,91,7	script	汶巴拉男子#ein_p	789,{
	if (event_umbala >= 3) {
		mes "[科米廉姆]";
		mes "嗨這邊的~";
		mes "看你的表情我就知道你懂我在說什麼了";
		mes "^333333*呼呼...!*^000000";
		next;
		emotion ET_HNG;
		mes "[科米廉姆]";
		mes "我在為飛空艇事業做一番貢獻呢.";
		mes "幫助機組員在汶巴拉做訓練以克服他們的恐高症.";
		mes "其實本來這事情就這麼簡單的.";
		next;
		mes "[科米廉姆]";
		mes "但是...";
		mes "有些人就是不能克服哇,";
		mes "甚至有一部分人真的被嚇到了 ^333333尼菲奧姆去了^000000.";
	}
	else {
		mes "[科米廉姆]";
		mes "巴就 嘎";
		mes "鈕克嗒 飛艇.";
		next;
		mes "[科米廉姆]";
		mes "...";
		mes "......";
		next;
		emotion ET_HNG;
		mes "[科米廉姆]";
		mes "沙巴 木歐 巴佳馬";
		mes "飛艇 庫拉哈 鉤鉤納";
		mes "薩魯. 達麻,庫庫納努";
		mes "魚庫塔. 嗄, 法樹庫 那 熱庫!";
	}
	close;
}

airplane,250,58,2	script	飛空艇職員#airplane	67,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes "[飛空艇職員]";
		mes "歡迎來到飛艇.";
		mes "請問您需要什麼幫助嗎?";
		next;
		select("請問您認識一位叫做西雅提的人嗎?");
		mes "[飛空艇職員]";
		mes "抱歉,我們沒有叫做這個名字的乘客呢.";
		close;
	}
	// Hugel quest end
	mes "[飛空艇職員]";
	mes "歡迎來到飛艇上.";
	mes "請問您需要什麼幫助嗎?";
	next;
	switch (select("這個飛空艇到底怎麼用呢:船長室在哪裡:內部設施介紹:取消")) {
	case 1:
		mes "[飛空艇職員]";
		mes "當您聽到我們已經抵達你的目的地這個消息的時候,";
		mes "請利用飛艇北部或者南部的出口離開飛艇就可以了.";
		next;
		mes "[飛空艇職員]";
		mes "如果您不小心錯過了也別急,";
		mes "飛艇是不斷循環著飛的,";
		mes "請等到下一次抵達您想去的目的地之後再下車吧.";
		close;
	case 2:
		mes "[飛空艇職員]";
		mes "船長室就在飛艇的前部.";
		mes "您可以和飛艇船長和飛行員去打個招呼.";
		close;
	case 3:
		mes "[飛空艇職員]";
		mes "飛艇上為各位乘客提供了各種迷你娛樂遊戲.";
		mes "您可以試試手氣,練練手法,玩玩看這些小遊戲~";
		close;
	case 4:
		mes "[飛空艇職員]";
		mes "那麼,希望您在我們飛艇上的旅途愉快.";
		mes "謝謝您選擇了我們的服務.";
		mes "祝您有個美好的一天.";
		close;
	}
}

airplane,80,71,2	script	澤爾塔#01airplane	834,{
	mes "[澤爾塔]";
	mes "哦,冒險者你好.";
	mes "我現在陷入了一場可怕的旅途,";
	mes "請看在同是盧恩-米德加茲大陸人的份兒上,幫我祈禱一下吧.";
	close;
}

airplane,65,63,4	script	麥林#01airplane	714,{
	mes "[麥林]";
	mes "啊,這飛空艇去不去薑餅城呢?";
	mes "我都等了這麼長時間了,";
	mes "但是還沒有聽到什麼關於薑餅城的,";
	close;
}

airplane,72,34,6	script	阿諾斯#01airplane	702,{
	mes "[阿諾斯]";
	mes "哦哇哦~";
	mes "從這裡往下看,天空如此不同,";
	mes "如此美麗啊!";
	close;
}

airplane,221,158,2	script	飛行員#airplane	852,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes "[飛行員]";
		mes "好想來一杯冰鎮啤酒喝.";
		mes "喝酒就是我人生第一大樂趣了哈哈!酒給予我力量!";
		mes "離開酒,我什麼也不是啊!";
		next;
		mes "[飛行員]";
		mes "但是!醉酒駕機是不可以的.";
		mes "但是!那只會讓我更加想喝酒!";
		emotion ET_CRY;
		next;
		select("請問你認識一位名叫西雅提的乘客嗎?");
		mes "[飛行員]";
		mes "這件制服真是漂亮,";
		mes "但是就這麼一路穿著還真是太厚重了啊.";
		next;
		mes "[飛行員]";
		mes "...";
		mes "......";
		mes "沒有人曾經怎麼進來過這個房間.";
		mes "船長是頭馴鹿.";
		mes "^FFFFFF ^000000 我只能看看那些裝運工來著.";
		next;
		emotion ET_HUK;
		mes "[飛行員]";
		mes "哇!?那是誰?";
		next;
		mes "- ...他就沒在聽你說話. -";
		close;
	}
	// Hugel quest end
	switch (rand(1,4)) {
	case 1:
		mes "[飛行員]";
		mes "好久沒有好好享受一份上好的冰鎮麥酒了.";
		mes "這工作要求我要時刻保持清醒啊";
		next;
		mes "[飛行員]";
		mes "喝酒一定要適度啊!";
		mes "我還是記不大清楚上次放假或者有天休息是什麼時候的事了.";
		mes "烈酒幾瓶,薯條幾包,電視看看,";
		mes "這就是我想像的完美休息方式.";
		emotion ET_CRY;
		close;
	case 2:
		mes "[飛行員]";
		mes "嘿,今天天氣不錯呢.";
		mes "明亮,開闊的天空保證了可視度,";
		mes "飛行會很安全的.";
		close;
	case 3:
		mes "[飛行員]";
		mes "你知道我們的船長是一個很讓人尊敬的傢伙.";
		mes "他和他的兄弟在飛艇行業裡頭真心很有名氣.";
		mes "誰會知道他們是麋鹿呢?";
		next;
		mes "[飛行員]";
		mes "嘿,別和別人說啊,就我倆知道,";
		mes "告訴你個小秘密.";
		mes "那個聖誕老人真的很厲害,";
		mes "讓那些淘氣小精靈和麋鹿都為他工作.";
		mes "他一定是個天才!";
		close;
	default:
		mes "[飛行員]";
		mes "你知道在天上這樣子飛是最新科技產物的.";
		mes "他們匆匆忙忙趕出來這個飛艇.";
		next;
		emotion ET_HUK;
		mes "[飛行員]";
		mes "但他們還是真的很用心在做的,真的提前思考了很多東西.";
		mes "他們在飛空艇還在研發的時候,就開始訓練我們了.";
		mes "真是牛掰啊,對吧?!";
		close;
	}
}

airplane,50,66,5	script	蘋果商人#airplane	86,{
	mes "[萍國]";
	mes "歡迎來到萍國的蘋果小超市.";
	mes "在這兒你可以買蘋果,也可以把蘋果榨汁.";
	next;
	switch (select("買蘋果.:榨蘋果汁.:你在這裡做什麼?:取消.")) {
	case 1:
		mes "[萍國]";
		mes "輸入你想要購買的數量吧.";
		mes "單價 15 zeny ,一次最多購買500個.";
		mes "不想要就輸入0就可以了.";
		next;
		while (1) {
			input .@input;
			set .@pay, .@input * 15;
			if (.@input == 0) {
				mes "[萍國]";
				mes "謝謝關注我的小店,";
				mes "拜拜!";
				mes "當你覺得你想要啃蘋果的時候,";
				mes "就來我萍國的小店吧~";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[萍國]";
				mes "你輸入了超過500的數量喔.";
				mes "請重新輸入你想要蘋果的數量.";
				next;
			}
			else {
				mes "[萍國]";
				mes "一共 ^FF0000" + .@input + "^000000 個蘋果";
				mes "花費 ^FF0000" + .@pay + " Zeny^000000 zeny.";
				mes "願意繼續嗎?";
				next;
				if (select("是的:不要") == 2) {
					mes "[萍國]";
					mes "謝謝惠顧喔";
					mes "拜拜!";
					mes "當你覺得你想要啃蘋果的時候,";
					mes "就來我萍國的小店吧~";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[萍國]";
			mes "抱歉,但是你沒有帶來買這些蘋果要的錢呢.";
			mes "檢查一下你的錢包,少買點吧.";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[萍國]";
			mes "嗯,好吧.你包包裡沒有足夠的位子了.";
			mes "先清理一下你的包包再來吧.";
			close;
		}
		else {
			set Zeny, Zeny - .@pay;
			getitem 512,.@input; //Apple
			mes "[萍國]";
			mes "謝謝惠顧喔~";
			mes "希望";
			mes "你可以好好享受這些蘋果的美味~!";
			close;
		}
	case 2:
		mes "[萍國]";
		mes "好的,我需要";
		mes "^FF00003 個蘋果 和 1 空瓶^000000";
		mes "如果你想要做一瓶蘋果汁的話.";
		mes "想要繼續嗎?";
		next;
		switch (select("是的:不要")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[萍國]";
				mes "啊,你沒帶夠做蘋果汁的材料";
				mes "記住哦 3 個蘋果,一個空瓶.";
				close;
			}
			else {
				mes "[萍國]";
				mes "好的,";
				mes "稍等";
				next;
				mes "^3355FF*磨啊磨啊*";
				mes "*磨啊磨啊*";
				mes "*鈳琅...!*^000000";
				next;
				delitem 512,3; //Apple
				delitem 713,1; //Empty_Bottle
				getitem 531,1; //Apple_Juice
				mes "[萍國]";
				mes "嘿,給你~";
				mes "希望你好好享受!";
				mes "如果你想要蘋果或者蘋果汁的話,";
				mes "哈哈,隨時隨地都可以來找我呦~";
				close;
			}
		case 2:
			mes "[萍國]";
			mes "啊哈,謝謝關注我的小店.";
			mes "拜拜!";
			mes "當你覺得你想要啃蘋果的時候,";
			mes "就來我萍國的小店吧~";
			close;
		}
	case 3:
		mes "[萍國]";
		mes "我曾經是一個漫遊的流浪漢,";
		mes "直到有一天,我小睡的時候有東西砸到了我的頭上,";
		mes "把我從我的美夢中驚醒.";
		next;
		mes "[萍國]";
		mes "原來啊,我是在一棵蘋果樹下面睡覺,";
		mes "一顆蘋果落到了我頭上.";
		mes "我那時候真的餓死,準備就要去吃蘋果的時候...";
		next;
		mes "[萍國]";
		mes "那時候我挖礦時候結識的的老朋友凱恩,";
		mes "叫我去飛空艇上幫忙.";
		mes "所以我就去了,我發現有些人在玩色子遊戲.";
		next;
		mes "[萍國]";
		mes "我實在是太無聊了,又覺得這東西很新奇,";
		mes "就拿了我唯一的蘋果做賭注.";
		mes "誒,還別說,好像有人給我唱了幸運歌一樣,";
		mes "一個蘋果變成了兩個...";
		next;
		mes "[萍國]";
		mes "兩個變成了四個...";
		mes "在我意識到到底發生了什麼值錢,";
		mes "我擁有的蘋果數量達到了可以開商店的地步了!";
		mes "所以我就在飛空艇上開展了我自己的生意.";
		mes "哈,很詭異吧?";
		next;
		mes "[萍國]";
		mes "蘋果對你身體有好處的.";
		mes "他們也幫了我大忙,哈哈哈哈~!";
		close;
	case 4:
		mes "[萍國]";
		mes "謝謝拜訪了我的小店.";
		mes "拜拜~";
		close;
	}
}

//============================================================ 
//= International Airship Route
//= ... -> Izlude -> Juno -> Rachel -> ...
//============================================================ 

airplane_01,243,73,0	script	#AirshipWarp-3	45,1,1,{
	end;
	
OnTouch:
	switch ($@airplanelocation2) {
	case 0: warp "ra_fild12",292,204; end;
	case 1: 
		if(checkre(0))
			warp "izlude",200,73;
		else
			warp "izlude",200,56;
		end;
	case 2: warp "yuno",12,261; end;
	}

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;
	
OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;
}

airplane_01,243,29,0	duplicate(#AirshipWarp-3)	#AirshipWarp-4	45,1,1

airplane_01,1,1,0	script	International_Airship	-1,{
	end;
OnInit:
OnEnable:
	initnpctimer;
	end;
OnTimer25000:
	mapannounce "airplane_01","目前本飛空艇正在飛往依斯魯得島的途中。",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane_01","等一下飛空艇就要降落在依斯魯得島了。",bc_map,"0x00ff00";
	end;
OnTimer60000:
	set $@airplanelocation2,1;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","歡迎來到依斯魯得島，祝你能有一個愉快的旅程。",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane_01","我們目前正停靠在依斯魯得島，過會兒即將要出發啦。",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","飛空艇已經起飛了，下一個目的地是朱諾。",bc_map,"0x70dbdb";
	end;
OnTimer105000:
	mapannounce "airplane_01","目前本飛空艇正在飛往朱諾的途中。",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane_01","等一下飛空艇就要降落在朱諾了。",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	set $@airplanelocation2,2;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","歡迎來到朱諾，祝你能有一個愉快的旅程。",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane_01","我們目前正停靠在朱諾，過會兒即將要出發啦。",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","飛空艇已經離開了地面。我們下一個目的地是拉赫。",bc_map,"0xFF8200";
	end;
OnTimer185000:
	mapannounce "airplane_01","目前本飛空艇正在飛往拉赫的途中。",bc_map,"0xFF8200";
	end;
OnTimer210000:
	mapannounce "airplane_01","等一下飛空艇就要降落在拉赫了。",bc_map,"0xFF8200";
	end;
OnTimer220000:
	set $@airplanelocation2,0;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","歡迎來到拉赫，祝你能有一個愉快的旅程。",bc_map,"0xFF8200";
	end;
OnTimer230000:
	mapannounce "airplane_01","我們目前正停靠在拉赫，過會兒即將要出發啦。",bc_map,"0xFF8200";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","飛空艇已經起飛了，下一個目的地是依斯魯得島。",bc_map,"0x00ff00";
	stopnpctimer;
	set .moninv, .moninv + 1;
	if (.moninv == 7) {
		if (rand(1,3) == 3) {
			donpcevent "Airship#airplane02::OnEnable";
			end;
		}
		set .moninv, 0;
	}
	initnpctimer;
	end;
}

airplane_01,240,64,5	script	出口#airplane_011a::ExitAirplane01	857,{
	end;
}
airplane_01,247,64,5	duplicate(ExitAirplane01)	出口#airplane_011b	857
airplane_01,240,40,1	duplicate(ExitAirplane01)	出口#airplane_012a	857
airplane_01,247,40,1	duplicate(ExitAirplane01)	出口#airplane_012b	857

airplane_01,250,58,2	script	飛空艇職員#airplane01	67,{
	mes "[飛空艇職員]";
	mes "歡迎來到飛空艇上.";
	mes "請問要幫什麼忙嗎.";
	next;
	switch (select("這個飛空艇到底怎麼用呢:船長室在哪裡:內部設施介紹:取消")) {
	case 1:
		mes "[飛空艇職員]";
		mes "當您聽到我們已經抵達你的目的地這個消息的時候,";
		mes "請利用飛艇北部或者南部的出口離開飛艇就可以了.";
		next;
		mes "[飛空艇職員]";
		mes "如果您不小心錯過了也別急,";
		mes "飛艇是不斷循環著飛的,";
		mes "請等到下一次抵達您想去的目的地之後再下車吧.";
		close;
	case 2:
		mes "[飛空艇職員]";
		mes "船長室就在飛艇的前部.";
		mes "您可以和飛艇船長和飛行員去打個招呼.";
		close;
	case 3:
		mes "[飛空艇職員]";
		mes "飛艇上為各位乘客提供了各種迷你娛樂遊戲.";
		mes "您可以試試手氣,練練手法,玩玩看這些小遊戲~";
		close;
	case 4:
		mes "[飛空艇職員]";
		mes "那麼,希望您在我們飛艇上的旅途愉快.";
		mes "謝謝您選擇了我們的服務.";
		mes "祝您有個美好的一天.";
		close;
	}
}

airplane_01,50,66,5	script	蘋果商人#air01	86,{
	if (checkweight(1201,1) == 0) {
		mes "- 稍等啊 !! -";
		mes "- 你帶了好多東西誒 -";
		mes "- 請先減輕下重量 -";
		mes "- 再來買蘋果吧. -";
		close;
	}
	mes "[梅爾滋]";
	mes "歡迎來到梅爾滋小店.";
	mes "在這裡你可以買蘋果,也可以把蘋果榨汁";
	next;
	switch (select("買蘋果.:榨蘋果汁.:你在這裡做什麼?:取消.")) {
	case 1:
		mes "[梅爾滋]";
                mes "輸入你想要購買的數量吧.";
		mes "單價 15 zeny ,一次最多購買500個.";
		mes "不想要就輸入0就可以了.";
		next;
		while (1) {
			input .@input;
			set .@pay, .@input * 15;
			if (.@input == 0) {
				mes "[梅爾滋]";
				mes "謝謝關注我的小店,";
				mes "拜拜!";
				mes "當你覺得你想要啃蘋果的時候,";
				mes "就來我梅爾滋的小店吧~";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[梅爾滋]";
				mes "你輸入了超過500的數量喔.";
				mes "請重新輸入你想要蘋果的數量.";
				next;
			}
			else {
				mes "[梅爾滋]";
				mes "一共 ^FF0000" + .@input + "^000000 個蘋果";
				mes "花費 ^FF0000" + .@pay + " Zeny^000000 zeny.";
				mes "願意繼續嗎?";
				next;
				if (select("是的:不要") == 2) {
					mes "[梅爾滋]";
					mes "謝謝惠顧喔";
					mes "拜拜!";
					mes "當你覺得你想要啃蘋果的時候,";
					mes "就來我梅爾滋的小店吧~";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[梅爾滋]";
			mes "抱歉,但是你沒有帶來買這些蘋果要的錢呢.";
			mes "檢查一下你的錢包,少買點吧.";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[梅爾滋]";
			mes "嗯,好吧.你包包裡沒有足夠的位子了.";
			mes "先清理一下你的包包再來吧.";
			close;
		}
		else {
			set Zeny, Zeny - .@pay;
			getitem 512,.@input; //Apple
			mes "[梅爾滋]";
			mes "謝謝惠顧喔~";
			mes "希望";
			mes "你可以好好享受這些蘋果的美味~!";
			close;
		}
	case 2:
		mes "[梅爾滋]";
		mes "好的,我需要";
		mes "^FF00003 個蘋果 和 1 空瓶^000000";
		mes "如果你想要做一瓶蘋果汁的話.";
		mes "想要繼續嗎?";
		next;
		switch (select("是的:不要")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[梅爾滋]";
				mes "啊,你沒帶夠做蘋果汁的材料";
				mes "記住哦 3 個蘋果,一個空瓶.";
				close;
			}
			else {
				mes "[梅爾滋]";
				mes "好的,";
				mes "稍等";
				next;
				mes "^3355FF*磨啊磨啊*";
				mes "*磨啊磨啊*";
				mes "*鈳琅...!*^000000";
				next;
				delitem 512,3; //Apple
				delitem 713,1; //Empty_Bottle
				getitem 531,1; //Apple_Juice
				mes "[梅爾滋]";
				mes "嘿,給你~";
				mes "歡迎再次光臨!";
				close;
			}
		case 2:
			mes "[梅爾滋]";
			mes "謝謝關注我的小店~";
			mes "揮揮!";
			mes "想要啃蘋果就來找我呦~";
			close;
		}
	case 3:
		mes "[梅爾滋]";
		mes "謝謝關注我的小店~";
		mes "揮揮!";
		mes "想要啃蘋果就來找我呦~";
		close;
	}
}

airplane_01,221,158,2	script	飛行員#airplane_01	852,{
	switch (rand(1,4)) {
	case 1:
		mes "[飛行員]";
		mes "東經, 131 °.";
		mes "北緯, 37 °.";
		mes "船長！我們的方向正常！.";
		close;
	case 2:
		mes "[飛行員]";
		mes "看上去真是一個多雲的日子呢。";
		mes "唉，這種日子要想導航還是有些麻煩的。";
		mes "我們要需要放大雷達.";
		close;
	case 3:
		mes "[飛行員]";
		mes "船長是個好人,";
		mes "再沒有比他更加適合來指揮這艘飛空艇的人選了.";
		mes "他也很嚴酷,是一個很努力工作的駕駛員呢.";
		next;
		mes "[^ff0000嚴厲批評^000000]";
		mes "^ff0000餵...!^000000";
		mes "^ff0000少閒扯了^000000";
		mes "^ff0000多放點精力在導航上吧!^000000";
		next;
		mes "[飛行員]";
		mes "是-是的,先生!";
		mes "(明白了沒?)";
		close;
	default:
		mes "[飛行員]";
		mes "這件制服真的很炫,";
		mes "但是在整個路上都穿著的話,還是太重了點.";
		next;
		mes "[飛行員]";
		mes "...";
		mes "......";
		mes "沒什麼人進來過船長室的.";
		mes "我們的船長 '是' 一頭麋鹿啊..";
		mes "我連裝運工什麼樣子都很好奇呢.";
		next;
		emotion ET_HUK;
		mes "[飛行員]";
		mes "哦...! 啊哈哈啊哈哈! 那邊的!";
		mes "你的旅-旅-旅途還好吧?!";
		close;
	}
}

airplane_01,83,61,2	script	戴安妮#01airplane_01	72,2,2,{
	mes "[戴安妮]";
	mes "真是奇怪呢!";
	mes "我去船長室，";
	mes "但是只看到了一頭奇怪的麋鹿，";
	mes "哦！你說說這件事情奇怪不...";
	close;

OnTouch:
	emotion ET_CRY;
	end;
}

airplane_01,69,63,2	script	曼鐸兒#01airplane_01	55,{
	mes "[曼鐸兒]";
	mes "就像我所預計的一樣,";
	mes "機內提供的事物頂多也就是個三星級.";
	mes "*唉* 真的該把我的廚師帶來的,這樣我才可以好好享受一下呢.";
	close;
}

airplane_01,71,31,2	script	劍士石穆茲#air_01	106,{
	mes "[劍士石穆茲]";
	mes "在等待了五年之後...";
	mes "我終於迎來了我的復仇!";
	next;
	mes "[劍士石穆茲]";
	mes "我只是不知道怎麼確認我沒有落下站點.";
	mes "但是很快的,很快的,";
	mes "我的復仇就要開始了!";
	close;
}

//============================================================ 
//= Typing Challenge
//============================================================ 
airplane_01,32,61,4	script	尼爾斯#ein	49,1,1,{
	mes "[尼爾斯]";
	mes "歡迎來到";
	mes "^ff0000仙境傳說 瘋狂打字遊戲^000000.";
	mes "你想來玩一場迅速的,瘋狂的打字遊戲嗎?";
	next;
	switch (select("玩一場 ^ff0000仙境傳說 瘋狂打字遊戲^000000吧:我想再知道一些信息:查詢最高紀錄:取消")) {
	case 1:
		mes "[尼爾斯]";
		mes "好勒！我們迎來了新的一位挑戰者！";
		mes "用你的最大速度輸入這些字母吧！不要犯錯哦！";
		mes "開始吧~!";
		setarray .@line1_1$[0],	"^3cbcbccallipygian salacius lascivious^000000",
					"^3cbcbcBy the power of^000000",
					"^0000ffthkelfkskeldmsiejdlslehfndkelsheidl^000000",
					"^3cbcbcburrdingdingdingdilidingdingdingphoohudaamb^000000",
					"^3cbcbcCoboman no chikara-yumei na^000000",
					"^3cbcbcI'm the king of All Weirdos! Now^000000",
					"^3cbcbcYou give me no choice. I guess it's^000000";
		setarray .@line1_2$[0],	"^3cbcbclicentious prurient concupiscent^000000",
					"^3cbcbcp-po-poi-po-poi-poin-poing^000000",
					"^3cbcbcskemd^000000",
					"^3cbcbcandoorabambarambambambambamburanbamding^000000",
					"^3cbcbcchikara-daiookii na chikara da ze!^000000",
					"^3cbcbcyou know of my true power. Obey~!^000000",
					"^3cbcbctime for me to reveal my secret...^000000";
		setarray .@line1_3$[0],	"",
					"^3cbcbcGOD-POING. I NEVER LOSE!^000000",
					"",
					"",
					"^3cbcbcCOBO ON^000000",
					"",
					"";
		setarray .@word1$[0],	"callipygian salacius lascivious licentious prurient concupiscent",
					"By the power of p-po-poi-po-poi-poin-poing GOD-POING. I NEVER LOSE!",
					"thkelfkskeldmsiejdlslehfndkelsheidlskemd",
					"burrdingdingdingdilidingdingdingphoohudaambandoorabambarambambambambamburanbamding",
					"Coboman no chikara-yumei na chikara-daiookii na chikara da ze! COBO ON",
					"I'm the king of All Weirdos! Now you know of my true power. Obey~!",
					"You give me no choice. I guess it's time for me to reveal my secret...";
		setarray .@line2_1$[0],	"^3cbcbcuNflAPPaBLe LoVaBLe SeCreTs AnD^000000",
					"^ff1493LiGhTsPeEd RiGhT SPEed LeFT TURn^000000",
					"^ff1493hfjdkeldjsieldjshfjdjeiskdlefvbd^000000",
					"^ff1493burapaphuralanderamduanbatuhiwooi^000000",
					"^ff1493belief love luck grimace sweat rush^000000",
					"^800080opeN, Open!op3n.openOpen0p3nOpEn0pen^000000",
					"^3cbcbcfReeDoM ecstAcy JoUrnaliSm ArMplt^000000";
		setarray .@line2_2$[0],	"^3cbcbcboWLIiNg aGaINST tHe KarMA of YoUtH^000000",
					"^ff1493RiGhT BuRn OrIGInAL GaNgSteR SmACk^000000",
					"",
					"^ff1493kabamturubamdingding^000000",
					"^ff1493folktale rodimus optimus bumblebee^000000",
					"^800080`open'0Pen open? open!111OPENSESAME^000000",
					"^3cbcbcDisCoverY hEaDaChE MoonbeAmS jUsTiCE^000000";
		setarray .@word2$[0],	"uNflAPPaBLe LoVaBLe SeCreTs AnD boWLIiNg aGaINST tHe KarMA of YoUtH",
					"LiGhTsPeEd RiGhT SPEed LeFT TURn RiGhT BuRn OrIGInAL GaNgSteR SmACk",
					"hfjdkeldjsieldjshfjdjeiskdlefvbd",
					"burapaphuralanderamduanbatuhiwooikabamturubamdingding",
					"belief love luck grimace sweat rush folktale rodimus optimus bumblebee",
					"opeN, Open!op3n.openOpen0p3nOpEn0pen`open'0Pen open? open!111OPENSESAME",
					"fReeDoM ecstAcy JoUrnaliSm ArMplt DisCoverY hEaDaChE MoonbeAmS jUsTiCE";
		setarray .@letters[0],	1300,
					1250,
					1180,
					1380,
					1740,
					1440,
					1450;
		set .@wordtest, rand(7);
		next;
		mes "[Nils]";
		mes .@line1_1$[.@wordtest];
		mes .@line1_2$[.@wordtest];
		mes .@line1_3$[.@wordtest];
		set .@start_time, gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		next;
		input .@save1$;
		set .@end_time, gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		set .@total_time, .@end_time - .@start_time;
		mes "[Nils]";
		mes .@line2_1$[.@wordtest];
		mes .@line2_2$[.@wordtest];
		set .@start_time, gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		next;
		input .@save2$;
		set .@end_time, gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		set .@total_time, .@total_time + (.@start_time - .@end_time);
		set .@tasoo, (.@letters[.@wordtest] / .@total_time) * 6;
		if ((.@save1$ == .@word1$[.@wordtest]) && (.@save2$ == .@word2$[.@wordtest])) {
			mes "[尼爾斯]";
			mes "你的分數是 ^ff0000" + .@total_time + " 秒^000000 然後";
			mes "一共的分數是 " + .@tasoo + ".";
			next;
			if (.@tasoo >= 1300) {
				mes "[尼爾斯]";
				mes "哼...";
				mes "這個分數...";
				mes "如果你是人的話，那麼一定用了複製粘貼法.";
				mes "下次請記得不許作弊.";
				close;
			}
			if (.@tasoo >= $050320_ein_typing) {
				mes "[尼爾斯]";
				mes "前一項紀錄是";
				mes "由 ^0000ff" + $050320_minus1_typing$ + "^000000創造的";
				mes "一共分數是 ^0000ff" + $050320_ein_typing + "^000000 .";
				mes "但是, ^ff0000" + strcharinfo(0) + "^000000,";
				mes "你創造了歷史新高！";
				mes "恭喜恭喜啊!";
				set $050320_minus1_typing$, strcharinfo(0);
				set $050320_ein_typing, .@tasoo;
				close;
			}
			else {
				mes "[尼爾斯]";
				mes "^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "就是當前的記錄持有者.";
				mes "一共分數為 ^0000ff" + $050320_ein_typing + "^000000";
				mes "加油！下次要超過他啊~";
				close;
			}
		}
		else {
			mes "[尼爾斯]";
			mes "哦...";
			mes "抱歉，你輸入的文本錯誤...";
			close;
		}
	case 2:
		mes "[尼爾斯]";
		mes "這個 ^ff0000仙境傳說瘋狂打字遊戲^000000";
		mes "是一個要求你儘快輸入字母的遊戲.";
		mes "記錄持有者的名字將會被所有的後來者所知曉.";
		mes "想要成名嗎?機會來了!";
		next;
		mes "[尼爾斯]";
		mes "我想請你明白,你看見每個單行之後,";
		mes "要把你所看到的所有東西都打下來,";
		mes "所以不要按ENTER鍵,請點擊OK就可以.";
		close;
	case 3:
		mes "[尼爾斯]";
		mes "^0000ff" + $050320_minus1_typing$ + "^000000";
		mes "是當前的記錄持有者.";
		mes "他的總分數為 ^0000ff" + $050320_ein_typing + "^000000";
		mes "加油！下次要超過他啊~";
		close;
	case 4:
		mes "[尼爾斯]";
		mes "啊,放輕鬆,";
		mes "^ff0000仙境傳說瘋狂打字遊戲^000000";
		mes "什麼時候都可以玩的.我在這裡等著你呦~";
		close;
	}
}

//============================================================ 
//= Apple Gambling
//============================================================ 
airplane_01,33,68,4	script	克萊雷斯	74,{
	mes "[克萊雷斯]";
	mes "你好,我是克萊雷斯~";
	mes "你想要在這場友好的骰子遊戲中下些蘋果賭注嗎?";
	next;
	callfunc "applegamble","Clarice";
	end;
}

function	script	applegamble	{

	switch (select("賭博!:怎麼賭呢?:不要!")) {
	case 3:
		mes "["+getarg(0)+"]";
		mes "你什麼時候想玩都可以來找我.";
		mes "有了想要來賭一場的衝動,";
		mes "就來找我好了.";
		close;
	case 2:
		mes "["+getarg(0)+"]";
		mes "規則超簡單的.";
		mes "首先,下注.";
		mes "一次最多下注50個蘋果.";
		mes "為了讓這些見不得光的賭博玩意兒合法一點,";
		mes "只接受蘋果喔~";
		next;
		mes "["+getarg(0)+"]";
		mes "但是你想要買蘋果的話,";
		mes "去萍國地方就可以,隨你買多少都可以啊~";
		next;
		mes "["+getarg(0)+"]";
		mes "現在,我開始投擲2個六面骰子.";
		mes "然後你來,你也來一個2D6.";
		mes "在那之後,我們都會有選擇:是否再投擲一次骰子";
		next;
		mes "["+getarg(0)+"]";
		mes "下面的東西...請注意喔,很重要的.";
		mes "如果你的投擲總數大於12,那麼你就爆了,也就是輸了.";
		mes "然後,投擲數最高的人就是贏家.明白了沒?";
		next;
		mes "["+getarg(0)+"]";
		mes "現在,你先來決定你是否投擲第三次骰子吧.";
		mes "然後,根據你的結果,我會看看我是否有投擲第三次的必要.";
		next;
		mes "["+getarg(0)+"]";
		mes "你贏了的話,你會得到你下注兩倍的蘋果.";
		mes "但是如果我們平手的話,你所下注的蘋果就會被歸還.";
		mes "還算公平,對吧?.";
		close;
	case 1:
		break;
	}
	mes "["+getarg(0)+"]";
	mes "哦,你來玩嗎>-<棒耶!";
	mes "那麼你想要下注多少蘋果呢?一次最高50個喔.";
	mes "想要取消的話輸入 0 就可以";
	next;
	while(1) {
		input .@amount;
		if (.@amount == 0) {
			mes "["+getarg(0)+"]";
			mes "哦,變了主意了?";
			mes "啊,那麼希望可以有時間再一起玩吧.";
			close;
		}
		else if (.@amount < 1 || .@amount > 50) {
			mes "["+getarg(0)+"]";
			mes "不能下超過50的注!";
			mes "我們的注金必須要合理,";
			mes "請不要輸入超過50的數量.";
			next;
			continue;
		}
		mes "["+getarg(0)+"]";
		mes "那麼";
		mes "下注蘋果數目為 ^FF0000"+.@amount+"^000000 .";
		mes "確認一下,對不對?";
		next;
		if (select("嗯哪:不...") == 2) {
			mes "["+getarg(0)+"]";
			mes "嗯,啊?不對呢?";
			mes "那麼,輸入你想要下注的數目吧.";
			mes "想要取消的話輸入 0 就可以.";
			next;
			continue;
		}
		if (countitem(512) <.@amount) {
			mes "抱歉,但是...你的蘋果沒帶夠哦.";
			mes "不能下注又怎麼可以賭博呢,對吧?";
			next;
			continue;
		}
		delitem 512,.@amount;
		mes "["+getarg(0)+"]";
		mes "棒耶!烏拉!";
		mes "終於可以開始玩遊戲啦!";
		mes "啦啦啦啦,我先來投~";
		break;
	}
	mes "^3355FF*Rolling and rumbling*^000000";
	next;
	set .@giveapple, .@amount*2;
	set .@table1, rand(1,6);
	set .@table2, rand(1,6);
	set .@tablesub, .@table1 + .@table2;
	set .@tabletotal, .@tablesub;
	mes "["+getarg(0)+"]";
	mes "我是 ^0000FF" + .@table1 + "^000000 還有 ^0000FF" + .@table2 + "^000000.";
	mes "一共就是 ^0000FF" + .@tablesub + "^000000.";
	mes "^FF0000" + strcharinfo(0) + "^000000, 該你了.";
	next;
	select("菠蘿菠蘿蜜！真投擲·骰子！");
	mes "^3355FF*Rolling and rumbling*^000000";
	set .@player1, rand(1,6);
	set .@player2, rand(1,6);
	set .@playersub, .@player1 + .@player2;
	if (.@playersub > 9 && .@amount > 39) {
		set .@player1, rand(1,6);
		set .@player2, rand(1,6);
		set .@playersub, .@player1 + .@player2;
	}
	set .@playertotal, .@playersub;
	next;
	mes "["+getarg(0)+"]";
	mes "^FF0000" + strcharinfo(0) + "^000000, 你是 ^FF0000" + .@player1 + "^000000 還有 ^FF0000" + .@player2 + "^000000. 加起來一共就是 ^FF0000" + .@playersub + "^000000 .";
	next;
	mes "["+getarg(0)+"]";
	if(.@playersub == .@tablesub) {
		mes "現在我的總數是 ^0000FF" + .@tablesub + "^000000 然後 ^FF0000" + strcharinfo(0) + "^000000, 你的總數是 ^FF0000" + .@playersub + "^000000. 打平咯. 還想來一局嗎?";
	} else if (.@playersub > .@tablesub) {
		mes "現在我的總數是 ^0000FF" + .@tablesub + "^000000 然後 ^FF0000" + strcharinfo(0) + "^000000, 你的總數是 ^FF0000" + .@playersub + "^000000. ^FF0000" + strcharinfo(0) + "^000000, 你贏咯. 還想再來一局嗎?";
	} else if(.@tablesub > .@playersub) {
		mes "現在我的總數是 ^0000FF" + .@tablesub + "^000000 然後 ^FF0000" + strcharinfo(0) + "^000000, 你的總數是 ^FF0000" + .@playersub + "^000000. 我贏了. 還想再來玩一局嗎?";
	}
	next;
	switch (select("投擲骰子.:取消.")) {
	case 1:
		mes "^3355FF*Rolling and rumbling*^000000";
		set .@player3, rand(1,6);
		set .@playertotal, .@playertotal + .@player3;
		next;
		mes "["+getarg(0)+"]";
		if (.@playertotal > 12) {
			mes "^FF0000" + strcharinfo(0) + "^000000, 你得到了 ^FF0000" + .@player3 + "^000000 現在的總數是 ^FF0000" + .@playertotal + "^000000. 你輸咯. 抱歉...再來一次吧~好不好~";
			close;
		}
		else if (.@playertotal < .@tablesub) {
			mes "^FF0000" + strcharinfo(0) + "^000000, 你是 ^FF0000" + .@player3 + "^000000 現在總數是 ^FF0000" + .@playertotal + "^000000. 雖然你投了第三次，還是比我低呢...那我也沒有投擲第三次的必要咯,你輸啦...不好意思啊，再來試試手氣如何.";
			close;
		}
		else if (.@playertotal == .@tablesub) {
			if (.@tablesub > 8) {
				mes "^FF0000" + strcharinfo(0) + "^000000, 你是 ^FF0000" + .@player3 + "^000000 現在總數是 ^FF0000" + .@playertotal + "^000000. 我不想冒什麼風險...平局吧. 再來一次好吧~";
				close2;
				getitem 512,.@amount; //Apple
				end;
			}
		}
		else {
			mes "^FF0000" + strcharinfo(0) + "^000000, 你是 ^FF0000" + .@player3 + "^000000 現在總數是 ^FF0000" + .@playertotal + "^000000. 輪我了.";
		}
		break;
	case 2:
		mes "["+getarg(0)+"]";
		if (.@playersub > .@tablesub) {
			mes "我看看，你不想冒風險啊.那麼我就投一次骰子吧.";
		}
		else if (.@playersub == .@tablesub) {
			if (.@tablesub > 8) {
				mes "我看看，你不想冒風險啊.我也不想呢,那麼,平局. 下次再來火拚一次啦~";
				close2;
				getitem 512,.@amount; //Apple
				end;
			}
			mes "好吧.";
			mes "我再投一次骰子.";
		}
		else {
			mes "有時候嘗試的回報很豐厚呢...這次我贏啦..";
			mes "嘻嘻,別傷心啊,下回再來一起玩嘛..";
			close;
		}
		break;
	}
	next;
	mes "^3355FF*Rolling and rumbling*^000000";
	set .@table3, rand(1,6);
	set .@tabletotal, .@tabletotal + .@table3;
	next;
	mes "["+getarg(0)+"]";
	if (.@tabletotal > 12) {
		mes "我是 ^0000FF" + .@table3 + "^000000 現在的總數為 ^0000FF" + .@tabletotal + "^000000. 我輸了,因為我的總數超過了12. 那麼,我的蘋果歸你咯.不錯哦~這局很有意思呢.";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal > .@tabletotal) {
		mes "我得到了 ^0000FF" + .@table3 + "^000000 現在的總數為 ^0000FF" + .@tabletotal + "^000000. 你好像是一共 ^FF0000" + .@playertotal + "^000000 ,阿喻,你贏了, ^FF0000" + strcharinfo(0) + "^000000. 我的蘋果歸你了.啊,真是有趣,再來一次吧.";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal == .@tabletotal) {
		mes "我得到了 ^0000FF" + .@table3 + "^000000 現在的總數是 ^0000FF" + .@tabletotal + "^000000. 一共 ^FF0000" + .@playertotal + "^000000 那麼我們打成了平手呢, ^FF0000" + strcharinfo(0) + "^000000. 呼呼,接好了您的蘋果吶!下次再一起來玩吧~真是好期待呦~.";
		close2;
		getitem 512,.@amount; //Apple
		end;
	}
	else if (.@playertotal < .@tabletotal) {
		mes "哇哦,我是 ^0000FF" + .@table3 + "^000000 現在總數為 ^0000FF" + .@tabletotal + "^000000. 你是 ^FF0000" + .@playertotal + "^000000 ,哦,你輸啦, ^FF0000" + strcharinfo(0) + "^000000. 抱歉...但是再來一次也許會有很豐厚的回報呢.";
		close;
	}
}
