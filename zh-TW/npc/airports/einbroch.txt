//============================================================
//= rAthenaCN [飛空艇]漢化腳本 
//============================================================

//===== rAthena Script =======================================
//= Einbroch Airport Staff
//===== By: ================================================== 
//= L0ne_W0lf, Muad_Dib
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= Einbroch Airport Staff
//===== Additional Comments: ================================= 
//= 1.0 First version.
//============================================================ 

airport,143,43,5	script	機場職員#airport1a::airport1	90,{
	mes "[機場職員]";
	mes "歡迎來到艾音布羅克機場";
	mes "我們提供飛往朱諾,裡希塔樂和毀葛的航班.";
	mes "我們的飛空艇將不做間斷的飛向";
	mes "朱諾,裡希塔樂和毀葛.";
	next;
	if (select("搭乘飛空艇:取消") == 1) {
		mes "[機場職員]";
		mes "飛空艇搭乘費是 1,200 zeny,";
		mes "但是如果您有飛空艇免費搭乘券的話";
		mes "就可以取消這個費用.";
		mes "請問您是否要搭乘飛空艇呢？";
		next;
		if (select("是的:不要") == 1) {
			if (countitem(7311) > 0) {
				delitem 7311,1; //Free_Flying_Ship_Ticket
				warp "airport",148,51;
				end;
			}
			if (Zeny >= 1200) {
				set Zeny, Zeny - 1200;
				warp "airport",148,51;
				end;
			}
			mes "[機場職員]";
			mes "抱歉，但是您身上沒有免費搭乘券或是";
			mes " 1,200 zeny 的搭乘費用";
			mes "請記住我們的搭乘費用是 1,200 zeny.";
			close;
		}
	}
	mes "[機場職員]";
	mes "謝謝您的光臨.";
	mes "祝您有個美好的一天~";
	close;
}

airport,158,43,5	duplicate(airport1)	機場職員#airport1b	90
airport,126,43,5	duplicate(airport1)	機場職員#airport1c	90

airport,143,49,3	script	離站辦理職員#airport2a::airport2	90,{
	mes "[離站手續辦理職員]";
	mes "歡迎來到艾音布羅克機場.";
	mes "如果您從抵達此處的航班上剛下來,就由我來幫助您離站吧";
	mes "如果您是想要搭乘飛空艇的話,那麼就請搭乘飛空艇去";
	mes "朱諾,裡希塔樂或是毀葛吧.";
	next;
	if (select("離站.:取消.") == 1) {
		mes "[離站手續辦理職員]";
		mes "一旦您離站以後,如果還想要登機的話就要再付一次錢了.";
		mes "只有在您要去艾音布羅克的情況下才好在這裡離站";
		mes "那麼,請問您堅持要在朱諾這裡離站嗎？";
		next;
		if (select("是的:不要了") == 1) {
			warp "airport",142,40;
			end;
		}
	}
	mes "[離站手續辦理職員]";
	mes "謝謝您選擇了我們的服務,";
	mes "也祝您旅途愉快~";
	close;
}

airport,126,51,3	duplicate(airport2)	離站辦理職員#airport2b	90
airport,158,50,3	duplicate(airport2)	離站辦理職員#airport2c	90

einbroch,94,267,3	script	飛空艇職員#ein01	91,{
	mes "[飛空艇職員]";
	mes "歡迎來到艾音布羅克機場";
	mes "推入旁邊的門就可以搭乘飛空艇.";
	mes "本飛空艇將在";
	mes "修發茲共和國境內的艾音布羅克,裡希塔樂朱諾,裡希塔樂";
	mes "和毀葛做停靠.";
	next;
	mes "[飛空艇職員]";
	mes "如果艾音布羅克是您的航班終點的話";
	mes "樓梯下相關的離站辦理職員會幫助您辦理離站手續,,";
	mes "謝謝您選擇了我們的服務,祝您旅途愉快.";
	close;
}
