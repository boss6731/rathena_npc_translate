//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================

//===== rAthena Script ======================================= 
//= Bunny Band Quest
//===== By: ================================================== 
//= rAthena Dev Team
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Quest to obtain a Bunny Band headgear.
//===== Additional Comments: ================================= 
//= 1.5 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//	Changed @BUNYBAN back to BUNYBND, it should be this way.
//	Removed duplicates. NPC is ALberta-only.
//= 1.6 Cleaning. [Euphy]
//============================================================ 

alberta,26,229,0	script	卡普拉 職員#bunny	4_M_01,{
	if (BUNYBND == 1) {
		mes "[卡普拉 職員]";
		mes "那裡的朋友你好~!";
		mes "有沒有興趣";
		mes "參加我們卡普啦公司";
		mes "舉辦的特別活動";
		mes "^529DFF兔耳發圈製作活動^000000嗎?";
		next;
		switch(select("當然,我帶來了道具:活動信息:結束對話")) {
		case 1:
			mes "[卡普拉 職員]";
			mes "很好~ ";
			mes "麻煩你稍等一下,";
			mes "讓我檢查下你有沒帶齊";
			mes "所有的道具...";
			next;
			setarray .@items[0],949,100,706,1,722,1,2213,1;
			for(set .@i,0; .@i<8; set .@i,.@i+2)
				if (countitem(.@items[.@i]) < .@items[.@i+1]) {
					mes "[卡普拉 職員]";
					mes "啊,我很抱歉!";
					mes "材料好像不太對.";
					mes "你最少需要帶";
					mes .@items[.@i+1]+" 個"+getitemname(.@items[.@i])+"才可以啊~";
					close;
				}
			mes "[卡普拉 職員]";
			mes "很好,我已經看到了";
			mes "你的準備非常充分.";
			mes "來了足夠的道具.";
			mes "現在你只需.";
			mes "請稍等一下,";
			mes "我把它們組合在一起...";
			next;
			for(set .@i,0; .@i<8; set .@i,.@i+2)
				if (countitem(.@items[.@i]) < .@items[.@i+1]) {
					mes "[卡普拉 職員]";
					mes "嗯?我很遺憾,";
					mes "但是我真的不能";
					mes "馬上開始製作";
					mes "需要 "+.@items[.@i+1]+" "+getitemname(.@items[.@i])+((!.@i)?"個":"");
					mes "才能夠";
					mes "把兔耳發圈的材料組合在一起.";
					close;
				}
			delitem 949,100; //Feather
			delitem 706,1; //Four_Leaf_Clover
			delitem 722,1; //Scarlet_Jewel
			delitem 2213,1; //Cat_Hairband
			mes "[卡普拉 職員]";
			mes "啊,做好了~";
			mes "完美的兔耳發圈!";
			mes "我希望你能喜歡它.";
			getitem 2214,1; //Bunny_Band
			set BUNYBND,0;
			next;
			mes "[卡普拉 職員]";
			mes "感謝您參加這個特別的活動.";
			mes "希望您可以繼續使用我們的服務~";
			mes "請記住";
			mes "卡普拉公司永遠與您相伴.";
			mes "再見~";
			close;
		case 2:
			goto L_Info;
		case 3:
			close;
		}
	}
	mes "[卡普拉 職員]";
	mes "那裡的朋友你好~! 你想要";
	mes "參加由艾爾貝塔商人協會贊助的";
	mes "由卡普拉公司舉辦的";
	mes "特別的 ^529DFF兔耳發圈製作活動^000000 嗎?";
	mes "這可是個特別的活動哦!";
	next;
	switch(select("我想參加這個活動:活動信息:結束對話")) {
	case 1:
		set BUNYBND,1;
		mes "[卡普拉 職員]";
		mes "很好!";
		mes "感謝您的參與!";
		mes "如果你沒有聽清所有需要收集的道具,";
		mes "你是無法製作兔耳發圈的...";
		mes "我再重複一次,";
		mes "您記好了.";
		next;
		mes "[卡普拉 職員]";
		mes "如果想參加活動,你需要準備以下道具：";
		mes "100 個柔毛,";
		mes "1 個四葉幸運草,";
		mes "1 個貓耳發圈以及 1 個珍珠.";
		mes "我會在這裡等你";
		mes "祝你好運~";
		close;
	case 2:
		break;
	case 3:
		close;
	}
L_Info:
	mes "[卡普拉 職員]";
	mes "為了對各使用我們卡普拉服務的";
	mes "的冒險家表示感謝!";
	mes "卡普拉公司特別準備了";
	mes "這次活動.";
	mes "卡普拉職員們將會在冒險家的聚集地.";
	mes "等待冒險家帶來所需的道具.";
	next;
	mes "[卡普拉 職員]";
	mes "如果想參加活動,";
	mes "你需要準備以下道具：";
	mes "100 個柔毛,";
	mes "1 個四葉幸運草,";
	mes "1 個貓耳發圈,";
	mes "1 個珍珠.";
	next;
	mes "[卡普拉 職員]";
	mes "當你準備好時,";
	mes "請把那些道具拿給我,";
	mes "我會幫你製作兔耳發圈.";
	mes "特別感謝本次活動的贊助方:";
	mes "(艾爾貝塔商人協會)";
	close;
}

//============================================================
// Old changelog
//============================================================
//= 1.1 using duplicate command
//= 1.2 Fixed NPC location, removed NPC dupes [Lupus]
//= 1.3 RE-Fixed exploit V_V, also cleared used VAR [Lupus]
//= 1.4 replaced BUNYBND with @BUNYBND [Lupus]
//============================================================
