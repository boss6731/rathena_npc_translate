//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================

//===== rAthena Script ======================================= 
//= Quest NPCs related to Payon
//===== By: ================================================== 
//= kobra_k88
//===== Current Version: ===================================== 
//= 1.7
//===== Compatible With: ===================================== 
//= rAthena 7.15 +
//===== Description: ========================================= 
//= Pretend Murder, Ear Muffs, Oxygen, Derivuchi Cap, Helm of Angel Quests.
//===== Additional Comments: ================================= 
//= 1.1 Helm of Angel bug fixed [Lupus]
//= 1.2 Fixed skirt of virgin [Lupus]
//= 1.3 Fixed exploits [Lupus]
//= 1.4 Removed Duplicates [Silent]
//= 1.5 Updated Dialogs from "Pretend Murdered Quest" [Samuray22]
//= 1.5a replaced item "names" with item id. [Lupus]
//= 1.6 Updated Dialogs from "Oxygen Mask Quest" [Samuray22]
//= 1.7 Updated headgear quests. [L0ne_W0lf]
//============================================================ 

// 'Pretend Murdered' Quest
//============================================================
payon_in01,99,72,4	script	老太太	8_F_GRANDMOTHER,{
	if(countitem(1049) > 3 ) {
		mes "[老太太]";
		mes "我希望在下個月, 為我的孩子們縫製一套新衣服";
		mes "你能不能將你的^3355FF'韓服'^000000送給我?....我需要四件";
		mes "求求你了, 年輕人!";
		next;
		if(select( "沒問題:不行")==1) {
			delitem 1049,4;
			mes "[老太太]";
			mes "唉唷, 真是太感謝你了~我可以幫小孩子做韓服了~";
			mes "呵呵呵....啊!對了, 我年輕時候參加過話劇社團的演出....";
			mes "這是我結婚後離開劇團時帶出來的道具....";
			next;
			mes "[老太太]";
			mes "如果你不嫌棄的話...請收下吧!";
			mes "請慢走~";
			getitem 2293,1;
			close;
		}
		mes "[老太太]";
		mes "唉唷唉唷....我這樣一個老婦人要到哪去找^3355FF'韓服'^000000呢....";
		mes "這社會真是一點人情味都沒有啊....";
		close;
	}
	mes "[老太太]";
	mes "每天都一樣努力的上斐揚後山去摘香菇過活, 這個月的生活不知道又要怎麼過了";
	mes "....不過, 下個月就是節日了啊";
	mes "今年的過節小孩子們嚷嚷著一定要穿韓服, ";
	mes "要做韓服最少得要有四件^3355FF'韓服'^000000 啊....";
	next;
	mes "[老太太]";
	mes "年輕人, 我拜託你一件事情如果你在旅行的途中找到^0000FF'韓服'^000000 的話....請你一定要再來一趟";
	mes "雖然我沒有什麼東西可以給你, 但是你的恩惠我老人家一定不會忘記的....";
	close;
}

// 耳罩
payon_in01,18,10,4	script	神秘的姑娘	75,{
	mes "[神秘的姑娘]";
	mes "....";
	mes "我們家族歷代都在做寒冬中具有保暖功用的^3355FF'耳罩'^000000";
	mes "因為這附近都是屬於暖和的地區, 這裡的人幾乎都沒有體驗過寒冬, 加上一般人的收入只夠餬口, 負擔不起從其他地方支出的費用";
	mes "如果你打算到寒冷地區去的話, 你可以考慮讓我幫你做個^3355FF'耳罩'^000000讓你帶去....";
	next;
	mes "[神秘的姑娘]";
	mes "耳罩是我們家世待流傳的手制名品請帶^FF3355'詛咒紅寶石'^000000與^3311AA'耳機'^000000各一個來給我...";
	mes "另外還要準備^3355FF'柔毛 200根'^000000以及^DDDD00'5000 金幣'^000000, 我就幫你做";
	next;
	if (select("好的, 麻煩你了:謝謝, 不用了")==2) {
		mes "[神秘的姑娘]";
		mes "....";
		mes "如果有緣的話, 將來一定還有機會碰面的....";
		mes "....";
		close;
	}
	if(countitem(724)<1 || countitem(949)<200 || countitem(5001)<1 || Zeny<5000) {
		mes "[神秘的姑娘]";
		mes "....顧客, ";
		mes "你所準備的材料不足喔....";
		mes "我也想給你低廉的價格和合理的條件, 但是....";
		mes "我們也有困難....";
		close;
	}
	mes "[神秘的姑娘]";
	mes "....";
	mes "感謝您這是做好的耳罩, 請您收下...";
	delitem 724,1;
	delitem 949,200;
	delitem 5001,1;
	set Zeny, Zeny - 5000;
	getitem 2283,1;
	next;
	mes "[神秘的姑娘]";
	mes "....";
	mes "感謝您購買我們家所製造的物品, 您一定會滿意它的品質的...";
	close;
}

// 'Oxygen Mask' Quest
//============================================================
payon_in02,25,71,4	script	青年	4_M_04,{
	if(countitem(701) > 4) {
		mes "[青年]";
		mes "你說什麼! 啊..不行, 怎麼說都不行...";
		mes "哼.. 居然說那些傢伙很可愛... 絕對..不行";
		next;
		switch(select("給他看歐拉毆拉:給他歐拉毆拉:取消")) {
		case 1:
			mes "[青年]";
			mes "喔喔喔喔喔";
			mes "這個..是 歐拉歐拉...!";
			mes "我就長話短說吧! 你要是..把這個給我的話, 我就把我最珍貴的神秘寶物給你!";
			mes "如..如何啊?你不想要嗎?";
			close;
		case 2:
			delitem 701, 5;
			mes "[青年]";
			mes "呵呵呵呵呵..終於到手了! 歐拉毆拉!";
			mes "令人討厭的盜蟲,現在可以一掃而空了";
			next;
			mes "[青年]";
			mes "哈哈...哈哈...";
			mes "不好意思, 我太過興奮了.. 嗯.. 我就照約定把我說的那個神秘寶物給你吧!";
			next;
			getitem 5004,1;
			mes "[青年]";
			mes "把這個罩在嘴上深呼吸的話... 隨時隨地都可以呼吸到令人神輕氣爽的新鮮空氣";
			mes "以前我曾經到海邊工作一段時間, 這是那時候帶回來的";
			mes "這個現在我用不著了, 為了答謝你, 這個就送給你吧!";
			next;
			mes "[青年]";
			mes "呵呵呵呵呵! 就這樣吧!我得要趕快試用一下歐拉歐拉了!";
			close;
		case 3:
			mes "[青年]";
			mes "呵呵呵呵呵... 如果不能消除盜蟲的話...";
			mes "那我也無計可施了, 只好試著去喜歡盜蟲囉.. 呵呵呵呵呵...";
			close;
		}
	}
	mes "[青年]";
	mes "呃啊啊啊...真是.. 煩.. 煩死了.";
	mes "這些可恨的盜蟲";
	next;
	if( select( "繼續對話:取消") == 1) {
		mes "[青年]";
		mes "說起我的人生啊, 那真可以波瀾萬丈來形容";
		mes "我還小的時候父母親就去世了... 孤零零的我每天不眠不休打雜過活";
		next;
		mes "[青年]";
		mes "至今也已經十年了.. 現在我的手邊有一點存款...我夢想著能擁有一間屬於自己的房子";
		next;
		mes "[青年]";
		mes "但是我的存款還是不夠買一間比較像樣的房子...";
		mes "再加上.. 我現在已經有一間大卻不值錢的房子... 那是我不得已在沒有選擇的情況下買的...";
		next;
		mes "[青年]";
		mes "就是這樣, 所以整個房子幾乎都被該死的盜蟲給完全佔據作為巢穴了";
		next;
		mes "[青年]";
		mes "我也試過補殺它們..驅感它們.. 所有可以用的方法都用盡了..但是那也只是一時的方法, 不久後它們還是一樣會出現!";
		mes "我甚至還一度將自己辛辛苦苦好不容易買下的房子一把火給燒了!";
		next;
		mes "[青年]";
		mes "啊啊..我真的很想... 將它們消滅殆盡...是不是用了 ^3355FF歐拉歐拉^000000 就可以了呢?";
		mes "聽說這是在其他國家偶爾可以求得的相當珍貴稀少的東西";
		mes "如果有五個的話.. 我就可以完全消滅那些該死的鬼東西了...";
		close;
	}
	mes "[青年]";
	mes "怎麼殺也殺不死殺不盡...我已經感到....厭煩了";
	mes "我不管了..隨便吧...";
	close;
}

// 'Helm of Angel' and 'Deviruchi Cap' Quest
//============================================================
payon_in01,56,12,7	script	年輕人#12	4_M_ORIENT02,{
	mes "[年輕人]";
	mes "......有什麼事嗎?";
	next;
	mes "[年輕人]";
	mes "....你應該也大概聽過情況了吧..";
	mes "長話短說. 講重點吧.";
	next;
	mes "[年輕人]";
	mes "等一下!";
	mes "你準備的物品";
	mes "不管插著卡的, 冶煉的";
	next;
	mes "[年輕人]";
	mes "我都同一取代";
	mes "交給我的時候, 一定要注意.";
	mes "物品準備好了嗎? 拿給我吧.";
	next;
	switch (select("天使頭盔:小惡魔帽:下次再來找你")) {
	case1:
		mes "[年輕人]";
		mes "^3355FF金屬頭盔^000000 1 個(有洞)";
		mes "^3355FF天使發圈^000000 1 個.";
		mes "^3355FF卡倫之牙^000000 5 個.";
		next;
		mes "[年輕人]";
		mes "物品準備好了嗎? 拿給我吧.";
		next;
		if(countitem(2254) < 1 || countitem(2229) < 1 || countitem(7036) < 5) break;
		mes "[年輕人]";
		mes "拿去吧.. 這是你的天使頭盔.";
		delitem 2254,1;
		delitem 2229,1;
		delitem 7036,5;
		getitem 5025,1;
		close;
	case2:
		mes "[年輕人]";
		mes "^3355FF小惡魔角^000000 600 個.";
		mes "^3355FF獅鷲獸的指甲^000000 40 個.";
		next;
		mes "[年輕人]";
		mes "物品準備好了嗎? 拿給我吧.";
		next;
		if(countitem(1038) < 600 || countitem(7048) < 40) break;
		mes "[年輕人]";
		mes "拿去吧.. 這是你的小惡魔帽子.";
		delitem 1038,600;
		delitem 7048,40;
		getitem 5038,1;
		close;
	case3:
		mes "[年輕人]";
		mes "...... 慢走啊.";
		close;
	}
	mes "[年輕人]";
	mes "..... 物品數量不對啊.";
	mes "確認一下再來吧.";
	close;
}
