//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================

//===== rAthena Script =======================================
//= Eden Group Quests - Mission [26 - 40]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 26 - 40.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//============================================================

moc_para01,38,38,3	script	任務 [26 - 40]	857,{
	if (countitem(6219) < 1) {
		mes "你還不是會員，不能接受任務！";
		close;
	}
	mes "[26 ~ 40級 任務布告欄]";
	mes "- 所有任務均在布告欄中 -";
	mes "- 請選擇 -";
	next;
	if ((BaseLevel < 26) || (BaseLevel > 40)) {
		mes "你的級別還沒有達到要求。";
		close;
	}
	switch(select("狩獵任務:配送任務:收集任務:護送任務")) {
	case 1:
		mes "[狩獵任務]";
		mes "- 想要查看具體任務內容麼~ -";
		mes "- 請繼續 -";
		next;
		switch(select("狩獵毒魔菇:狩獵狸貓:狩獵長老樹精(Lv.30以上):狩獵松鼠(Lv.35以上):狩獵達拉蛙(Lv.38以上)")) {
			case 1: callsub L_Quest,8266,0,"狩獵毒魔菇","斯波尼亞","吉芬","狩獵10隻毒魔菇";
			case 2: callsub L_Quest,8267,0,"狩獵狸貓","斯波尼亞","吉芬","狩獵10隻狸貓";
			case 3: callsub L_Quest,8268,30,"狩獵長老樹精","斯波尼亞","吉芬","狩獵10隻長老樹精";
			case 4: callsub L_Quest,8269,35,"狩獵松鼠","斯波尼亞","吉芬","狩獵10隻松鼠";
			case 5: callsub L_Quest,8270,38,"狩獵達拉蛙","斯波尼亞","吉芬","狩獵10隻達拉蛙";
		}
	case 2:
		mes "[配送任務]";
		mes "- 查看配送任務明細 -";
		mes "- 請繼續 -";
		next;
		switch(select("配送殺蟲劑:配送蜂蜜:配送毛毯(Lv.35以上)")) {
			case 1: callsub L_Quest,8271,0,"配送殺蟲劑","迪斯信 曼","夢羅克金字塔（Morroc Ruins）","從迪斯信 曼處取得殺蟲劑，然後交給位於斐揚東南部原野的動物學家。";
			case 2: callsub L_Quest,8272,0,"配送蜂蜜","迪斯信 曼","夢羅克金字塔（Morroc Ruins）","從迪斯信 曼處取得蜂蜜，送到位於聖·卡普託利納修道院西部地圖，即普隆德拉東北部地區的昆蟲學家那兒。";
			case 3: callsub L_Quest,8273,35,"配送毛毯","迪斯信 曼","夢羅克金字塔（Morroc Ruins）","從迪斯信 曼處取得毛毯，送到薑餅城南部的生物學家手中。";
		}
	case 3:
		mes "[收集任務]";
		mes "- 查看收集任務明細 -";
		mes "- 請繼續 -";
		next;
		switch(select("收集遺骨:收集動物外皮:收集外殼(Lv.30以上):收集尾巴(Lv.32以上):收集餅乾(Lv.35以上):收集鬍鬚(Lv.35以上)")) {
			case 1: callsub L_Quest,8274,0,"收集遺骨","大夏","斐揚","收集'10個死人遺骨'後交給大夏";
			case 2: callsub L_Quest,8275,0,"收集動物外皮","大夏","斐揚","收集'10隻熊掌'和'5塊動物外皮'後交給大夏";
			case 3: callsub L_Quest,8276,30,"收集外殼","大夏","斐揚","收集'20個昆蟲外殼'和'20個加勒結晶'後交給大夏";
			case 4: callsub L_Quest,8277,32,"收集尾巴","大夏","斐揚","收集'10個猴子尾巴', '10個橡實' 以及 '10個變身葉子'後交給大夏";
			case 5: callsub L_Quest,8278,35,"收集餅乾","大夏","斐揚","收集'5個薑餅'和'1塊小蛋糕'後交給大夏";
			case 6: callsub L_Quest,8279,35,"收集鬍鬚","大夏","斐揚","收集'5個鼠須'和'3個鼠指甲'後交給大夏";
		}
	case 4:
		mes "[護送任務]";
		mes "目前沒有任何護送任務";
		close;
	}
	end;
L_Quest:
	//QuestID,ReqLvl,Quest,Client,Loc,Desc
	if (BaseLevel < getarg(1,26)) {
		mes "你的級別還沒有達到要求。你還不能接受'"+getarg(2)+"'任務。";
		close;
	}
	mes "["+getarg(2)+" !!]";
	mes "委託人 : "+getarg(3);
	mes "委託地點 : "+getarg(4);
	mes "委託任務內容 : "+getarg(5);
	mes " ";
	mes "你要接受這個任務嗎?";
	next;
	if(select("不，我不想接受這個任務。:是，我要接這個任務。") == 1) {
		mes "你拒絕了"+getarg(2)+"任務。";
		close;
	}
	if (checkquest(getarg(0)) == 1) mes "你已經做過這個任務了！";
	else {
		setquest getarg(0);
		mes "你接受了"+getarg(2)+"任務。";
	}
	mes "你可以在完成任務後去委託人那裡領取你的獎勵。";
	next;
	mes "可以按鍵'Alt + U', 打開任務窗口來查看更詳細的任務信息。";
	close;
}

geffen,103,42,5	script	斯波尼亞	99,{
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 3500) {
		mes "[斯波尼亞]";
		mes "你怎麼帶了這麼多物品?";
		mes "你是在參加一個訓練課程麼?";
		close;
	}
	mes "[斯波尼亞]";
	mes "Lu~lu~lu~lu~";
	mes "Lu~ Huh?";
	next;
	mes "[斯波尼亞]";
	mes "哇哦, 你是新~新~新~新~新來的吧?";
	mes "好一張純潔天真爛漫的面孔! 哈~ hohoho~.";
	emotion ET_KIK;
	next;
	mes "[斯波尼亞]";
	mes "別在這兒瞎晃悠浪費時間啦，快到外面的世界狩獵去吧！";
	next;
	if(select("離開。:我接了一個狩獵任務。") == 1) {
		mes "[斯波尼亞]";
		mes "哦，好吧，走吧。";
		mes "Bye~ bye.";
		close;
	}
	mes "[斯波尼亞]";
	mes "噢！你是從伊甸園來的?";
	mes "你接了什麼任務?";
	next;
	switch(select("狩獵毒魔菇:狩獵狸貓:狩獵長老樹精:狩獵松鼠:狩獵達拉蛙")) {
		case 1: callsub L_Quest,8266,"狩獵毒魔菇",1000,0,100,"你可以在吉芬塔地下1層或者普隆德拉的德東北方向找到毒魔菇，當然還有其他地方也有哦，這就留給你自己去找尋吧！";
		case 2: callsub L_Quest,8267,"狩獵狸貓",1000,0,100,"你可以在吉芬東北部或者西北部找到它們的蹤跡，當然哎喲泰雅那裡也是個不錯的選擇哦！";
		case 3: callsub L_Quest,8268,"狩獵長老樹精",1000,100,110,"你能在吉芬東北部，斐揚東部或者普隆德拉西南部找到長老樹精們哦！";
		case 4: callsub L_Quest,8269,"狩獵松鼠",1000,200,150,"你可以在斐揚東部，普隆德拉西南部或者哎喲泰雅東部找到松鼠們的蹤跡哦！";
		case 5: callsub L_Quest,8270,"狩獵達拉蛙",1500,300,200,"你可以在克魔島東邊洞穴或者龜島地區找到達拉蛙！";
	}
	end;
L_Quest:
	//QuestID,Quest,BExp,JExp,PotionAmt,Desc
	if (checkquest(getarg(0)) == 1) {
		if (checkquest(getarg(0),HUNTING) == 2) {
			mes "[斯波尼亞]";
			mes getarg(1)+"!";
			mes "Wow, 我沒指望你能完成這個任務，我都要放棄了！沒想到你幹的如此漂亮！太棒了！";
			next;
			mes "[斯波尼亞]";
			mes "很好，你出色得完成了我交給你的任務";
			mes "我會按照承諾給你獎勵的。";
			next;
			mes "[斯波尼亞]";
			mes "拿去吧！太感謝了！祝你一切順利！";
			getitem 569,getarg(4); //Novice_Potion
			getexp getarg(2),getarg(3);
			erasequest getarg(0);
			close;
		}
		mes "[斯波尼亞]";
		mes "噢，你來啦。";
		next;
		mes "[斯波尼亞]";
		mes "但是你還沒完成任務呢！你怎麼就回來了？";
		mes "如果你能完成任務的話我會給你酬勞的，怎麼樣？";
		next;
		mes "[斯波尼亞]";
		mes getarg(5);
		mes "你可以按'ctrl + ~' 來查看世界地圖。";
		next;
		mes "[斯波尼亞]";
		mes "好了，我會在這兒等你的，完成任務後再回來找我吧！";
		close;
	}
	mes "[斯波尼亞]";
	mes "Huh? 好像有些事情不太對。。。";
	next;
	mes "[斯波尼亞]";
	mes "好像有點奇怪。。";
	mes "請按'alt + u'查看任務窗口。";
	close;
}

moc_ruins,173,55,3	script	迪斯信 曼	89,{
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 3500) {
		mes "[迪斯信 曼]";
		mes "你怎麼帶了這麼多物品?";
		mes "你是在參加一個訓練課程麼?";
		close;
	}
	mes "[迪斯信 曼]";
	mes "啊哈~~我們是運送員~ ";
	mes "我們的運送速度超乎想像地快~!";
	next;
	mes "[迪斯信 曼]";
	mes "歡迎~歡迎~!";
	mes "我們是最牛的快遞公司--迪斯信公司。";
	next;
	mes "[迪斯信 曼]";
	mes "有什麼需要幫忙麼？";
	next;
	if(select("我沒什麼事。:我接受了一個配送任務。") == 1) {
		mes "[迪斯信 曼]";
		mes "哦，好吧。 Bye~!";
		close;
	}
	mes "[迪斯信 曼]";
	mes "噢！你是從伊甸園來的?";
	mes "你接了什麼任務?";
	next;
	switch(select("配送殺蟲劑:配送蜂蜜:配送毛毯")) {
		case 1: callsub L_Quest,8271;
		case 2: callsub L_Quest,8272;
		case 3: callsub L_Quest,8273;
	}
	end;
L_Quest:
	if (checkquest(getarg(0)) != 1) {
		mes "[迪斯信 曼]";
		mes "Huh? 好像有什麼不太對。。。";
		next;
		mes "[迪斯信 曼]";
		mes "好像有點奇怪。。";
		mes "請按'alt + u'查看任務窗口。";
		close;
	}
	if (countitem(6218) > 0) {
		mes "[迪斯信 曼]";
		mes "等等，你已經有一個配送箱子了。";
		next;
		mes "[迪斯信 曼]";
		mes "你不能同時擁有兩個配送箱子。所以，請你先完成之前的配送任務。";
		next;
		mes "[迪斯信 曼]";
		mes "如果你忘記了送貨地點，請按'alt + u'查看任務窗口。";
		close;
	}
	switch(getarg(0)) {
	case 8271:
		mes "[迪斯信 曼]";
		mes "啊哈！對了！";
		mes "殺蟲劑？這肯定是斐揚東南部的動物學家想要的吧！";
		break;
	case 8272:
		mes "[迪斯信 曼]";
		mes "啊哈！對了！";
		mes "蜂蜜？這一定是位於聖·卡普託利納修道院西部地圖，即普隆德拉東北部地區的昆蟲學家想要的吧！";
		break;
	case 8273:
		mes "[迪斯信 曼]";
		mes "哦！對了！";
		mes "毛毯！這一定是薑餅城南部的生物學家想要的吧！";
		next;
		mes "[迪斯信 曼]";
		mes "你可以去艾爾帕蘭尋找連結耶誕老人，他會幫助你去往薑餅城的！";
		break;
	}
	next;
	mes "[迪斯信 曼]";
	mes "迪斯信公司的宗旨是給予顧客最快最安全的快遞服務。所以，請儘快將貨物送達。";
	next;
	mes "[迪斯信 曼]";
	mes "如果你忘記了送貨地點，請按'alt + u'查看任務窗口。";
	getitem 6218,1; //Disin_Delivery_Box
	close;
}

pay_fild07,83,89,7	script	動物學家	89,{
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 3500) {
		mes "[動物學家]";
		mes "你怎麼帶了這麼多物品?";
		mes "你是在參加一個訓練課程麼?";
		close;
	}
	mes "[動物學家]";
	mes "你聽說過一種叫大腳熊的怪物麼？";
	mes "它看起來就像有著紅色皮毛的熊，在它肩上還有'X'型疤痕。";
	next;
	mes "[動物學家]";
	mes "根據最新的研究顯示，昆蟲和大腳熊之間是一種共生關係。";
	next;
	mes "[動物學家]";
	mes "我最近正在研究這個課題，所以計劃殺死大腳熊周圍的昆蟲來進行觀察研究，以此證明這個理論的正確性。";
	next;
	if(select("俄。。我對此沒興趣。:這是你要的殺蟲劑！") == 1) {
		mes "[動物學家]";
		mes "哦！我需要殺蟲劑來繼續這項研究！";
		close;
	}
	if (checkquest(8271) != 1) {
		mes "[動物學家]";
		mes "Huh?";
		mes "你不是迪斯信公司的送貨員?";
		next;
		mes "[動物學家]";
		mes "我正在等一份重要的快遞。";
		close;
	}
	if (countitem(6218) > 0) {
		mes "[動物學家]";
		mes "哦！天哪！就是這個！";
		next;
		mes "[動物學家]";
		mes "這就是我要的殺蟲劑！你是迪斯信公司派來的是麼？";
		next;
		mes "[動物學家]";
		mes "我要等的正是這個！";
		mes "請給我吧！";
		next;
		mes "[動物學家]";
		mes "我這就能去做實驗了！來的速度比我想像的快多啦！";
		next;
		mes "[動物學家]";
		mes "幹得好！太棒了！我感謝你！這是給你的一點點心意，請收下！";
		delitem 6218,1; //Disin_Delivery_Box
		erasequest 8271;
		getitem 569,100; //Novice_Potion
		getexp 1000,0;
		close;
	}
	mes "[動物學家]";
	mes "感謝你的好意！";
	mes "你給我帶來了什麼呢？";
	next;
	mes "[動物學家]";
	mes "唔。。我一直認為迪斯信公司送貨又快又準，可是你什麼都沒給我帶來，太令我失望了！";
	next;
	mes "[動物學家]";
	mes "請給我快遞樣東西來吧，迪斯信會給你我想要的東西。";
	close;
}

prt_fild03,30,254,3	script	昆蟲學家	83,{
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 3500) {
		mes "[昆蟲學家]";
		mes "你怎麼帶了這麼多物品?";
		mes "你是在參加一個訓練課程麼?";
		close;
	}
	mes "[昆蟲學家]";
	mes "你聽說過一種叫鍬形蟲的怪物麼？";
	mes "它外形像昆蟲，有光滑的黑色外皮，頭上有巨大的犄角。";
	next;
	mes "[昆蟲學家]";
	mes "最新研究顯示，這種怪物可以通過蜂蜜被馴化。";
	next;
	mes "[昆蟲學家]";
	mes "我目前正在研究這個課題，希望用蜂蜜來觀察它們的反應，並藉以此來證明這個理論。";
	next;
	if(select("俄。。。我對此沒興趣。:這是你要的蜂蜜！") == 1) {
		mes "[昆蟲學家]";
		mes "哎。。我需要蜂蜜才能繼續進行研究。";
		close;
	}
	if (checkquest(8272) != 1) {
		mes "[昆蟲學家]";
		mes "Huh?";
		mes "你不是迪斯信公司的送貨員?";
		next;
		mes "[昆蟲學家]";
		mes "我正在等一份重要的快遞";
		close;
	}
	if (countitem(6218) > 0) {
		mes "[昆蟲學家]";
		mes "哦！天哪！就是這個！";
		next;
		mes "[昆蟲學家]";
		mes "這就是我要的蜂蜜！你是迪斯信公司派來的是麼？";
		next;
		mes "[昆蟲學家]";
		mes "我要等的正是這個！";
		mes "請給我吧！";
		next;
		mes "[昆蟲學家]";
		mes "我這就能去做實驗了！來的速度比我想像的快多啦！";
		next;
		mes "[昆蟲學家]";
		mes "幹得好！太棒了！我感謝你！這是給你的一點點心意，請收下！";
		delitem 6218,1; //Disin_Delivery_Box
		erasequest 8272;
		getitem 569,100; //Novice_Potion
		getexp 1000,0;
		close;
	}
	mes "[昆蟲學家]";
	mes "感謝你的好意！";
	mes "你給我帶來了什麼呢？";
	next;
	mes "[昆蟲學家]";
	mes "唔。。我一直認為迪斯信公司送貨又快又準，可是你什麼都沒給我帶來，太令我失望了！";
	next;
	mes "[昆蟲學家]";
	mes "請給我快遞樣東西來吧，迪斯信會給你我想要的東西。";
	close;
}

xmas_fild01,92,57,3	script	生物學家	97,{
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 3500) {
		mes "[生物學家]";
		mes "你怎麼帶了這麼多物品?";
		mes "你是在參加一個訓練課程麼?";
		close;
	}
	mes "[生物學家]";
	mes "你聽說過冰波利這種怪物麼？";
	mes "它看起來像普通的波利，不過它擁有自己獨特的特質。";
	next;
	mes "[生物學家]";
	mes "波利看到地上的掉落物便會迫不及待地衝過去，不過冰波利似乎對此毫無興趣。所以我想知道，為什麼他們看起來如此相似，但是冰波利卻對收集物品毫無興趣。";
	next;
	mes "[生物學家]";
	mes "但是，這兒真是太他媽冷了！";
	next;
	if(select("俄。。。我對此沒興趣。:這是你要的毛毯！") == 1) {
		mes "[生物學家]";
		mes "俄。。好吧。那小心點兒，這兒路面很滑。";
		close;
	}
	if (checkquest(8273) != 1) {
		mes "[生物學家]";
		mes "Huh?";
		mes "你不是迪斯信公司的送貨員?";
		next;
		mes "[生物學家]";
		mes "我正在等一份重要的快遞";
		close;
	}
	if (countitem(6218) > 0) {
		mes "[生物學家]";
		mes "哦！天哪！就是這個！";
		next;
		mes "[生物學家]";
		mes "這就是我要的毛毯！你是迪斯信公司派來的是麼？";
		next;
		mes "[生物學家]";
		mes "我要等的正是這個！";
		mes "請給我吧！";
		next;
		mes "[生物學家]";
		mes "我這就能去做實驗了！來的速度比我想像的快多啦！";
		next;
		mes "[生物學家]";
		mes "幹得好！太棒了！我感謝你！這是給你的一點點心意，請收下！";
		delitem 6218,1; //Disin_Delivery_Box
		erasequest 8273;
		getitem 569,100; //Novice_Potion
		getexp 1000,0;
		close;
	}
	mes "[生物學家]";
	mes "感謝你的好意！";
	mes "你給我帶來了什麼呢？";
	next;
	mes "[生物學家]";
	mes "唔。。我一直認為迪斯信公司送貨又快又準，可是你什麼都沒給我帶來，太令我失望了！";
	next;
	mes "[生物學家]";
	mes "請給我快遞樣東西來吧，迪斯信會給你我想要的東西。";
	close;
}

payon,179,66,3	script	大夏	931,{
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 3500) {
		mes "[大夏]";
		mes "你怎麼帶了這麼多物品?";
		mes "你是在參加一個訓練課程麼?";
		close;
	}
	mes "[大夏]";
	mes "這兒有些毛髮~一個壞了的鬧鐘~!";
	next;
	mes "[大夏]";
	mes "壞鬧鐘。。？俄。。";
	next;
	mes "[大夏]";
	mes "Hey~! 哥們！你看來是個很有錢的冒險家嘛！~";
	next;
	if(select("我只是個窮冒險家。:我接受了一個收集任務。") == 1) {
		mes "[大夏]";
		mes "真的？好吧，你看起來不是那麼地圓滑世故。";
		next;
		mes "[大夏]";
		mes "貧窮的冒險家~祝你好運！~";
		close;
	}
	mes "[大夏]";
	mes "噢！你是從伊甸園來的?";
	mes "你接了什麼任務?";
	next;
	switch(select("收集遺骨:收集動物外皮:收集外殼:收集尾巴:收集餅乾:收集鬍鬚")) {
	case 1:
		callsub L_Quest,8274;
		if (countitem(932) > 9) {
			callsub L_Reward;
			delitem 932,10; //Skel_Bone
			getitem 569,100; //Novice_Potion
			getexp 1000,100;
			erasequest 8274;
			close;
		}
		mes "[大夏]";
		mes "可以看出來你已經接受了一個收集任務。";
		mes "可是，死人遺骨在哪兒呢？";
		next;
		mes "[大夏]";
		mes "你怎麼回來了？你還沒有完成任務呢！";
		next;
		mes "[大夏]";
		mes "你還沒有足夠的'死人遺骨'。。 去K邪骸戰士吧！";
		next;
		mes "[大夏]";
		mes "想找邪骸戰士的話，可以去斐揚北邊的斐揚洞窟1層看看。";
		break;
	case 2:
		callsub L_Quest,8275;
		if ((countitem(948) > 9) && (countitem(919) > 4)) {
			callsub L_Reward;
			getitem 569,100; //Novice_Potion
			delitem 948,10; //Bear's_Foot
			delitem 919,5; //Animal's_Skin
			getexp 1000,100;
			erasequest 8275;
			close;
		}
		mes "[大夏]";
		mes "可以看出來你已經接受了一個收集任務。";
		mes "可是，熊掌和動物外皮在哪兒呢？?";
		next;
		mes "[大夏]";
		mes "你怎麼回來了？你還沒有完成任務呢！";
		next;
		mes "[大夏]";
		mes "你必須帶回來10個熊掌和5個動物外皮！";
		mes "大腳熊會掉落熊掌，至於動物外皮麼，很多動物身上都能找到。";
		next;
		mes "[大夏]";
		mes "你可以在吉芬北部或斐揚東南邊找到大腳熊的蹤跡。";
		break;
	case 3:
		callsub L_Quest,8276;
		if ((countitem(955) > 19) && (countitem(910) > 19)) {
			callsub L_Reward;
			delitem 955,20; //Worm_Peelings
			delitem 910,20; //Garlet
			getitem 569,100; //Novice_Potion
			getexp 1000,100;
			erasequest 8276;
			close;
		}
		mes "[大夏]";
		mes "可以看出來你已經接受了一個收集任務。";
		mes "可是，昆蟲外殼和加勒結晶在哪兒呢？";
		next;
		mes "[大夏]";
		mes "你必須帶回'20個昆蟲外殼'和'20個加勒結晶'才能完成任務。";
		next;
		mes "[大夏]";
		mes "你可以從白蟻，兵蟻，工蟻，黑蟻，盜蟲，浮勒盜蟲，瑪勒盜蟲身上找到這些收集品。";
		next;
		mes "[大夏]";
		mes "你可以在夢羅克西南方的螞蟻地獄密穴中找到白蟻，兵蟻，工蟻和黑蟻。";
		next;
		mes "[大夏]";
		mes "盜蟲，浮勒盜蟲，瑪勒盜蟲則棲息在於普隆德拉的地下水道中。";
		break;
	case 4:
		callsub L_Quest,8277;
		if ((countitem(942) > 9) && (countitem(1026) > 9) && (countitem(945) > 9)) {
			callsub L_Reward;
			delitem 942,10; //Yoyo_Tail
			delitem 1026,10; //Acorn
			delitem 945,10; //Raccoon_Leaf
			getitem 569,150; //Novice_Potion
			getexp 1000,200;
			erasequest 8277;
			close;
		}
		mes "[大夏]";
		mes "可以看出來你已經接受了一個收集任務。";
		mes "可是，猴子尾巴，橡實和變身葉子在哪兒呢？";
		next;
		mes "[大夏]";
		mes "你怎麼回來了？你還沒有完成任務呢！";
		mes "你必須帶回來'10根猴子尾巴' ，'10個橡實'還有'10個變身葉子'才能完成這個任務。";
		next;
		mes "[大夏]";
		mes "'溜溜猴'掉落猴子尾巴, '松鼠'掉落橡實，'狸貓'掉落變身葉子。";
		next;
		mes "[大夏]";
		mes "你可以很輕鬆地在哎喲泰雅的東部原野上找到所有這3種生物。";
		next;
		mes "[大夏]";
		mes "想去哎喲泰雅的話可以去艾爾貝塔找'Aibakthing'。（當然，你也知道的，其實我們可以直接用小助手的嘛）";
		break;
	case 5:
		callsub L_Quest,8278;
		if ((countitem(538) > 4) && (countitem(539) > 0)) {
			callsub L_Reward;
			delitem 538,5; //Well_Baked_Cookie
			delitem 539,1; //Piece_Of_Cake
			getitem 569,150; //Novice_Potion
			getexp 1000,200;
			erasequest 8278;
			close;
		}
		mes "[大夏]";
		mes "可以看出來你已經接受了一個收集任務。";
		mes "可是，薑餅和小蛋糕在哪兒呢？";
		next;
		mes "[大夏]";
		mes "你怎麼回來了？你還沒有完成任務呢！";
		mes "你必須帶回'5塊薑餅'和'1塊小蛋糕'才能完成這個任務。";
		next;
		mes "[大夏]";
		mes "你可以從'綠餅人'或'甜餅人'那兒獲取薑餅。";
		mes "至於小蛋糕，可以找'邪惡禮盒'！~";
		next;
		mes "[大夏]";
		mes "所有的怪物都可以在玩具工廠內找到哦！";
		break;
	case 6:
		callsub L_Quest,8279;
		if ((countitem(1017) > 4) && (countitem(1018) > 2)) {
			callsub L_Reward;
			delitem 1017,5; //Moustache_Of_Mole
			delitem 1018,3; //Nail_Of_Mole
			getitem 569,150; //Novice_Potion
			getexp 1000,200;
			erasequest 8279;
			close;
		}
		mes "[大夏]";
		mes "可以看出來你已經接受了一個收集任務。";
		mes "可是，鼠須和鼠指甲在哪兒呢？";
		next;
		mes "[大夏]";
		mes "你怎麼回來了？你還沒有完成任務呢！";
		mes "你必須帶回'5個鼠須'和'3個鼠指甲'才能完成任務。";
		next;
		mes "[大夏]";
		mes "你可以從'土撥鼠'那裡找到你想要的。";
		mes "它們出沒在妙勒尼廢棄礦場中。不過二層很危險，要小心！!";
		next;
		mes "[大夏]";
		mes "去吉芬找卡普拉工作人員，她會幫助你去往你想去的地方！";
		break;
	}
	next;
	mes "[大夏]";
	mes "好了，我會在這兒等你的。完成任務後再回來找我吧！";
	close;
L_Quest:
	if (checkquest(getarg(0)) != 1) {
		mes "[大夏]";
		mes "Huh? 好像有些事情不太對。。。";
		next;
		mes "[大夏]";
		mes "好像有點奇怪。。";
		mes "請按'alt + u'查看任務窗口。";
		close;
	}
	return;
L_Reward:
	mes "[大夏]";
	mes "Wow~ 你幹得太棒了!";
	next;
	mes "[大夏]";
	mes "太感謝了！上帝保佑你!";
	emotion ET_SMILE;
	next;
	mes "[大夏]";
	mes "哈哈！";
	mes "我會按照約定給你應得的獎勵的！";
	return;
}
