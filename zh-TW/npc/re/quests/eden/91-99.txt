//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================

//===== rAthena Script =======================================
//= Eden Group Quests - Mission [91 - 99]
//===== By: ==================================================
//= Chilly
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 91 - 99.
//===== Additional Comments: =================================
//= 1.0 First Version. [Chilly]
//= 1.1 Some little improvements here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//============================================================

moc_para01,48,177,3	script	任務 [91 - 99]	857,{
	if (countitem(6219) < 1) {
		mes "- 只有成為伊甸園的成員後， -";
		mes "- 才能閱讀這塊布告欄。 -";
		close;
	}
	if (BaseLevel < 91) {
		mes "- 這些任務只適合更高級別的冒險者。 -";
		mes "- 還是先去其他布告欄中找找合適我的任務吧！ -";
		close;
	}
	if (BaseLevel > 99) {
		setarray .@Exp[0],90000,94000,114000,96000,96000,146000,96000,102000,110000,102000,108000,110000,138000,118000,96000,134000;
		setarray .@Mob$[0],"巖石龜","水靈龜","火焰龜","邪惡使者","瑞比歐","暗黑祭司","石頭波利","鼬","小雪怪","火忍西怒畢","妖仙女","小惡魔","礦石魔","卡浩","奈利蟲","假面鬼";
		for(set .@i,0; .@i<16; set .@i,.@i+1)
			if (checkquest(4181+.@i,HUNTING) == 2) {
				mes "你已經狩獵完30隻"+.@Mob$[.@i]+"了。";
				mes "非常感謝！";
				next;
				mes "這是給你的小小禮物。";
				mes "儘管不多，但是希望能對你有所幫助。";
				next;
				erasequest 4181+.@i;
				getexp .@Exp[.@i],311000;
				mes "哇哦~";
				mes "祝你好運~！";
				close;
			}
		mes "- 這些任務只適合對應級別的冒險家。 -";
		mes "- 我應該去其他的布告欄上看看有沒有我能做的任務。 -";
		close;
	}
	mes "- 布告欄上現有的狩獵任務： -";
	next;
	switch(select("龜島地區:克雷斯特漢姆古城地區:拉赫地區:本地區域:其它區域")) {
	case 1:
		switch(select("巖石龜:水靈龜:火焰龜")) {
			case 1: callsub L_Quest,4181,"巖石龜",90000;
			case 2: callsub L_Quest,4182,"水靈龜",94000;
			case 3: callsub L_Quest,4183,"火焰龜",114000;
		}
	case 2:
		switch(select("邪惡使者:瑞比歐:暗黑祭司")) {
			case 1: callsub L_Quest,4184,"邪惡使者",96000;
			case 2: callsub L_Quest,4185,"瑞比歐",96000;
			case 3: callsub L_Quest,4186,"暗黑祭司",146000;
		}
	case 3:
		switch(select("石頭波利:鼬:小雪怪")) {
			case 1: callsub L_Quest,4187,"石頭波利",96000;
			case 2: callsub L_Quest,4188,"鼬",102000;
			case 3: callsub L_Quest,4189,"小雪怪",110000;
		}
	case 4:
		switch(select("火忍西怒畢:妖仙女")) {
			case 1: callsub L_Quest,4190,"火忍西怒畢",102000;
			case 2: callsub L_Quest,4191,"妖仙女",108000;
		}
	case 5:
		switch(select("小惡魔:礦石魔:卡浩:奈利蟲:假面鬼")) {
			case 1: callsub L_Quest,4192,"小惡魔",110000;
			case 2: callsub L_Quest,4193,"礦石魔",138000;
			case 3: callsub L_Quest,4194,"卡浩",118000;
			case 4: callsub L_Quest,4195,"奈利蟲",96000;
			case 5: callsub L_Quest,4196,"假面鬼",134000;
		}
	}
	end;
L_Quest:
	if (checkquest(getarg(0),HUNTING) == 2) {
		mes "你已經狩獵完30隻"+getarg(1)+"了。";
		mes "非常感謝！";
		next;
		mes "這是給你的小小禮物。";
		mes "儘管不多，但是希望能對你有所幫助。";
		next;
		erasequest getarg(0);
		getexp getarg(2),311000;
		mes "哇哦~";
		mes "祝你好運~！";
		close;
	}
	if (checkquest(getarg(0)) == -1) {
		setquest getarg(0);
		mes "請狩獵完30隻"+getarg(1)+"後再返回這裡。";
		close;
	}
	mes "俄。。你還沒有完成呢。。";
	mes "需要狩獵30隻"+getarg(1)+"哦~";
	mes "要不你再檢查一下看看？";
	close;
}
