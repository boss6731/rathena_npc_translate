//===== Hercules Script =======================================
//= Quest NPCs located in Lighthalzen
//===== By: ==================================================
//= Ziu, Heris (translation)
//===== Current Version: =====================================
//= 1.2
//===== Description: =========================================
//= Quests related to Lighthalzen City.
//===== Additional Comments: =================================
//= 1.0 Updated to match the new Izlude Map. [Masao]
//= 1.1 Added Somatology Laboratory F4 quest. [Euphy]
//= 1.2 Added Izlude duplicates. [Euphy]
//============================================================

// Renewal split [Daegaladh]
//============================================================
//izlude,172,73,6	duplicate(Scamp)	惡棍#iz	4_M_YURI
//izlude_a,172,73,6	duplicate(Scamp)	惡棍#iz_a	4_M_YURI
//izlude_b,172,73,6	duplicate(Scamp)	惡棍#iz_b	4_M_YURI
//izlude_c,172,73,6	duplicate(Scamp)	惡棍#iz_c	4_M_YURI
//izlude_d,172,73,6	duplicate(Scamp)	惡棍#iz_d	4_M_YURI

// Somatology Laboratory F4
//============================================================
lighthalzen,303,304,5	script	賈科莫·吉羅拉莫#start	4_M_KHMAN,{
	if (lhz_dun04 == 0) {
		mes "[賈科莫·吉羅拉莫]";
		mes "噢, 多麼"+((Sex)?"強壯的男人啊!":"美麗的女人!");
		next;
		mes "[賈科莫·吉羅拉莫]";
		mes ((Sex)?"先生":"女士")+", 請留意 --";
		mes "最近有一個可怕謠言.";
		mes "鎮裡的所有人都非常害怕.";
		mes "現在最好的方法就是待在家裡,";
		mes "把門緊鎖.";
		
		// To do: check BaseLevel requirement.
		if (BaseLevel < 90 || hg_tre < 54) close;

		next;
		select("可怕的謠言?");
		mes "[賈科莫·吉羅拉莫]";
		mes "謠言說在雷根貝勒研究所";
		mes "正在進行可怕的秘密實驗";
		mes "試驗的內容可能和生物";
		mes "相關!";
		next;
		select("什麼? 實驗?");
		mes "[賈科莫·吉羅拉莫]";
		mes "沒錯, 生物實驗!!";
		mes "在貧民窟的地下實驗室, ";
		mes "謠言說就在那裡.";
		mes "同時那片區域的居民非常貧窮";
		mes "他們可能收到不公正的待遇";
		mes "被剝奪了很多社會福利.";
		next;
		mes "[賈科莫·吉羅拉莫]";
		mes "謠傳不應該被忽視.";
		mes "應該調查這裡的生物學實驗室.";
		mes "進行的實驗";
		mes "是否與外界傳聞的一樣.";
		mes "這件事真的需要立即";
		mes "開始調查!";
		setquest 5109;
		set lhz_dun04,1;
		close;
	}
	mes "[賈科莫·吉羅拉莫]";
	mes "請調查實驗室的最深處";
	mes "我希望能儘快聽到你的調查";
	mes "結果.";
	close;
}

// Is this the correct behavior? (limited and conflicting sources)
// - NPC in lhz_dun04 warps into que_lhz. (?)
// - lhz_dun03 warps directly to que_lhz. (?)
function	script	lhz4enter	{
	if (getmapusers("que_lhz") == 0 && mobcount("que_lhz","#controllhz4::OnMyMobDead") < 1) {
		if (getarg(0) == 0) {
			mes "- 有了賈科莫·吉羅拉莫的提醒";
			mes "你忍住呼吸";
			mes "開始穿過被水淹沒的";
			mes "電梯. -";
		} else {
			mes "[科研人員]";
			mes "你是誰?";
			mes "................";
			emotion ET_HUK;
			next;
			mes "男子逃跑, 你需要追上他.";
		}
		if (lhz_dun04 > 1) {
			changequest 5110,5109;
			set lhz_dun04,1;
		}
		close2;
		// Check twice because of 'next'.
		if (getmapusers("que_lhz") == 0 && mobcount("que_lhz","#controllhz4::OnMyMobDead") < 1) {
			donpcevent "#lhz4mes1::OnEnable";
			warp "que_lhz",244,52;
		}
		end;
	}
	mes "現在有其他人在裡面, 輪到你等著了.";
	close;
}

lhz_dun03,239,77,0	script	#warplhz3lhz4	WARPNPC,1,1,{
OnTouch:
	if (lhz_dun04 == 0) {
		mes "遊過這些被淹沒的電梯沒有任何意義.";
		close;
	} else if (lhz_dun04 < 6) {
		callfunc "lhz4enter",0;
		end;
	}
	warp "lhz_dun04",244,52;
	end;
}
lhz_dun04,245,61,0	warp	#warplhz4lhz3	1,1,lhz_dun03,240,74

// Does this portal exist?
que_lhz,245,61,0	warp	#warplhzqlhz3	1,1,lhz_dun03,240,74

que_lhz,244,52,0	script	#lhz4mes1	-1,2,2,{
OnTouch:
	mes "- 你找到了另外一部電梯";
	mes "已經被拉拽至水面上方. -";
	mes "";
	// Missing dialogue.
	close2;
	donpcevent "#lhz4mes1::OnDisable";
	end;
OnInit:
OnDisable:
	disablenpc "#lhz4mes1";
	end;
OnEnable:
	enablenpc "#lhz4mes1";
	end;
}

lhz_dun04,151,276,3	script	科研人員#lhz4	4_LGTSCIENCE,{
	if (lhz_dun04 == 0) {
		mes "[科研人員]";
		mes "................";
		close;
	} else if (lhz_dun04 < 6) {
		callfunc "lhz4enter",1;
		end;
	} else if (lhz_dun04 == 6) {
		mes "因為這些試驗品的關係, 經過再三考慮之後, 必須關閉這個地方.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "你已經阻止不了我了, 最後的四個生命體我已經創建出來了!";
		next;
		mes "[沃爾岑夫]";
		mes "嗯, 這個地方將被完全淹沒...";
		next;
		mes "[沃爾岑夫]";
		mes "什麼情況?";
		next;
		mes "[沃爾岑夫]";
		mes "唔... 該死的高層官員毀了我的實驗室, 該死!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "嘿! 沃爾岑夫!!";
		next;
		mes "[沃爾岑夫]";
		mes "恩是? 為了取悅他們! ^FF0080使用不同的標本^000000 進行生化組合試驗.";
		next;
		mes "[沃爾岑夫]";
		mes "可能這些 ^00FF00東西^000000 的首領...";
		mes "我無法控制我自己的實驗... 這裡, 全部都是 ^808000作為誘餌的奴隸們^000000 實驗室將被關閉.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "嘿, 沃爾岑夫!! 你口中所說的 ^00FF00首領^000000 ,它是個什麼東西?";
		next;
		mes "[沃爾岑夫]";
		mes "...在我的實驗室. 我喚醒了它. 我試圖把它們都摧毀, 但是這沒生效... 你必須出馬了...";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "我怎樣才能進入你的實驗室?";
		next;
		mes "[沃爾岑夫]";
		mes "現在... 此時!";
		mes "我會協助你.";
		next;
		mes "[沃爾岑夫]";
		mes "真走運, 首領還在沉睡中. 它的實力非常強大並極具破壞性.";
		next;
		mes "[沃爾岑夫]";
		mes "不過還是非常危險. 它和它的同類現在都能自由活動了, 這意味著我們不能再浪費任何一點時間了.";
		set lhz_dun04,7;
		completequest 5111;
		setquest 5113;
		setquest 5114;
		setquest 5115;
		setquest 5116;
		setquest 5117;
		setquest 5118;
		setquest 5119;
		setquest 5120;
		setquest 5121;
		setquest 5122;
		setquest 5123;
		setquest 5124;
		setquest 5125;
		close;
	} else if (lhz_dun04 == 7) {
		mes "[沃爾岑夫]";
		mes "你準備一個人去?";
		mes "你需要一個配置非常優秀的小隊才能夠從Boss手下活著回來.";
		close;
	}
	end;
}

que_lhz,207,75,3	script	科研人員#ins	4_LGTSCIENCE,3,3,{
OnTouch:
	if (lhz_dun04 == 1) {
		mes "[科研人員]";
		mes "..........";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "你是沃爾岑夫教授?";
		emotion ET_HUK,1;
		next;
		mes "[沃爾岑夫]";
		mes "很多人都來這裡找我. 不過絕大部分都不是自願的.";
		mes "你在這裡想幹嘛?";
		emotion ET_KIK;
		next;
		mes "["+strcharinfo(0)+"]";
		mes "我聽說這裡正在進行生物實驗.";
		mes "沃爾岑夫!!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "這些實驗...";
		next;
		mes "[沃爾岑夫]";
		mes "我想我知道你想說什麼, 你是不是在指責我?";
		mes "我可不想聽你嘮叨.";
		mes "你最好快點離開這裡.";
		emotion ET_KIK;
		next;
		mes "[沃爾岑夫]";
		mes "他似乎已經哭了很長一段時間... 我很忙, 失陪!";
		next;
		mes "沃爾岑夫的雙眼充滿的淚水, 之後他狂笑起來, 並且跑了.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "嘿! 沃爾岑夫 這...";
		emotion ET_HUK,1;
		changequest 5109,5110;
		set lhz_dun04,2;
		close2;
		warp "que_lhz",96,136;
		end;
	}
	mes "[科研人員]";
	mes "..........";
	close;
}

que_lhz,94,119,3	script	科研人員#ins2	4_LGTSCIENCE,{
	if (lhz_dun04 == 2) {
		mes "[沃爾岑夫]";
		mes "嘿嘿嘿. 你真的執著! 你想從我這裡得到什麼?";
		emotion ET_PROFUSELY_SWEAT;
		next;
		mes "["+strcharinfo(0)+"]";
		mes "沃爾岑夫... 你自己都坦言它是危險的. 所有這些調查...";
		next;
		mes "[沃爾岑夫]";
		mes "你說的對...呵呵.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "你的調查太危險了. 你認為每個人都希望你繼續查下去?";
		next;
		mes "[沃爾岑夫]";
		mes "每個人都想成為強大的可以主宰別人的人. 讓所有人服從, 得到一切尊重和敬仰. 難道你不想要? 嘿嘿.";
		emotion ET_KIK;
		next;
		mes "["+strcharinfo(0)+"]";
		mes "... ... ...";
		next;
		mes "[沃爾岑夫]";
		mes "如此危險的生命體, 都難免被毀滅... 你看到了嗎?. 嘿嘿.";
		next;
		mes "又一次, 沃爾岑夫匆匆逃離.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "噢! 他... 和...";
		set lhz_dun04,3;
		close2;
		warp "que_lhz",148,215;
		end;
	}
	mes "[科研人員]";
	mes "..........";
	close;
}
que_lhz,147,224,3	script	科研人員#ins3	4_LGTSCIENCE,{
	if (lhz_dun04 == 4 || mobcount("que_lhz","#controllhz4::OnMyMobDead") > 0) {
		mes "[沃爾岑夫]";
		mes "沃爾岑夫驚呆了. 整個人失魂落魄.";
		emotion e_turn;
		close;
	} else if (lhz_dun04 == 3) {
		mes "[沃爾岑夫]";
		mes "你真的像一隻蒼蠅一樣煩";
		mes "是不是, 這些是不是就";
		mes "是你想問的一切?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "... ... ...";
		emotion ET_THINK,1;
		next;
		mes "沃爾岑夫再也不想和你說話了, 他變笑邊按下了手中的一個控制器. 然後他瘋狂了.";
		specialeffect EF_SIGHTRASHER;
		next;
		mes "[沃爾岑夫]";
		mes "你太不走運了, 這裡會是你的墳墓.";
		donpcevent "#controllhz4::OnEnable";
		next;
		mes "[沃爾岑夫]";
		mes "你真是個傻瓜.";
		emotion ET_SURPRISE;
		next;
		mes "[沃爾岑夫]";
		mes "看到這些生命體麼, 你會被他們包圍, 讓我們來看看好戲吧.";
		next;
		donpcevent "#controllhz4::OnEnable2";
		progressbar "ffff00",6;
		set lhz_dun04,4;
		next;
		mes "[沃爾岑夫]";
		mes "不!! ...你幹了什麼!! 你";
		mes "是個瘋子!!!";
		emotion ET_HUK;
		next;
		mes "[沃爾岑夫]";
		mes "回去!";
		next;
		mes "首先, 讓我們摧毀所有這些實驗體!";
		close;
	} else if (lhz_dun04 == 5) {
		mes "沃爾岑夫驚呆了. 整個人失魂落魄.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "*扇他幾下耳光* 嘿, 快醒醒!";
		mes "我們搞定它們了!";
		specialeffect EF_STUNATTACK;
		sleep2 1000;
		specialeffect EF_STUNATTACK;
		next;
		mes "["+strcharinfo(0)+"]";
		mes "讓我們先離開這裡! 有什麼地方我們能去的?";
		specialeffect EF_DARKBREATH;
		emotion ET_STARE_ABOUT,1;
		next;
		mes "[沃爾岑夫]";
		mes "這裡. 我的實驗室... 我的實驗室...";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "實驗室? 我到底在哪?";
		next;
		mes "[沃爾岑夫]";
		mes "回到開始的地方... 這裡...";
		next;
		mes "- 沃爾岑夫有氣無力的指向12點鐘方向. -";
		next;
		mes "我必須去 ^0000FF 沃爾岑夫的實驗室^000000...";
		changequest 5110,5111;
		set lhz_dun04,6;
		donpcevent "#controllhz4d::OnEnable";
		close;
	} else if (lhz_dun04 == 6) {
		mes "我必須去 ^0000FF 沃爾岑夫的實驗室^000000...";
		close;
	}
	end;
}

-	script	甦醒的生命體#lhz	-1,{
	end;
OnInit:
OnDisable:
	hideonnpc strnpcinfo(0);
	end;
OnEnable:
	hideoffnpc strnpcinfo(0);
	end;
OnEffect1:
	specialeffect EF_SUI_EXPLOSION;
	end;
OnEffect2:
	specialeffect EF_BEGINASURA;
	end;
OnEffect3:
	specialeffect EF_FLASHER;
	end;
}
que_lhz,148,231,0	duplicate(甦醒的生命體#lhz)	甦醒的生命體#0	853
que_lhz,140,231,1	duplicate(甦醒的生命體#lhz)	甦醒的生命體#1	853
que_lhz,140,224,2	duplicate(甦醒的生命體#lhz)	甦醒的生命體#2	853
que_lhz,140,216,3	duplicate(甦醒的生命體#lhz)	甦醒的生命體#3	853
que_lhz,147,216,4	duplicate(甦醒的生命體#lhz)	甦醒的生命體#4	853
que_lhz,155,216,5	duplicate(甦醒的生命體#lhz)	甦醒的生命體#5	853
que_lhz,155,224,6	duplicate(甦醒的生命體#lhz)	甦醒的生命體#6	853
que_lhz,155,231,7	duplicate(甦醒的生命體#lhz)	甦醒的生命體#7	853

-	script	#controllhz4	-1,{
OnInit:
	disablenpc "#controllhz4";
	end;
OnEnable:
	enablenpc "#controllhz4";
	donpcevent "甦醒的生命體#0::OnEnable";
	donpcevent "甦醒的生命體#1::OnEnable";
	donpcevent "甦醒的生命體#2::OnEnable";
	donpcevent "甦醒的生命體#3::OnEnable";
	donpcevent "甦醒的生命體#4::OnEnable";
	donpcevent "甦醒的生命體#5::OnEnable";
	donpcevent "甦醒的生命體#6::OnEnable";
	donpcevent "甦醒的生命體#7::OnEnable";
	end;
OnEnable2:
	initnpctimer;
	end;
OnTimer1000:
	donpcevent "甦醒的生命體#0::OnEffect1";
	end;
OnTimer2000:
	donpcevent "甦醒的生命體#0::OnEffect2";
	donpcevent "甦醒的生命體#7::OnEffect1";
	end;
OnTimer3000:
	donpcevent "甦醒的生命體#7::OnEffect2";
	donpcevent "甦醒的生命體#6::OnEffect1";
	end;
OnTimer4000:
	donpcevent "甦醒的生命體#6::OnEffect2";
	donpcevent "甦醒的生命體#5::OnEffect1";
	end;
OnTimer5000:
	donpcevent "甦醒的生命體#5::OnEffect2";
	donpcevent "甦醒的生命體#4::OnEffect1";
	end;
OnTimer6000:
	donpcevent "甦醒的生命體#4::OnEffect2";
	donpcevent "甦醒的生命體#3::OnEffect1";
	end;
OnTimer7000:
	donpcevent "甦醒的生命體#3::OnEffect2";
	donpcevent "甦醒的生命體#2::OnEffect1";
	end;
OnTimer8000:
	donpcevent "甦醒的生命體#2::OnEffect2";
	donpcevent "甦醒的生命體#1::OnEffect1";
	end;
OnTimer9000:
	donpcevent "甦醒的生命體#1::OnEffect2";
	end;
OnTimer11000:
	donpcevent "甦醒的生命體#0::OnEffect3";
	donpcevent "甦醒的生命體#1::OnEffect3";
	donpcevent "甦醒的生命體#2::OnEffect3";
	donpcevent "甦醒的生命體#3::OnEffect3";
	donpcevent "甦醒的生命體#4::OnEffect3";
	donpcevent "甦醒的生命體#5::OnEffect3";
	donpcevent "甦醒的生命體#6::OnEffect3";
	donpcevent "甦醒的生命體#7::OnEffect3";
	end;
OnTimer14000:
	donpcevent "甦醒的生命體#0::OnDisable";
	donpcevent "甦醒的生命體#1::OnDisable";
	donpcevent "甦醒的生命體#2::OnDisable";
	donpcevent "甦醒的生命體#3::OnDisable";
	donpcevent "甦醒的生命體#4::OnDisable";
	donpcevent "甦醒的生命體#5::OnDisable";
	donpcevent "甦醒的生命體#6::OnDisable";
	donpcevent "甦醒的生命體#7::OnDisable";
	end;
OnTimer16000:
	monster "que_lhz",148,231,"甦醒的生命體",2244,1,"#controllhz4::OnMyMobDead";
	monster "que_lhz",140,231,"甦醒的生命體",2244,1,"#controllhz4::OnMyMobDead";
	monster "que_lhz",140,224,"甦醒的生命體",2244,1,"#controllhz4::OnMyMobDead";
	monster "que_lhz",140,216,"甦醒的生命體",2244,1,"#controllhz4::OnMyMobDead";
	monster "que_lhz",147,216,"甦醒的生命體",2244,1,"#controllhz4::OnMyMobDead";
	monster "que_lhz",155,216,"甦醒的生命體",2244,1,"#controllhz4::OnMyMobDead";
	monster "que_lhz",155,224,"甦醒的生命體",2244,1,"#controllhz4::OnMyMobDead";
	monster "que_lhz",155,231,"甦醒的生命體",2244,1,"#controllhz4::OnMyMobDead";
	donpcevent "#controllhz4b::OnEnable";
	stopnpctimer;
	end;
OnMyMobDead:
	if (mobcount("que_lhz","#controllhz4::OnMyMobDead") < 1) {
		donpcevent "#controllhz4b::OnStop";
		donpcevent "#controllhz4c::OnEnable";
		disablenpc "#controllhz4";
		end;
	}
	end;
}

-	script	#controllhz4b	-1,{
OnInit:
OnDisable:
	disablenpc "#controllhz4b";
	end;
OnEnable:
	enablenpc "#controllhz4b";
	initnpctimer;
	end;
OnStop:
	stopnpctimer;
	donpcevent "#controllhz4b::OnDisable";
	end;
OnTimer400000:
	killmonster "que_lhz","#controllhz4::OnMyMobDead";
	mapwarp "que_lhz","lighthalzen",263,200;
	disablenpc "#controllhz4";
	disablenpc "#controllhz4b";
	stopnpctimer;
	end;
}

-	script	#controllhz4c	-1,{
OnInit:
	disablenpc "#controllhz4c";
	end;
OnEnable:
	enablenpc "#controllhz4c";
	initnpctimer;
	end;
OnTimer10000:
	mapannounce "que_lhz","一個微弱的聲音: 救命... 求求你... 啊啊... 我不想... 死....",bc_map,0x0080C0;
	donpcevent "#controllhz4d::OnEnable";
	stopnpctimer;
	disablenpc "#controllhz4c";
	end;
}

que_lhz,147,248,0	script	#controllhz4d	-1,6,4,{
OnTouch:
	if (lhz_dun04 == 4) {
		mes "看上去全搞定了.";
		set lhz_dun04,5;
		donpcevent "#controllhz4d::OnDisable";
		close;
	} else if (lhz_dun04 == 6) {
		mes "- 房屋開始倒塌";
		mes "你側身閃避, 讓開從";
		mes "屋頂落下的混凝土. -";
		mes "";
		close2;
		warp "lhz_dun04",148,269;
		donpcevent "#lhz4mes2::OnEnable";
		donpcevent "#controllhz4d::OnDisable";
		end;
	}
	end;
OnInit:
OnDisable:
	disablenpc "#controllhz4d";
	end;
OnEnable:
	enablenpc "#controllhz4d";
	end;
}

lhz_dun04,148,269,0	script	#lhz4mes2	-1,2,2,{
OnTouch:
	if (lhz_dun04 == 6) {
		heal -Hp/2,0; // To do: check HP value.
		mes "- 你被救了, 模糊的聽到";
		mes "四周都是忙碌的腳步聲";
		mes "你... -";
		close2;
		donpcevent "#lhz4mes2::OnDisable";
		end;
	}
	end;
OnInit:
OnDisable:
	disablenpc "#lhz4mes2";
	end;
OnEnable:
	enablenpc "#lhz4mes2";
	end;
}
