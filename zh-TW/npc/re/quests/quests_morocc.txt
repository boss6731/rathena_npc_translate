//===== Hercules Script ======================================
//= Morroc Quest NPCs
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Description: =========================================
//= [Official Conversion]
//= Quest NPCs related to Morroc.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.0a Gathered remaining dialogues. [Euphy]
//= 1.1 Added new warpers for Satan Morroc maps. [Euphy]
//============================================================

// Resurrection of Satan Morroc
// ============================================================
-	script	大陸衛兵#man	-1,{
	mes "[大陸衛兵]";
	mes "我們已經收到了總部命令, 阻止任何人進入這個領域. 因為出現了一個不尋常的時空現象.";
	next;
	mes "[大陸衛兵]";
	mes "如果你真的想要進入該區域, 你需要為自己的安全負責.";
	next;
	switch(select("- 明白了.:- 進入該領域.")) {
	case 1:
		mes "[大陸衛兵]";
		mes "明智的選擇.";
		mes "祝你一路順風.";
		close;
	case 2:
		mes "[大陸衛兵]";
		mes "我明白了.";
		mes "我馬上送你進去.";
		close2;
		warp "moc_fild20",208,207;
		end;
	}
}
moc_fild07,380,202,1	duplicate(大陸衛兵#man)	大陸衛兵#man1	4_M_EIN_SOLDIER
moc_fild11,189,360,6	duplicate(大陸衛兵#man)	大陸衛兵#man2	4_M_EIN_SOLDIER
prt_fild10,263,23,1	duplicate(大陸衛兵#man)	大陸衛兵#man3	4_M_EIN_SOLDIER
prt_fild09,95,19,1	duplicate(大陸衛兵#man)	大陸衛兵#man4	4_M_EIN_SOLDIER
prt_fild09,246,17,1	duplicate(大陸衛兵#man)	大陸衛兵#man5	4_M_EIN_SOLDIER
moc_fild13,32,171,6	duplicate(大陸衛兵#man)	大陸衛兵#man6	4_M_EIN_SOLDIER
moc_fild16,124,381,4	duplicate(大陸衛兵#man)	大陸衛兵#man7	4_M_EIN_SOLDIER
moc_fild16,333,380,4	duplicate(大陸衛兵#man)	大陸衛兵#man8	4_M_EIN_SOLDIER
moc_fild01,84,19,1	duplicate(大陸衛兵#man)	大陸衛兵#man9	4_M_EIN_SOLDIER

// Pyramids Nightmare Mode
//============================================================
moc_prydb1,103,54,3	script	可疑的貓#night	4_CAT_SAILOR4,{
	mes "[可疑的貓]";
	mes "我不能相信一隻木乃伊對我做這樣的事情. 哦, 人類. 太令人心痛了, 沒有人能夠理解....";
	next;
	if(select("- 木乃伊? 發生什麼了?:- 假裝沒事.") == 2)
		close;
	mes "[可疑的貓]";
	mes "你竟然抓我! 該死...我沒想到我的老臉, 會因為這樣一個'古墓麗影'式的行動而被丟光...";
	next;
	mes "[可疑的貓]";
	mes "但事情很不正常, 它們太奇怪了! 它們是木乃伊! 僅僅只是木乃伊, 你知道嗎? 他們不應該如此之強!";
	next;
	if(select("- 強大的木乃伊? 在什麼地方?:- 結束談話.") == 2) {
		mes "[可疑的貓]";
		mes "神馬馬馬~ 你竟然不想聽我的故事? 好的! 你最好不要在別處提我的名字!";
		close;
	}
	mes "[可疑的貓]";
	mes "哈哈, 這是一個秘密.";
	mes "在我偷東西的時候被發現了... 不不, 我的意思是, 我只是在路過金字塔的時候.";
	next;
	mes "[可疑的貓]";
	mes "當我看到一具木乃伊向我走來, 我從來不喜歡管閒事. 開始並沒有在意, 但隨後它把我撞倒了還扇一個我耳光! 它怎麼能對像我這麼可愛的一隻貓做出這種事情! 這個渾身包布的傢伙... 給我走著瞧!";
	next;
	if(select("- 我到想去那裡看看.:- 結束談話.") == 2)
		close;
	mes "[可疑的貓]";
	mes "那不行! 我可是很久才找到的路徑!!";
	next;
	mes "[可疑的貓]";
	mes "嗯... 但如果你堅持, 只要給我 5000 Zeny. 我就帶你去.";
	mes "什麼~? 不要給我這樣的臉色看, 你怎麼會知道一隻貓要在米德加茲混下去有多難!";
	next;
	mes "[可疑的貓]";
	mes "^FF0000不過仔細想想, 你這樣的弱小實力肯定沒法在那裡活著出來. 運氣差的話可能都過不了1分鐘.^000000";
	mes "好吧, 我已經警告過你啦, 反正沒我的事!";
	next;
	switch(select("- 付 5,000 Zeny 並放了它.:- 忘了這事吧.")) {
	case 1:
		if (Zeny < 5000) {
			mes "[可疑的貓]";
			mes "你還想不想去哪地方, 5000都沒真是窮鬼!";
			close;
		}
		Zeny -= 5000;
		mes "[可疑的貓]";
		mes "呸, 呢麼舊的錢... 你就沒有金幣或者銀幣?!";
		mes "啊啊, 算了跟我來吧!";
		close2;
		warp "moc_prydn1",93,96;
		end;
	case 2:
		mes "[可疑的貓]";
		mes "天哪, 我猜你沒 5000 Zeny 吧, 是不是啊?";
		close;
	}
}

moc_prydn1,94,98,3	script	可疑的貓#night2	4_CAT_SAILOR4,{
	mes "[可疑的貓]";
	mes "那個愚蠢的木乃伊又打了我一巴掌, 而你正在看別處! 聞聞... 讓我們回去, 喵!";
	next;
	switch(select("- 返回盜賊公會:- 消滅噩夢畢萊特:- 消滅噩夢木乃伊:- 沒事")) {
	case 1:
		mes "[可疑的貓]";
		mes "好好... 我們回去.";
		close2;
		warp "moc_prydb1",100,57;
		end;
	case 2:
		set .@playtime, checkquest(2290,PLAYTIME);
		if (.@playtime == 0 || .@playtime == 1) {
			mes "[可疑的貓]";
			mes "看那個畢萊特, 它怕我們了! 哈哈哈哈! 我們安全了, 至少可以休息一段時間!";
			close;
		} else if (.@playtime == 2)
			erasequest 2290;
		set .@hunting, checkquest(2289,HUNTING);
		if (.@hunting == -1) {
			mes "[可疑的貓]";
			mes "你知道嗎, 貓和狗合不來?";
			next;
			mes "[可疑的貓]";
			mes "看在那個醜陋和猙獰的畢萊特. 皺著眉頭, 搖著尾巴, 試圖靠近我們.";
			mes "哦, 你沒有尾巴, 對不對? 我想它把我看成是眼中釘肉中刺, 他準備我們幹架了!";
			next;
			mes "[可疑的貓]";
			mes "你看看他這鳥樣, 如此貪婪的眼神還流口水...錯器啊! 還有這傢伙的呼吸聲. 我無法忍受這聽上去像狼吞虎咽的聲音...";
			mes "這也太明顯了, 它的等著從後面攻擊我.";
			next;
			mes "[可疑的貓]";
			mes "天哪, 如果不是我突然肚子疼, 我可以狠狠的把它的臉給抓花!";
			mes "哎, 你可別以為我怕了米洛斯!";
			next;
			mes "[可疑的貓]";
			mes "如果你想太平回去, 最好K掉那個醜陋的狗! 不然肯定會被咬!";
			next;
			switch(select("- 幫他:- 站著看戲")) {
			case 1:
				mes "[可疑的貓]";
				mes "好了, 目標數量為20! 祝你好運!";
				setquest 2289;
				close;
			case 2:
				mes "[可疑的貓]";
				mes "呸! 你不管我死活啊? 好吧, 你這個叛徒!";
				close;
			}
		} else if (.@hunting == 0 || .@hunting == 1) {
			mes "[可疑的貓]";
			mes "畢萊特獵殺的順利嗎?";
			next;
			switch(select("- 當然.:- 我不幹了.")) {
			case 1:
				mes "[可疑的貓]";
				mes "很好. 繼續努力.";
				close;
			case 2:
				mes "[可疑的貓]";
				mes "神馬? 不負責任的混蛋!";
				erasequest 2289;
				close;
			}
		} else if (.@hunting == 2) {
			mes "[可疑的貓]";
			mes "你看, 這個就不錯! 非常好的一個!";
			mes "你考慮下在來找我吧, 怎麼樣? 哈哈!";
			changequest 2289,2290;
			getexp 300000,100000;
			close;
		}
	case 3:
		set .@playtime, checkquest(2291,PLAYTIME);
		if (.@playtime == 0 || .@playtime == 1) {
			mes "[可疑的貓]";
			mes "很好, 我會在你需要幫忙的時候第一時間出現的, 不過我覺得肚子疼的問題應該是首要問題.....";
			close;
		} else if (.@playtime == 2)
			erasequest 2291;
		set .@hunting, checkquest(2292,HUNTING);
		if (.@hunting == -1) {
			mes "[可疑的貓]";
			mes "這裡的珍貴寶物都應該被隱藏在深層的秘密地方!";
			mes "我敢肯定, 這具木乃伊收藏的珍貴的寶物都藏在地下2層.";
			next;
			mes "[可疑的貓]";
			mes "在我知道這些事情的時候, 那些噩夢木乃伊抓住了我, 還打我的腦袋!";
			next;
			mes "[可疑的貓]";
			mes "我敢肯定, 它們正在試圖通過減少我的腦細胞, 把我弄成白痴來阻止我尋找寶藏!!";
			next;
			mes "[可疑的貓]";
			mes "他們不能阻止我! 讓我們甩掉這些噩夢木乃伊!!";
			next;
			mes "[可疑的貓]";
			mes "別問為什麼! 我敢肯定的說, 你也不想這些美麗的寶藏就浪費在這個烏漆麻黑的地洞裡吧!";
			next;
			switch(select("- 幫助他:- 在邊上看熱鬧")) {
			case 1:
				mes "[可疑的貓]";
				mes "好了, 目標數量為20! 祝你好運!";
				setquest 2292;
				close;
			case 2:
				mes "[可疑的貓]";
				mes "呸, 你這麼冷血.";
				close;
			}
		} else if (.@hunting == 0 || .@hunting == 1) {
			mes "[可疑的貓]";
			mes "木乃伊獵殺的順利嗎?";
			next;
			switch(select("- 當然.:- 我不幹了.")) {
			case 1:
				mes "[可疑的貓]";
				mes "很好. 繼續努力.";
				close;
			case 2:
				mes "[可疑的貓]";
				mes "神馬? 不負責任的混蛋!";
				erasequest 2292;
				close;
			}
		} else if (.@hunting == 2) {
			mes "[可疑的貓]";
			mes "你看, 這個就不錯! 非常好的一個!";
			mes "你考慮下在來找我吧, 怎麼樣? 哈哈!";
			changequest 2292,2291;
			getexp 600000,200000;
			close;
		}
	case 4:
		close;
	}
}
