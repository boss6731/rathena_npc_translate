//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================

//===== rAthena Script =======================================
//= HD Refiners
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Refiners that use HD ores to refine equipment. Upon
//= failure, the equipment is not destroyed; rather, its
//= refine level decreases by 1. The success rate is identical
//= to that for Enriched ores.
//= - "Blacksmith Mighty Hammer" only refines from +7~9.
//= - "Basta" only refines from +10 and up.
//===== Additional Comments: =================================
//= 1.0 First version. [Euphy]
//============================================================

// Blacksmith Mighty Hammer (+7~9) :: cash_smelting79
//============================================================
-	script	::MightyHammer	-1,{
	disable_items;
	mes "[鐵匠 麥特·鐵錘]";
	mes "不像其他人, 我是一個鐵匠, 精煉的物品數量非常有限.";
	mes "我只精煉等級在 ^CC0000+7 到 +9^000000 之間的裝備.";
	next;
	mes "[鐵匠 麥特·鐵錘]";
	mes "我可以在精煉失敗的時候完整的保護裝備不損壞, 僅讓裝備下降一個精煉等級, 是不是很偉大?";
	next;
	mes "[鐵匠 麥特·鐵錘]";
	mes "好吧讓我們言歸正傳, 你想要精煉哪個裝備?";
	next;
	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for(set .@i,1; .@i<=10; set .@i,.@i+1)
		set .@menu$, .@menu$ + ( getequipisequiped(.@indices[.@i]) ? getequipname(.@indices[.@i]) : F_getpositionname(.@indices[.@i]) +" - [未裝備]" ) +":";
	set .@part, .@indices[ select(.@menu$) ];
	if (!getequipisequiped(.@part)) {
		mes "[鐵匠 麥特·鐵錘]";
		switch(.@part) {
		case 1:
			mes "我是個鐵匠, 不是一個髮型師.";
			break;
		case 2:
			mes "用我的鐵錘, 我會讓你成為天上的明星.";
			break;
		case 3:
		case 4:
			mes "做假肢可不是我的專長.";
			break;
		case 5:
			mes "穿上要精煉的裝備!";
			break;
		case 6:
			mes "這是哪裡來腳臭?";
			break;
		case 7:
		case 8:
			mes "裝飾品在哪裡?";
			break;
		case 9:
			mes "你想讓我敲哪裡?";
			break;
		case 10:
			mes "噗? 你想讓我幹嘛?";
			break;
		}
		close;
	}
	if (!getequipisenableref(.@part)) {
		mes "[鐵匠 麥特·鐵錘]";
		mes "這裝備不能精煉.";
		close;
	}
	if (getequiprefinerycnt(.@part) < 7 || getequiprefinerycnt(.@part) > 9) {
		mes "[鐵匠 麥特·鐵錘]";
		mes "我只精煉精煉值在 +7 到 +9 之間的裝備.";
		close;
	}
	switch(getequipweaponlv(.@part)) {
	default:
	case 0:
		set .@price,20000;
		set .@material,6241; //HD_Elunium
		break;
	case 1:
	case 2:
	case 3:
	case 4:
		set .@price,20000;
		set .@material,6240; //HD_Oridecon
		break;
	}
	mes "[鐵匠 麥特·鐵錘]";
	mes "精煉這件裝備你需要支付 ^ff9999"+getitemname(.@material)+"^000000 和 20,000 zeny 服務費.";
	mes "你準備好材料和錢了嗎?";
	next;
	if(select("- 是:- 否") == 2) {
		mes "[鐵匠 麥特·鐵錘]";
		mes "裝備好了再來找我吧.";
		close;
	}
	if (getequippercentrefinery(.@part) < 100) {
		mes "[鐵匠 麥特·鐵錘]";
		mes "看上去精煉這個裝備可能會失敗.";
		mes "如果失敗的話, 它將會降低一個精煉級別.";
		mes "你是否想繼續?";
		next;
		if(select("- 是:- 否") == 2) {
			mes "[鐵匠 麥特·鐵錘]";
			mes "只有那些不懼怕害怕失敗的人, 才會獲得一個舉世無雙的成就.";
			close;
		}
	}
	if (countitem(.@material) == 0 || Zeny < .@price) {
		mes "[鐵匠 麥特·鐵錘]";
		mes "你剛才不是說你一切準備好了嗎?";
		close;
	}
	delitem .@material,1;
	set Zeny, Zeny-.@price;
	mes "[鐵匠 麥特·鐵錘]";
	mes "鏘! 鏘! 鏘!";
	if (getequippercentrefinery(.@part) > rand(100) || getequippercentrefinery(.@part) > rand(100)) {
		successrefitem .@part;
		next;
		emotion ET_BEST;
		mes "[鐵匠 麥特·鐵錘]";
		mes "每當我的腦海中響起這種聲音.";
		mes "我就知道成功了, 拿去, 完美的作品!";
		close;
	}
	downrefitem .@part;
	next;
	emotion ET_HUK;
	mes "[鐵匠 麥特·鐵錘]";
	mes "唉呀!!";
	next;
	mes "[鐵匠 麥特·鐵錘]";
	mes "相信你絕不會埋怨我把你的精煉值弄掉了1級.";
	close;
}
prt_in,59,54,3	duplicate(MightyHammer)	麥特·鐵錘#prt	826
morocc_in,65,30,3	duplicate(MightyHammer)	麥特·鐵錘#morocc	826
payon,148,176,3	duplicate(MightyHammer)	麥特·鐵錘#pay	826
alberta_in,16,56,3	duplicate(MightyHammer)	麥特·鐵錘#alb	826
yuno_in01,171,18,3	duplicate(MightyHammer)	麥特·鐵錘#yuno	826
ein_in01,22,82,3	duplicate(MightyHammer)	麥特·鐵錘#ein	826
lhz_in02,280,19,3	duplicate(MightyHammer)	麥特·鐵錘#lhz	826

// iRO NPC locations:
// payon,174,133,4	duplicate(MightyHammer)	Mighty Hammer#im	826

// Basta (+10 and up) :: cash_smelting
//============================================================
-	script	::Basta	-1,{
	disable_items;
	mes "[巴斯塔]";
	mes "我是世界上最好的鐵匠, 巴斯塔.";
	mes "不過我不會做平凡的精煉.";
	mes "我只精煉 ^CC0000大於等於 +10^000000 的裝備.";
	next;
	mes "[巴斯塔]";
	mes "你想精煉哪個裝備?";
	next;
	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for(set .@i,1; .@i<=10; set .@i,.@i+1)
		set .@menu$, .@menu$ + ( getequipisequiped(.@indices[.@i]) ? getequipname(.@indices[.@i]) : F_getpositionname(.@indices[.@i]) +" - [未裝備]" ) +":";
	set .@part, .@indices[ select(.@menu$) ];
	if (!getequipisequiped(.@part)) {
		mes "[巴斯塔]";
		switch(.@part) {
		case 1:
			mes "你的頭難道就是個裝備?";
			break;
		case 2:
			mes "你想讓我幹嘛?";
			break;
		case 3:
		case 4:
			mes "做假肢可不是我的專長.";
			break;
		case 5:
			mes "你到底知不知道什麼是披肩?";
			break;
		case 6:
			mes "如果想精煉你的腳, 那你找錯人了, 你應該去跑幾次馬拉松.";
			break;
		case 7:
		case 8:
			mes "裝飾品在哪裡?";
			break;
		case 9:
			mes "好吧... 我沒有看到任何裝備可以精煉.";
			break;
		case 10:
			mes "我不能讓你變聰明, 去找你的小學老師去.";
			break;
		}
		close;
	}
	if (!getequipisenableref(.@part)) {
		mes "[巴斯塔]";
		mes "我不能精煉這件裝備, 完全沒有辦法.";
		close;
	}
	if (getequiprefinerycnt(.@part) < 10) {
		mes "[巴斯塔]";
		mes "難道我沒告訴過你? 我只會精煉 +10 以上的裝備.";
		close;
	}
	if (getequiprefinerycnt(.@part) == 20) {
		mes "[巴斯塔]";
		mes "這件裝備已經完美了, 不需要再次精煉~";
		close;
	}
	switch(getequipweaponlv(.@part)) {
	default:
	case 0:
		set .@price,100000;
		set .@material,6225; //HD_Carnium
		set .@type$,"防具";
		break;
	case 1:
	case 2:
	case 3:
	case 4:
		set .@price,100000;
		set .@material,6226; //HD_Bradium
		set .@type$,"武器";
		break;
	}
	mes "[巴斯塔]";
	mes "唔... 這就是你想精煉的裝備?";
	mes "要精煉這件裝備, 我需要收取 1 ^ff9999"+getitemname(.@material)+"^000000 和 100,000 zeny 的手續費.";
	mes "你確定要繼續精煉嗎?";
	next;
	if(select("- 是:- 否") == 2) {
		mes "[巴斯塔]";
		mes "好吧. 如你所願...";
		close;
	}
	if (getequippercentrefinery(.@part) < 100) {
		mes "[巴斯塔]";
		mes "這件 "+.@type$+" 已經精煉的足夠高級別了.";
		mes "如果繼續嘗試精煉的話, 精煉級別有可能反而會降低.";
		next;
		mes "[巴斯塔]";
		mes "我和其它地方的鐵匠不同.";
		mes "精煉降級可能會一下子掉 3 或者 4級... 這聽起來很嚇人.";
		mes "但是在我這裡最多掉 1 級.";
		next;
		mes "[巴斯塔]";
		mes "相比其他鐵匠, 風險較小.";
		mes "我已經給了你所有的忠告. 你還想繼續嘗試嗎?";
		next;
		if(select("- 是:- 否") == 2) {
			mes "[巴斯塔]";
			mes "好吧~";
			mes "放棄挑戰, 可能也是一種智慧的表現.";
			close;
		}
	}
	if (countitem(.@material) == 0 || Zeny < .@price) {
		mes "[巴斯塔]";
		mes "唔... 你沒帶齊所需的所有材料.";
		mes "等你都帶了再來吧.";
		close;
	}
	delitem .@material,1;
	set Zeny, Zeny-.@price;
	mes "Pow! Pow! Pow! Pow!";
	if (getequippercentrefinery(.@part) > rand(100) || getequippercentrefinery(.@part) > rand(100)) {
		successrefitem .@part;
		next;
		emotion ET_BEST;
		mes "[巴斯塔]";
		mes "很好! 幹的漂亮!!";
		mes "我真的是整個世界裡最好的鐵匠!";
		close;
	}
	downrefitem .@part;
	next;
	emotion (!rand(5))?ET_MONEY:ET_HUK;
	mes "[巴斯塔]";
	mes "Aaaaaaaaaaak!!!";
	next;
	mes "[巴斯塔]";
	mes "他媽的!";
	mes "精煉失敗, 精煉值降低了!";
	mes "就算是最好的鐵匠, 也不會每次都100%成功的!";
	mes "太倒黴了.";
	next;
	mes "[巴斯塔]";
	mes "下次我會做的更好的! 別擔心!";
	close;
}
prt_in,57,54,3	duplicate(Basta)	巴斯塔#prt	826
morocc_in,68,30,3	duplicate(Basta)	巴斯塔#morocc	826
payon,148,174,3	duplicate(Basta)	巴斯塔#payon	826
alberta_in,18,56,3	duplicate(Basta)	巴斯塔#alberta	826
yuno_in01,173,18,3	duplicate(Basta)	巴斯塔#yuno	826
ein_in01,24,82,3	duplicate(Basta)	巴斯塔#einbroch	826
lhz_in02,280,17,3	duplicate(Basta)	巴斯塔#lighthalzen	826
