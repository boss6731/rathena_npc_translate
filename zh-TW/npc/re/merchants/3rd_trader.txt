//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================

//===== rAthena Script =======================================
//= 3rd Item Seller
//===== By: ================================================== 
//= Masao, Mercurial
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Sells some 3rd Job related items:
//= - Poison Herbs, Rune Stones, Rare Herbs, Points
//===== Additional Comments: ================================= 
//= 1.0 First Version.
//= 1.1 Optimized Poison Herb Salesman NPC
//= 1.2 Optimized and standardized. [Euphy]
//= 1.3 Added Malangdo/Mora Points NPC. [Euphy]
//= 1.4 Updated to match the official scripts. [Euphy]
//= 1.5 Added Izlude duplicates. [Euphy]
//= 1.5a Remove useless array & misc. [Capuche]
//============================================================ 

// Poison Herb Merchants (Guillotine Cross) :: poison_herb_3rd_trader
//============================================================
-	script	::phs	-1,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "- 請稍等 !! -";
		mes "- 當前您所攜帶的物品 -";
		mes "- 種類過於繁多或者超過負重. -";
		mes "- 請稍做處理後 -";
		mes "- 再進行交易. -";
		close;
	}
	mes "[毒藥藥草商人]";
	mes "我是專業的";
	mes "毒藥藥草商人.";
	mes "每個藥草的售價 4,000 zeny.";
	mes "您想要買哪個?";
	next;
	set .@i, select("- 夾竹桃:- 馬纓丹:- 斑葉竹:- 蛇足草:- 賽莨菪:- 透骨草:- 製毒工具箱:- 取消")-1;
	mes "[毒藥藥草商人]";
	if (.@i < 6) {
		.@item = 7932+.@i;
		.@price = 4000;
		.@max = 2000;
	} else if (.@i == 6) {
		.@item = 7931;
		.@price = 5000;
		.@max = 500;
		mes "每個製毒工具箱費用 5,000 zeny.";
	} else {
		mes "好吧, 我明白了. 請隨時惠顧~!";
		close;
	}
	mes "您需要購買多少個?";
	mes "您最大可以購買 '"+.@max+"' 個.";
	mes "如果您想取消, 請按  '0'.";
	next;
	input .@amount;
	if (.@amount == 0) {
		mes "[毒藥藥草商人]";
		mes "您已經取消交易.";
		close;
	}
	if (.@amount > .@max) {
		mes "[毒藥藥草商人]";
		mes "數量必須小於 "+callfunc("F_InsertComma",.@max)+"!";
		close;
	}
	.@total = .@amount * .@price;
	if (Zeny < .@total) {
		mes "[毒藥藥草商人]";
		mes "您的錢不夠.";
		mes "請先檢查您所攜帶的Zeny.";
		close;
	}
	Zeny = Zeny - .@total;
	getitem .@item, .@amount;
	mes "[毒藥藥草商人]";
	mes "謝謝. 歡迎下次再來~!";
	close;
}
job3_guil01,79,96,3	duplicate(phs)	毒藥藥草商人	4_F_YUNYANG
morocc,191,94,4	duplicate(phs)	毒藥藥草商人#moc	4_F_YUNYANG	//Official: (190,96) -> in a wall
lhz_in02,16,205,4	duplicate(phs)	毒藥藥草商人#lhz	4_F_YUNYANG

// Rune Stone Merchants (Rune Knight) :: rune_3rd_trader
//============================================================
-	script	::runesale	-1,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "- 請稍等 !! -";
		mes "- 當前您所攜帶的物品 -";
		mes "- 種類過於繁多或者超過負重. -";
		mes "- 請稍做處理後 -";
		mes "- 再進行交易. -";
		close;
	}
	mes "[符文商人]";
	mes "嘿, 您需要符文嗎?!";
	mes "我出售各種符文石!";
	mes "您喜歡哪個?";
	next;
	.@i = select("- 購買高級符文原石.:- 購買普通符文原石.");
	mes "[符文商人]";
	if (.@i == 1) {
		mes "高級符文原石!";
		mes "需要 2,500 zeny 每個!";
		.@item = 12734; //Runstone_Quality
		.@price = 2500;
	} else {
		mes "普通符文原石?";
		mes "需要 1,000 zeny 每個!";
		.@item = 12737; //Runstone_Ordinary
		.@price = 1000;
	}
	mes "告訴我您需要多少個,";
	mes "但記住, 您最多";
	mes "只能購買 '2000' 個.";
	next;
	input .@amount;
	if (.@amount == 0) {
		mes "[符文商人]";
		mes "您不買嗎? 請走開!";
		close;
	}
	if (.@amount > 2000) {
		mes "[符文商人]";
		mes "數量必須小於 2,000!";
		close;
	}
	.@total = .@amount * .@price;
	if (Zeny < .@total) {
		mes "[符文商人]";
		mes "Zeny! Zeny!";
		mes "您的錢不夠.";
		mes "請先檢查下您有多少錢.";
		close;
	}
	if (!checkweight(.@item,.@amount)) {
		mes "[符文商人]";
		mes "看上去您應該拿不了呢麼多, 請再次嘗試一下?!";
		close;
	}
	Zeny = Zeny - .@total;
	getitem .@item, .@amount;
	mes "[符文商人]";
	mes "謝謝.";
	mes "請下次再來~!";
	close;
}
job3_rune01,90,62,3	duplicate(runesale)	符文商人#job3	4_M_YURI
prontera,168,228,3	duplicate(runesale)	符文商人#prt	4_M_YURI

// Rare Herb Merchants (Guillotine Cross) :: rare_herb_3rd_trader
//============================================================
job3_guil01,91,93,3	script	稀有草藥收集者	1_M_03,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "- 請稍等 !! -";
		mes "- 當前您所攜帶的物品 -";
		mes "- 種類過於繁多或者超過負重. -";
		mes "- 請稍做處理後 -";
		mes "- 再進行交易. -";
		close;
	}
	if (Class == Job_Guillotine_Cross || Class == Job_Guillotine_Cross_T || Class == Job_Baby_Cross) {
		mes "[稀有草藥收集者]";
		mes "我徘徊在世界各地收集罕見的毒藥藥材. 最近, 我開始處理藥草稱為Izidor. 如果您有興趣, 您可以購買它.";
		next;
		.@i = select("- 我應該如何購買?:- 用家畜血來交換:- 用苦味草來交換:- 用青嫩毒草來交換:- 用冰雕玫瑰來交換:- 用曼特療傷藥來交換:- 用西納雷花來交換")-2;
		if (.@i == -1) {
			mes "[稀有草藥收集者]";
			mes "您可以花 10,000 zeny 購買他們, 同時還需要, 家畜血, 苦味草, 青嫩毒草, 冰雕玫瑰, 曼特療傷藥, 西納雷花之一.";
			next;
			mes "[稀有草藥收集者]";
			mes "為什麼我問錢? 這只是付出一點對於一個像我這樣的收藏家. 哈哈哈...";
			close;
		} else {
			setarray .@exchange[0],702,621,631,749,605,703;
			.@item = .@exchange[.@i];
			.@price = 10000;

			mes "[稀有草藥收集者]";
			mes "您要多少?";
			mes "您只能購買最多 '2000' 個.";
			mes "如果您想取消, 請按  '0'.";
			next;
			input .@amount;
			if (.@amount == 0) {
				mes "[稀有草藥收集者]";
				mes "您已經取消交易.";
				close;
			}
			if (.@amount > 2000) {
				mes "[稀有草藥收集者]";
				mes "數量必須小於 2,000!";
				close;
			}
			.@total = .@amount * .@price;
			if (countitem(.@item) < .@amount || Zeny < .@total) {
				mes "[稀有草藥收集者]";
				mes "等您裝備好所有的東西我們再進行交易吧.";
				close;
			}
			mes "[稀有草藥收集者]";
			mes "我收錢和特殊物品.";
			delitem .@item, .@amount;
			Zeny = Zeny - .@total;
			getitem 709, .@amount; //Izidor
			close;
		}
	}
	mes "[稀有草藥收集者]";
	mes "我徘徊在世界各地收集罕見的毒藥藥材. 但我想賣給像您這樣的人...哈哈哈...";
	close;
}

// 精華商人s (Sorcerer) :: point_3rd_trader
//============================================================
-	script	::pss	-1,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "- 請稍等 !! -";
		mes "- 當前您所攜帶的物品 -";
		mes "- 種類過於繁多或者超過負重. -";
		mes "- 請稍做處理後 -";
		mes "- 再進行交易. -";
		close;
	}
	mes "[精華商人]";
	mes "您好. 我賣元素使的催化劑被稱為 ^FF0000精華^000000. 您需要什麼?";
	next;
	.@i = select("- 火靈碎片 - 200z:- 地靈精華 - 200z:- 水靈碎片 - 200z:- 風靈精華 - 200z:- 取消")-1;
	mes "[精華商人]";
	if (.@i == 4) {
		mes "沒有您需要的東西嗎?";
		close;
	}
	setarray .@itemid[0],6360,6363,6361,6362;
	setarray .@color$[0],"火靈","地靈","水靈","風靈";
	mes "您選擇了 "+.@color$[.@i]+" 精華.";
	mes "您需要購買多少個?";
	mes "如果您想取消, 請按 '0'.";
	next;
	input .@amount;
	mes "[精華商人]";
	if (.@amount == 0) {
		mes "您已經取消交易.";
		close;
	}
	if (Zeny < .@amount*200) {
		mes "您的錢不夠.";
		close;
	}
	if (!checkweight(.@itemid[.@i],.@amount)) {
		mes "您沒有足夠的空間來存放您購買的各類精華.";
		close;
	}
	Zeny = Zeny-(.@amount*200);
	getitem .@itemid[.@i],.@amount;
	mes "非常感謝, 再見~!";
	close;
}
gef_tower,105,172,5	duplicate(pss)	精華商人#Sorcerer	8_F_GIRL
prt_in,131,66,0	duplicate(pss)	精華商人#Prontera	8_F_GIRL
alberta,105,52,7	duplicate(pss)	精華商人#Alberta	8_F_GIRL
aldebaran,133,114,5	duplicate(pss)	精華商人#Aldebaran	8_F_GIRL
comodo,193,159,5	duplicate(pss)	精華商人#Comodo	8_F_GIRL
geffen,129,49,5	duplicate(pss)	精華商人#Geffen	8_F_GIRL
izlude,138,163,5	duplicate(pss)	精華商人#Izlude	8_F_GIRL	// Old coordinates: izlude (135,121)
izlude_a,138,163,5	duplicate(pss)	精華商人#Izlude_a	8_F_GIRL
izlude_b,138,163,5	duplicate(pss)	精華商人#Izlude_b	8_F_GIRL
izlude_c,138,163,5	duplicate(pss)	精華商人#Izlude_c	8_F_GIRL
izlude_d,138,163,5	duplicate(pss)	精華商人#Izlude_d	8_F_GIRL
malangdo,214,163,5	duplicate(pss)	精華商人#Malangdo	8_F_GIRL
mora,115,118,3	duplicate(pss)	精華商人#Mora	8_F_GIRL
ra_in01,256,273,3	duplicate(pss)	精華商人#Rachel	8_F_GIRL
veins,202,128,5	duplicate(pss)	精華商人#Veins	8_F_GIRL
dicastes01,207,200,5	duplicate(pss)	精華商人#Dicastes	8_F_GIRL
manuk,261,206,3	duplicate(pss)	精華商人#Manuk	8_F_GIRL
splendide,207,160,5	duplicate(pss)	精華商人#Splendide	8_F_GIRL
mid_camp,224,237,3	duplicate(pss)	精華商人#Midgard	8_F_GIRL
