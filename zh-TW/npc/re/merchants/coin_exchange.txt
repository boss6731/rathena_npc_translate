//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================


//===== rAthena Script =======================================
//= Coin Merchants
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Renewal coin redemption NPCs.
//===== Additional Comments: =================================
//= 1.0 First version. [Euphy/Lemongrass]
//= 1.1 Added remaining Malangdo traders. [Euphy/Lemongrass]
//= 1.2 Added Eclage traders (not fully complete). [Euphy]
//============================================================

// Malangdo Item Machines :: mal_yong
//============================================================
malangdo,220,167,5	script	硬幣兌換CX-1	564,{
	if (checkweight(1201,1) == 0) {
		mes "您攜帶的物品種類過多. 等您解決這個問題後我們才能繼續.";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "無法繼續, 因為您的負重不足. 等您解決負重問題後我們再繼續.";
		close;
	}
	mes "[硬幣兌換CX-1]";
	mes "歡迎使用CX-1. 如果您有硬幣, 您可以在這裡進行各類兌換.";
	next;
	setarray .@coins[1],
		6417, //Silvervine
		6418, //Agrade_Coin
		6419, //Bgrade_Coin
		6420, //Cgrade_Coin
		6421, //Dgrade_Coin
		6422; //Egrade_Coin
	set .@menu$,"Stop:";
	for(set .@i,1; .@i<=6; set .@i,.@i+1) {
		if (countitem(.@coins[.@i]))
			set .@menu$, .@menu$+getitemname(.@coins[.@i])+":";
		else
			set .@menu$, .@menu$+"^aaaaaa"+getitemname(.@coins[.@i])+" (無)^000000:";
	}
	set .@i, select(.@menu$);
	switch(.@i) {
	case 1:
		mes "[硬幣兌換CX-1]";
		mes "歡迎光臨.";
		close;
	default:
		set .@coin, .@coins[.@i-1];
		set .@coin_select, .@i-1;
		break;
	}
	if (countitem(.@coin) == 0) {
		mes "[硬幣兌換CX-1]";
		mes "您選擇出讓的是您沒有的硬幣種類.";
		mes "歡迎光臨.";
		close;
	}
	mes "[硬幣兌換CX-1]";
	mes "----兌換匯率----";
	mes "由低級兌換高級 - 30 換 10";
	mes "由高級兌換低級 - 10 換 30";
	mes "------------------------";
	mes "^ff3333如果您選擇兌換業務, 我們會按當日的行情收取一部分手續費.^000000";
	next;
	setarray .@exchange_name$[0],"由低至高","由高至低";
	setarray .@exchange_rate[0],30,10;
	setarray .@exchange_loss[0],rand(1,4),rand(1,2); // Amount deducted per exchange.
	setarray .@exchange_id[0],.@coins[.@coin_select-1],.@coins[.@coin_select+1];
	if (.@exchange_id[0] == .@coins[1]) set .@exchange_id[0],0; // Cannot exchange for Silvervine.
	set .@menu$,"Stop:";
	for(set .@i,0; .@i<2; set .@i,.@i+1) {
		if (.@exchange_id[.@i] == 0)
			set .@menu$, .@menu$+"^ff3333無法兌換 "+.@exchange_name$[.@i]+" 硬幣^000000:";
		else if (countitem(.@coin) < .@exchange_rate[.@i])
			set .@menu$, .@menu$+"^aaaaaa兌換為 "+.@exchange_name$[.@i]+" 硬幣 (零)^000000:";
		else
			set .@menu$, .@menu$+"兌換為 "+.@exchange_name$[.@i]+" 硬幣 - "+getitemname(.@coin)+" ("+.@exchange_rate[.@i]+" needed):";
	}
	set .@i, select(.@menu$)-2;
	if (.@i == -1) {
		mes "[硬幣兌換CX-1]";
		mes "歡迎光臨.";
		close;
	}
	if (.@exchange_id[.@i] == 0) {
		mes "[硬幣兌換CX-1]";
		mes "這個硬幣無法兌換成 "+.@exchange_name$[.@i]+" 硬幣.";
		mes "歡迎光臨.";
		close;
	}
	if (countitem(.@coin) < .@exchange_rate[.@i]) {
		mes "[硬幣兌換CX-1]";
		mes "您所持的硬幣不足以兌換 "+.@exchange_name$[.@i]+" 硬幣.";
		mes "歡迎光臨.";
		close;
	}
	set .@exchange_total, .@exchange_rate[(!.@i)] - .@exchange_loss[(!.@i)];
	delitem .@coin, .@exchange_rate[.@i];
	getitem .@exchange_id[.@i], .@exchange_total;
	mes "[硬幣兌換CX-1]";
	mes "^003399"+.@exchange_rate[.@i]+"^000000 單位(s) 的 ^003399 "+getitemname(.@coin)+"^000000 換取.";
	mes "我想把它們兌換成 ^003399"+.@exchange_name$[.@i]+" 硬幣^000000, 扣除 ^003399"+.@exchange_loss[(!.@i)]+"^000000 單位(s), ^003399"+.@exchange_total+"^000000 單位(s) 剩餘.";
	close;
}

malangdo,218,165,5	script	特別版自動售貨機	2_DROP_MACHINE,{
	if (checkweight(1201,1) == 0) {
		mes "您攜帶的物品種類過多. 等您解決這個問題後我們才能繼續.";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "無法繼續, 因為您的負重不足. 等您解決負重問題後我們再繼續.";
		close;
	}
	mes "[特別版自動售貨機]";
	mes "歡迎使用CX-82. 使用新鮮水果可以兌換到綿綿島很多特色的物品.";
	next;
	callsub L_AddItem, //DisplayName,ItemID,Cost
		"- 海神護身符[23小時]",16740,4,
		"- 海神護身符[7天]",16763,12,
		"- 海神護身符[15天]",16764,24,
		"- 捕章魚棍[23小時]",16765,2,
		"- 捕章魚棍[3天]",16766,5,
		"- 捕章魚棍[7天]",16767,10,
		"- 高級潤滑油",6441,56,
		"- 普通潤滑油",6440,36,
		"- 王水瓶",6443,192;
L_AddItem:
	set .@menu$,"詳情說明:";
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+3)
		set .@menu$, .@menu$+getarg(.@i)+":";
	set .@i, select(.@menu$)-2;
	if (.@i == -1) {
		mes "[特別版自動售貨機]";
		mes "海神護身符是進入排水渠副本的必要物品.";
		next;
		mes "[特別版自動售貨機]";
		mes "捕章魚棍是進入章魚洞穴副本的必要物品.";
		next;
		mes "[特別版自動售貨機]";
		mes "高級潤滑油與普通潤滑油是用於分離的裝備與卡片. 如果你遇到專家傑裡米的話, 他會專門為你解釋相關的用途.";
		next;
		mes "[特別版自動售貨機]";
		mes "王水瓶是一件特殊的物品, 使用它可以將裝備與MVP卡片拆分. 這是潤滑油無法做到的.";
		close;
	}
	set .@cost, getarg(.@i*3+2);
	mes "[特別版自動售貨機]";
	mes "你選擇 ^005500"+getarg(.@i*3)+"^000000. 購買這些, 您需要花費 ^005500"+.@cost+"^000000 單位(s) 的獮猴桃果實?";
	next;
	if (countitem(6417) < .@cost)
		set .@ven_menu$, "^999999獮猴桃果實(減少 "+(.@cost - countitem(6417))+")^000000";
	else
		set .@ven_menu$, "購買 - 獮猴桃果實 (有 "+countitem(6417)+")";
	switch(select("停止:"+.@ven_menu$)) {
	case 1:
		mes "[特別版自動售貨機]";
		mes "歡迎光臨.";
		close;
	case 2:
		if (countitem(6417) < .@cost) {
			mes "[特別版自動售貨機]";
			mes "獮猴桃果實數量不足.";
			mes "關閉交易.";
			mes "歡迎光臨.";
			close;
		}
		mes "[特別版自動售貨機]";
		mes "交易完成. 歡迎光臨.";
		delitem 6417,.@cost; //Silvervine
		getitem getarg(.@i*3+1),1;
		close;
	}
}

// Malangdo Coin Exchange :: mal_mo_coin
//============================================================
malangdo,236,179,5	script	黑市商人K	554,{
	if (checkweight(1201,1) == 0) {
		mes "您攜帶的物品種類過多. 等您解決這個問題後我們才能繼續.";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "無法繼續, 因為您的負重不足. 等您解決負重問題後我們再繼續.";
		close;
	}
	mes "[黑市商人K]";
	mes "歡迎惠顧.";
	mes "我們兌換 ^ff000010個C級貨幣^000000 為 ^3333ff1個莫拉幣^000000. 是否想現在就兌換?";
	next;
	setarray .@coin_amount[0],10,100,500,1000;
	while(1) {
		set .@menu$,"";
		for(set .@i,0; .@i<getarraysize(.@coin_amount); set .@i,.@i+1) {
			if (countitem(6420) >= .@coin_amount[.@i])
				set .@menu$, .@menu$+"兌換 "+(.@coin_amount[.@i]/10)+" 莫拉幣:";
			else
				set .@menu$, .@menu$+"^aaaaaa兌換 "+(.@coin_amount[.@i]/10)+" 莫拉幣 (數量不足)^000000:";
		}
		set .@i, select(.@menu$+"離開")-1;
		if (.@i == getarraysize(.@coin_amount)) {
			mes "[黑市商人K]";
			mes "讓我們下次再交易.";
			close;
		}
		if (countitem(6420) < .@coin_amount[.@i]) {
			mes "[黑市商人K]";
			mes "你需要 ^ff0000"+.@coin_amount[.@i]+"^000000 C級代幣來完成交易. 但是看上去您沒帶夠.";
			close;
		}
		delitem 6420,.@coin_amount[.@i]; //Cgrade_Coin
		getitem 6380,.@coin_amount[.@i]/10; //Mora_Coin
		mes "[黑市商人K]";
		mes "C級代幣 ^003399"+.@coin_amount[.@i]+"^000000 深受歡迎."; //FIXME: Dialogue.
		mes "兌換成 ^003399"+(.@coin_amount[.@i]/10)+"^000000 莫拉幣.";
		next;
		mes "[黑市商人K]";
		mes "是否想繼續?";
		next;
	}
}

malangdo,233,180,3	script	黑色設備TX100	564,{
	if (checkweight(1201,1) == 0) {
		mes "您攜帶的物品種類過多. 等您解決這個問題後我們才能繼續.";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "無法繼續, 因為您的負重不足. 等您解決負重問題後我們再繼續.";
		close;
	}
	mes "[黑色設備TX100]";
	mes "歡迎使用TX100. 您可以通過我在不同級別的硬幣之間進行兌換.";
	next;
	mes "[黑色設備TX100]";
	mes "低級代幣的一次性交易量最大是500個, 高級代幣的一次性交易量最大是99個.";
	next;
	setarray .@coins[1],
		6417, //Silvervine
		6418, //Agrade_Coin
		6419, //Bgrade_Coin
		6420, //Cgrade_Coin
		6421, //Dgrade_Coin
		6422; //Egrade_Coin
	set .@menu$,"離開:";
	for(set .@i,1; .@i<=6; set .@i,.@i+1) {
		if (countitem(.@coins[.@i]))
			set .@menu$, .@menu$+getitemname(.@coins[.@i])+" (有 "+countitem(.@coins[.@i])+"):";
		else
			set .@menu$, .@menu$+"^aaaaaa"+getitemname(.@coins[.@i])+" (無)^000000:";
	}
	set .@i, select(.@menu$)-1;
	if (.@i == 0) {
		mes "[黑色設備TX100]";
		mes "歡迎光臨.";
		close;
	}
	set .@coin, .@coins[.@i];
	setarray .@exchange_name$[0],"由低至高","由高至低";
	setarray .@exchange_id[0],.@coins[.@i-1],.@coins[.@i+1];
	if (.@exchange_id[0] == .@coins[1]) set .@exchange_id[0],0; // Cannot exchange for Silvervine.
	if (.@i > getarraysize(.@coins)) {
		mes "[黑色設備TX100]";
		mes "菜單狀態異常.";
		mes "中止交易. 歡迎光臨.";
		close;
	}
	if (countitem(.@coin) == 0) {
		mes "[黑色設備TX100]";
		mes "您選擇出讓的是您沒有的硬幣種類.";
		mes "中止交易. 歡迎光臨.";
		close;
	}
	mes "[黑色設備TX100]";
	mes "----兌換匯率----";
	mes "低級兌換高級 - 3 比 1";
	mes "高級兌換低級 - 1 比 3";
	mes "---------------------";
	mes "^ff3333兌換過程中我們收取少量的服務費.^000000";
	next;
	set .@menu$, "Quit:";
	if (.@exchange_id[0] == 0)
		set .@menu$, .@menu$+"^ff3333無法兌換更高級別的代幣^000000:";
	else {
		if (countitem(.@coin) >= 99)
			set .@menu$, .@menu$+"兌換 99 "+getitemname(.@coin)+" 到高一級代幣:";
		else
			set .@menu$, .@menu$+"^aaaaaa兌換成高一級代幣 (數量不足)^000000:";
	}
	if (.@exchange_id[1] == 0)
		set .@menu$, .@menu$+"^ff3333無法兌換再低級別的代幣^000000:";
	else {
		if (countitem(.@coin) > 500)
			set .@menu$, .@menu$+"兌換 500 "+getitemname(.@coin)+" 到低一級代幣:";
		else if (countitem(.@coin))
			set .@menu$, .@menu$+"兌換 "+countitem(.@coin)+" "+getitemname(.@coin)+" 到低一級代幣:";
		else
			set .@menu$, .@menu$+"^aaaaaa兌換成低一級代幣 (數量不足)^000000:";
	}
	set .@i, select(.@menu$)-2;
	if (.@i == -1) {
		mes "[黑色設備TX100]";
		mes "歡迎光臨.";
		close;
	}
	if (.@exchange_id[.@i] == 0) {
		mes "[黑色設備TX100]";
		mes "此硬幣不能被兌換成 "+.@exchange_name$[.@i]+" 級代幣.";
		mes "交易中止. 歡迎光臨.";
		close;
	}
	if ((.@i == 0 && countitem(.@coin) < 100) || (.@i == 1 && countitem(.@coin) == 0)) {
		mes "[黑色設備TX100]";
		mes "您的代幣數量不足以兌換 "+.@exchange_name$[.@i]+" 級代幣";
		mes "交易中止. 歡迎光臨.";
		close;
	}
	if (countitem(.@exchange_id[.@i]) > 28000) {
		mes "[黑色設備TX100]";
		mes "您的 "+.@exchange_name$[.@i]+" 級代幣的數量過多. 請您做下處理.";
		close;
	}
	switch(.@i) {
	case 0:
		set .@payment_amount,99;
		set .@reward_amount, .@payment_amount/3;
		set .@coin_text$,"High";
		break;
	case 1:
		set .@payment_amount,(((countitem(.@coin) > 500))?500:countitem(.@coin));
		set .@reward_amount, .@payment_amount*3;
		set .@coin_text$,"Low";
		break;
	}
	set .@fee, rand(1,3);
	delitem .@coin, .@payment_amount;
	getitem .@exchange_id[.@i], .@reward_amount - .@fee;
	mes "[黑色設備TX100]";
	mes "收到 ^003399"+getitemname(.@coin)+"^000000 的 ^003399"+.@payment_amount+"^000000 單位(s).";
	mes "我們將您的代幣兌換為 ^003399"+(.@reward_amount-.@fee)+"^000000 單位(s) 的 ^003399"+.@coin_text$+" 級代幣^000000, 扣除 ^003399"+.@fee+"^000000 單位(s) 的手續費.";
	close;
}

// Malangdo Can Exchange :: mal_can_rep
//============================================================
malangdo,175,145,4	script	問題罐頭回收機構	549,{
	if (checkweight(1201,1) == 0) {
		mes "您攜帶的物品種類過多. 等您解決這個問題後我們才能繼續.";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "無法繼續, 因為您的負重不足. 等您解決負重問題後我們再繼續.";
		close;
	}
	mes "[問題罐頭回收機構]";
	mes "嘿, 就是你... 你有沒有發現周圍都是 ^0000ff綿綿島貓食罐頭^000000?";
	next;
	switch(select("- 為什麼要這麼問?:- 呃, 你的意思是這些?")) {
	case 1:
		mes "[問題罐頭回收機構]";
		mes "嗯.";
		mes "我們最近在當地的機構得到了一些可疑情報...";
		next;
		mes "[問題罐頭回收機構]";
		mes "綿綿島的特產罐頭的生產線發生故障了. 這些流水線上的罐頭按規定需要被處理, 但是卻被一個零售商人給收走了.";
		next;
		mes "[問題罐頭回收機構]";
		mes "他們實際上是很難區分出這些罐頭是否正常. 但他顯然是在說外面的那些 ^0000ff綿綿島貓食罐頭^000000.";
		next;
		mes "[問題罐頭回收機構]";
		mes "出現問題的產品在市場上流通, 我們的職責就是一定要回收問題產品. 你有沒有見到過什麼人像我描述那樣的?";
		next;
		mes "[問題罐頭回收機構]";
		mes "如果你發現什麼可疑的在銷售罐頭, 請一定來告訴我.";
		close;
	case 2:
		if (countitem(12633) == 0) {
			mes "[問題罐頭回收機構]";
			mes "嗯? 我沒看到你身上有 ^0000ff綿綿島貓食罐頭^000000. 不要忘了帶來給我, 如果你找到的話...";
			close;
		}
		break;
	}
	mes "[問題罐頭回收機構]";
	mes "綿綿島貓食罐頭是有問題的產品. 我出3個E級代理來換. 如何?";
	next;
	switch(select("- 我不想換:- ^0000ff1^000000 罐 -> ^0000ff3^000000 E級代幣:- ^0000ff10^000000 罐 -> ^0000ff30^000000 E級代幣:- ^0000ff100^000000 罐 -> ^0000ff300^000000 E級代幣:- 全部換給你")) {
	case 1:
		mes "[問題罐頭回收機構]";
		mes "嗯, 我不會長期在這裡, 如果你想兌換的話, 就乘早.";
		close;
	case 2:
		set .@check,1;
		set .@count,1;
		break;
	case 3:
		set .@check,10;
		set .@count,10;
		break;
	case 4:
		set .@check,100;
		set .@count,100;
		break;
	case 5:
		set .@check,1;
		set .@count,countitem(12633);
		break;
	}
	if (countitem(12633) < .@check) {
		if (.@check == 1) {
			mes "[問題罐頭回收機構]";
			mes "嗯? 我沒看到你身上有 ^0000ff綿綿島貓食罐頭^000000. 不要忘了帶來給我, 如果你找到的話...";
			close;
		} else {
			mes "[問題罐頭回收機構]";
			mes "嗯? 我覺得你不夠東西來交換... 等你弄到足夠多的時候再來找我.";
			close;
		}
	}
	delitem 12633,.@count; //Malang_Cat_Can
	getitem 6422,.@count*3; //Egrade_Coin
	mes "[問題罐頭回收機構]";
	mes "交換了 "+.@count+" 綿綿島貓食罐頭 為 "+(.@count*3)+" E級代幣. 請清點.";
	close;
}

// Malangdo Traders :: malang_trader
//============================================================
function	script	F_mal_coin	{

// Initial dialogue and checks:
// -- callfunc "F_mal_coin",0,"[<NPC Name>]",<NPC ID>;
	if (getarg(0) == 0) {
		if (getarg(2) != 2 && BaseLevel < 50) {
			mes getarg(1);
			mes "啊哈哈~";
			mes "如果你想要買這把法杖,";
			mes "就來找我吧";
			mes "可能你還太小.";
			mes "對於小孩來說這把法杖";
			mes "實在太奢侈了~";
			close;
		}
		mes getarg(1);
		mes "見到你很高興~~";
		mes "我是! 是的~~ 非常正確!";
		mes "我在出售特別的法杖";
		mes "在綿綿島上獨此一家!";
		next;
		mes getarg(1);
		mes "東西如何? 你還在考慮什麼?";
		mes "為什麼不顯看看";
		mes "直觀的了解下這把法杖?";
		next;
		switch(getarg(2)) {
		case 1:
			mes "[流浪商人]";
			mes "噢~";
			mes "這些都是租賃物品, 你的先弄明白, 它們分成例如 ^0000FF1小時^000000 或者 ^0000FF7天^000000 版本.";
			next;
			break;
		case 2:
			break;
		case 3:
			mes "[巡遊商人]";
			mes "為您提供信息,";
			mes "問題劍魚, 海星, 八爪魚, 飛魚, 等物品, 都只能使用1小時.";
			next;
			break;
		}
		mes getarg(1);
		mes "你是不是要看看我的商品呢?";
		next;
		switch(select("- 是:- 否")) {
		case 1:
			if (checkweight(1201,1) == 0) {
				mes getarg(1);
			mes "您攜帶的物品種類過多. 等您解決這個問題後我們才能繼續.";
			close;
			}
			if (MaxWeight - Weight < 2500) {
				mes getarg(1);
			mes "無法繼續, 因為您的負重不足. 等您解決負重問題後我們再繼續.";
			close;
			}
			mes getarg(1);
			mes "好的, 來見識下吧~~~";
			next;
			return;
		case 2:
			mes getarg(1);
			mes "好的, 呢麼. 希望下次有機會.";
			mes "再見.";
			close;
		}
	}

// Exchange function:
// -- callfunc "F_mal_coin",1,"[<NPC Name>]","<Display Name>",<Item ID>,<Egrade_Coin>,<Malang_Sp_Can>,<Silvervine>
	if (getarg(0) == 1) {
		mes getarg(1);
		mes "這件 ^0000FF["+getarg(2)+"]^000000 的價格是";
		if (getarg(4) && getarg(5)) { // Type 1: Egrade_Coin or Malang_Sp_Can
			mes getarg(4)+" E級代幣或者 "+getarg(5)+" 綿綿島風味罐頭.";
			set .@type,1;
			set .@menu$,"- 是.:- 否, 我想用罐頭換.:- 我什麼都不想買.";
		} else if (getarg(5)) { // Type 2: Malang_Sp_Can only
			mes getarg(5)+" 綿綿島風味罐頭.";
			set .@type,2;
			set .@menu$,"- 是.::- 否.";
		} else { // Type 3: Silvervine only
			mes getarg(6)+" 獮猴桃果實.";
			set .@type,3;
			set .@menu$,"- .::- 我不想兌換.";
		}
		next;
		mes getarg(1);
		if (.@type == 1) {
			mes "你想用";
			mes "^0000FF["+getarg(2)+"]^000000 E級代幣來購買?";
		} else {
			mes "你想";
			mes "購買 ^0000FF["+getarg(2)+"]^000000?";
		}
		next;
		switch(select(.@menu$)) {
		case 2:
			set .@type,2;
		case 1:
			switch(.@type) {
			case 1:
				set .@item,6422; //Egrade_Coin
				set .@amount,getarg(4);
				set .@str$,"E級代幣";
				break;
			case 2:
				set .@item,12636; //Malang_Sp_Can
				set .@amount,getarg(5);
				set .@str$,"綿綿島風味罐頭";
				break;
			case 3:
				set .@item,6417; //Silvervine
				set .@amount,getarg(6);
				set .@str$,"獮猴桃果實";
				break;
			}
			if (countitem(.@item) < .@amount) {
				mes getarg(1);
				mes "我很抱歉, 你需要更多的 "+.@str$+".";
				close;
			}
			if (MaxWeight - Weight < getiteminfo(getarg(3),6)) {
				mes getarg(1);
				mes "對不起, 你帶了太多東西了.";
				mes "你需要整理一下你的負重問題. 然後再來.";
				close;
			}
			mes getarg(1);
			mes "謝謝.";
			delitem .@item,.@amount;
			getitem getarg(3),1;
			return;
		case 3:
			mes getarg(1);
			mes "在你購物時";
			mes "請保持謹慎.";
			close;
		}
	}
}
malangdo,162,146,5	script	流浪商人#mal	4_M_MERCAT1,{
	callfunc "F_mal_coin",0,"[流浪商人]",1;
	setarray .@items[1],
		16743,16747,16741,16745,16749,16751, // 1 hour items.
		16742,16744,16746,16748,16752,16750; // 7 day items.
	setarray .@names$[1],
		"劍旗魚槍","鮪魚錘","白帶魚劍","竹莢魚劍","綿綿大蟹弓","斑紋鰻魚弓",
		"白帶魚劍(7天)","劍旗魚槍(7天)","竹莢魚劍(7天)","鮪魚錘(7天)","斑紋鰻魚弓(7天)","綿綿大蟹弓(7天)";
	while(1) {
		set .@i, select(
			"[劍旗魚槍(1小時)] 8 E級代幣/50 罐",
			"[鮪魚錘(1小時)] 8 E級代幣/50 罐",
			"[白帶魚劍(1小時)] 8 E級代幣/50 罐",
			"[竹莢魚劍(1小時)] 8 E級代幣/50 罐",
			"[綿綿大蟹弓(1小時)] 8 E級代幣/50 罐",
			"[斑紋鰻魚弓(1小時)] 8 E級代幣/50 罐",
			"[白帶魚劍(7天)] 10 獮猴桃果實",
			"[劍旗魚槍(7天)] 10 獮猴桃果實",
			"[竹莢魚劍(7天)] 10 獮猴桃果實",
			"[鮪魚錘(7天)] 10 獮猴桃果實",
			"[斑紋鰻魚弓(7天)] 10 獮猴桃果實",
			"[綿綿大蟹弓(7天)] 10 獮猴桃果實",
			"中止交易."
		);
		if (.@i == 13) {
			mes "[流浪商人]";
			mes "好的, 呢麼. 希望下次有機會.";
			mes "再見.";
			close;
		}
		mes "[流浪商人]";
		mes "您對 ^0000FF["+.@names$[.@i]+"]^000000感興趣?";
		switch(.@i) {
		case 1: //Spearfish_Box1
		case 8: //Spearfish_Box2
			mes "這是... 隨著洋流遷徙而來的魚種, 非常好吃!";
			mes "不過這僅僅是食物, 美味~";
			next;
			mes "[流浪商人]";
			mes "當然, 這是 ^DC143C雙手矛^000000.";
			mes "會有很高概率";
			mes "造成對手出血狀態";
			mes "特別是當你使用連刺或者螺旋刺擊的時候~";
			next;
			mes "[流浪商人]";
			mes "更多的細節是...";
			mes "技能攻擊增加 100% 傷害 同時 法術攻擊增加 50% 傷害,";
			mes "基本等級超過100級後 ATK + 30.";
			mes "基本攻擊是 220.";
			next;
			break;
		case 2: //Tuna_Box1
		case 10: //Tuna_Box2
			mes "這是一種非常受歡迎的魚";
			mes "被用於各類菜餚.";
			mes "劍士, 商人, 和服侍系可以使用.";
			mes "就像 ^DC143C鈍器^000000.";
			next;
			mes "[流浪商人]";
			mes "有較高概率使對手暈眩, 並自動詠唱猛擊.";
			mes "是近戰職業非常趁手的兵器.";
			mes "基礎攻擊 180.";
			next;
			mes "[流浪商人]";
			mes "更多的細節...";
			mes "基本等級超過100級後 ATK + 20,";
			mes "低概率冰凍自己.";
			next;
			break;
		case 3: //Hairtail_Box1
		case 7: //Hairtail_Box2
			mes "這種魚看起來像一條長皮帶.";
			next;
			mes "[流浪商人]";
			mes "這是一把 ^DC143C雙手劍^000000";
			mes "最適合劍士系使用.";
			mes "提高 50% 必殺傷害, 並且增加 CRI + 20.";
			mes "基本攻擊是 220.";
			next;
			mes "[流浪商人]";
			mes "更多的細節...";
			mes "基本等級超過100級後 ATK + 50.";
			next;
			break;
		case 4: //Saurel_Box1
		case 9: //Saurel_Box2
			mes "這是一把 ^DC143C匕首^000000 附帶 MATK + 100";
			mes "同時物理攻擊時有一定概率7秒內 ATK + 30.";
			next;
			mes "[流浪商人]";
			mes "更多的細節...";
			mes "法術攻擊時有一定概率7秒內 MATK + 20.";
			mes "基本等級超過100級後 MATK + 10, ATK + 10.";
			mes "基本攻擊 160.";
			next;
			break;
		case 5: //Malang_Crab_Box1
		case 12: //Malang_Crab_Box2
			mes "這是一把 ^DC143C弓^000000 附加 LUK + 3 和 50% 爆擊傷害,";
			mes "基本等級超過100級後增加 20% 遠程攻擊傷害.";
			mes "基本攻擊 120.";
			next;
			mes "[流浪商人]";
			mes "更多的細節...";
			mes "弓箭手, 盜賊和流氓系可以使用.";
			next;
			break;
		case 6: //Brindle_Eel_Box1
		case 11: //Brindle_Eel_Box2
			mes "這是一把 ^DC143C弓^000000.";
			mes "由於它具有極強彈性,";
			mes "所以我們把它當成弓來用.";
			mes "基本攻擊 180.";
			next;
			mes "[流浪商人]";
			mes "更多的細節...";
			mes "遠程物理攻擊有一定概率5秒內使 AGI + 3, 和 ASPD + 2.";
			mes "基本等級超過100級後增加 20% 遠程攻擊傷害.";
			mes "獵人, 詩人, 和舞者系可以使用.";
			next;
			break;
		}
		if (.@i < 7)
			setarray .@price[0],8,50,0; // 1 hour items cost 8 Egrade_Coin or 50 Malang_Sp_Can.
		else
			setarray .@price[0],0,0,10; // 7 day items cost 10 Silvervine.
		callfunc "F_mal_coin",1,"[流浪商人]",.@names$[.@i],.@items[.@i],.@price[0],.@price[1],.@price[2];
		next;
	}
}

malangdo,173,145,4	script	臭臭的商人	4_M_MERCAT2,{
	callfunc "F_mal_coin",0,"[臭臭的商人]",2;
	setarray .@items[1],2873,16015;
	setarray .@names$[1],"貓爪手套","貓爪木錘";
	while(1) {
		set .@i, select(
			"[貓爪手套] 32 E級代幣/200 罐",
			"[貓爪木錘] 32 E級代幣/200 罐",
			"結束採購."
		);
		if (.@i == 3) {
			mes "[臭臭的商人]";
			mes "好的, 呢麼, 希望下次再見.";
			mes "再見.";
			close;
		}
		mes "[臭臭的商人]";
		mes "你對 ^0000FF["+.@names$[.@i]+"]^000000感興趣麼?";
		switch(.@i) {
		case 1: //Cat_Hand_Glove
			mes "這是一個可以帶來幸福的手套";
			mes "戴著它就行了.";
			next;
			mes "[臭臭的商人]";
			mes "這是一個 ^DC143C裝飾品^000000";
			mes "它的防禦 +5.";
			mes "同時還增加";
			mes "VIT + 1, AGI + 1, DEX + 1, LUK + 1.";
			next;
			mes "[臭臭的商人]";
			mes "更多詳細數據...";
			mes "1洞 / 不可精煉 / 不會損壞";
			next;
			break;
		case 2: //Cat_Club
			mes "它看上去不可破壞, 你怎麼認為的?";
			mes "但是我告訴你, 它可以傷害別人.";
			next;
			mes "[臭臭的商人]";
			mes "T這是一個^DC143C錘子^000000 對動物的傷害有15%增幅.";
			mes "基本攻擊 88.";
			mes "其他的數據是";
			mes "武器等級: 1 / SLOT: 3 / 精煉: 可 / 損壞: 不會";
			next;
			break;
		}
		callfunc "F_mal_coin",1,"[臭臭的商人]",.@names$[.@i],.@items[.@i],32,200,0;
		next;
	}
}

malangdo,150,135,5	script	巡遊商人	4_M_MERCAT1,{
	callfunc "F_mal_coin",0,"[巡遊商人]",3;
	setarray .@items[1],12639,12637,12638,12640;
	setarray .@names$[1],"飛魚箱子","鼠婦蟲子彈箱子","八爪魚箱子","劍魚箱子";
	while(1) {
		set .@i, select(
			"[飛魚箱子] 200 罐",
			"[鼠婦蟲子彈箱子] 200 罐",
			"[八爪魚箱子] 200 罐",
			"[劍魚箱子] 200 罐",
			"中止交易."
		);
		if (.@i == 5) {
			mes "[巡遊商人]";
			mes "好的, 呢麼, 希望下次再見.";
			mes "再見.";
			close;
		}
		mes "[巡遊商人]";
		mes "你對 ^0000FF["+.@names$[.@i]+"]^000000感興趣嗎?";
		switch(.@i) {
		case 1: //Flying_Fish_Box
			mes "這裡有些不一樣的武器";
			mes "需要有強大的肌肉力量才可操控.";
			next;
			mes "[巡遊商人]";
			mes "這是一件 ^DC143C投擲武器^000000.";
			mes "當然, 它對對手是致命的.";
			mes "並會在攻擊時激活效果.";
			next;
			mes "[巡遊商人]";
			mes "它是無屬性的, 基礎攻擊 ATK +50.";
			next;
			break;
		case 2: //Gong_Bug_Pocket
			mes "這個業務, 我需要想你解釋一下.";
			mes "這是 ^DC143C子彈^000000 有一定概率使對手暈眩.";
			next;
			mes "[巡遊商人]";
			mes "我覺得有可能";
			mes "我不適合做生意...";
			mes "它是無屬性的, 基礎攻擊 ATK +50.";
			next;
			break;
		case 3: //Dried_Squid_Box
			mes "我要解釋下這個物品.";
			mes "這是一種 ^DC143C投擲武器^000000 有一定概率讓對手陷入黑暗.";
			mes "它是無屬性武器, 基礎攻擊 ATK +50.";
			next;
			mes "[巡遊商人]";
			mes "這已經經過壓縮了";
			mes "被儲存在很小的容器裡,";
			mes "你使用的時候要千萬小心.";
			next;
			break;
		case 4: //Starfish_Box
			mes "我要解釋下這個物品.";
			mes "這是一種 ^DC143C投擲武器^000000 有一定概率使對手陷入暈眩.";
			next;
			mes "[巡遊商人]";
			mes "這已經經過嚴密的包裝了";
			mes "所以你看不到裡面的樣子.";
			mes "它是無屬性武器, 基礎攻擊 ATK +110.";
			next;
			break;
		}
		mes "[巡遊商人]";
		mes "還有一個信息是,";
		mes "一箱有";
		mes "200 個.";
		next;
		callfunc "F_mal_coin",1,"[巡遊商人]",.@names$[.@i],.@items[.@i],0,200,0;
		next;
	}
}

// Eclage
//============================================================
ecl_in01,66,95,2	script	護甲商人納費亞	4_F_FAIRYKID,{
	mes "[護甲商人]";
	mes "你好, 這裡是納費亞的商店, 我賣的都是高級貨.";
	mes "你需要點啥??";
	next;
	set .@i, select("- 力量手套:- 智力手套:- 敏捷手套:- 體力手套:- 靈巧手套:- 幸運手套e");
	mes "[護甲商人]";
	switch(.@i) {
	case 1: // Str Glove
		mes "^3131FF力量手套^000000";
		mes "^3131FFMHP + 100, MSP + 20^000000";
		mes "^3131FFATK+1 增加, 每 STR+10^000000";
		mes "^3131FFATK +1% 增加, 高於 STR 110^000000";
		set .@item,2917; //Str_Glove
		break;
	case 2:
		mes "^3131FF智力手套^000000";
		mes "^3131FFMHP + 100, MSP + 20^000000";
		mes "^3131FFMATK+1 增加, 每 INT+10^000000";
		mes "^3131FFMATK +1% 增加, 高於 INT 110^000000";
		set .@item,2918; //Int_Glove
		break;
	case 3:
		mes "^3131FFAgi Glove^000000";
		mes "^3131FFMHP + 100, MSP + 20^000000";
		mes "^3131FFFLEE+1 增加, 每 AGI+10^000000";
		mes "^3131FFComplete Flee +1 增加, 高於 AGI 110^000000";
		set .@item,2919; //Agi_Glove
		break;
	case 4:
		mes "^3131FFVit Glove^000000";
		mes "^3131FFMHP + 100, MSP + 20^000000";
		mes "^3131FFMHP+50 增加, 每 VIT+10^000000";
		mes "^3131FFMHP+1% 增加, 高於 VIT 110^000000";
		set .@item,2920; //Vit_Glove
		break;
	case 5:
		mes "^3131FFDex Glove^000000";
		mes "^3131FFMHP + 100, MSP + 20^000000";
		mes "^3131FFHIT+1 增加, 每 DEX+10^000000";
		mes "^3131FF遠程攻擊力 +1% 高於 DEX 110^000000";
		set .@item,2921; //Dex_Glove
		break;
	case 6:
		mes "^3131FFLuk Glove^000000";
		mes "^3131FFMHP + 100, MSP + 20^000000";
		mes "^3131FFCRI+1 增加, 每 LUK+10^000000";
		mes "^3131FF暴擊傷害 +1% 高於 LUK 110^000000";
		set .@item,2922; //Luk_Glove
		break;
	}
	mes "^3131FF需要等級: 100^000000";
	mes "^3131FF洞: 0^000000";
	mes "^3131FF重量: 10^000000";
	mes "這些都是這東西的特色.";
	next;
	mes "[護甲商人]";
	mes "這需要10個輝煌領域硬幣.";
	mes "少年要買一個麼?";
	next;
	if(select("- 買一個.:- 不買.") == 2)
		close;
	if (countitem(6081) < 10) {
		mes "[護甲商人]";
		mes "你好像沒那麼多硬幣啊.";
		close;
	}
	mes "[護甲商人]";
	mes "拿好了!"; //custom
	delitem 6081,10; //Splendide_Coin
	getitem .@item,1;
	close;
}

ecl_in01,64,97,4	script	開洞專家納圖爾#ecl	4_F_FAIRYKID,{
	mes "[開洞專家]";
	mes "我是開洞專家納圖爾.";
	mes "如果你能從我的小兄弟納菲亞那裡把我需要的東西帶來, 再給我5個輝煌領域硬幣的話, 我將為你打洞.";
	next;
	if(select("- 打個洞.:- 算了吧.") == 2)
		close;
	mes "[開洞專家]";
	mes "打洞真TM是個很難的事啊,  就算你超注意的拿著放大鏡開洞還是有可能失敗.";
	next;
	mes "[開洞專家]";
	mes "當然, 如果你心胸夠寬廣的話,你是不會介意的. 對吧? 我還要繼續麼?";
	next;
	if(select("- 繼續吧.:- 算了吧.") == 2)
		close;
	mes "[開洞專家]";
	mes "你想給哪件東西打洞?";
	next;
	setarray .@noslots[0],2917,2918,2919,2920,2921,2922;
	setarray .@slotted[0],2923,2924,2925,2926,2927,2928;
	set .@i, select("- 力量手套:- 智力手套:- 敏捷手套:- 體力手套:- 靈巧手套:- 幸運手套")-1;
	set .@item, .@noslots[.@i];
	set .@new_item, .@slotted[.@i];
	mes "[開洞專家]";
	mes "讓我最後確認一次. 你想打洞的是 "+getitemname(.@item)+"?";
	next;
	if(select("是的:不") == 2) {
		mes "[開洞專家]";
		mes "為什麼不確定了再和我說話.";
		close;
	}
	if (countitem(.@item) == 0) {
		mes "[開洞專家]";
		mes "你沒那東西. 為什麼不確定了再和我說話.";
		close;
	}
	if (countitem(6081) < 5) {
		mes "[開洞專家]";
		mes "你沒用足夠的硬幣. 為什麼不確定了再和我說話."; //custom
		close;
	}
	delitem 6081,5; //Splendide_Coin
	delitem .@item,1;
	//custom to the end
	if (rand(100) < 5) {
		mes "[開洞專家]";
		mes "喔不, 我失敗了! 我很抱歉,但願你下次好運吧.";
		close;
	}
	mes "[開洞專家]";
	mes "成功了!我成功的打洞了.";
	getitem .@new_item,1;
	close;
}

ecl_in01,67,39,4	script	複製專家帕爾特	4_M_FAIRYKID4,{
	emotion ET_THROB;
	mes "[帕爾特]";
	mes "喔厚厚. 你需要點啥? 我這裡有很多好東西.";
	next;
	switch(select("- 不了,謝謝.:- 再說一遍?:- 我知道你手裡有不少好東西!")) {
	case 1:
		emotion ET_FRET;
		mes "[帕爾特]";
		mes "真的? 喔?";
		close;
	case 2:
		mes "[帕爾特]";
		mes "我可以說在艾喀拉蚩沒有人比我在運用魔法上更厲害了.";
		next;
		mes "[帕爾特]";
		mes "無論這是啥, 我都會給你個好價錢的. 相信我.";
		next;
		select("你能為我做什麼?");
		emotion ET_BEST;
		mes "[帕爾特]";
		mes "嘿,我喜歡你的態度.";
		next;
		mes "[帕爾特]";
		mes "噢- 你現在帶的那頂帽子";
		next;
		emotion ET_QUESTION;
		mes "[帕爾特]";
		mes "你還知道倉庫裡哪裡有這樣的堅固的不過十分醜的帽子麼?你知道, 對吧?";
		next;
		select(".........");
		emotion ET_THINK,1;
		emotion ET_SMILE;
		mes "[帕爾特]";
		mes "看見了吧!我就知道. 從第一分鐘看到你我就知道.";
		next;
		mes "[帕爾特]";
		mes "如果你帶給我 ^ff0000 50 輝煌領域硬幣和^000000, 我會給你做一個和現在一模一樣的帽子, 不過戴起來就像是^ff0000定做的^000000.";
		next;
		mes "[帕爾特]";
		mes "唔,那是啥...?";
		mes "我認為你們這些冒險家叫他 ^0571B0定製^000000?對麼?";
		next;
		select("是滴~ 你要點啥?");
		mes "[帕爾特]";
		mes "我需要 1 ^0571B0帽子來複製^000000 同時";
		mes "我需要^E1281E 4 水晶鏡子^000000 來利用魔力, ^E1281E 1 精靈魔法粉末^000000 作為催化劑,和 ^E1281E 綠寶石, 紅寶石, 藍寶石, 黃寶石, 和皓石, 每樣十個^000000, 來放大魔力.";
		next;
		emotion ET_STARE_ABOUT;
		mes "[帕爾特]";
		mes "這要挺多材料的,對吧? 好吧, 我必須要這些材料.與魔法打交道可不是兒戲.";
		next;
		select("你能複製哪些帽子?");
		mes "[帕爾特]";
		mes "哈哈! 你挺聰明的嘛!";
		mes "我都快忘了最重要的事了.";
		next;
		mes "[帕爾特]";
		mes "我能複製的帽子有:";
		mes "^0571B0青蛙帽, 花葉, 山羊頭盔, 紅暈, 瓦爾基裡的頭盔, 小惡魔帽子, 刺客口罩, 妖精長耳朵飾, 傳統面具, 和 學生帽^000000 這10種帽子.";
		next;
		mes "[帕爾特]";
		mes "你覺得怎樣? 如果有材料的佣金, 我馬上就能給你做.";
		next;
		if(select("- 晚一些再說吧.:- 展示給我看!") == 1) {
			emotion ET_THINK;
			mes "[帕爾特]";
			mes "什麼? 你在開玩笑吧? 請告訴我你是在開玩笑的!";
			close;
		}
		break;
	case 3:
		break;
	}
	mes "[帕爾特]";
	mes "山口山.";
	mes "你做了正確的決定. 我覺得我都快餓死了, 因為全拉菲尼斯人都對美麗的帽子不感興趣.";
	next;
	mes "[帕爾特]";
	mes "我會給你個好價錢的. 你真應該知道我給了你一個好價錢.";
	next;
	mes "[帕爾特]";
	mes "你心裡在想些啥? 說出來聽聽?";
	next;
	setarray .@items[0],
		5447, //Frog_Cap
		2269, //Centimental_Flower
		2256, //Magestic_Goat
		5040, //Blush
		5171, //Valkyrie_Helm
		5038, //Deviruchi_Cap
		5096, //Assassin_Mask_
		2286, //Elven_Ears
		5176, //Hahoe_Mask
		5016; //Boy's_Cap
	setarray .@costumes[0],
		19548, //C_Frog_Cap
		19552, //C_Centimental_Flower
		19549, //C_Magestic_Goat
		19550, //C_Blush
		19546, //C_Valkyrie_Helm
		19547, //C_Deviruchi_Cap
		19553, //C_Assassin_Mask_
		19551, //C_Elven_Ears
		19554, //C_Hahoe_Mask
		19545; //C_Boys_Cap
	if (countitem(6081) >= 50 && countitem(747) >= 4 && countitem(6395) >= 1 && countitem(721) >= 10 && countitem(723) >= 10 && countitem(726) >= 10 && countitem(728) >= 10 && countitem(729) >= 10)
		set .@item_check,1;
	for(set .@i,0; .@i<getarraysize(.@items); set .@i,.@i+1) {
		if (.@item_check && countitem(.@items[.@i]))
			set .@menu$, .@menu$+getitemname(.@items[.@i])+":"; //custom
		else
			set .@menu$, .@menu$+getitemname(.@items[.@i])+" (^777777所需材料不足^000000):";
	}
	set .@i, select(.@menu$)-1;
	set .@item, .@items[.@i];
	set .@new_item, .@costumes[.@i];
	mes "[帕爾特]";
	mes "是 ^0571B0"+getitemname(.@item)+"^000000 麼?";
	next;
	if (.@item_check == 0 || countitem(.@item) == 0) {
		emotion ET_CRY;
		mes "[帕爾特]";
		mes "呀... 賒帳是不好的.";
		next;
		mes "[帕爾特]";
		mes "^0571B0"+getitemname(.@item)+"^000000 and";
		mes "^E1281E50 輝煌硬幣, 4 水晶鏡子, 1 精靈魔法粉末, 綠寶石, 紅寶石, 藍寶石, 黃寶石, 和青綠寶石每樣10個^000000 是必須的.";
		next;
		mes "[帕爾特]";
		mes "我現在的生活也很差. 也請想想我的處境.";
		close;
	}
	//custom to the end
	if(select("- 繼續.:- 算了吧.") == 2)
		close;
	delitem .@item,1;
	delitem 6081,50; //輝煌硬幣
	delitem 747,4; //水晶鏡子
	delitem 6395,1; //精靈魔法粉末
	delitem 721,10; //綠寶石
	delitem 723,10; //紅寶石
	delitem 726,10; //藍寶石
	delitem 728,10; //黃寶石
	delitem 729,10; //青綠寶石
	getitem .@new_item,1;
	mes "[帕爾特]";
	mes "十分感謝! 正如我承諾的那樣, 這是你定製的帽子.";
	close;
}

ecl_in01,33,98,4	script	藥草商人普拉芬娜#e	4_M_FAIRYKID2,{
	mes "[藥草商人]";
	mes "歡迎, 這是普拉芬娜的草藥商店, 每日供應最新鮮的草藥~!";
	next;
	switch(select("- 雪覆:- 芍藥之母:- 敲擊香草:- 世界之樹之塵:- 結束對話")) {
	case 1:
		mes "[藥草商人]";
		mes "雪覆對以下狀態有特效 ^3131FF灼燒, 出血, 深睡眠, 睡眠^000000.";
		set .@item,12812; //Snow_Flip
		set .@cost,5;
		break;
	case 2:
		mes "[藥草商人]";
		mes "芍藥之母對以下狀態有特效 ^3131FF結冰, 冰凍, 凍結^000000.";
		set .@item,12813; //Peony_Mommy
		set .@cost,5;
		break;
	case 3:
		mes "[藥草商人]";
		mes "敲擊香草對以下狀態有特效^3131FF眩暈, 恐懼, 混亂, 幻像^000000.";
		set .@item,12814; //Slapping_Herb
		set .@cost,1;
		break;
	case 4:
		mes "[藥草商人]";
		mes "世界之樹之塵對以下狀態有特效 ^3131FF暗黑, 詛咒, 減速, 顛倒^000000.";
		set .@item,12815; //Yggdrasil_Dust
		set .@cost,1;
		break;
	case 5:
		mes "[藥草商人]";
		mes "請隨時回來.";
		close;
	}
	mes "需要5秒之間使用.";
	mes "這需要 "+.@cost+" 輝煌領域硬幣"+((.@cost == 1)?"":"s")+" 每個.";
	next;
	switch(select("- 買一個.:- 買十個.:- 不買.")) {
	case 1:
		set .@amount,1;
		break;
	case 2:
		set .@amount,10;
		break;
	case 3:
		close;
	}
	set .@price,.@amount*.@cost;
	mes "[藥草商人]";
	mes "你想要買 "+.@amount+" "+getitemname(.@item)+"?";
	next;
	if(select("- 買.:- 不買.") == 2)
		close;
	if (countitem(6081) < .@price) {
		mes "[藥草商人]";
		mes "你沒有足夠的硬幣.";
		close;
	}
	mes "[藥草商人]";
	mes "這是你的草藥."; //custom
	delitem 6081,.@price; //Splendide_Coin
	getitem .@item,.@amount;
	end;
}
