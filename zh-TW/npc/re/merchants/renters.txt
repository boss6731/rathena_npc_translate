//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================


//===== Hercules Script ======================================
//= Renters
//===== By: ==================================================
//= Hercules Dev Team
//===== Current Version: =====================================
//= 2.1
//===== Description: =========================================
//= [Official Conversion]
//= Renewal-specific breeder NPCs.
//===== Additional Comments: =================================
//= 2.1 Moved some renters to a separate renewal file. [Daegaladh]
//= 2.2 Updated to match the official scripts. [Euphy]
//============================================================

// Dragon Breeder
//============================================================
job3_rune01,88,62,5	script	坐騎龍飼養員	8W_SOLDIER,{
	mes "[坐騎龍飼養員]";
	if (Class == Job_Rune_Knight || Class == Job_Rune_Knight_T || Class == Job_Baby_Rune) {
		mes "您好啊, 冒險者, 想弄條龍來騎騎嗎?";
		next;
		switch(select("是:否")) {
		case 1:
			//if (!getskilllv("RK_DRAGONTRAINING")) {
			if (!getskilllv("KN_RIDING")) {
				mes "[坐騎龍飼養員]";
				mes "您得拿了駕駛執照(訓龍術)我才敢給您租龍啊!";
				close;
			}
			else if (checkriding()) {
				mes "[坐騎龍飼養員]";
				mes "您這胯下不騎著一頭嗎?";
				close;
			}
			else if(ismounting()) {
				mes "[坐騎龍飼養員]";
				mes "冒險者, 我這小本生意, 不賒帳!";
				close;
			}
			setdragon;
			close;
		case 2:
			mes "[坐騎龍飼養員]";
			mes "沒事兒, 做不成生意做朋友嘛, 下次再來嘿!";
			close;
		}
	}
	mes "這位客官, 有什麼事兒能幫您嗎?";
	mes "龍很暴躁的, 只有符文騎士才能駕馭得了啊!";
	close;
}

// Dragon/Gryphon Master
//============================================================
prontera,130,213,5	script	獸騎管理員	8W_SOLDIER,{
	mes "[獸騎管理員]";
	if (Class == Job_Rune_Knight || Class == Job_Rune_Knight_T || Class == Job_Baby_Rune) {
		mes "你好啊, 冒險家, 想租用一頭坐騎嗎?";
		next;
		switch(select("- 是:- 否")) {
		case 1:
			//if (!getskilllv("RK_DRAGONTRAINING")) {
			if (!getskilllv("KN_RIDING")) {
				mes "[獸騎管理員]";
				mes "請先學會乘騎術, 好嗎?";
				close;
			}
			else if (checkriding()) {
				mes "[獸騎管理員]";
				mes "你已經騎著一頭了嘿......";
				close;
			}
			else if(ismounting()) {
				mes "[獸騎管理員]";
				mes "我們不打白條, 謝謝!";
				close;
			}
			setdragon;
			close;
		case 2:
			mes "[獸騎管理員]";
			mes "我知道了. 那麼, 祝你好運!";
			close;
		}
	}
	if (Class == Job_Royal_Guard || Class == Job_Royal_Guard_T || Class == Job_Baby_Guard) {
		mes "你好啊, 騷年, 想租用一頭獅鷲嗎?";
		next;
		switch(select("是:否")) {
		case 1:
			if(!getskilllv("KN_RIDING")) {
				mes "[獸騎管理員]";
				mes "請先學會乘騎術, 好嗎?";
				close;
			}
			else if (checkriding()) {
				mes "[獸騎管理員]";
				mes "你已經騎著一頭了嘿......";
				close;
			}
			else if(ismounting()) {
				mes "[獸騎管理員]";
				mes "我們不打白條, 謝謝!";
				close;
			}
			setriding;
			close;
		case 2:
			mes "[獅鷲管理員]";
			mes "我知道了. 那麼, 祝你好運!";
			close;
		}
	}
	mes "我就是為 符文騎士 和 皇家衛士 提供坐騎的管理員啦!";
	close;
}

// Peco removing NPC
//============================================================
prontera,125,208,5	script	大嘴鳥管理員	8W_SOLDIER,{
	mes "[衛兵]";
	mes "如果你苦惱於這隻大嘴鳥硬是要你騎他,";
	mes "不讓你下來, ";
	mes "那麼來找我就對啦!";
	if (checkriding()) {
		next;
		mes "[衛兵]";
		mes "你正騎著一隻大嘴鳥嘿,";
		mes "不想再騎了嗎?";
		next;
		switch(select("- 是:- 否")) {
		case 1:
			setriding 0;
			mes "[衛兵]";
			mes "怎麼樣, ";
			mes "腳踏實地的感覺?";
			mes "HOLD住嗎?";
			close;
		case 2:
			mes "[衛兵]";
			mes "勸你別逗我玩,";
			mes "我發起怒來自己都怕.";
			close;
		}
	}
	close;
}

// Magic Gear Renter
//============================================================
-	script	::mgm	-1,{
	mes "[機械師]";
	if (Class == Job_Mechanic || Class == Job_Mechanic_T || Class == Job_Baby_Mechanic) {
		mes "你好啊, 機匠!";
		mes "想租用一臺手推車, 還是";
		mes "租用一臺高達?";
		next;
		switch(select("- 手推車:- 機甲:- 取消")) {
		case 1:
			if (checkcart()) {
				mes "[機械師]";
				mes "你已經有手推車了.";
				close;
			}
			setcart;
			mes "[機械師]";
			mes "沒事兒別煩我. ";
			close;
		case 2:
			if (checkmadogear()) {
				mes "[機械師]";
				mes "你已經坐在機甲裡面了.";
				close;
			}
			else if (!getskilllv("NC_MADOLICENCE")) {
				mes "[機械師]";
				mes "拿到了駕駛執照再來見我!";
				close;
			}
			else if(ismounting()) {
				mes "[機械師]";
				mes "最為商人系錢不夠是恥辱?";
				close;
			}
			setmadogear;
			mes "[機械師]";
			mes "質保三年, 記得帶發票來找我!";
			close;
		case 3:
			close;
		}
	}
	mes "我能幫到你嗎?";
	mes "機甲只給機匠用, 記住了!";
	close;
}
prontera,163,178,3	duplicate(mgm)	魔導機械管理員#prt	8W_SOLDIER
geffen,103,55,5	duplicate(mgm)	魔導機械管理員#gef	8W_SOLDIER
payon,166,106,5	duplicate(mgm)	魔導機械管理員#pay	8W_SOLDIER
aldebaran,133,112,5	duplicate(mgm)	魔導機械管理員#alde	8W_SOLDIER
yuno,167,187,3	duplicate(mgm)	魔導機械管理員#yuno	8W_SOLDIER
rachel,106,134,5	duplicate(mgm)	魔導機械管理員#ra	8W_SOLDIER
dicastes01,187,207,3	duplicate(mgm)	魔導機械管理員#dic	8W_SOLDIER
manuk,273,212,5	duplicate(mgm)	魔導機械管理員#man	8W_SOLDIER
splendide,180,174,5	duplicate(mgm)	魔導機械管理員#spl	8W_SOLDIER
mid_camp,242,243,3	duplicate(mgm)	魔導機械管理員#mid	8W_SOLDIER
