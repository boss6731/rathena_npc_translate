//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================

//===== rAthena Script =======================================
//= Card Separation System
//===== By: ================================================== 
//= Muad_Dib
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Separates cards from equipment.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Euphy]
//= 1.1 Added "Richard" NPC. [Euphy]
//============================================================ 

-	script	::CardSeparation_mal	-1,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "您攜帶的物品種類過多. 等您解決這個問題後我們才能繼續.";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "無法繼續, 因為您的負重不足. 等您解決負重問題後我們再繼續.";
		close;
	}
	if (strnpcinfo(1) == "傑瑞米") {
		set .@Jeremy,1;
		set .@n$, "[Jeremy]";
		setarray .@equip_name$[0],  "Armor",  "Shoes",  "Garment", "Upper Hat";
		setarray .@equip_slot[0], EQI_ARMOR,EQI_SHOES,EQI_GARMENT,EQI_HEAD_TOP;

		mes .@n$;
		mes "好久不見~";
		mes "我已經學會了新的技能, 可以從武器. 頭飾, 護甲, 鞋子, 披肩和頭飾上分離卡片. 想不想試試看?";
	} else {
		set .@Jeremy,0;
		set .@n$, "[Richard]";
		setarray .@equip_name$[0], "Left hand", "Right hand";
		setarray .@equip_slot[0],   EQI_HAND_L,   EQI_HAND_R;

		mes .@n$;
		mes "我不用觸摸的武器和盾牌. 因為拆除卡片, 可能會損壞. 這就是為什麼我準備一個的卡片技能來操控武器和盾牌...";
	}
	next;
	mes .@n$;
	mes "費用一般是 1,000,000 Zeny. 在操作之前, 你可以使用 ^990000特殊的物品來降低卡片或者裝備受損的概率^000000. 我們不收取額外的費用.";
	next;
	mes .@n$;
	mes "使用了特殊物品還是有機率損壞裝備和卡片. 同時, ^ff0000裝備精煉等級也會消失^000000. 你有需要分離的裝備嗎?";
	next;

	for(set .@i,0; .@i<getarraysize(.@equip_slot); set .@i,.@i+1) {
		if (getequipisequiped(.@equip_slot[.@i]))
			set .@menu$, .@menu$+getequipname(.@equip_slot[.@i])+":";
		else
			set .@menu$, .@menu$+"^999999"+.@equip_name$[.@i]+" (empty)^000000:";
	}

	set .@i, select("- 停止拆卡:"+((.@Jeremy)?"- 有什麼可能性?":"")+":"+.@menu$);
	switch(.@i) {
	case 1:
		mes .@n$;
		mes "如果你想操作, 隨時來找我.";
		close;
	case 2:
		mes .@n$;
		mes "你想了解特殊物品. 好吧, 我不喜歡隨口說說, 所以讓我告訴你整個完整的故事...";
		next;
		mes "^000099傑瑞米舒展了下筋骨. 他可能是在等待某人與他交談.^000000";
		next;
		mes .@n$;
		mes "你知不知道喵喵島的特產是罐頭食品?";
		next;
		select("- 我知道那很不錯. 難道有什麼限制?");
		mes .@n$;
		mes "嘿嘿... 人人都喜歡. 不過從前它確實有些問題.";
		next;
		select("- 問題? 是不是用了有汙染的魚?");
		mes .@n$;
		mes "不不, 不是魚的問題. 問題是, 很多加工後產生的魚油. 也就是說等同於工業廢棄物.";
		next;
		mes .@n$;
		mes "然而, 在一次製作流程革新之後, 把這些魚油變廢為寶並成功用於舊設備保養和用作工業潤滑劑.";
		next;
		mes .@n$;
		mes "此外, 我們可以通過金幣購買普通或者高級潤滑油, 這些潤滑油對於我們分離裝備上的卡片非常有用.";
		next;
		mes .@n$;
		mes "不用擔心錢的問題. 高級潤滑油是昂貴的. 如果你支付一些金幣, 我可以給你便宜的潤滑劑.";
		next;
		mes .@n$;
		mes "我不確定普通潤滑劑的效果如何. 不過, 它確實很便宜, 對吧?";
		close;
	default:
		set .@equip_num, .@equip_slot[.@i-3];
		if (!getequipisequiped(.@equip_num)) {
			mes .@n$;
			if (.@Jeremy)
				mes "這部分的說明, 你聽明白了嗎?";
			else
				mes "關於這部分你有沒有問題?";
			close;
		}
		break;
	}

	setarray .@equip_card[0], getequipcardid(.@equip_num,0),getequipcardid(.@equip_num,1),getequipcardid(.@equip_num,2),getequipcardid(.@equip_num,3);
	set .@mvp_list$,
		"|4408|4128|4456|4168|4142"+  //Gloom_Under_Night_Card, Golden_Bug_Card, Nidhogg_Shadow_Card, Dark_Lord_Card, Doppelganger_Card
		"|4134|4137|4386|4407|4357"+  //Dracula_Card, Drake_Card, Detale_Card, Randgris_Card, B_Seyren_Card
		"|4146|4132|4147|4372|4145"+  //Maya_Card, Mistress_Card, Baphomet_Card, Bacsojin_Card, Berzebub_Card
		"|4374|4352|4367|4236|4425"+  //Apocalips_H_Card, B_Ygnizem_Card, B_Shecil_Card, Amon_Ra_Card, Atroce_Card
		"|4359|4123|4144|4135|4143"+  //B_Eremes_Card, Eddga_Card, Osiris_Card, Orc_Load_Card, Orc_Hero_Card
		"|4263|4131|4430|4276|4419"+  //Incant_Samurai_Card, Moonlight_Flower_Card, Ifrit_Card, Lord_Of_Death_Card, Ktullanux_Card
		"|4403|4399|4376|4441|4302"+  //Kiel_Card, Thanatos_Card, Lady_Tanee_Card, Fallen_Bishop_Card, Tao_Gunka_Card
		"|4305|4148|4318|4121|4365"+  //Turtle_General_Card, Pharaoh_Card, Knight_Windstorm_Card, Phreeoni_Card, B_Katrinn_Card
		"|4363|4324|4361|4330|4342|"; //B_Magaleta_Card, Garm_Card, B_Harword_Card, Dark_Snake_Lord_Card, Rsx_0806_Card

	if (.@Jeremy) {
		for(set .@i,0; .@i<4; set .@i,.@i+1) {
			if (.@equip_card[.@i] >= 4700) // Armor Enchant System
				set .@equip_card[.@i],0;
		}
		if (!getarraysize(.@equip_card)) {
			mes .@n$;
			mes "沒有檢測到卡片, 請檢查下你的裝備?";
			close;
		}
		if ((.@equip_card[0] && compare(.@mvp_list$,"|"+.@equip_card[0]+"|")) ||
			(.@equip_card[1] && compare(.@mvp_list$,"|"+.@equip_card[1]+"|")) ||
			(.@equip_card[2] && compare(.@mvp_list$,"|"+.@equip_card[2]+"|")) ||
			(.@equip_card[3] && compare(.@mvp_list$,"|"+.@equip_card[3]+"|")))
			set .@boss_chk,1;
	} else {
		// Official "Richard" script uses a hardcoded list including every possible item.
		//if (!getequipisequiped(.@equip_num)) {
		//	mes "[Richard]";
		//	mes "非常抱歉, 我們還不支持這個裝備.";
		//	close;
		//}

		mes "[理察]";
		mes "你想從那個卡片插槽上拔下卡片? 從左向右看的話, 分別為 1,2,3,4號插槽.";
		next;
		set .@menu$,"";
		for(set .@i,0; .@i<4; set .@i,.@i+1) {
			if (.@equip_card[.@i] && .@equip_card[.@i] < 4700) // Armor Enchant System
				set .@menu$, .@menu$+"插槽 "+(.@i+1)+" - "+getitemname(.@equip_card[.@i])+":";
			else
				set .@menu$, .@menu$+"^777777Socket "+(.@i+1)+" - 沒有卡片^000000:";
		}
		set .@i, select("- 停止拆除:"+.@menu$);
		switch(.@i) {
		case 1:
			mes .@n$;
			mes "等你有需要的時候, 可以再來找我.";
			close;
		default:
			set .@slot, .@i-2;
			if (.@equip_card[.@slot] == 0 || .@equip_card[.@slot] >= 4700) {
				mes .@n$;
				mes "此卡片插槽內沒有配備任何卡片, 請再次檢查?";
				close;
			}
			break;
		}
		if (compare(.@mvp_list$,"|"+.@equip_card[.@slot]+"|"))
			set .@boss_chk,1;
	}
	if (.@boss_chk == 0) {
		mes .@n$;
		if (.@Jeremy)
			mes "例外的卡片, ^ff0000附魔效果都將消失.^000000 如果你同意, 請選擇繼續製作選項:";
		else
			mes "請選擇潤滑劑.";
		next;
		set .@menu$,
			"Next time...:"+
			((Zeny >= 1000000)?"使用 1,000,000z (不使用任何潤滑劑):":"^999999使用 1,000,000z (不足)^000000:")+
			((countitem(6441))?"使用 高級潤滑劑 :":"^999999高級潤滑劑 (不足)^000000:")+
			((countitem(6440))?"使用 普通潤滑劑":"^999999普通潤滑劑 (不足)^000000");
		switch(select(.@menu$)) {
		case 1:
			mes .@n$;
			mes "你需要時,來這裡可以找到我.";
			close;
		case 2:
			if (Zeny < 1000000) {
				mes .@n$;
				mes "你的錢沒帶夠. 等有錢的時候再來吧.";
				close;
			}
			mes .@n$;
			mes "這是一件非常舊的裝備. 這會導致在分離過程中被損毀的概率大大提升. 你確定要繼續?";
			next;
			if(select("- 下次再說...:- 繼續") == 1) {
				mes .@n$;
				mes "你需要時,來這裡可以找到我.";
				close;
			}
			set .@sf_c_num,150;
			set .@sf_r_num,150;
			set .@sf_w_num,150;
			set Zeny, Zeny - 1000000;
			break;
		case 3:
			if (countitem(6441) == 0) {
				mes .@n$;
				mes "你沒有高級潤滑劑.";
				close;
			}
			mes .@n$;
			mes "如果使用高級潤滑劑, 安全係數會大大提高, 但是我無法給你 100% 保證. 你確定要繼續?";
			next;
			if(select("- 下次再說...:- 繼續") == 1) {
				mes "[傑瑞米]";
				mes "你需要時,來這裡可以找到我.";
				close;
			}
			set .@sf_c_num,75;
			set .@sf_r_num,75;
			set .@sf_w_num,75;
			delitem 6441,1; //High_RankLubricant
			break;
		case 4:
			if (countitem(6440) == 0) {
				mes .@n$;
				mes "你沒有普通潤滑劑.";
				close;
			}
			mes .@n$;
			mes "如果使用普通潤滑劑, 安全係數會有所提高, 但是我無法給你 100% 保證. 你確定要繼續?";
			next;
			if(select("- 下次再說...:- 繼續") == 1) {
				mes "[傑瑞米]";
				mes "你需要時,來這裡可以找到我.";
				close;
			}
			set .@sf_c_num,75;
			set .@sf_r_num,150;
			set .@sf_w_num,150;
			delitem 6440,1; //General_Lubricant
			break;
		}
	} else if (.@boss_chk == 1) {
		mes .@n$;
		mes "這件裝備插了一張 MVP 卡片. 這類卡片無法通過潤滑劑來分離. 如果你帶來超級活性劑 ^0000ff王水瓶^000000, 我就能夠操作了.";
		next;
		switch(select("- 下次再說...:- 繼續.")) {
		case 1:
			mes .@n$;
			mes "你需要時,來這裡可以找到我.";
			close;
		case 2:
			if (countitem(6443) == 0) {
				mes .@n$;
				mes "你沒有王水瓶.";
				close;
			}
			break;
		}
		mes .@n$;
		if (.@Jeremy) {
			mes "例外的卡片, ^ff0000附魔效果都將消失.^000000 如果你同意, 請選擇繼續製作選項:";
			set .@menu$,"Alright, let's do it!";
		} else {
			mes "May I continue?";
			set .@menu$,"I got it. Just do it quickly!";
		}
		next;
		switch(select("- 下次再說...:- 沒問題,繼續!")) {
		case 1:
			mes .@n$;
			mes "你需要時,來這裡可以找到我.";
			close;
		case 2:
			set .@sf_c_num,60;
			set .@sf_r_num,60;
			set .@sf_w_num,60;
			delitem 6443,1; //Sillit_Pong_Bottle
			break;
		}
	}

	set .@equip_id, getequipid(.@equip_num);
	set .@equip_refine, getequiprefinerycnt(.@equip_num);

	// anti-hack
	if (callfunc("F_IsEquipCardHack", .@equip_num, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]))
		close;

	delequip .@equip_num;

	// Chance of retaining refine level.
	if (rand(1,.@sf_r_num) >= 61)
		set .@equip_refine,0;

	if (.@Jeremy) {
		// Chance of retaining equipment.
		if (rand(1,.@sf_w_num) < 61) {
			set .@equip_safe,1;
			getitem2 .@equip_id,1,1,.@equip_refine,0,0,0,0,0;
		}

		// Chance of retaining cards.
		for(set .@i,0; .@i<4; set .@i,.@i+1) {
			if (.@equip_card[.@i]) {
				if (rand(1,.@sf_c_num) < 61)
					getitem .@equip_card[.@i],1;
				else
					set .@card_break,1;
			}
		}
	} else {
		set .@card, .@equip_card[.@slot];
		set .@equip_card[.@slot],0;

		// Chance of retaining equipment.
		if (rand(1,.@sf_w_num) < 61) {
			set .@equip_safe,1;
			getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];
		}

		// Chance of retaining card.
		if (rand(1,.@sf_c_num) < 61)
			getitem .@card,1;
		else
			set .@card_break,1;
	}

	// Display corresponding effect.
	if (!.@equip_safe && .@card_break)
		specialeffect2 EF_LORD;
	else if (.@equip_safe && .@card_break)
		specialeffect2 EF_SUI_EXPLOSION;
	else if (!.@equip_safe && !.@card_break)
		specialeffect2 EF_FIREPILLAR;
	else
		specialeffect2 EF_MAXPOWER;

	// Output results.
	mes "-- 拆卡結果 --";
	if (.@equip_safe) {
		mes "卡片在分離過程中未發現裂紋.";
		mes "^0000FF裝備檢測正常.^000000";
	} else {
		mes "卡片在分離過程中未發現裂紋.";
		mes "裝備損毀了. ^ff0000無法還原.^000000";
	}
	mes "-------------------";
	if (!.@card_break) {
		mes "分離過程中, 卡片未被腐蝕.";
		mes "^0000ff拆卡成功.^000000";
	} else {
		mes "發生在分離過程中, 卡片的表面受到侵蝕.";
		mes "卡片損毀了. ^ff0000無法還原.^000000";
	}
	next;
	mes .@n$;
	mes "這是拆卡可能出現的所有結果. 再見.";
	close;
}
malangdo,215,166,4	duplicate(CardSeparation_mal)	傑瑞米#pa0829	4_CAT_DOWN
malangdo,208,166,6	duplicate(CardSeparation_mal)	理察#pa0829	4_CAT_ADV1
