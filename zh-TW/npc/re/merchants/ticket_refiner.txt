//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================

//===== rAthena Script =======================================
//= Ticket Refiner
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Refiner that uses +5~9/+11 refine tickets to refine
//= equipment with no chance of failure.
//= NOTE: This NPC is currently disabled on official servers.
//===== Additional Comments: =================================
//= 1.0 First version. [Euphy]
//= 1.1 Do not refine above ticket level. [Euphy]
//============================================================

// Main NPC :: safety_Ref_NPC
//============================================================
prontera,195,173,6	script	精煉研究家	851,{
	disable_items;
	if (countitem(6238) || countitem(6228) || countitem(6229) || countitem(6230) || countitem(6231) || countitem(6456))
		set .@bWeaponUp,1;
	if (countitem(6239) || countitem(6232) || countitem(6233) || countitem(6234) || countitem(6235) || countitem(6457))
		set .@bArmorUp,1;
	if (!.@bWeaponUp && !.@bArmorUp) {
		mes "[精煉研究家]";
		mes "你好!";
		mes "有什麼事情?";
		mes "我是專家.";
		mes "在物品精煉領域,";
		mes "但我已經退休了.";
		next;
		switch(select("- 那我走了.:- 嗯......這讓我好奇.")) {
		case 1:
			mes "[精煉研究家]";
			mes "保重, 探險家.";
			close;
		case 2:
			mes "[精煉研究家]";
			mes "事實上, 我有時冒險家與提供完善服務 ^006400精煉能力提升券的服務^000000...";
			mes "再見~!";
			close;
		}
	}
	emotion ET_SURPRISE;
	mes "[精煉研究家]";
	mes "你好!";
	mes "你可以將你的物品精煉到 ^006400與精煉能力提升券相同的級別^000000.";
	mes "您不必擔心! 裝備不會有精煉失敗的可能.";
	next;
	if(select("- 我馬上回來.:- 用券來精煉裝備.") == 1) {
		mes "[精煉研究家]";
		mes "好的.";
		mes "等你回來.";
		close;
	}
	mes "[精煉研究家]";
	mes "你想精煉什麼裝備?";
	next;
	setarray .@position$[1],"Head upper","Armor","Left hand","Right hand","Robe","Shoes","Accessory 1","Accessory 2","Head middle","Head lower";
	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for(set .@i,1; .@i<=10; set .@i,.@i+1)
		set .@menu$, .@menu$+((getequipisequiped(.@indices[.@i]))?getequipname(.@indices[.@i]):.@position$[.@i]+" - [未裝備]")+":";
	set .@part, .@indices[ select(.@menu$) ];
	if (!getequipisequiped(.@part)) {
		mes "[精煉研究家]";
		mes "你必須裝備要精煉的裝備.";
		close;
	}
	if (!getequipisenableref(.@part)) {
		emotion ET_OTL;
		mes "[精煉研究家]";
		mes "噢, 我很抱歉.";
		mes "這個物品無法被精煉.";
		close;
	}
	switch(getequipweaponlv(.@part)) {
	default:
	case 0:
		setarray .@tickets[0],6457,6235,6234,6233,6232,6239;
		setarray .@levels[0],5,6,7,8,9,11;
		set .@type$,"Armor";
		set .@check,.@bArmorUp;
		break;
	case 1:
	case 2:
	case 3:
	case 4:
		setarray .@tickets[0],6456,6231,6230,6229,6228,6238;
		setarray .@levels[0],5,6,7,8,9,11;
		set .@type$,"Weapon";
		set .@check,.@bWeaponUp;
		break;
	}
	if (!.@check) {
		emotion ET_THINK;
		mes "[精煉研究家]";
		mes "如果你想要精煉這個 ^006400"+.@type$+"^000000, 請帶上 ^006400"+.@type$+"精煉能力提升券^000000.";
		mes "再見!";
		close;
	}
	mes "[精煉研究家]";
	mes "請選擇你想精煉 ^006400"+.@type$+"^000000 所使用的精煉能力提升券.";
	next;
	set .@menu$,"";
	for(set .@i,0; .@i<getarraysize(.@tickets); set .@i,.@i+1)
		set .@menu$, .@menu$+getitemname(.@tickets[.@i])+":";
	set .@select, select(.@menu$)-1;
	set .@ticket_lv, .@levels[.@select];
	set .@ticket_id, .@tickets[.@select];
	if (countitem(.@ticket_id) == 0) {
		emotion ET_QUESTION;
		mes "[精煉研究家]";
		mes getitemname(.@ticket_id)+" 不再你身上, 你是不是把它放倉庫了?";
		mes "請檢查一下.";
		mes "再見!";
		close;
	}
	if (getequiprefinerycnt(.@part) >= .@ticket_lv) {
		emotion ET_PROFUSELY_SWEAT;
		mes "[精煉研究家]";
		mes "^8B4513這個道具已經精煉到極致了.^000000";
		mes "請拿一件不到你精煉能力提升券級別的裝備來.";
		close;
	}
	mes "[精煉研究家]";
	mes "我將精煉 ^006400"+getequipname(.@part)+"^8B4513 至 +"+.@ticket_lv+" 級別^000000 消耗 ^006400"+getitemname(.@ticket_id)+"^000000.";
	mes "我繼續?";
	next;
	if(select("- 否.:- 是.") == 1) {
		emotion ET_THINK;
		mes "[精煉研究家]";
		mes "噢, 你改變主意了.";
		mes "好吧.";
		mes "你需要的時候再來吧.";
		close;
	}
	mes "[精煉研究家]";
	mes "很好.";
	mes "如你所願!";
	mes "我有我自己特殊的方式來精煉...";
	mes ".......咔嘭!";
	specialeffect EF_SUI_EXPLOSION;
	if (countitem(.@ticket_id))
		delitem .@ticket_id,1;
	else {
		next;
		mes "錯誤!";
		mes "請報給GM.";
		close;
	}
	successrefitem .@part, .@ticket_lv - getequiprefinerycnt(.@part);
	next;
	emotion ET_DELIGHT;
	mes "[精煉研究家]";
	mes "好了, 就是它了~";
	mes "很好, ^0000FF"+strcharinfo(0)+"^000000!";
	mes "恭喜你那閃耀的 "+.@type$+".";
	mes "你看起來棒極了!";
	mes "再見~!";
	close;
}
