//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================


//===== rAthena Script =======================================
//= Lighthalzen rewards NPCs
//===== Description: =========================================
//= Enchant and reward NPCs from Bio instance/dungeon item drop.
//===== Changelogs: ==========================================
//= 1.0 Moved the NPCs from WolfchevLaboratory.txt. [Capuche]
//============================================================

// Weird Old Man (Rewards)
//============================================================
lighthalzen,342,291,4	script	古怪的老人#Bio4Reward	4_M_EINOLD,{
	if ((MaxWeight - Weight) < 2800 || !checkweight(1201, 1)) {
		mes "^FF0000 檢查你的庫存, 你有太多物品而導致不能繼續. ^000000";
		close;
	}
	mes "[古怪的老人]";
	mes "啊, 現在的年輕人, 是如此的粗魯和沒禮貌.";
	next;
	if (select("不理睬他", "和他對話") == 1) {
		mes "[古怪的老人]";
		mes "你, 是個很壞的年輕人!";
		close;
	}
	mes "["+ strcharinfo(0) +"]";
	mes "先生, 你還好嗎??";
	next;
	mes "[古怪的老人]";
	mes "....?? 你不像其他年輕人. 你很有禮貌! 你知道我發生了什麼..?";
	next;
	switch(rand(1,2)) {
	case 1:
		mes "[古怪的老人]";
		mes "我在探望兒子的路上, 那個地方很遠, 我需要坐飛機去, 和..";
		next;
		mes "[古怪的老人]";
		mes "那些年輕人...實在太吵...我站著卻沒一個人給我讓座, 他們還大聲說話, 讓我很煩.";
		next;
		mes "[古怪的老人]";
		mes "如果我年輕10歲的話, 我真想給他們上一課.";
		next;
		mes "[古怪的老人]";
		mes "但...我又不喜歡時間流逝...";
		next;
		mes "[古怪的老人]";
		mes "所以, 你...你會怎麼做??";
		next;
		mes "[古怪的老人]";
		mes "在公共的地方, 你應該要保持安靜, 看見老人和體弱者應該讓座.";
		next;
		break;
	case 2:
		mes "[古怪的老人]";
		mes "我在探望兒子的路上, 那個地方很遠, 我需要坐飛機去, 和..";
		next;
		mes "[古怪的老人]";
		mes "那些年輕人...實在太吵...我站著卻沒一個人給我讓座, 他們還大聲說話, 讓我很煩.";
		next;
		mes "[古怪的老人]";
		mes "如果我年輕10歲的話, 我真想給他們上一課.";
		next;
		mes "[古怪的老人]";
		mes "但...我又不喜歡時間流逝...";
		next;
		mes "[古怪的老人]";
		mes "所以, 你...你會怎麼做??";
		next;
		mes "[古怪的老人]";
		mes "在公共的地方, 你應該要保持安靜, 看見老人和體弱者應該讓座, 對嗎?";
		next;
		mes "[古怪的老人]";
		mes "我說的對不對?!";
		next;
		switch(rand(1,2)) {
		case 1:
			mes "[古怪的老人]";
			mes "哦, 然後! 你知道嗎...?";
			next;
			mes "[古怪的老人]";
			mes "有一次我走在路上, 一些男孩隨地吐痰, 正好有位女孩路過...嘖嘖!";
			next;
			mes "[古怪的老人]";
			mes "這些人的行為想只狗... 不知道怎麼想的?? 這樣的行為是很不道德的...嘖嘖.";
			next;
			switch(rand(1,2)) {
			case 1:
				mes "[古怪的老人]";
				mes "哦, 然後! 你知道嗎...?";
				next;
				break;
			case 2:
				mes "[古怪的老人]";
				mes "哦, 然後! 你知道嗎...?";
				next;
				mes "[古怪的老人]";
				mes "現在的年輕人不那麼有耐心!!!!";
				next;
				mes "[古怪的老人]";
				mes "通常, 他們說他們會改, 可一直不會更正錯誤, 就連找工作也成為難事...嘖嘖.";
				next;
				mes "[古怪的老人]";
				mes "找一份工作並不容易, 被錄取是很困難的. 但他們一個星期都堅持不了, 最後放棄了工作.";
				next;
				mes "[古怪的老人]";
				mes "這些人是多麼不耐煩和不負責任!!";
				next;
				mes "[古怪的老人]";
				mes "哦, 然後! 你知道嗎...?";
				next;
				break;
			}
		case 2:
			mes "[古怪的老人]";
			mes "哦, 然後! 你知道嗎...?";
			next;
			break;
		}
	}
	mes "^E7A43D老人一次又一次不斷地說話.^000000";
	next;
	mes "[古怪的老人]";
	mes "嗯...你不像普通的年輕人, 看起來很有耐心, 我喜歡你這樣的人!!!";
	next;
	mes "[古怪的老人]";
	mes "現在我雖然已經老了, 但在我年輕的時候, 我是一位著名的商人.";
	next;
	mes "[古怪的老人]";
	mes "在這一段時間裡, 我停止了生意,";
	mes "但最近我之前的客人聯繫我並且問我";
	next;
	mes "[古怪的老人]";
	mes "如果我能給他一些物品 '勇士的意志', '血的饑渴', '亡者的寒氣'.";
	next;
	mes "[古怪的老人]";
	mes "我喜歡你很有禮貌, 和大多數年輕人不一樣, 所以如果你能把我的話傳達, 我會給你這些物品.";
	next;
	while(1) {
		switch(select("我去收集, 我會回來的", "顯示我的裝備", "顯示我的武器")) {
		case 1:
			mes "[古怪的老人]";
			mes "OK, 年輕人, 回頭見.";
			close;
		case 2:
			mes "[古怪的老人]";
			mes "所以, 你對裝備有興趣哦.";
			next;
			mes "[古怪的老人]";
			mes "這些都是我的裝備.";
			next;
			for(.@i = 1; .@i < 8; .@i++) {
				if (((.@i == 1 || .@i == 5) && (countitem(6469) >= 100) && (countitem(6470) >= 50) && (countitem(6471) >= 35)) || 
				    ((.@i == 2 || .@i == 3) && (countitem(6469) >= 100) && (countitem(6470) >= 50) && (countitem(6471) >= 22)) ||
				    ((.@i == 4 || .@i == 7) && (countitem(6469) >= 300) && (countitem(6470) >= 300) && (countitem(6471) >= 400)) ||
				    ((.@i == 6) && (countitem(6469) >= 100) && (countitem(6470) >= 50) && (countitem(6471) >= 30))) {
					.@armor$[.@i] = "^0000FF";
				} else {
					.@armor$[.@i] = "^FF0000";
				}
			}
			while(1) {
				switch(select(
					"給我看其他物品.",
					""+.@armor$[1]+"守護巨人鋼盾^000000",
					""+.@armor$[2]+"葛帔尼亞的古書(水)^000000",
					""+.@armor$[3]+"誓約之書(第2卷)^000000",
					""+.@armor$[4]+"救援鬥篷^000000",
					""+.@armor$[5]+"刺客的手銬^000000",
					""+.@armor$[6]+"綠色研究服^000000",
					""+.@armor$[7]+"古代的金飾^000000"
				)) {
				case 1:
					mes "[古怪的老人]";
					mes "請看.";
					next;
					.@break = 1;
					break;
				case 2:
					mes "<<守護巨人鋼盾[1]>>";
					mes "遭受大型怪物的傷害 -5%, 當精練能力+9時減少量 +5%.";
					mes "盾牌 / Def 130 / 重量 280 / 要求等級 100 / 洞數 1 / 三轉劍士系列";
					callsub L_Reward, 2160, 100, 50, 35; //Giant_Shield
					break;
				case 3:
					mes "<<葛帔尼亞的古書(水)[1]>>";
					mes "INT + 1, MDEF + 2. 基礎INT 120時 MATK +10, MHP+800.";
					mes "與暴風雪法杖一起裝備時, 冰霧/霜凍爆破/暴風雪的變動詠唱減少%為盾牌精鍊度的4倍.";
					mes "盾牌 / Def 30 / 重量 100 / 要求等級 100 / 洞數 1 / 大法師";
					callsub L_Reward, 2161, 100, 50, 22; //Geffenia_Book_Water
					break;
				case 4:
					mes "<<誓約之書第2卷[1]>>";
					mes "MDEF + 5, 可使用奧丁之神力 2Lv. 自己使用的治癒量 +5%.";
					mes "盾牌 / Def 20 / 重量 50 / 要求等級 140 / 洞數 1 / 大主教";
					callsub L_Reward, 2162, 100, 50, 22; //Bible_Of_Promise2
					break;
				case 5:
					mes "<<救援鬥篷[0]>>";
					mes "可使用金蟬脫殼 1Lv, 該技能的冷卻時間增加至 30秒, 裝備時所有技能的消耗SP為2倍, 卸除時 SP - 1200.";
					mes "披肩 / Def 1 / 重量 40 / 要求等級 Lv 130 / 洞數 0 / 大法師/大主教";
					callsub L_Reward, 2582, 100, 50, 35; //Salvage_Cape
					break;
				case 6:
					mes "<<刺客的手銬[1]>>";
					mes "MSP + 20, CRI + 3. 與奎師那之刃一起裝備時, ATK +50, 音速投擲的攻擊力 +50%, FLEE - 30. 與脈輪之刃一起裝備時, CRI +4, 必殺攻擊力 +40%, MHP - 10%.";
					mes "裝飾品 / Def 3 / 重量 40 / 要求等級 Lv 100 / 洞數 1 / 十字切割者";
					callsub L_Reward, 2892, 300, 300, 400; //Assassin_Handcuffs
					break;
				case 7:
					mes "<<綠色研究服[1]>>";
					mes "DEX + 1, MSP + 30. 與手術刀一起裝備後消滅人類系/動物系怪物時, 有機率掉落不死心臟/酒精. 綠色研究服的精鍊度會略微提升掉寶率.";
					mes "鎧甲 / Def 66 / 重量 66 / 要求等級 Lv 100 / 洞數 1 / 基因學者";
					callsub L_Reward, 15044, 100, 50, 30; //Green_Operation_Coat
					break;
				case 8:
					mes "<<古代的金飾[1]>>";
					mes "DEX + 1, MSP + 30. 劍士,商人,盜賊系列時 ATK +8%, 魔法師,服侍系列時 MATK +8%, 使用的治癒量 +7%, 弓箭手系列時 DEX +3, 遠距離物理攻擊力 +10%.";
					mes "頭盔 / Def 66 / 重量 66 / 要求等級 Lv 100 / 洞數 1 / 三轉弓箭手系列";
					callsub L_Reward, 18570, 300, 300, 400; //Ancient_Gold_Deco
					break;
				}
				if (.@break) {
					.@break = 0;
					break;
				}
			}
			break;
		case 3:
			mes "[古怪的老人]";
			mes "所以, 你有興趣的武器.";
			next;
			mes "[古怪的老人]";
			mes "這些是我有的武器";
			next;
			for(.@i = 1; .@i < 15; .@i++) {
				if (((.@i == 1) && (countitem(6469) >= 50) && (countitem(6470) >= 100) && (countitem(6471) >= 10)) ||
				((.@i == 2) && (countitem(6469) >= 50) && (countitem(6470) >= 100) && (countitem(6471) >= 85)) ||
				((.@i == 3 || .@i == 4 || (.@i >= 8 && .@i < 11) || .@i >= 12) && (countitem(6469) >= 50) && (countitem(6470) >= 100) && (countitem(6471) >= 35)) ||
				((.@i == 5 || .@i == 7 || .@i == 11) && (countitem(6469) >= 50) && (countitem(6470) >= 100) && (countitem(6471) >= 40)) ||
				((.@i == 6) && (countitem(6469) >= 300) && (countitem(6470) >= 300) && (countitem(6471) >= 400))) {
					.@weapon$[.@i] = "^0000FF";
				} else {
					.@weapon$[.@i] = "^FF0000";
				}
			}
			while(1) {
				switch(select(
					"看看其他物品.",
					""+.@weapon$[1]+"特務拳刃^000000",
					""+.@weapon$[2]+"斬首拳刃^000000",
					""+.@weapon$[3]+"火焰戰斧^000000",
					""+.@weapon$[4]+"急凍戰斧^000000",
					""+.@weapon$[5]+"鈣礦石長矛^000000",
					""+.@weapon$[6]+"守護巨人長矛^000000",
					""+.@weapon$[7]+"寒氣篇魔法書^000000",
					""+.@weapon$[8]+"元氣之光魔杖^000000",
					""+.@weapon$[9]+"綠光魔短劍^000000",
					""+.@weapon$[10]+"紅光魔短劍^000000",
					""+.@weapon$[11]+"嗜血十字錘^000000",
					""+.@weapon$[12]+"元戎弩^000000",
					""+.@weapon$[13]+"超異能十字弓^000000",
					""+.@weapon$[14]+"束縛之弓^000000"
				)) {
				case 1:
					mes "[古怪的老人]";
					mes "看看.";
					next;
					.@break = 1;
					break;
				case 2:
					mes "<<特務拳刃[1]>>";
					mes "基礎LUK每加2點, HIT +1.";
					mes "拳刃 / ATK 170 / 重量 120 / 武器等級 Lv 3 / 要求等級 Lv 100 / 洞數 1 / 十字切割者";
					callsub L_Reward, 1290, 50, 100, 10; //Agent_Katar
					break;
				case 3:
					mes "<<斬首拳刃[1]>>";
					mes "DEX + 2. FLEE - 30. 對人類系的敵人傷害 +50%. 十字斬的傷害 +30%.";
					mes "拳刃 / ATK 200 / 重量 150 / 武器等級 Lv 4 / 要求等級 Lv 140 / 洞數 1 / 十字切割者";
					callsub L_Reward, 1291, 50, 100, 85; //Guillotine_Katar
					break;
				case 4:
					mes "<<火焰戰斧[1]>>";
					mes "近距離物理攻擊時有機率對敵人和自己發動著火.";
					mes "雙手斧 / ATK 250 / 重量 190 / 火屬性 / 武器等級 Lv 4 / 要求等級 Lv 95 / 洞數 1 / 轉生劍士/商人系列";
					callsub L_Reward, 1392, 50, 100, 35; //Ygnus_Stale
					break;
				case 5:
					mes "<<急凍戰斧[1]>>";
					mes "近距離物理攻擊時有機率對敵人和自己發動結晶";
					mes "雙手斧 / ATK 250 / 重量 190 / 水屬性 / 武器等級 Lv 4 / 要求等級 Lv 95 / 洞數 1 / 轉生劍士/商人系列";
					callsub L_Reward, 1393, 50, 100, 35; //End_Sektura
					break;
				case 6:
					mes "<<鈣礦石長矛[1]>>";
					mes "MSP - 100, [加農炮]攻擊力 +10%, 該技能攻擊力額外增加到精煉值的3倍";
					mes "單手槍 / ATK 180 / 重量 160 / 武器等級 Lv 4 / 要求等級 Lv 100 / 洞數 1 / 皇家衛士";
					callsub L_Reward, 1435, 50, 100, 40; //Cannon_Spear
					break;
				case 7:
					mes "<<守護巨人長矛[0]>>";
					mes "ASPD - 10, 卸除時 SP - 600. 螺旋擊刺的再使用延遲 +20秒. 基礎STR 120以上時, ATK +300.";
					mes "雙手槍 / ATK 20 / 重量 2000 / 武器等級 Lv 4 / 要求等級 Lv 140 / 洞數 0 / 符文騎士";
					callsub L_Reward, 1490, 300, 300, 400; //Giant_Lance
					break;
				case 8:
					mes "<<寒氣篇魔法書[2]>>";
					mes "MATK + 160, DEX + 1. 冰箭術, 鑽石星塵的攻擊力增加%幅度為精鍊度的3倍, 該技能的消耗SP增加幅度為精鍊度的5倍.";
					mes "書 / ATK 100 / 重量 100 / 水屬性 / 武器等級 Lv 4 / 要求等級 Lv 100 / 洞數 2 / 元素使";
					callsub L_Reward, 1584, 50, 100, 40; //Chilly_Spell_Book
					break;
				case 9:
					mes "<<元氣之光魔杖[1]>>";
					mes "MATK + 160. 依精鍊度治癒量和治癒時所消耗的SP也隨之大幅度的增加. 不會損壞.";
					mes "單手杖 / ATK 30 / 重量 40 / 聖屬性 / 武器等級 Lv 4 / 要求等級 Lv 110 / 洞數 1 / 大主教.";
					callsub L_Reward, 1659, 50, 100, 35; //Recovery_Light
					break;
				case 10:
					mes "<<綠光魔短劍[0]>>";
					mes "MATK + 80. 近距離物理攻擊時有機率對目標發動冰凍, 依精鍊度冰凍機率會增加.";
					mes "短劍 / ATK 160 / 重量 50 / 風屬性 / 武器等級 Lv 4 / 要求等級 Lv 110 / 洞數 0 / 轉生劍士/商人/盜賊系列";
					callsub L_Reward, 13069, 50, 100, 35; //Aztoe_Nail
					break;
				case 11:
					mes "<<Scarletto Nail[0]>>";
					mes "MATK + 80. For Physical Melee Attack, cast Stone curse to the target with a certain chance. The chance is affected by Refine Lv.";
					mes "Dagger / ATK 160 / Weight 50 / Fire Property / Weapon Lv 4 / Required Lv 110 / Socket 0 / for High Swordman/Merchant/Theif Classes";
					callsub L_Reward, 13070, 50, 100, 35; //Scarletto_Nail
					break;
				case 12:
					mes "<<嗜血十字錘[0]>>";
					mes "近距離物理攻擊時有機率發動自動念咒 毀滅烈焰1Lv, 依精鍊度該技能的發動機率也會增加";
					mes "鈍器 / ATK 170 / 重量 150 / 暗屬性 / 武器等級 Lv 4 / 要求等級 Lv 100 / 洞數 0 / 轉生服侍系列";
					callsub L_Reward, 16017, 50, 100, 40; //Bloody_Cross
					break;
				case 13:
					mes "<<元戎弩[2]>>";
					mes "三角射擊的攻擊力增加%幅度為精鍊度的2倍, 該技能的消耗SP也會增加.";
					mes "弓 / ATK 150 / 重量 110 / 武器等級 Lv 4 / 要求等級 Lv 100 / 洞數 2 / 逐影";
					callsub L_Reward, 18109, 50, 100, 35; //Catapult
					break;
				case 14:
					mes "<<超異能十字弓[2]>>";
					mes "箭雨風暴攻擊力增加%幅度為精鍊度的5倍, 該技能的消耗SP也會增加, 基礎AGI 120時 ASPD+1.";
					mes "弓 / ATK 160 / 重量 90 / 武器等級 Lv 4 / 要求等級 Lv 110 / 洞數 2 / 遊俠";
					callsub L_Reward, 18110, 50, 100, 35; //Big_CrossBow
					break;
				case 15:
					mes "<<束縛之弓[2]>>";
					mes "DEX +1, 物理攻擊時有機率發動自動念咒 易燃之網 1Lv.";
					mes "弓 / ATK 150 / 重量 150 / 武器等級 Lv 3 / 要求等級 Lv 120 / 洞數 2 / 三轉弓箭手系列";
					callsub L_Reward, 18111, 50, 100, 35; //Creeper_Bow
					break;
				}
				if (.@break) {
					.@break = 0;
					break;
				}
			}
		}
	}
	close;

L_Reward:
	setarray .@amount[0], getarg(1), getarg(2), getarg(3);
	next;
	mes "我需要"+ .@amount[0] +" 勇士的意志, "+ .@amount[1] +" 血的饑渴, "+ .@amount[2] +" 亡者的寒氣";
	next;
	if (countitem(6469) >= .@amount[0] && countitem(6470) >= .@amount[1] && countitem(6471) >= .@amount[2]) {
		if (select("不換", "換取") == 1) {
			mes "[古怪的老人]";
			mes "好的..";
			close;
		}
		emotion ET_PROFUSELY_SWEAT, "Weird old man#Bio4Reward";
		progressbar "FF00FF", 2;
		delitem 6469, .@amount[0]; //Will_Of_Warrior
		delitem 6470, .@amount[1]; //Blood_Thirst
		delitem 6471, .@amount[2]; //Goast_Chill
		getitem getarg(0), 1;
			mes "[古怪的老人]";
			mes "你去那裡.訪問我的時候, 我們可以談談";
		close;
	}
	return;
}

// Sorcerer (Rewards)
//============================================================
lhz_cube,233,24,4	script	巫師#Bio4Reward	4_M_UMDANCEKID,{
	if ((MaxWeight - Weight) < 1200 || !checkweight(1201, 1)) {
		mes "^FF0000 無法完成任務因為你身上太重或者東西太多. ^000000";
		close;
	}
	disable_items;
	mes "[布丁]";
	mes "... .... ...";
	next;
	switch(select("Umba umba humbaa", "你在這裡做什麼?", "這是迄今為止發現的設備?", "給你, 先生")) {
	case 1:
		emotion ET_FRET;
		mes "[布丁]";
		mes "嘿, 對不起, 但我不是汶巴拉人!你真粗魯";
		close;
	case 2:
		mes "[布丁]";
		mes "正如你所看到的, 我是個巫師, 我的名字叫布丁.";
		next;
		select("....?");
		emotion ET_FRET;
		mes "[布丁]";
		mes "... 那是什麼臉?我開始感到不愉快..";
		next;
		emotion ET_OK;
		mes "[布丁]";
		mes "前兩天,我感到奇怪, 古怪色彩.所以我也跟著說, 它把我帶到這裡";
		next;
		select("我看到.");
		mes "[布丁]";
		mes "好吧, 讓我繼續.請別打擾我.";
		next;
		mes "[布丁]";
		mes "我進了這裡的實驗室做研究, 哦天哪!我發現那些怪物, 其中像從「吉芬地下城」的魔物充斥在其中.";
		next;
		mes "[布丁]";
		mes "從這些怪物中, 我能找到稀有物品 '^F2766E勇士的意志^000000' '^952420血的饑渴^000000' '^88C6F7亡者的寒氣^000000' 他們不會出現在其他地方.";
		next;
		emotion ET_QUESTION, "Sorcerer#Bio4Reward";
		mes "[布丁]";
		mes "很多時間對我自己的研究後, 我終於想出了一個辦法來使用這些物品.你想知道嗎?";
		next;
		switch(select("呵呵....","當然了, 我真的很想知道")) {
		case 1:
			emotion ET_HUK, 1;
			emotion ET_FRET, "Sorcerer#Bio4Reward";
			mes "[布丁]";
			mes "噢, 我的!你睡著了, 你叫我什麼呢?好吧, 反正我覺得懶得再解釋, 所以我只會繼續下去";
			next;
			break;
		case 2:
			emotion ET_SMILE, "Sorcerer#Bio4Reward";
			mes "[布丁]";
			mes "不錯, 你仔細聽我說話然後我會一一解釋.";
			next;
			break;
		}
		mes "[布丁]";
		mes "Those '^F2766E勇士的意志^000000' and '^952420血的饑渴^000000'..... 可以用來做保暖的材料.";
		next;
		mes "[布丁]";
		mes "而 '^F2766E勇士的意志^000000' 能夠在裝備的第四槽為你的裝備增加能力,";
		next;
		mes "[布丁]";
		mes "還有 '^952420血的饑渴^000000' 可以在裝備第三槽進行附魔.";
		next;
		mes "[布丁]";
		mes "而且!!!";
		mes "'^88C6F7亡者的寒氣^000000' 是一種冷凍材料.";
		mes "它允許從'^F2766E勇士的意志^000000' 或者 '^952420血的饑渴^000000'返回能量.";
		next;
		mes "[布丁]";
		mes "你說什麼?沒有聽起來有趣嗎?但是, 有一個問題...";
		next;
		mes "[布丁]";
		mes "只能從實驗室的那些怪物身上獲取這些材料";
		next;
		select("那麼, 結論是什麼?");
		emotion ET_QUESTION, 1;
		emotion ET_FRET;
		mes "[布丁]";
		mes "嘖嘖......我告訴過你在我說話的時候不要打斷我, 好吧, 我會回答這個重要的問題的.";
		next;
		mes "[布丁]";
		mes "如果你給我帶來10個 '^F2766E勇士的意志^000000' 或者 10個 '^952420血的饑渴^000000', 我會幫你進行附魔, 而這些材料可以在實驗室裡獲得.";
		next;
		mes "[布丁]";
		mes "如果你不滿意你擁有強大的力量, 給我帶來10個 '^88C6F7亡者的寒氣^000000', 我將給你的裝備進行附魔";
		next;
		mes "[布丁]";
		mes "你說什麼?有兩個選項.....相當不錯, 是吧?";
		next;
		mes "[布丁]";
		mes "如果您獲得 '^F2766E勇士的意志^000000' 或者 '^952420血的饑渴^000000', 並且還有可以附魔的裝備, 我將對你的裝備進行附魔, 快點拿來給我, 我會在這裡等你.";
		close;
	case 3:
		mes "[布丁]";
		mes "這是迄今發現的....";
		next;
		mes "- 檢查 -";
		mes "布丁從口袋裡拿出了一張紙";
		next;
		mes "[布丁]";
		mes "讓我看看....";
		mes "6盔甲, 14武器";
		next;
		while(1) {
			switch(select("好吧, 我需要知道更多信息", "6個盔甲", "14個武器")) {
			case 1:
				mes "[布丁]";
				mes "如果您獲得 '^F2766E勇士的意志^000000' 或者 '^952420血的饑渴^000000', 並且還有可以附魔的裝備, 我將對你的裝備進行附魔, 快點拿來給我, 我會在這裡等你.";
				close;
			case 2:
				mes "< 防具列表 >";
				mes "守護巨人鋼盾[1]";
				mes "葛帔尼亞的古書(水)[1]";
				mes "誓約之書第2卷[1]";
				mes "救援鬥篷[0]";
				mes "刺客的手銬[1]";
				mes "綠色研究服[1]";
				mes "古代的金飾[1]";
				next;
				mes "[布丁]";
				mes "到目前為止, 這些都是發現了的裝甲.難道你想看到其他的東西?";
				next;
				break;
			case 3:
				mes "< 武器列表 >";
				mes "特務拳刃[1]";
				mes "斬首拳刃[1]";
				mes "火焰戰斧[1]";
				mes "急凍戰斧[1]";
				mes "鈣礦石長矛[1]";
				mes "守護巨人長矛[0]";
				mes "寒氣篇魔法書[2]";
				mes "元氣之光魔杖[1]";
				mes "綠光魔短劍[0]";
				mes "紅光魔短劍[0]";
				mes "嗜血十字錘[0]";
				mes "元戎弩[2]";
				mes "超異能十字弓[2]";
				mes "束縛之弓[2]";
				next;
				mes "[布丁]";
				mes "到目前為止, 這些都是發現了的武器.難道你想看到其他的東西?";
				next;
				break;
			}
		}
		close;
	case 4:
		emotion ET_SPARK;
		mes "[布丁]";
		mes "啊啊啊啊, 你沒有把它們帶來嗎?";
		next;
		break;
	}
	mes "[布丁]";
	mes "好啊, 你想要什麼能力?";
	next;
	setarray .@parts[0], EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_TOP;
	for(.@i = 0; .@i < 7; .@i++) {
		if (!getequipisequiped(.@parts[.@i])) {
			switch(.@i) {
				case 0:
					.@equipped$[.@i] = "^999999衣服-[沒有裝備]^000000";
					break;
				case 1:
					.@equipped$[.@i] = "^999999左手-[沒有裝備]^000000";
					break;
				case 2:
					.@equipped$[.@i] = "^999999右手-[沒有裝備]^000000";
					break;
				case 3:
					.@equipped$[.@i] = "^999999披肩-[沒有裝備]^000000";
					break;
				case 4:
					.@equipped$[.@i] = "^999999裝飾品-[沒有裝備]^000000";
					break;
				case 5:
					.@equipped$[.@i] = "^999999裝飾品-[沒有裝備]^000000";
					break;
				case 6:
					.@equipped$[.@i] = "^999999頭下-[沒有裝備]^000000";
					break;
			}
		} else {
			.@equipped$[.@i] = getequipname(.@parts[.@i]);
		}
	}
	switch(select("我改變主意了.", .@equipped$[0], .@equipped$[1], .@equipped$[2], .@equipped$[3], .@equipped$[4],
	.@equipped$[5], .@equipped$[6])) {
	case 1:
		mes "[布丁]";
		mes "好吧, 我期待著這樣做, 但我不能幫助";
		close;
	case 2:
		if (!getequipisequiped(EQI_ARMOR)) {
			mes "[布丁]";
			mes "嘿, 你的衣服呢?";
			close;
		}
		.@part = EQI_ARMOR;
		break;
	case 3:
		if (!getequipisequiped(EQI_HAND_L)) {
			mes "[布丁]";
			mes "你的左手沒有任何裝備";
			close;
		}
		.@part = EQI_HAND_L;
		break;
	case 4:
		if (!getequipisequiped(EQI_HAND_R)) {
			mes "[布丁]";
			mes "你的右手沒有任何裝備.";
			close;
		}
		.@part = EQI_HAND_R;
		break;
	case 5:
		if (!getequipisequiped(EQI_GARMENT)) {
			mes "[布丁]";
			mes "呃, 你的披肩呢?";
			close;
		}
		.@part = EQI_GARMENT;
		break;
	case 6:
		if (!getequipisequiped(EQI_ACC_L)) {
			mes "[布丁]";
			mes ".....你的裝飾品呢?";
			close;
		}
		.@part = EQI_ACC_L;
		break;
	case 7:
		if (!getequipisequiped(EQI_ACC_R)) {
			mes "[布丁]";
			mes ".....你的裝飾品呢?";
			close;
		}
		.@part = EQI_ACC_R;
		break;
	case 8:
		if (!getequipisequiped(EQI_HEAD_TOP)) {
			mes "[布丁]";
			mes "嘿, 這部位好像沒有裝備.";
			close;
		}
		.@part = EQI_HEAD_TOP;
		break;
	}
	.@refine_count = getequiprefinerycnt(.@part);
	.@equip_item = getequipid(.@part);
	.@lhz_max_num = 4000;
	if (.@equip_item == 13069 || .@equip_item == 1291 || .@equip_item == 1392 || 
	    .@equip_item == 1393 || .@equip_item == 1435 || .@equip_item == 1490 || 
	    .@equip_item == 13069 || .@equip_item == 13070 || .@equip_item == 16017) {
		.@type = 1;
		if (.@equip_item == 1490)
			.@lhz_max_num = 4200;
	} else if (.@equip_item == 18109 || .@equip_item == 18110 || .@equip_item == 18111) {
		.@type = 2;
	} else if (.@equip_item == 1584 || .@equip_item == 1659) {
		.@type = 3;
	} else if (.@equip_item == 2160 || .@equip_item == 2161 || .@equip_item == 2162 ||
	           .@equip_item == 2582 || .@equip_item == 2892 || .@equip_item == 15044 || 
	           .@equip_item == 18570) {
		.@type = 4;
		if (.@equip_item == 2582 || .@equip_item == 18570)
			.@lhz_max_num = 4200;
	} else {
		mes "[布丁]";
		mes "在這之前我需要檢查一下該裝備是否可以附魔.";
		next;
		specialeffect2 EF_SPHERE;
		progressbar "ffff00", 2;
		specialeffect2 EF_SUI_EXPLOSION;
		emotion ET_CRY;
		mes "[布丁]";
		mes "呃, 該裝備不能接受'^F2766E勇士的意志^000000' 或者 '^952420血的饑渴^000000'的能力.";
		close;
	}
	emotion ET_DELIGHT;
	mes "[布丁]";
	mes "這裝備可以接受 '^F2766E勇士的意志^000000' 或者 '^952420血的饑渴^000000'的能力.";
	next;
	if (countitem(6469) >= 10)
		.@menu_in$[1] = "^F2766勇士的意志^000000的能力";
	else
		.@menu_in$[1] = "^999999'勇士的意志'^000000還不夠";

	if (countitem(6470) >= 10)
		.@menu_in$[2] = "'^952420血的饑渴^000000'的能力";
	else
		.@menu_in$[2] = "^999999'血的饑渴'^000000還不夠";

	if (countitem(6471) >= 10)
		.@menu_in$[3] = "消費'^88C6F7亡者的寒氣^000000'";
	else
		.@menu_in$[3] = "^999999'亡者的寒氣'^000000還不夠";
	mes "[布丁]";
	mes "好了!你想在哪個裝備中注入能力?";
	next;
	switch(select("我不需要!!", .@menu_in$[1], .@menu_in$[2], .@menu_in$[3])) {
	case 1:
		mes "[布丁]";
		mes "哦..我很期待這樣做, 但是這一切都取決於你......";
		close;
	case 2:
		if (countitem(6469) < 10) {
			mes "[布丁]";
			mes "'^F2766E勇士的意志^000000'還不夠, 你需要10個'^F2766E勇士的意志^000000'";
			close;
		}
		else if (getequipcardid(.@part, 3) > 0) {
			mes "[布丁]";
			mes "這個裝備已經獲得了'^F2766E勇士的意志^000000'.";
			close;
		} else {
			.@socket_type = 4;
		}
		break;
	case 3:
		if (countitem(6470) < 10) {
			mes "[布丁]";
			mes "'^952420血的饑渴^000000'還不夠, 你需要10個'^952420血的饑渴^000000'";
			close;
		}
		else if (getequipcardid(.@part, 2) > 0) {
			mes "[布丁]";
			mes "這個裝備已經獲得了'^952420血的饑渴^000000'.";
			close;
		} else {
			.@socket_type = 3;
		}
		break;
	case 4:
		// Enchant Removal
		if (countitem(6471) < 10) {
			.@menu_clear$[1] = "^999999'亡者的寒氣'^000000還不夠";
			.@menu_clear$[2] = "^999999'亡者的寒氣'^000000還不夠";
			.@4thzptsodyd = 1;
			.@3thzptsodyd = 1;
		} else {
			if (getequipcardid(.@part, 3) > 0) {
				.@menu_clear$[1] = "移除'^F2766E勇士的意志^000000'的能力";
			} else {
				.@menu_clear$[1] = "^999999沒有能力可以移除^000000";
				.@4thzptsodyd = 1;
			}
			if (getequipcardid(.@part, 2) > 0) {
				.@menu_clear$[2] = "移除'^952420血的饑渴^000000'的能力";
			} else {
				.@menu_clear$[2] = "^999999沒有能力可以移除^000000";
				.@3thzptsodyd = 1;
			}
		}
		mes "[布丁]";
		mes "好吧, 你想要把能力移除?";
		next;
		.@menu = select("不, 我不想.", .@menu_clear$[1], .@menu_clear$[2]);
		switch(.@menu){
		case 1:
			mes "[布丁]";
			mes "真的?好吧..";
			close;
		case 2:
		case 3:
			if (countitem(6471) < 10) {
				mes "[布丁]";
				mes "'^88C6F7亡者的寒氣^000000'不夠, 你需要10個'^88C6F7亡者的寒氣^000000'";
				close;
			}
			if ( (.@menu==3 && .@3thzptsodyd == 1) || (.@menu==2 && .@4thzptsodyd == 1) ) {
				mes "[布丁]";
				mes "該裝備沒有獲得任何能力.";
				close;
			}
			setarray .@equip_card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);
			if (.@menu == 2)
				.@equip_card[3] = 0;
			else
				.@equip_card[2] = 0;
			.@equip_item = getequipid(.@part);
			specialeffect EF_BEGINSPELL2, AREA, "巫師#Bio4Reward";
			progressbar "ffff00", 2;
			specialeffect2 EF_FROSTWEAPON;
			delitem 6471, 10; //Goast_Chill
			delequip .@part;
			getitem2 .@equip_item, 1, 1, .@refine_count, 0, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3];
			mes "[布丁]";
			if (.@menu == 3)
				mes "這個'^952420血的饑渴^000000'的能力已經移除.";
			else
				mes "這個'^F2766E勇士的意志^000000'的能力已經移除.";
			close;
		}
	}
	mes "[布丁]";
	mes "在我注入能量之前我給你解釋下注意事項.";
	next;
	mes "[布丁]";
	mes "While dwelling the power of '^F2766EWill of Warrior^000000' or '^952420Thirst for Blood^000000', the equipment would be not capable to accept that and will be destryed..";
	next;
	mes "[布丁]";
	mes "^9D2628如果裝備被損壞, 你將失去裝備上的卡片和精鍊度^000000";
	next;
	mes "[布丁]";
	mes "好吧, 你真的想注入能量到裝備?";
	next;
	if(select("等等, 我再考慮下.", "當然, 就現在.") == 1) {
		mes "[布丁]";
		mes "好的, 讓你考慮下!";
		close;
	}
	mes "[布丁]";
	mes "我希望你做了一個正確的決定...";
	next;
	if (.@socket_type >= 3) {
		switch(.@type){
			case 1:	// Short Distance
				.@r = rand(1, .@lhz_max_num);
				if      (.@r < 401)  .@enchant = 4700; //Strength1
				else if (.@r < 441)  .@enchant = 4701; //Strength2
				else if (.@r < 445)  .@enchant = 4702; //Strength3
				else if (.@r < 845)  .@enchant = 4730; //Agility1
				else if (.@r < 885)  .@enchant = 4731; //Agility2
				else if (.@r < 889)  .@enchant = 4732; //Agility3
				else if (.@r < 1289) .@enchant = 4740; //Vitality1
				else if (.@r < 1329) .@enchant = 4741; //Vitality2
				else if (.@r < 1333) .@enchant = 4742; //Vitality3
				else if (.@r < 1523) .@enchant = 4710; //Inteligence1
				else if (.@r < 1542) .@enchant = 4711; //Inteligence2
				else if (.@r < 1544) .@enchant = 4712; //Inteligence3
				else if (.@r < 1844) .@enchant = 4720; //Dexterity1
				else if (.@r < 1874) .@enchant = 4721; //Dexterity2
				else if (.@r < 1877) .@enchant = 4722; //Dexterity3
				else if (.@r < 2067) .@enchant = 4750; //Luck1
				else if (.@r < 2086) .@enchant = 4751; //Luck2
				else if (.@r < 2088) .@enchant = 4752; //Luck3
				else if (.@r < 2491) .@enchant = 0;
				else if (.@r < 2891) .@enchant = 4811; //Fighting_Spirit1
				else if (.@r < 2931) .@enchant = 4810; //Fighting_Spirit2
				else if (.@r < 2935) .@enchant = 4809; //Fighting_Spirit3
				else if (.@r < 3125) .@enchant = 4815; //Spell1
				else if (.@r < 3144) .@enchant = 4814; //Spell2
				else if (.@r < 3146) .@enchant = 4813; //Spell3
				else if (.@r < 3148) .@enchant = 4807; //Atk_Speed1
				else if (.@r < 3348) .@enchant = 4818; //Sharp1
				else if (.@r < 3368) .@enchant = 4817; //Sharp2
				else if (.@r < 3370) .@enchant = 4816; //Sharp3
				else if (.@r < 3560) .@enchant = 4832; //Expert_Archer1
				else if (.@r < 3579) .@enchant = 4833; //Expert_Archer2
				else if (.@r < 3581) .@enchant = 4834; //Expert_Archer3
				else                 .@enchant = 0;
				break;
			case 2:	// Long Distance
				.@r = rand(1, .@lhz_max_num);
				if      (.@r < 191)  .@enchant = 4700; //Strength1
				else if (.@r < 210)  .@enchant = 4701; //Strength2
				else if (.@r < 212)  .@enchant = 4702; //Strength3
				else if (.@r < 612)  .@enchant = 4730; //Agility1
				else if (.@r < 652)  .@enchant = 4731; //Agility2
				else if (.@r < 656)  .@enchant = 4732; //Agility3
				else if (.@r < 846)  .@enchant = 4740; //Vitality1
				else if (.@r < 865)  .@enchant = 4741; //Vitality2
				else if (.@r < 867)  .@enchant = 4742; //Vitality3
				else if (.@r < 1167) .@enchant = 4710; //Inteligence1
				else if (.@r < 1197) .@enchant = 4711; //Inteligence2
				else if (.@r < 1200) .@enchant = 4712; //Inteligence3
				else if (.@r < 1600) .@enchant = 4720; //Dexterity1
				else if (.@r < 1640) .@enchant = 4721; //Dexterity2
				else if (.@r < 1644) .@enchant = 4722; //Dexterity3
				else if (.@r < 1834) .@enchant = 4750; //Luck1
				else if (.@r < 1853) .@enchant = 4751; //Luck2
				else if (.@r < 1855) .@enchant = 4752; //Luck3
				else if (.@r < 2258) .@enchant = 0;
				else if (.@r < 2448) .@enchant = 4811; //Fighting_Spirit1
				else if (.@r < 2467) .@enchant = 4810; //Fighting_Spirit2
				else if (.@r < 2469) .@enchant = 4809; //Fighting_Spirit3
				else if (.@r < 2669) .@enchant = 4815; //Spell1
				else if (.@r < 2689) .@enchant = 4814; //Spell2
				else if (.@r < 2691) .@enchant = 4813; //Spell3
				else if (.@r < 2693) .@enchant = 4807; //Atk_Speed1
				else if (.@r < 3093) .@enchant = 4818; //Sharp1
				else if (.@r < 3133) .@enchant = 4817; //Sharp2
				else if (.@r < 3137) .@enchant = 4816; //Sharp3
				else if (.@r < 3537) .@enchant = 4832; //Expert_Archer1
				else if (.@r < 3577) .@enchant = 4833; //Expert_Archer2
				else if (.@r < 3581) .@enchant = 4834; //Expert_Archer3
				else                 .@enchant = 0;
				break;
			case 3:
				.@r = rand(1, .@lhz_max_num);
				if      (.@r < 301)  .@enchant = 4700; //Strength1
				else if (.@r < 331)  .@enchant = 4701; //Strength2
				else if (.@r < 334)  .@enchant = 4702; //Strength3
				else if (.@r < 534)  .@enchant = 4730; //Agility1
				else if (.@r < 554)  .@enchant = 4731; //Agility2
				else if (.@r < 556)  .@enchant = 4732; //Agility3
				else if (.@r < 956)  .@enchant = 4740; //Vitality1
				else if (.@r < 996)  .@enchant = 4741; //Vitality2
				else if (.@r < 1000) .@enchant = 4742; //Vitality3
				else if (.@r < 1400) .@enchant = 4710; //Inteligence1
				else if (.@r < 1440) .@enchant = 4711; //Inteligence2
				else if (.@r < 1444) .@enchant = 4712; //Inteligence3
				else if (.@r < 1844) .@enchant = 4720; //Dexterity1
				else if (.@r < 1884) .@enchant = 4721; //Dexterity2
				else if (.@r < 1888) .@enchant = 4722; //Dexterity3
				else if (.@r < 2078) .@enchant = 4750; //Luck1
				else if (.@r < 2097) .@enchant = 4751; //Luck2
				else if (.@r < 2099) .@enchant = 4752; //Luck3
				else if (.@r < 2502) .@enchant = 0;
				else if (.@r < 2692) .@enchant = 4811; //Fighting_Spirit1
				else if (.@r < 2711) .@enchant = 4810; //Fighting_Spirit2
				else if (.@r < 2713) .@enchant = 4809; //Fighting_Spirit3
				else if (.@r < 3113) .@enchant = 4815; //Spell1
				else if (.@r < 3153) .@enchant = 4814; //Spell2
				else if (.@r < 3157) .@enchant = 4813; //Spell3
				else if (.@r < 3159) .@enchant = 4807; //Atk_Speed1
				else if (.@r < 3349) .@enchant = 4818; //Sharp1
				else if (.@r < 3368) .@enchant = 4817; //Sharp2
				else if (.@r < 3370) .@enchant = 4816; //Sharp3
				else if (.@r < 3560) .@enchant = 4832; //Expert_Archer1
				else if (.@r < 3579) .@enchant = 4833; //Expert_Archer2
				else if (.@r < 3581) .@enchant = 4834; //Expert_Archer3
				else                 .@enchant = 0;
				break;
			case 4:	// Armor
				.@r = rand(1, .@lhz_max_num);
				if      (.@r < 301)  .@enchant = 4700; //Strength1
				else if (.@r < 331)  .@enchant = 4701; //Strength2
				else if (.@r < 334)  .@enchant = 4702; //Strength3
				else if (.@r < 634)  .@enchant = 4730; //Agility1
				else if (.@r < 664)  .@enchant = 4731; //Agility2
				else if (.@r < 667)  .@enchant = 4732; //Agility3
				else if (.@r < 967)  .@enchant = 4740; //Vitality1
				else if (.@r < 997)  .@enchant = 4741; //Vitality2
				else if (.@r < 1000) .@enchant = 4742; //Vitality3
				else if (.@r < 1300) .@enchant = 4710; //Inteligence1
				else if (.@r < 1330) .@enchant = 4711; //Inteligence2
				else if (.@r < 1333) .@enchant = 4712; //Inteligence3
				else if (.@r < 1633) .@enchant = 4720; //Dexterity1
				else if (.@r < 1663) .@enchant = 4721; //Dexterity2
				else if (.@r < 1666) .@enchant = 4722; //Dexterity3
				else if (.@r < 1966) .@enchant = 4750; //Luck1
				else if (.@r < 1996) .@enchant = 4751; //Luck2
				else if (.@r < 1999) .@enchant = 4752; //Luck3
				else if (.@r < 2399) .@enchant = 0;
				else if (.@r < 2549) .@enchant = 4786; //Mdef2
				else if (.@r < 2599) .@enchant = 4787; //Mdef4
				else if (.@r < 2609) .@enchant = 4788; //Mdef6
				else if (.@r < 2614) .@enchant = 4789; //Mdef8
				else if (.@r < 2616) .@enchant = 4790; //Mdef10
				else if (.@r < 2766) .@enchant = 4791; //Def3
				else if (.@r < 2866) .@enchant = 4792; //Def6
				else if (.@r < 2876) .@enchant = 4793; //Def9
				else if (.@r < 2878) .@enchant = 4794; //Def12
				else if (.@r < 3278) .@enchant = 0;
				else if (.@r < 3428) .@enchant = 4859; //Evasion1
				else if (.@r < 3478) .@enchant = 4860; //Evasion3
				else if (.@r < 3488) .@enchant = 4762; //Evasion6
				else if (.@r < 3498) .@enchant = 4800; //SP50
				else if (.@r < 3734) .@enchant = 4795; //HP100
				else if (.@r < 3834) .@enchant = 4796; //HP200
				else if (.@r < 3884) .@enchant = 4797; //HP300
				else                 .@enchant = 0;
			break;
		}
		setarray .@equip_card[0], getequipcardid(.@part,0),getequipcardid(.@part,1),getequipcardid(.@part,2),getequipcardid(.@part,3);
		.@equip_card[(.@socket_type-1)] = .@enchant;
		.@equip_item = getequipid(.@part);
		specialeffect EF_BEGINSPELL3, AREA, "巫師#Bio4Reward";
		progressbar "ffff00", 2;
		if (.@socket_type == 3)
			delitem 6470, 10; //Blood_Thirst
		else
			delitem 6469, 10; //Will_Of_Warrior
		delequip .@part;
		
		if (.@enchant == 0){
			specialeffect2 EF_SUI_EXPLOSION;
			emotion ET_OTL;
			mes "[布丁]";
			mes "唉, 該裝備無法接受 "+((.@socket_type==3)?"'^952420血的饑渴^000000'":"'^F2766E勇士的意志^000000'")+", 它將被破壞";
			close;
		} else {
			specialeffect2 EF_FIREHIT;
			getitem2 .@equip_item, 1, 1, .@refine_count, 0, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3];
			emotion ET_DELIGHT;
			mes "[布丁]";
			mes "幸運的是"+((.@socket_type==3)?"'^952420血的饑渴^000000'":"'^F2766E勇士的意志^000000'")+"已經注入到裝備上了..";
			close;
		}
	}
	end;
}
