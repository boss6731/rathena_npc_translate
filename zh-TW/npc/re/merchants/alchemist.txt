//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================


//===== Hercules Script ======================================
//= Alchemist Shop
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.0
//===== Description: =========================================
//= [Official Conversion]
//= Merchants for Alchemist Materials and Manuals.
//===== Additional Comments: =================================
//= 1.0 Split Geneticist shop from main file. [Euphy]
//=     Updated dialogue to match the official script.
//============================================================

alde_alche,31,186,3	script	製作書商人#alde	4_M_ALCHE_D,{
	mes "[製作書商人]";
	mes "歡迎.";
	mes "我在這裡出售";
	mes "基因學者的^0000FF特殊製作書^000000.";
	next;
	mes "[製作書商人]";
	mes "基因學者有一些技能";
	mes "需要製作書來激活.";
	next;
	mes "[製作書商人]";
	mes "更多的製作書,";
	mes "可以讓你使用更多的技能.";
	next;
	mes "[製作書商人]";
	mes "請明白一點";
	mes "我只賣這些特殊的製作書";
	mes "我^FF0000不賣任何其他東西^000000.";
	next;
	mes "[製作書商人]";
	mes "您是不是需要看一下我的製作書?";
	next;
	if(select("- 當然.:- 不用.") == 2) {
		mes "[製作書商人]";
		mes "感謝您光臨我的鋪子.";
		mes "祝您一路順風.";
		close;
	}
	if (checkweight(1201,1) == 0) {
		mes "[製作書商人]";
		mes "抱歉, 您的物品攜帶種類過多.";
		mes "請先整理下背包.";
		close;
	}
	if (MaxWeight - Weight < 2500) {
		mes "[製作書商人]";
		mes "請先減輕一下負重.";
		close;
	}
	mes "[製作書商人]";
	mes "好的, 這裡是製作書的列表.";
	next;
	switch(select("[蘋果炸彈製造書] 100,000 Zeny:[菠蘿炸彈製造書] 100,000 Zeny:[椰果炸彈製造書] 100,000 Zeny:[甜瓜炸彈製造書] 100,000 Zeny:[香蕉炸彈製造書] 100,000 Zeny:[植物基因栽培法] 100,000 Zeny:[高級藥水製造手冊] 100,000 Zeny:[組合烹飪料理書] 100,000 Zeny:[體力爆發研究書] 100,000 Zeny:[活力藥水製造法] 100,000 Zeny:關閉")){
	case 1: set .@item,6279; break;
	case 2: set .@item,6280; break;
	case 3: set .@item,6281; break;
	case 4: set .@item,6282; break;
	case 5: set .@item,6283; break;
	case 6: set .@item,6284; break;
	case 7: set .@item,6285; break;
	case 8: set .@item,11022; break;
	case 9: set .@item,11023; break;
	case 10: set .@item,11024; break;
	case 11:
		mes "[製作書商人]";
		mes "感謝您的惠顧.";
		mes "請下次再來.";
		close;
	}
	mes "[製作書商人]";
	mes "^0000FF["+getitemname(.@item)+"] 需要";
	mes "100,000 Zeny.";
	mes "您希望購買多少本?";
	next;
	input .@amount;
	if (.@amount == 0) {
		mes "[製作書商人]";
		mes "您是否對其它的書籍感興趣呢?";
		close;
	}
	if (.@amount < 1 || .@amount > 99 ){
		mes "[製作書商人]";
		mes "您每次不能購買超過 100 本.";
		close;
	}
	mes "[製作書商人]";
	mes "您輸入了購買數量為 "+.@amount+"本 ^0000FF["+getitemname(.@item)+"]^000000.";
	mes "是否要繼續交易?";
	next;
	if (select("- 是:- 否") == 1) {
		set .@total, .@amount * 100000;
		if (Zeny < .@total) {
			mes "[製作書商人]";
			mes "抱歉, 您的Zeny不夠.";
			close;
		}
		if (checkweight(.@item,.@amount) == 0){
			mes "[製作書商人]";
			mes "看上去您的背包很滿了.";
			mes "請先處理下再來吧.";
			close;
		}
		mes "[製作書商人]";
		mes "感謝您的採購.";
		Zeny -= .@total;
		getitem .@item,.@amount;
		close;
	}
	mes "[製作書商人]";
	mes "決定之前";
	mes "請略花費一些時間.";
	close;
}
