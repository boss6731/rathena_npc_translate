//============================================================
//= rAthenaCN [太陽眼鏡製作/打洞]漢化腳本 
//============================================================


//===== Hercules Script ======================================
//= Slotted Sunglasses Quest
//===== By: ==================================================
//= amichan
//===== Current Version: =====================================
//= 1.8
//===== Description: =========================================
//= Quest to get Slotted Sunglasses.
//= Will reset after quest completion.
//===== Additional Comments: =================================
//= 1.0 by Aegis 1.1 by aichan 1.2 by x[tsk] 1.3 by Darkchild
//= 1.5 Fixed Exploit [Lupus]
//= 1.6 Fixed Exploit [KarLaeda]
//= 1.7 Relocated 'Maseph' to moc_fild07. [Kisuka]
//= 1.8 Cleaning. [Euphy]
//============================================================

alberta,88,193,5	script	太陽眼鏡商人	1_F_MERCHANT_01,{
	mes "[太陽眼鏡商人]";
	if(SG_QUEST1 == 1) {
		mes "我再也沒有什麼可以告訴你的.";
		next;
		mes "[太陽眼鏡商人]";
		mes "去找瑪瑟菲. 她在夢洛克北部.";
		close; }
	mes "您好.有什麼可以幫你?";
	next;
	if(select("- 我聽說你可以製作 ^0000FF洞墨鏡^000000.:- 沒什麼, 抱歉打擾你.")==2) {
		mes "[太陽眼鏡商人]";
		mes "你準備好了回來找我.";
		close; }
	mes "[太陽眼鏡商人]";
	mes "我不能製作它, 但我可以告訴你在哪裡可以找到製作的人. 一小筆費用...";
	next;
	if(select("- 多少錢?:- 沒門, 我會自己找到她!")==2) {
		mes "[太陽眼鏡商人]";
		mes "隨你便, 製造者將不會給你製造 ^0000FF洞墨鏡^000000 除非她知道你來了.";
		mes "只有我可以告訴她你來了.";
		next;
		mes "[太陽眼鏡商人]";
		mes "當你放棄了回來找我. 哈哈哈...";
		close; }
	mes "[太陽眼鏡商人]";
	mes "我要告訴你有關 ^0000FF洞墨鏡^000000 你需要我:";
	mes " - ^0000881克拉鑽石^000000";
	mes " - ^00008850 柔毛^000000";
	mes " - ^000088100000z^000000";
	next;
	if(select("- 好吧, 在這裡.:- 那太多了!")==2) {
		mes "[太陽眼鏡商人]";
		mes "隨你便.";
		close; }
	mes "[太陽眼鏡商人]";
	if (countitem(730) < 1 || countitem(949) < 50 || Zeny < 100000) {
		mes "你少了什麼東西. 當你已經收集了所有的材料再回來.";
		close; }
	delitem 730,1;
	delitem 949,50;
	set Zeny, Zeny - 1000000;
	set SG_QUEST1,1;
	mes "[太陽眼鏡商人]";
	mes "太好了. 現在,仔細聽.";
	next;
	mes "[太陽眼鏡商人]";
	mes "找一個在夢洛克北部名叫瑪瑟菲的人.";
	mes "我會送她一個消息讓她知道你來了.";
	close;
}

moc_fild07,334,186,5	script	瑪瑟菲	4_F_GON,{
	mes "[瑪瑟菲]";
	if(SG_QUEST1 != 1) {
		mes "好天氣,不是麼 ?";
		close; }
	mes "你好.";
	mes "你來這裡為了 ^000088洞墨鏡^000000, 對麼?";
	next;
	if(select("- 是的:- 不,抱歉打擾你.")==2) {
		mes "[瑪瑟菲]";
		mes "那你可以走了.";
		close; }
	mes "[瑪瑟菲]";
	mes "做一個, 我需要一對 ^000088太陽眼鏡^000000 和 40000000z.";
	next;
	if(select("- 給你.:- 不了,謝謝.")==2) {
		mes "[瑪瑟菲]";
		mes "隨便你.";
		close; }
	mes "[瑪瑟菲]";
	if (countitem(2201) < 1) {
		mes "我需要一副 ^000088太陽眼鏡^000000. 當你有了再來.";
		close; }
	if (Zeny < 40000000) {
		mes "你沒有足夠的錢. 當你有了再來.";
		close; }
	delitem 2201,1;
	set Zeny, Zeny - 40000000;
	mes "謝謝. 我馬上就做...";
	next;
	mes "[瑪瑟菲]";
	mes "拿去吧, 我的朋友.";
	mes "享受你的 ^000088洞墨鏡^000000.";
	getitem 2202,1;
	set SG_QUEST1,0;
	close;
}
