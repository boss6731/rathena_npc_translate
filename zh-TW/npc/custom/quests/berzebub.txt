//============================================================
//= rAthenaCN [貝雷傑卡片任務]漢化腳本 
//============================================================


//===== Hercules Script ======================================
//= Berzebub Card Quest
//===== By: ==================================================
//= jabs
//===== Current Version: =====================================
//= 1.1
//===== Description: =========================================
//= Quest to get the Berzebub Card
//===== Additional Comments: =================================
//= 1.1 Fixed ingredients. Was too exploitable [Lupus]
//= 1.2 Optimized. [Euphy]
//============================================================

prontera,165,178,7	script	Old Woman	8_F_GRANDMOTHER,{

	setarray .@items[0],714,25,1061,100,1095,200,618,10,1551,2,1614,1,727,20;
	set .@n$,"[Old Woman]";

	mes .@n$;
	if(BerzQuest == 1) {
		mes "嘿,我記得你,我已經告訴你,只能完成一次呀!";
		close; }
	mes "你好,女士,當我在古城摘蘑菇的時候我發現一個真正漂亮的物品!";
	next;
	if(select("繼續說話:離開")==2) goto M_Leave;
		
		mes .@n$;
		mes "我以前從來沒有見過像這樣的東西。我被告知它可以被在任何附魔與槽可用";
	next;
	mes .@n$;
	mes "如果你有興趣在這個東西,我將告訴你你需要我幫你做一個你自己的。我只能給你其中的一個角色,所以一旦你完成這個任務,你可能不會再做一次";
	next;
	if(select("告訴我關於他的:我不關心這些")==2) goto M_Leave;

		mes .@n$;
		mes "我相信這個道具就叫他Berzebub 卡片,他能讓你施法速度十分快";
	next;
	mes .@n$;
	mes "我需要下列全部材料";
	for(set .@i,0; .@i<getarraysize(.@items); set .@i,.@i+2) {
		mes " ~ "+.@items[.@i+1]+"x "+getitemname(.@items[.@i]);
		if (countitem(.@items[.@i]) < .@items[.@i+1]) set .@nr,1; }
	next;
	if(select("我已經有所有的材料了,我要製作!")==2) goto M_Leave;

	mes .@n$;
	if (.@nr) {
		mes "你撒謊,滾出去,還有這些廢柴,你就像舞臺上的小丑";
		close; }
	for(set .@i,0; .@i<getarraysize(.@items); set .@i,.@i+2)
		delitem .@items[.@i], .@items[.@i+1];
	getitem 4145,1;
		 set BerzQuest, 1;
		mes "恭喜你! 你帶來了全部的材料,拿好你的Berzebub卡吧!";
		 emotion ET_CONGRATULATION;
		 close;

M_Leave:
		 mes .@n$;
		 mes "多麼偉大的神器,我已經擁有";
	close;
}
