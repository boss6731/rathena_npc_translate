//============================================================
//= rAthenaCN [克拉克斯]漢化腳本 
//============================================================


//===== Hercules Script =======================================
//= Cluck! Cluck! Boom!
//===== By: ==================================================
//= Keale
//===== Current Version: =====================================
//= 1.2a
//===== Description: =========================================
//= Click the chicken and try retrieve the item at a low 
//= chance. If you fail he will nuke, freeze, stone, 
//= stun, or make you fall asleep.
//= The prize is configurable and triggered by the NPC.
//===== Additional Comments: =================================
//= 1.0 First release.
//= 1.1 Using 'switch rand' instead.
//= 1.2 Cleaned and standardized. [Euphy]
//= 1.2a Switched 'atcommand' to 'unitskilluseid'.
//============================================================

prontera,156,219,4	script	克拉克斯	4_NFCOCK,{

	set .@GMAccess,80;	// GM level required to access NPC

if (.startcluck) {
specialeffect2 2;
switch(rand(15)) {
			case 0: npctalk "克拉拉拉拉克!!!"; unitskilluseid getcharid(3),"NPC_SELFDESTRUCTION",1; emotion 23; break;
			case 1: npctalk "克拉拉拉拉拉拉拉拉克!~"; break;
			case 2: unitskilluseid getcharid(3),"NPC_SELFDESTRUCTION",1; break;
			case 3: sc_start SC_FREEZE,10000,0; break;
			case 4: npctalk "克拉拉拉拉拉拉拉拉克!!!"; unitskilluseid getcharid(3),"NPC_SELFDESTRUCTION",1; emotion 23; break;
			case 5: sc_start SC_SLEEP,10000,0; break;
			case 6: sc_start SC_STONE,10000,0; emotion 29; break;
			case 7: npctalk "克拉拉拉拉拉拉拉拉克!!!"; unitskilluseid getcharid(3),"NPC_SELFDESTRUCTION",1; emotion 23; break;
			case 8: npctalk "克拉克! 克拉拉克!!"; unitskilluseid getcharid(3),"NPC_SELFDESTRUCTION",1; emotion 23; break;
			case 9: sc_start SC_STUN,10000,0; break;
			case 10: sc_start SC_SLEEP,10000,0; emotion 29; break;
			case 11: npctalk "克拉克! 克拉克!"; break;
			case 12: sc_start SC_STUN,10000,0; break;
			case 13: unitskilluseid getcharid(3),"NPC_SELFDESTRUCTION",1; break;
			default:
				if (rand(50) < 4) {
					npctalk "伍夫!...........";
					specialeffect2 72;
					announce "[克拉克! 克拉克! 啊啊啊啊(歡呼)!] " + strcharinfo(0) + " 擠出這個獎品! 幹得好!",0;
					getitem $cluck_item_id,$cluck_item_amount;
					set .startcluck,0;
				} else {
					npctalk "克拉克! 克拉拉拉克!!";
					unitskilluseid getcharid(3),"NPC_SELFDESTRUCTION",1;
				}
				break;
		}
		end;
	}
	mes "[克拉克斯]";
	if (getgmlevel() >= .@GMAccess) {
		mes "克拉克 克拉克! 克拉拉拉拉克? ^FF0000~Hi GM " + strcharinfo(0) + ", 想要今天比賽麼?~^000000";
		mes "克拉克 克拉克... 克拉克! ^FF0000~告訴我該做什麼!~^000000";
		next;
		while(1) switch(select("- 開始活動:- 獎勵查詢:- 設置獎勵:- 不是今天克拉克斯")) {
			case 1:
				mes "[克拉克斯]";
				mes "克拉克! ^FF0000~當然!~^000000";
				emotion 33;
				close2;
				announce "[克拉克! 克拉克! 啊啊啊啊(歡呼)!] 即將在普隆德拉開始!",bc_blue;
				initnpctimer;
				end;
			case 2:
				mes "[克拉克斯]";
				mes "克拉克, 克拉克 克拉克... 克拉克! ^FF0000~這次的獎勵是^000000 ^008000"+ $cluck_item_amount +" "+ getitemname($cluck_item_id) +".^000000";
				next;
				break;
			case 3:
				mes "[克拉克斯]";
				mes "克拉克 克拉克? 克拉拉克??? ^FF0000~應該獲得的獎品是? 請輸入名稱 ID.~^000000";
				input $cluck_item_id;
				next;
				mes "[克拉克斯]";
				mes "克拉克? 克拉克? ^FF0000~我該放棄幾個項目?~^000000";
				input $cluck_item_amount;
				next;
				mes "[克拉克斯]";
				mes "克拉克 克拉克..? 克拉克. ^FF0000~所以,獎品是^000000 ^008000"+ $cluck_item_amount +" "+ getitemname($cluck_item_id) +"^000000? ^FF0000很好.~^000000";
				emotion 33;
				next;
				break;
			case 4:
				mes "[克拉克斯]";
				mes "克拉克 克拉克 克拉克...";
				close;
		}
	}
	mes "克拉克 克拉克! 克拉拉拉拉克?";
	mes "克拉克....";
	close;

OnTimer10000:
	announce "如果你想參加請點擊噴泉背後的小雞!",bc_blue;
	end;
OnTimer20000:
	announce "克拉克斯把它吃了! 我怕別人看見它!",bc_blue;
	end;
OnTimer30000:
	announce "瘋狂的點擊小雞! 如果你幸運的話就會獲得獎品! 祝你好運! 你會點出來的!!",bc_blue;
	end;
OnTimer40000:
	announce "開始吧! 瘋狂的點擊小雞吧!",bc_blue;
	stopnpctimer;
	if (!$cluck_item_id) set $cluck_item_id,512;
	if (!$cluck_item_amount) set $cluck_item_amount,1;
	set .startcluck,1;
	end;
}
