//============================================================
//= rAthenaCN [典獄長]漢化腳本 
//============================================================


//===== Hercules Script ======================================
//= Penal Servitude 
//===== By: ==================================================
//= Lupus
//===== Current Version: =====================================
//= 1.2
//===== Description: =========================================
//= A simple Penal Servitude Script.
//= It could cheer up your prisoners a bit.
//===== Additional Comments: =================================
// This script uses CHEQUES of the 2nd KAFRA_BANK.TXT
// var PRISON - it counts number of your imprisonments.
// 1.1 English translation
// 1.2 Stricted the conditions a bit
//============================================================

sec_pri,36,58,1	script	典獄長	8W_SOLDIER,{ 
	mes "[典獄長]";
	emotion 1;
	if(Sex) {
		mes "犯人 #"+BaseLevel+JobLevel+",";
	} else {
		mes "犯人 #"+BaseLevel+JobLevel+",";
	}
	mes "那邊的噪聲是什麼?";
	next;
	menu "- 讓我自由, 我付錢!",-, "- 我為自由而工作!",M_Q1, "- 沒事",M_NO_THANKS;

M_PAY:
	mes "[典獄長]";
	set @MUSTPAY,(PRISON+1)*1000000;
	if (@MUSTPAY<1000000) set @MUSTPAY,1000000;
	if (@MUSTPAY>100000000) set @MUSTPAY,100000000;
	if (PRISON==1) mes "我不記得你. 你第一次進來?";
	if (PRISON>1) mes "你又進來了? 看來有人進來一次還學不聰明...";
	mes "好，要釋放你的話你得付 ^FF0000"+@MUSTPAY+"z^000000.";
	next;
	menu "- 我付現金!",M_PAYCASH,"- 你收支票麼?",M_PAYBANK,"- 我改主意了.",-;

	mes "[典獄長]";
	mes "你可以考慮一下...";
	close;

M_PAYCASH:
	if (@MUSTPAY>Zeny) goto L_NOCASH;
	set Zeny, Zeny - @MUSTPAY;
	mes "[典獄長]";
	mes "好，唱首歌吧.";
	goto L_RELEASE;

L_NOCASH:
	mes "[典獄長]";
	mes "什麼? 這些錢不夠!";
	close;

M_PAYBANK:
	if (@MUSTPAY>#kafrabank) goto L_NOBANK;
	set #kafrabank,#kafrabank-@MUSTPAY;
	mes "[典獄長]";
	mes "好,在你的支票上簽名吧.";
	goto L_RELEASE;

L_NOBANK:
	mes "[典獄長]";
	if (#kafrabank==0) mes "你帳戶沒錢.";
	if (#kafrabank!=0) mes "你的帳戶還有... ^FF0000"+#kafrabank+"z^000000... 不夠支付費用的.";
	mes "停止你愚蠢的行動!";
	close;

L_RELEASE:
	set PRISON,PRISON+1;
	next;
	mes "[典獄長]";
	mes "你自由了!";
	next;
	savepoint "izlude",105,112;
	warp "izlude",105,112;
	close;

M_Q1:
	mes "[典獄長]";
	mes "我們的公司缺少一些黴菌, 你要不要到我們的蘑菇農場工作?";
	next;
	menu "- 好,我要去工作!",-, "- 我喜歡這個監獄!",M_NO_THANKS;

	mes "[典獄長]";
	if(checkcart() || checkfalcon() || checkriding()){
		emotion ET_SCRATCH;
		mes "你要取消你的手推車,獵鷹,狼!";
		close;
	}
	mes "去跟我們的監工說吧,奧利弗.";
	next;

	nude;

	set @rw,rand(1,4);
	if (@rw==2) goto L_W2;
	if (@rw==3) goto L_W3;
	if (@rw==4) goto L_W4;

L_W1:
	savepoint "sec_in02",179,76;
	warp "sec_in02",179,76;
	close;

L_W2:
	savepoint "sec_in02",139,32;
	warp "sec_in02",139,32;
	close;

L_W3:
	savepoint "sec_in02",100,28;
	warp "sec_in02",100,28;
	close;

L_W4:
	savepoint "sec_in02",107,75;
	warp "sec_in02",107,75;
	close;

M_NO_THANKS:
	mes "[典獄長]";  
	if (rand(2)) mes "今天是聖誕節麼？?";
	mes "閉上嘴一邊去!";
	if (rand(2)) emotion 23;
	close;  
}

sec_in02,137,57,1	script	監工	4_M_PAY_SOLDIER,{ 
	mes "[奧利弗]";

	delitem 4002,countitem(4002);//Items: Fabre_Card,
	delitem 4009,countitem(4009);//Items: Chonchon_Card,
	delitem 4022,countitem(4022);//Items: Spore_Card,
	delitem 4048,countitem(4048);//Items: Poison_Spore_Card,
	
	if (PRISON_Q <= 0 ) goto L_GET_Q;

	mes "你要去收集 ^FF0000"+PRISON_Q+" 桔色網蘑菇^000000.";
	if ( countitem(1069)<PRISON_Q ) close;//Items: Orange_Net_Mushroom,
	mes "把桔色網蘑菇給我吧...";
	mes "我看看... "+countitem(1069)+" 個可使用的...";//Items: Orange_Net_Mushroom,
	mes "和 "+countitem(1070)+" 沒用的死蘑菇.";//Items: OrangET_GOoey_Mushroom_,
	next;
	mes "[奧利弗]";
	mes "謝謝. 你自由了!";
	set PRISON_Q,0;
	delitem 1069,countitem(1069);//Items: Orange_Net_Mushroom,
	delitem 1070,countitem(1070);//Items: OrangET_GOoey_Mushroom_,
	next;
	savepoint "izlude",105,112;
	warp "izlude",105,112;
	close;

L_GET_Q:
	set PRISON,PRISON+1;
	if (PRISON > 1) mes "嗯...你已經在這了..很好...";

	if ( countitem(1069) || countitem(1070) ) mes "扔掉這些蘑菇!! 他們有毒!";//Items: Orange_Net_Mushroom, OrangET_GOoey_Mushroom_,
	delitem 1069,countitem(1069);//Items: Orange_Net_Mushroom,
	delitem 1070,countitem(1070);//Items: OrangET_GOoey_Mushroom_,

	set PRISON_Q, PRISON*3 + BaseLevel/3;
	set PRISON_Q, PRISON_Q * (readparam(bAspd)/55 + 1);
	set PRISON_Q, PRISON_Q+rand(50,60);
	mes "好,你要給我拿來 ^FF0000"+PRISON_Q+"^000000 可使用的 ^FF0000桔色網狀蘑菇^000000 菌蓋.";
	if(readparam(bAspd)>=100) mes "你真是個幹活麻利的人! 你還可以弄更多的蘑菇.";
	if (rand(10)<4) mes "你可以自己把那些死的吃了.";
	close;
}


sec_in02	mapflag	nomemo
sec_in02	mapflag	nosave	SavePoint
sec_in02	mapflag	noteleport
sec_in02	mapflag	nobranch
sec_in02	mapflag	nowarp
sec_in02	mapflag	nowarpto
sec_in02	mapflag	noexp
sec_in02	mapflag	noskill
//sec_in02	mapflag	pvp
//sec_in02	mapflag	pvp_noparty
//sec_in02	mapflag	gvg
sec_in02	mapflag	pvp_nightmaredrop	random,all,300

sec_in02,138,55,100,100	monster	蘑菇	1182,30,10000,10000,1
sec_in02,138,55,100,100	monster	綠棉蟲	1184,10,20000,20000
sec_in02,138,55,100,100	monster	蒼蠅	1183,11,20000,20000
sec_in02,138,55,100,100	monster	魔菇	1014,12,20000,20000
sec_in02,138,55,100,100	monster	毒魔菇	1077,3,20000,20000
