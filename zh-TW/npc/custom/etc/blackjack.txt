//============================================================
//= rAthenaCN [二十一點莊家]漢化腳本 
//============================================================


//===== Hercules Script ======================================
//= 二十一點（二十一點）
//===== By: ==================================================
//= kobra_k88
//===== Current Version: =====================================
//= 1.0
//===== Description: =========================================
//= 二十一點紙牌遊戲.  基於非標準賭場的遊戲
//= 二十一點規則.  莊家必須至少擁有17點才能停牌, 不然會
//= 自動拿牌到17點或以上, 之後停牌.  玩家必須至少擁有
//= 13點才能停牌.  A牌可以是11點或1點.  可以加倍下注.
//= 目前不允許進行買保險操作或分成2副牌操作.
//===== Additional Comments: =================================
//= 
//============================================================

cmd_in02,188,89,1	script	二十一點莊家	1_M_LIBRARYMASTER,{
	mes "[莊家]";
	mes "你好! 你想玩二十一點遊戲嗎?";
M_Menu:
	next;
	menu "- 規則是什麼?",M_0, "- 我想玩.",L_Play, "- 不, 謝謝.",M_End;

M_0:
	mes "[莊家]";
	mes "你想知道些什麼?";
sM_Menu0:
	next;
	menu "- 基本說明.",sM_0a, "- 輸贏方式.",sM_0b, "- '加倍下注'.",sM_0c,
	"- A牌的數值.",sM_0d, "- 不用了.",M_Menu;

sM_0a:
	mes "[莊家]";
	mes "二十一點是以目標接近或達到21點的一種紙牌遊戲";
	mes "儘可能的接近21點但不要超過.";
	next;
	mes "[莊家]";
	mes "遊戲開始前, 玩家必須下注.";
	mes "一旦下注, 莊家和玩家分別分給2張牌.";
	mes "根據你擁有的牌, 你可以選擇 ^5533FF'停牌'^000000,";
	mes "或者你可以選擇 ^5533FF'分牌'^000000.";
	next;
	mes "[莊家]";
	mes "當你選擇 '停牌', 你就是告訴了莊家你不再需要牌了.";
	mes "這樣如果莊家想分牌的話, 就可以分牌.";
	mes "要想停牌, 你必須 ^FF3355至少有13點^000000. ";
	mes "莊家只能在他擁有^FF3355 17點或更多時^000000才可停牌.";
	next;
	mes "[莊家]";
	mes "當你選擇 '分牌', 你就是告訴了莊家你想再要一張牌了";
	mes "通過分得更多的牌你會增加自己的總點數.";
	mes "只要你 ^FF3355少於21^000000 點,";
	mes "你就可以分牌.";
	goto sM_Menu0;
sM_0b:
	mes "[莊家]";
	mes "有3種方式可以在玩二十一點時產生輸贏.";
	next;
	mes "[莊家]";
	mes "1.) 在二十一點最後一輪, 如果你的點數";
	mes "比莊家的多你就贏得了這局.  反過來如果你";
	mes "比莊家的點數少你就輸了.";
	next;
	mes "[莊家]";
	mes "2.) 如果你分牌時使得你的點數超過了21點你";
	mes "自動輸的這局.  這個叫 ^5533FF'爆牌'^000000.  ";
	mes "如果莊家爆牌那麼你就贏得了這局.";
	next;
	mes "[莊家]";
	mes "3.) 如果你在最初的2張牌的點數達到了21點, ";
	mes "你就自動贏得了這局.  這個叫做 ^5533FF'二十一點'^000000";
	mes "而這發生在你得到了A牌和10牌. ";
	mes "如果莊家得到了二十一點他就贏得了這局.";
	next;
	mes "[莊家]";
	mes "4.) 除了輸和贏, 你可以和莊家打平.  ";
	mes "如果你和莊家在最後一輪的總點數相同,";
	mes "這就導致沒有輸贏的平局";
	next;
	mes "[莊家]";
	mes "這個被稱作和莊家 ^5533FF'平手'^000000 .  這也";
	mes "適用於你和莊家同時拿到二十一點.";
	goto sM_Menu0;
sM_0c:
	mes "[莊家]";
	mes "'加倍下注' 選擇允許你加倍當前的賭注,";
	mes "但缺點是你將只能夠多拿一張牌.";
	mes ".  這個選擇只能在每局的開始可以選擇.";
	next;
	mes "[莊家]";
	mes "這是一個加倍下注有用的例子, ";
	mes "當你前2張牌的總點數是11.  ";
	mes "你就有機會從下一張牌得到21點或20點.  ";
	mes "這將會是加倍下注的好手法.";
	goto sM_Menu0;
sM_0d:
	mes "[莊家]";
	mes "A牌是二十一點裡面的特殊牌, ";
	mes "因為他有2個點數.  一張A牌可以作為11點,";
	mes "或者當做1點.  例如你有一張A牌和4牌, 那麼";
	mes "你可以擁有15點或5點.";
	next;
	mes "[莊家]";
	mes "如果你決定停牌, A牌自動計算為11點";
	mes "使你擁有15點.";
	next;
	mes "[莊家]";
	mes "如果你決定分牌並且得到了9牌, ";
	mes "A牌自動計算為1點使得你總點數為14.";
	mes "如果把A牌當做11點,";
	mes "你就會使總點數超過21點而輸掉比賽.";
	next;
	mes "[莊家]";
	mes "正是因為A牌的靈活性";
	mes "使其成為了遊戲中最強大的牌.";
	goto sM_Menu0;
M_End:
	mes "[莊家]";
	mes "歡迎隨時回來";
	close;

//================
L_Play:
	mes "[莊家]";
	mes "請下注...";
	next;
	menu "- 2z",M_1a, "- 100z",M_1b, "- 20000z",M_1c, "- 1000000z",M_1d, "- 承受不了....",M_End;

M_1a:
	if(Zeny < 2) goto sL_NotEnuf;
	set @bet, 2;
	goto L_Cont0;
M_1b:
	if(Zeny < 100) goto sL_NotEnuf;
	set @bet, 100;
	goto L_Cont0;
M_1c:
	if(Zeny < 20000) goto sL_NotEnuf;
	set @bet, 20000;
	goto L_Cont0;
M_1d:
	if(Zeny < 1000000) goto sL_NotEnuf;
	set @bet, 1000000;
	goto L_Cont0;

sL_NotEnuf:
	mes "[莊家]";
	mes "很抱歉你沒有足夠的Zeny下注.";
	close;

L_Cont0:
	mes "(正在洗牌)";
	next;
	deletearray $@card[0],13;
	set @dealerTurn, 0;
	set @numP, 0;
	set @numD, 0;
	set @pAce, 0;
	set @dAce, 0;
	callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
	callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
	callsub sF_GetCards, @numD, @dealCard[@numD], @dealCard$[@numD], @dAce;
	callsub sF_GetCards, @numD, @dealCard[@numD], @dealCard$[@numD], @dAce;

//==============
L_Start:
	callsub sF_GetTot;

	mes "- 這是 ^FF5533莊家的^000000 的牌:";
	if (@numD==2) callsub sF_D2cards;
	if (@numD==3) callsub sF_D3cards;
	if (@numD==4) callsub sF_D4cards;
	if (@numD==5) callsub sF_D5cards;
	mes "    莊家擁有: ^FF5533"+@dealTot+"^000000";
	mes " ";
	mes "- 這是 ^5533FF你的^000000 牌:";
	if (@numP==2) callsub sF_P2cards;
	if (@numP==3) callsub sF_P3cards;
	if (@numP==4) callsub sF_P4cards;
	if (@numP==5) callsub sF_P5cards;
	if(@pAce != 1 || @playTot == 21) mes "    你有: ^5533FF" +@playTot+ "^000000";
	if(@pAce == 1 && @playTot != 21) mes "    你有: ^5533FF" +@playTot+ "^000000, or ^5533FF" +(@playTot-10)+ "^000000";
	next;
	if(@playTot==21 && @dealTot==21) goto sL_Push;
	if(@numP==2 && @playTot == 21) goto sL_Win;
	if(@numD==2 && @dealTot == 21) goto sL_Lose;
	if(@playTot > 21) goto sL_Lose;
	if(@dealTot > 21) goto sL_Win;
	if(@numP==2 && @dealerTurn==0) menu "- 分牌",M_Hit, "- 停牌",M_Stay, "- 加倍下注",M_Double;
	if(@dealerTurn == 0) menu "- 分牌",M_Hit, "- 停牌",M_Stay;

M_Stay:
	mes "[莊家]";
	if(@playTot < 13) goto sL_PlayToLow;
	if(@dealTot > 16) mes "莊家停牌.";
	if(@dealTot > 16 || @numD == 5) goto L_Check;
	mes "莊家正在分牌";
	next;
	callsub sF_GetCards, @numD, @dealCard[@numD], @dealCard$[@numD], @dAce;
	set @dealerTurn, 1;
	goto L_Start;

sL_PlayToLow:
	mes "抱歉你沒有達到停牌的足夠的點數.  你必須分牌.";
	next;
	goto M_Hit;

M_Hit:
	if(@numP == 5) goto M_Stay;
	callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
	goto L_Start;
M_Double:
	mes "[莊家]";
	mes "玩家選擇加倍下注.  你當前的賭注加倍,";
	mes "你可以額外得到一張牌.";
	next;
	set @dealerTurn, 1;
	set @bet, @bet*2;
	callsub sF_GetCards, @numP, @playCard[@numP], @playCard$[@numP], @pAce;
	goto L_Start;

//=============
L_Check:
	next;
	if(@playTot < @dealTot) goto sL_Lose;
	if(@playTot == @dealTot) goto sL_Push;

sL_Win:
	mes "[莊家]";
	mes "恭喜, 你贏了!";
	next;
	set Zeny, Zeny + @bet;
	goto L_Play;
sL_Lose:
	mes "[莊家]";
	mes "抱歉你輸了.";
	set Zeny, Zeny - @bet;
	next;
	goto L_Play;
sL_Push:
	mes "[莊家]";
	mes "平手.  你與莊家打平.";
	next;
	goto L_Play;


//==================================
	// Sub function for dealing/pulling the cards
sF_GetCards:
	set @rnd, rand(1,13);
	if($@card[@rnd] == 4) goto sF_GetCards;
	set $@card[@rnd], $@card[@rnd] + 1;
	set getarg(1), @rnd;
	if(getarg(1) > 10) set getarg(1), 10;
	if(getarg(1) == 1 && getarg(3) < 1) set getarg(1), 11;
	if(getarg(1) == 11) set getarg(3), 1;
	set getarg(2), " " + getarg(1) + " ";
	if(@rnd == 10) set getarg(2), getarg(1);
	if(@rnd == 1) set getarg(2), " A ";
	if(@rnd == 11) set getarg(2), " J ";
	if(@rnd == 12) set getarg(2), " Q ";
	if(@rnd == 13) set getarg(2), " K ";
	set getarg(0), getarg(0) + 1;
	return;

//==================================
	// Sub function for getting the total score for each hand
sF_GetTot:
	set @i, 0;
	set @dealTot, 0;
	set @playTot, 0;

GetDealTot:
	set @dealTot, @dealTot + @dealCard[@i];
	set @i, @i + 1;
	if(@i < @numD) goto GetDealTot;
	if(@dAce == 1 && @dealTot > 21) set @dealTot, @dealTot - 10;
	set @i, 0;
GetPlayTot:
	set @playTot, @playTot + @playCard[@i];
	set @i, @i + 1;
	if(@i < @numP) goto GetPlayTot;
	if(@pAce == 1 && @playTot > 21) set @pAce, 2;
	if(@pAce > 0 && @playTot > 21) set @playTot, @playTot - 10;
	return;

//=======================================
	// Sub function for displaying the Cards
sF_D2cards:
	mes "    .-----.   .-----. ";
	mes "    | "+@dealCard$[0]+" |   | "+@dealCard$[1]+" | ";
	mes "    '-----'   '-----' ";
	return;
sF_P2cards:
	mes "    .-----.   .-----. ";
	mes "    | "+@playCard$[0]+" |   | "+@playCard$[1]+" | ";
	mes "    '-----'   '-----' ";
	return;
sF_D3cards:
	mes "    .-----.   .-----.   .-----. ";
	mes "    | "+@dealCard$[0]+" |   | "+@dealCard$[1]+" |   | "+@dealCard$[2]+" | ";
	mes "    '-----'   '-----'    '-----' ";
	return;
sF_P3cards:
	mes "    .-----.   .-----.   .-----. ";
	mes "    | "+@playCard$[0]+" |   | "+@playCard$[1]+" |   | "+@playCard$[2]+" | ";
	mes "    '-----'   '-----'    '-----' ";
	return;
sF_D4cards:
	mes " .-----.  .-----.  .-----. ";
	mes " | "+@dealCard$[0]+" |  | "+@dealCard$[1]+" |  | "+@dealCard$[2]+" | ";
	mes " '-----'  '-----'   '-----' ";
	mes "    .-----.  ";
	mes "    | "+@dealCard$[3]+" |";
	mes "    '-----'   ";
	return;
sF_P4cards:
	mes " .-----.  .-----.  .-----. ";
	mes " | "+@playCard$[0]+" |  | "+@playCard$[1]+" |  | "+@playCard$[2]+" |";
	mes " '-----'  '-----'   '-----' ";
	mes "    .-----.  ";
	mes "    | "+@playCard$[3]+" |";
	mes "    '-----'   ";
	return;
sF_D5cards:
	mes " .-----.  .-----.  .-----. ";
	mes " | "+@dealCard$[0]+" |  | "+@dealCard$[1]+" |  | "+@dealCard$[2]+" | ";
	mes " '-----'  '-----'   '-----' ";
	mes "    .-----.   .-----. ";
	mes "    | "+@dealCard$[3]+" |   | "+@dealCard$[4]+" | ";
	mes "    '-----'   '-----' ";
	return;
sF_P5cards:
	mes " .-----.  .-----.  .-----. ";
	mes " | "+@playCard$[0]+" |  | "+@playCard$[1]+" |  | "+@playCard$[2]+" | ";
	mes " '-----'  '-----'   '-----' ";
	mes "    .-----.   .-----. ";
	mes "    | "+@playCard$[3]+" |   | "+@playCard$[4]+" | ";
	mes "    '-----'   '-----' ";
	return;
}
