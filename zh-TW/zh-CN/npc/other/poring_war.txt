//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================

//===== rAthena Script ======================================= 
//= Poring War
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Join a team in battle between Angeling and Deviling.
//===== Additional Comments: ================================= 
//= 1.0 First version. [Kisuka][5511]
//= 1.1 Fixed some typos/bugs. [CalciumKid]
//= 1.1a Fixed Waiting rooms witht he default MAX_LEVEL [Slim]
//= 1.1b Fixed incorrect label calls. [Euphy]
//= 1.2 Added missing barrier resets, credits to Zopokx. [Euphy]
//============================================================

// 波利戰爭傳送員
//============================================================
prt_fild08,159,371,3	script	波利戰爭傳送員#wop	4_PORING,{
	mes "[波利]";
	mes "!!!!!";
	mes "是誰-! 人類, 呤呤呤~!!";
	mes "快躲起來~!";
	mes "別被騙了! 他們用垃圾在誘惑我們!";
	next;
	mes "[波利]";
	mes "哇靠!!";
	mes "呤呤呤, 呤呤呤~ 你的小夥伴們怎麼啦..?";
	mes "嘿.. 嘿, 那裡. 是.. 人類...";
	mes "波利..";
	next;
	mes "[波利]";
	mes "我.. 我有些有意思的事情.. 你感興趣麼?";
	mes "我們.. 我們波利需要一些非常非常勇敢的人類戰士, 呤呤呤~.";
	next;
	switch(select("- 好, 我幫你!:- 什麼事情:- 忽略")) {
	case 1:
		if (Zeny > 499) {
			mes "[波利]";
			mes "噢, 需要 500 Zeny 的入場費.";
			mes "祝你玩的開心, 呤呤呤.";
			delitem 7773,countitem(7773);
			set Zeny,Zeny-500;
			close2;
			warp "poring_w01",112,138;
			end;
		} else {
			mes "[波利]";
			mes "噢, 需要 500 Zeny 的入場費.";
			mes "...........";
			mes "嘿, 這就是生活, 呤呤呤~ 我們也需要 Zeny 你知道不!";
			close;
		}
	case 2:
		mes "[波利]";
		mes "事情是這樣的,在我們波利世界有一些小的衝突.";
		mes "所以嘛.............來一場對決來解決它..";
		mes "到那裡你就知道了.";
		close;
	case 3:
		mes "[波利]";
		mes "唔唔唔! 呼.. 人類太冷漠, 太殘忍了!!!";
		close;
	}
}

// 波利兌獎機
//============================================================
poring_w01,100,97,3	script	波利兌獎機#w	4_PORING,{
	if (checkweight(714,3) == 0) {
		mes "- 你超負重了,無法使用波利兌獎機. -";
		close;
	}
	mes "這是一臺波利兌獎機. 你可以使用波利硬幣來兌換.";
	next;
	if(select("- 兌換.:- 閱讀有關商品的描述.") == 1) {
		mes "你需要使用波利硬幣進行兌換.";
		mes "你無法使用Zeny進行兌換.";
		mes "物品名稱 - 波利硬幣(P.Co)";
		next;
		switch(select("- 神奇的獎牌[4xP.Co]:- 組織的團結力[20xP.Co]:- 波利箱子[30xP.Co]:- 繼續")) {
		case 1:
			callsub S_PoringVending,7515,4;
		case 2:
			callsub S_PoringVending,658,20;
		case 3:
			callsub S_PoringVending,12109,30;
		case 4:
			mes "這是個特殊的物品.";
			mes "物品名稱 - 波利硬幣(P.Co)";
			next;
			switch(select("- 狂暴野貓召喚書[15xP.Co]:- 死靈召喚書[20xP].Co:- 闇●劍士召喚書[20xP.Co]:- 收集品")) {
			case 1:
				callsub S_PoringVending,12300,15;
			case 2:
				callsub S_PoringVending,12301,20;
			case 3:
				callsub S_PoringVending,12302,20;
			case 4:
				mes "一次轉職職業包含初心者的手辦!";
				mes "除了初心者手辦之外, 其它的都是^4d4dff帳號綁定物品^000000.";
				mes "請先弄明白後再選擇兌換~";
				mes "物品名稱 - 波利硬幣(P.Co)";
				next;
				switch(select("- 初心者手辦[50xP.Co]:- 劍士手辦[100xP.Co]:- 盜賊手辦[100xP.Co]:- 商人手辦[100xP.Co]:- 服侍手辦[100xP.Co]:- 魔法師手辦[100xP.Co]:- 弓箭手手辦[100xP.Co]:- 隨機抽獎[50xP.Co]:- 取消")) {
				case 1:
					callsub S_PoringVending,2765,50;
				case 2:
					callsub S_PoringVending,2766,100;
				case 3:
					callsub S_PoringVending,2770,100;
				case 4:
					callsub S_PoringVending,2771,100;
				case 5:
					callsub S_PoringVending,2767,100;
				case 6:
					callsub S_PoringVending,2768,100;
				case 7:
					callsub S_PoringVending,2769,100;
				case 8:
					mes "你選擇了隨機抽獎.";
					mes "這7種類型都有可能抽中.";
					next;
					if(select("- 抽獎:- 取消") == 1) {
						if (countitem(7539) >= 50) {
							mes "放入波利硬幣之後, 拉動搖杆.";
							mes "然後點擊~ 在設備搖獎停止後, 獎勵會從自動設備裡彈出來.";
							mes "它會是什麼呢?";
							next;
							switch(rand(1,17)) {
							case 5: set .@Random_Figure,2766; break; // Swordman_Figure
							case 6: set .@Random_Figure,2767; break; // Acolyte_Figure
							case 8: set .@Random_Figure,2770; break; // Thief_Figure
							case 11: set .@Random_Figure,2771; break; // Merchant_Figure
							case 13: set .@Random_Figure,2769; break; // Archer_Figure
							case 14: set .@Random_Figure,2768; break; // Mage_Figure
							default: set .@Random_Figure,2765; break; // Novice_Figure
							}
							mes "一個漂亮的" + getitemname(.@Random_Figure) + ".";
							delitem 7539,50; // Poring_Coin
							getitem .@Random_Figure,1;
							close;
						} else {
							mes "波利硬幣數量不夠.";
							close;
						}
					}
					mes "["+strcharinfo(0)+"]";
					mes "... 希望下次光臨...";
					close;
				case 9:
					mes "["+strcharinfo(0)+"]";
					mes "... 希望下次光臨...";
					close;
				}
			}
		}
	}
	mes "出售列表";
	mes "====================";
	mes "[神奇的獎牌]";
	mes " : 只有對毀葛有巨大貢獻的才能夠獲得的特殊金屬製成的獎章.";
	mes " ";
	mes "[組織的團結力]";
	mes " : 可讓一個團體, 擁有增強團結力的圖案. 實際上看到這雕像的人, 讓人感覺到有團結力增強的感覺.";
	mes " ";
	mes "[波利箱子]";
	mes " : 包裝上印有波利花紋的箱子, 好像聽到有某種東西在裡面動彈的聲音.";
	mes " ";
	mes "[狂暴野貓召喚書]";
	mes " : 可以把狂暴野貓召喚成傭兵的召喚書.";
	mes " ";
	mes "[死靈召喚書]";
	mes " : 可以把死靈召喚成傭兵的召喚書. 不過比實際上的魔物弱地多.";
	mes " ";
	mes "[闇●劍士召喚書]";
	mes " : 可以把死靈召喚成傭兵的召喚書. 不過比實際上的魔物弱地多.";
	mes " ";
	mes "[初心者手辦]";
	mes " : 以初心者的形象為原型的手辦. 裝備位置為 '^4d4dff裝飾品^000000'.";
	mes " HP + 70, 初心者裝備時額外增加 HP + 30 .";
	mes " ";
	mes "[劍士手辦]";
	mes " : 以劍士的形象為原型的手辦. 裝備位置為 '^4d4dff裝飾品^000000'.";
	mes " VIT + 1, 劍士裝備時額外增加 DEF + 2.";
	mes " ";
	mes "[商人手辦]";
	mes " : 以商人的形象為原型的手辦. 裝備位置為 '^4d4dff裝飾品^000000'.";
	mes " STR + 1, 商人裝備時額外增加 CRI + 5.";
	mes " ";
	mes "[盜賊手辦]";
	mes " : 以盜賊的形象為原型的手辦. 裝備位置為 '^4d4dff裝飾品^000000'.";
	mes " AGI + 1, 盜賊裝備時額外增加 ASPD + 3%.";
	mes " ";
	mes "[魔法師手辦]";
	mes " : 以魔法師的形象為原型的手辦. 裝備位置為 '^4d4dff裝飾品^000000'.";
	mes " INT + 1, 魔法師裝備時額外增加 SP恢復速度 5% .";
	mes " ";
	mes "[服侍手辦]";
	mes " : 以服侍的形象為原型的手辦. 裝備位置為 '^4d4dff裝飾品^000000'.";
	mes " INT + 1, 服侍裝備時額外增加 SP + 50.";
	mes " ";
	mes "[弓箭手手辦]";
	mes " : 以弓箭手的形象為原型的手辦. 裝備位置為 '^4d4dff裝飾品^000000'.";
	mes " DEX + 1, 弓箭手裝備時額外增加 ATK + 10.";
	close;
	end;

S_PoringVending:
	if (countitem(7539) >= getarg(1)) {
		mes "點擊~ 在設備搖獎停止後, 獎勵會從自動設備裡彈出來.";
		delitem 7539,getarg(1); // Poring_Coin
		getitem getarg(0),1;
	} else {
		mes "波利硬幣數量不夠.";
	}
	close;
}

// Information
//============================================================
poring_w01,96,97,3	script	小惡魔#wop	4_DEVIRUCHI,{
	if (MaxWeight-Weight < 2000 || checkweight(1201,1) == 0) {
		mes "- 稍等 !! -";
		mes "- 檢查你的負重 -";
		mes "- 你帶了太多東西了. -";
		mes "- 等你減輕一些負重後 -";
		mes "- 再來試一次吧. -";
		close;
	}
	mes "[小惡魔]";
	mes "噢, 又一個人類戰士!";
	mes "今天怎麼呢麼多人類要參與我們的波利戰爭?";
	next;
	mes "[小惡魔]";
	mes "好吧,正好讓我多賺點錢~";
	mes "好吧, 勇士-! 哈哈哈,我多麼想大聲喊出來~!!";
	next;
	mes "[小惡魔]";
	mes "你, 勇敢的戰士, 已經準備好參與這場天使與惡魔的戰爭了嗎?";
	next;
	mes "[小惡魔]";
	mes "啊哈哈哈, 我有點害羞了.";
	mes "無論如何, 人類. 你準備好參加波利戰爭了嗎?";
	next;
	switch(select("我有資格參加嗎?:我如何參加?:取消:快讓我離開這裡!")) {
	case 1:
		mes "[小惡魔]";
		mes "沒有種族歧視! 任何願意參加戰鬥的人都可以參與.";
		mes "奇怪的是你們人類是我們最大的敵人之一.";
		next;
		mes "[小惡魔]";
		mes "戰爭招募者早就已經準備好了, 如果你要參站就找他吧.";
		mes "完全取決於你要加入哪個隊伍~";
		close;
	case 2:
		mes "[小惡魔]";
		mes "簡單說. 你看到那個團隊招聘房間嗎?";
		mes "每個隊伍5個成員. 當5個成員齊聚時, 比賽就自動開始了.";
		mes "殺掉對方的波利們,你就勝利了.";
		next;
		mes "[小惡魔]";
		mes "這就代表比賽開啟需要一共10個人.";
		next;
		mes "[小惡魔]";
		mes "當10個人都選擇完畢後, 隊伍中的成員都有份參與. 沒個隊伍中5個成員要看他們是否組隊在一起.";
		next;
		mes "[小惡魔]";
		mes "最後一半的人代表天使隊";
		mes "另一半則代表惡魔隊.";
		next;
		mes "[小惡魔]";
		mes "如果你代表天使隊出戰, 則要擊殺惡魔隊成員, 反之亦然.";
		next;
		mes "[小惡魔]";
		mes "每支隊伍都將試圖殺死對方基地中的波利. 每殺死一隻, 你有一段限制擊殺時間. 否則, 你殺死的波利會復活.";
		next;
		mes "[小惡魔]";
		mes "他看上去就像一個抓捕遊戲, 只不過由波利來替代了. 明白?";
		mes "你必須搞明白誰和你在一個隊伍中.";
		close;
	case 3:
		mes "[" + strcharinfo(0) + "]";
		mes "嗯, 我明白.";
		close;
	case 4:
		mes "[小惡魔]";
		mes "好了.";
		mes "完成後我會送到你記錄點.";
		close2;
		if(WoP_SaveMap$ != "") {
			savepoint WoP_SaveMap$,WoP_SaveMap_X,WoP_SaveMap_Y;
			set WoP_SaveMap$,"";
			set WoP_SaveMap_X,0;
			set WoP_SaveMap_Y,0;
		}
		warp getsavepoint(0), getsavepoint(1), getsavepoint(2);
		end;
	}

OnPCLogoutEvent:
	getmapxy .@map$,.@x,.@y,0;
	if (.@map$ == "poring_w02") {
		if(WoP_SaveMap$ != "") {
			savepoint WoP_SaveMap$,WoP_SaveMap_X,WoP_SaveMap_Y;
			set WoP_SaveMap$,"";
			set WoP_SaveMap_X,0;
			set WoP_SaveMap_Y,0;
		}
		delitem 7773,countitem(7773);
		set wop_team,0;
	}
	end;

OnPCDieEvent:
	getmapxy .@map$,.@x,.@y,0;
	if (.@map$ == "poring_w02" && wop_team) {
		if(getsavepoint(0) != "poring_w02" && WoP_SaveMap$ == "") {
			set WoP_SaveMap$,getsavepoint(0);
			set WoP_SaveMap_X,getsavepoint(1);
			set WoP_SaveMap_Y,getsavepoint(2);
		}
		if (wop_team == 1) {
			savepoint "poring_w02",rand(44,51),rand(76,87);
		}
		if (wop_team == 2) {
			savepoint "poring_w02",rand(146,153),rand(76,87);
		}
	}
	end;

OnPCKillEvent:
	getmapxy .@map$,.@x,.@y,0;
	if (.@map$ == "poring_w02" && wop_team) {
		getnameditem 7773,rid2name(killedrid);
	}
	end;

OnInit:
	removemapflag "poring_w02",mf_partylock;
	removemapflag "poring_w02",mf_pvp;
	removemapflag "poring_w02",mf_pvp_noguild;
	removemapflag "poring_w02",mf_pvp_nocalcrank;
	set $@wop_teamcount,0;
	set $@wop_deadcount_a,0;
	set $@wop_deadcount_d,0;
	set $@wop_team_a,0;
	set $@wop_team_d,0;
	set $@wop_doorcount_a,0;
	set $@wop_doorcount_d,0;
	end;
}

// Poring Registers
//============================================================
poring_w01,91,97,3	script	波利#wop_door_all	1002,{
	end;
	
OnInit:
	waitingroom "[招募10名參賽者]",11,"波利#wop_door_all::OnStartArena",10,500,9;
	enablewaitingroomevent;
	end;

OnStartArena:
	warpwaitingpc "poring_w01",101,70;
	donpcevent "#wop_warp_rtry::OnReady";
	disablewaitingroomevent;
	end;

OnEnable:
	enablewaitingroomevent;
	end;

OnDisable:
	disablewaitingroomevent;
	end;
}

poring_w01,91,68,5	script	波利#wop_door_a	1002,{
	end;

OnInit:
	waitingroom "[天使陣營招募]",6,"波利#wop_door_a::OnStartArena",5,0,9;
	enablewaitingroomevent;
	end;

OnStartArena:
	warpwaitingpc "poring_w02",26,175;
	disablewaitingroomevent;
	if ($@wop_teamcount == 0) {
		set $@wop_teamcount,1;
		donpcevent "波利#wop_door_d::OnDevilingStart";
		donpcevent "#wop_warp_rtry::OnStop";
	}
	else if ($@wop_teamcount == 1) {
		donpcevent "#wop_master::OnStart";
		donpcevent "波利#wop_door_d::OnStop";
		stopnpctimer;
	}
	end;

OnEnable:
	enablewaitingroomevent;
	end;

OnDisable:
	disablewaitingroomevent;
	end;

OnAngelingStart:
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTimer1000:
	mapannounce "poring_w01","偉大的戰鬥即將開始, 惡魔戰隊招募完成, 呤!",0,0xf08080;
	end;

OnTimer4000:
	mapannounce "poring_w01","加入擁有令人驕傲天使之翼的天使陣營!!",0,0xf08080;
	end;

OnTimer8000:
	mapannounce "poring_w01","你只有1分鐘的考慮時間, 是否加入天使戰隊. 超過1分鐘戰鬥將取消!!",0,0xf08080;
	donpcevent "#wop_master::OnAngelingWarn";
	end;

OnTimer13000:
	mapannounce "poring_w01","就是現在, 加入偉大的天使陣營. 呤!",0,0xf08080;
	end;

OnTimer20000:
	mapannounce "poring_w01","為了波利的榮耀而戰! 呤! 加入天使陣營!!",0,0xf08080;
	end;

OnTimer30000:
	mapannounce "poring_w01","我們所剩的時間不多了, 呤. 不要讓惡魔們汙染你!",0,0xf08080;
	end;

OnTimer40000:
	mapannounce "poring_w01","來吧! 加入我們!",0,0xf08080;
	end;

OnTimer50000:
	mapannounce "poring_w01","太遺憾了! 真不可思議, 勇敢的戰士們去哪裡了!",0,0xf08080;
	end;

OnTimer55000:
	mapannounce "poring_w01","惡魔陣營: 你不能永遠不能離開我們!! 我們就快要被取消比賽了, 呤!",0,0x33FF66;
	end;

OnTimer55100:
	donpcevent "波利#wop_door_a::OnDisable";
	donpcevent "波利#wop_door_d::OnDisable";
	donpcevent "波利#wop_door_all::OnDisable";
	set $@wop_teamcount,0;
	donpcevent "#wop_master::OnAngelingEnd";
	donpcevent "#wop_warp_rtry::OnEnable";
	end;

OnTimer58000:
	mapannounce "poring_w01","...我們已經盡力了, 呤... 讓我們為下一次搖旗吶喊吧, 呤.",0,0xf08080;
	donpcevent "#wop_master::OnReset";
	stopnpctimer;
	end;
}

poring_w01,112,68,3	script	波利#wop_door_d	1002,{
	end;

OnInit:
	waitingroom "[惡魔陣營招募]",6,"波利#wop_door_d::OnStartArena",5,0,9;
	enablewaitingroomevent;
	end;

OnStartArena:
	warpwaitingpc "poring_w02",170,175;
	disablewaitingroomevent;
	if ($@wop_teamcount == 0) {
		set $@wop_teamcount,1;
		donpcevent "波利#wop_door_a::OnAngelingStart";
		donpcevent "#wop_warp_rtry::OnStop";
	}
	else if ($@wop_teamcount == 1) {
		donpcevent "#wop_master::OnStart";
		donpcevent "波利#wop_door_a::OnStop";
		stopnpctimer;
	}
	end;

OnEnable:
	enablewaitingroomevent;
	end;

OnDisable:
	disablewaitingroomevent;
	end;

OnDevilingStart:
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTimer1000:
	mapannounce "poring_w01","沒有更好的人選了, 天使陣營已經完成招募, 呤!",0,0x33FF66;
	end;

OnTimer4000:
	mapannounce "poring_w01","惡魔陣營的成員們! 一起來加油, 呤!!",0,0x33FF66;
	end;

OnTimer8000:
	mapannounce "poring_w01","這場戰鬥將會取消,如果不能在1分鐘內完成隊員招募的話!!",0,0x33FF66;
	donpcevent "#wop_master::OnDevilingWarn";
	end;

OnTimer13000:
	mapannounce "poring_w01","是時候加入勇敢的惡魔陣營了, 呤!",0,0x33FF66;
	end;

OnTimer20000:
	mapannounce "poring_w01","D,E,V,I,L,I,N,G! 惡魔陣營! 快來加入我們!",0,0x33FF66;
	end;

OnTimer30000:
	mapannounce "poring_w01","時間不夠了, 呤! 如果你想成為惡魔陣營的一員, 來吧, 加入我們!",0,0x33FF66;
	end;

OnTimer40000:
	mapannounce "poring_w01","D,E,V,I,L,I,N,G! 惡魔陣營! 快來加入我們!!",0,0x33FF66;
	end;

OnTimer50000:
	mapannounce "poring_w01","日啊! 不敢相信, 我們的人竟然不夠!",0,0x33FF66;
	end;

OnTimer55000:
	mapannounce "poring_w01","天使陣營: 我們等了很久了, SB惡魔戰隊! 戰鬥要取消了, 呤!",0,0xf08080;
	end;

OnTimer55100:
	donpcevent "波利#wop_door_a::OnDisable";
	donpcevent "波利#wop_door_d::OnDisable";
	donpcevent "波利#wop_door_all::OnDisable";

	set $@wop_teamcount,0;
	donpcevent "#wop_master::OnDevilingEnd";
	donpcevent "#wop_warp_rtry::OnEnable";
	end;

OnTimer58000:
	mapannounce "poring_w01","...Ughhhhhh... 告訴我發生什麼了, 呤! 好吧, 為了下一次做準備, 呤!!",0,0x33FF66;
	donpcevent "#wop_master::OnReset";
	stopnpctimer;
	end;
}

poring_w01,103,70,0	script	#wop_warp_rtry	HIDDEN_WARP_NPC,13,13,{
	end;

OnInit:
	disablenpc "#wop_warp_rtry";
	end;

OnEnable:
	enablenpc "#wop_warp_rtry";
	end;

OnDisable:
	disablenpc "#wop_warp_rtry";
	end;

OnReady:
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTouch:
	warp "poring_w01",112,138;
	end;

OnTimer3000:
	mapannounce "poring_w01","裁判: 我給你1分鐘的時間. 選擇自己的隊伍, 呤!",0,0xf08080;
	end;

OnTimer33000:
	mapannounce "poring_w01","裁判: 30秒過去了! 快啊, 選擇隊伍, 呤?!!",0,0xf08080;
	end;

OnTimer58000:
	mapannounce "poring_w01","裁判: 你確定你是戰士?!!! 我驚呆了, 呤!",0,0xf08080;
	end;

OnTimer60000:
	mapannounce "poring_w01","由於雙方隊伍無法募集足夠的隊員, 因此比賽取消了!!",0,0xf08080;
	end;

OnTimer61000:
	enablenpc "#wop_warp_rtry";
	end;

OnTimer65000:
	disablenpc "#wop_warp_rtry";
	donpcevent "#wop_master::OnReset";
	stopnpctimer;
	end;
}

// 多佩爾先生
//============================================================
poring_w02,26,181,3	script	多佩爾先生#wop_team_a	8_DOPPEL,{
	if (checkweight(714,3) == 0) {
		mes "- 稍等!! -";
		mes "- 檢查你的負重 -";
		mes "- 你帶的東西太多啦. -";
		mes "- 等你清理下負重後 -";
		mes "- 再來找我吧. -";
		close;
	}
	set .@a_tname$,getpartyname($@wop_team_a);
	set .@pname$,getpartyname(getcharid(1));
	if ($@wop_team_a != 0 && $@wop_team_a == getcharid(1)) {
		mes "[多佩爾先生]";
		mes "所有人都組隊了嗎?";
		mes "隊伍名稱是... " + .@pname$ + ", 對嗎?";
		mes "我將傳送你們到比賽場地.";
		next;
		switch(select("- 不不! 等等!:- 去比賽場地.")) {
		case 1:
			mes "[多佩爾先生]";
			mes "怎麼回事?";
			mes "有什麼其他的意見?";
			mes "沒時間了. 現在決定!";
			next;
			mes "[多佩爾先生]";
			mes "如果現在不去比賽場地, 就將失去比賽資格了.";
			close;
		case 2:
			if ((getpartyleader(getcharid(1),2) == getcharid(0))) {
				mes "[多佩爾先生]";
				mes "你是隊長吧. 在去比賽場地之前, 你要核查你所有的隊員.";
				next;
				switch(select("- 很好.這是最後一次 .:- 我是最後一個, 送我去比賽場地.")) {
				case 1:
					mes "[多佩爾先生]";
					mes "首先, 確定所有隊員都在你的組隊裡.";
					close;
				case 2:
					mes "[多佩爾先生]";
					mes "很好. 你的準備很充分.";
					delitem 7773,countitem(7773);
					close2;
					set wop_team,0;
					warp "poring_w02",44,82;
					end;
				}
			}
			else {
				mes "[多佩爾先生]";
				mes "很好.我要送你們去比賽場地了.";
				mes "等待其他參賽隊伍.";
				delitem 7773,countitem(7773);
				close2;
				set wop_team,0;
				warp "poring_w02",44,82;
				end;
			}
		}
	}
	else {
		if ((getpartyleader(getcharid(1),2) == getcharid(0))) {
			if ($@wop_team_a == 0) {
				mes "[多佩爾先生]";
				mes "你是天使陣營的隊長吧. ";
				mes "確定所有隊員在你的組隊裡?";
				mes "首先, 讓我們註冊你們的戰隊名字, 然後, 我們將檢查你的隊伍.";
				next;
				mes "[多佩爾先生]";
				mes "讓我看看... 隊伍名稱是...";
				mes " ";
				mes "^4d4dff " + .@pname$ + " ^000000";
				mes " ";
				mes "對吧? 這是你想起的名字?";
				next;
				switch(select("- 不不! 你錯了.:- 是的. 我註冊了這名字.:- 取消")) {
				case 1:
					mes "[多佩爾先生]";
					mes "嘿, 我可沒有一天時間來陪你! 快點想啊.";
					mes "不要忘記告訴所有隊員一起組隊. 只有註冊過的隊伍才可以參加戰鬥.";
					close;
				case 2:
					mes "[多佩爾先生]";
					mes "我把你們的隊伍名稱註冊為 - " +.@pname$ + " -.";
					mes "現在, 告訴你的 ^4d4dff隊員們一起組隊^000000.";
					mes "我馬上送你們去比賽場地.";
					set $@wop_team_a,getcharid(1);
					mapannounce "poring_w02","天使戰隊的註冊已經完成. 隊伍成員們請到 多佩爾先生.這裡確認.",0,0x33FF66;
					close;
				case 3:
					mes "[多佩爾先生]";
					mes "浪費時間啊. 動作要快啊.";
					close;
				}
			}
			else {
				mes "[多佩爾先生]";
				mes "好的,你是隊長吧.";
				mes "隊伍註冊好了沒?";
				mes "你必須在一個已經註冊過的隊伍裡!";
				next;
				mes "[多佩爾先生]";
				mes "隊伍名稱是 ";
				mes " " + .@a_tname$ + " ";
				mes "請, 確認.";
				close;
			}
		}
		else {
			mes "[多佩爾先生]";
			mes "如果你不是隊伍的註冊成員, 你是無法參加戰鬥的.";
			next;
			mes "[多佩爾先生]";
			mes "我在檢查一次, 之後我將送你們去比賽場地.";
			mes "我們必須在一起, 這是一個團隊遊戲. 不然我們會有麻煩的.";
			close;
		}
	}

OnInit:
	disablenpc "多佩爾先生#wop_team_a";
	end;

OnEnable:
	enablenpc "多佩爾先生#wop_team_a";
	end;

OnDisable:
	disablenpc "多佩爾先生#wop_team_a";
	end;
}

poring_w02,170,181,3	script	多佩爾先生#wop_team_d	8_DOPPEL,{
	if (checkweight(714,3) == 0) {
		mes "- 稍等!! -";
		mes "- 檢查你的負重 -";
		mes "- 你帶的東西太多啦. -";
		mes "- 等你清理下負重後 -";
		mes "- 再來找我吧. -";
		close;
	}
	set .@d_tname$,getpartyname($@wop_team_d);
	set .@pname$,getpartyname(getcharid(1));
	if ($@wop_team_d != 0 && $@wop_team_d == getcharid(1)) {
		mes "[多佩爾先生]";
		mes "所有人都組隊了嗎?";
		mes "隊伍名稱是... " + .@pname$ + ", 對嗎?";
		mes "我將傳送你們到比賽場地.";
		next;
		switch(select("- 不不! 等等!:- 去比賽場地.")) {
		case 1:
			mes "[多佩爾先生]";
			mes "怎麼回事?";
			mes "有什麼其他的意見?";
			mes "沒時間了. 現在決定!";
			next;
			mes "[多佩爾先生]";
			mes "如果現在不去比賽場地, 就將失去比賽資格了.";
			close;
		case 2:
			if ((getpartyleader(getcharid(1),2) == getcharid(0))) {
				mes "[多佩爾先生]";
				mes "你是隊長吧. 在去比賽場地之前, 你要核查你所有的隊員.";
				next;
				switch(select("- 很好.這是最後一次 .:- 我是最後一個, 送我去比賽場地.")) {
				case 1:
					mes "[多佩爾先生]";
					mes "首先, 確定所有隊員都在你的組隊裡.";
					close;
				case 2:
					mes "[多佩爾先生]";
					mes "很好. 你的準備很充分.";
					delitem 7773,countitem(7773);
					close2;
					set wop_team,0;
					warp "poring_w02",153,82;
					end;
				}
			}else{
				mes "[多佩爾先生]";
				mes "很好.我要送你們去比賽場地了.";
				mes "等待其他參賽隊伍.";
				delitem 7773,countitem(7773);
				close2;
				set wop_team,0;
				warp "poring_w02",153,82;
				end;
			}
		}
	} else {
		if ((getpartyleader(getcharid(1),2) == getcharid(0))) {
			if ($@wop_team_d == 0) {
				mes "[多佩爾先生]";
				mes "你是惡魔陣營的隊長吧. ";
				mes "確定所有隊員在你的組隊裡?";
				mes "首先, 讓我們註冊你們的戰隊名字, 然後, 我們將檢查你的隊伍.";
				next;
				mes "[多佩爾先生]";
				mes "讓我看看... 隊伍名稱是...";
				mes " ";
				mes "^4d4dff " + .@pname$ + " ^000000";
				mes " ";
				mes "對吧? 這是你想起的名字?";
				next;
				switch(select("- 不不! 你錯了.:- 是的. 我註冊了這名字.:- 取消")) {
				case 1:
					mes "[多佩爾先生]";
					mes "嘿, 我可沒有一天時間來陪你! 快點想啊.";
					mes "不要忘記告訴所有隊員一起組隊. 只有註冊過的隊伍才可以參加戰鬥.";
					close;
				case 2:
					mes "[多佩爾先生]";
					mes "我把你們的隊伍名稱註冊為 - " +.@pname$ + " -.";
					mes "現在, 告訴你的 ^4d4dff隊員們一起組隊^000000.";
					mes "我馬上送你們去比賽場地.";
					set $@wop_team_d,getcharid(1);
					mapannounce "poring_w02","天使戰隊的註冊已經完成. 隊伍成員們請到 多佩爾先生.這裡確認.",0,0x33FF66;
					close;
				case 3:
					mes "[多佩爾先生]";
					mes "浪費時間啊. 動作要快啊.";
					close;
				}
			}
			else {
				mes "[多佩爾先生]";
				mes "好的,你是隊長吧.";
				mes "隊伍註冊好了沒?";
				mes "你必須在一個已經註冊過的隊伍裡!";
				next;
				mes "[多佩爾先生]";
				mes "隊伍名稱是 ";
				mes " " + .@d_tname$ + " ";
				mes "請, 確認.";
				close;
			}
		}else{
			mes "[多佩爾先生]";
			mes "如果你不是隊伍的註冊成員, 你是無法參加戰鬥的.";
			next;
			mes "[多佩爾先生]";
			mes "我在檢查一次, 之後我將送你們去比賽場地.";
			mes "我們必須在一起, 這是一個團隊遊戲. 不然我們會有麻煩的.";
			close;
		}
	}

OnInit:
	disablenpc "多佩爾先生#wop_team_d";
	end;

OnEnable:
	enablenpc "多佩爾先生#wop_team_d";
	end;

OnDisable:
	disablenpc "多佩爾先生#wop_team_d";
	end;
}

poring_w02,1,1,0	script	#wop_master	-1,{
	end;

OnReset:
	removemapflag "poring_w02",mf_partylock;
	removemapflag "poring_w02",mf_pvp;
	removemapflag "poring_w02",mf_pvp_noguild;
	removemapflag "poring_w02",mf_pvp_nocalcrank;
	mapwarp "poring_w02","poring_w01",112,138;
	donpcevent "#wop_angellium1::OnReset";
	donpcevent "#wop_angellium2::OnReset";
	donpcevent "#wop_devillium1::OnReset";
	donpcevent "#wop_devillium2::OnReset";
	set $@wop_team_a,0;
	set $@wop_team_d,0;
	set $@wop_deadcount_a,0;
	set $@wop_deadcount_d,0;
	set $@wop_teamcount,0;
	set $@wop_doorcount_a,0;
	set $@wop_doorcount_d,0;
	donpcevent "#wop_warp_a::OnDisable";
	donpcevent "#wop_warp_d::OnDisable";
	donpcevent "#wop_warp_rtry::OnDisable";
	donpcevent "Poring#wop_door_a::OnEnable";
	donpcevent "Poring#wop_door_d::OnEnable";
	donpcevent "Poring#wop_door_all::OnEnable";
	stopnpctimer;
	end;

OnStart:
	initnpctimer;
	end;

OnAngelingWarn:
	mapannounce "poring_w02","惡魔陣營招募完成. 如果天使陣營不在1分鐘內完成隊員招募, 將自動取消這場戰鬥.",0,0xf08080;
	end;

OnDevilingWarn:
	mapannounce "poring_w02","天使陣營招募完成. 如果惡魔陣營不在1分鐘內完成隊員招募, 將自動取消這場戰鬥.",0,0xf08080;
	end;

OnDevilingEnd:
	mapannounce "poring_w02","惡魔陣營招募失敗. 這場戰鬥將馬上取消.",0,0xf08080;
	end;

OnAngelingEnd:
	mapannounce "poring_w02","天使陣營招募失敗. 這場戰鬥將馬上取消.",0,0xf08080;
	end;

OnStop:
	stopnpctimer;
	end;

OnAngelingWin:
	set .@i,1;
OnDevilingWin:
	removemapflag "poring_w02",mf_partylock;
	removemapflag "poring_w02",mf_pvp;
	removemapflag "poring_w02",mf_pvp_noguild;
	removemapflag "poring_w02",mf_pvp_nocalcrank;
	if (.@i)
		mapannounce "poring_w02","多佩爾先生: 天使陣營隊伍 " + getpartyname($@wop_team_a) + " 贏得了比賽的勝利!",0,0xf08080;
	else
		mapannounce "poring_w02","多佩爾先生: 惡魔陣營隊伍 " + getpartyname($@wop_team_d) + " 贏得了比賽的勝利!",0,0xf08080;
	donpcevent "小惡魔#wop_endmaster::OnEnable";
	stopnpctimer;
	// Reset Barriers
	donpcevent "#aroom_ingate_wop::OnDisable";
	donpcevent "#aroom_outgate_wop::OnDisable";
	donpcevent "#droom_ingate_wop::OnDisable";
	donpcevent "#droom_outgate_wop::OnDisable";
	end;

OnTimer5000:
	mapannounce "poring_w02","多佩爾先生: 歡迎所有參加戰鬥的勇士們.",0,0xf08080;
	donpcevent "多佩爾先生#wop_team_a::OnEnable";
	donpcevent "多佩爾先生#wop_team_d::OnEnable";
	end;

OnTimer8000:
	mapannounce "poring_w02","多佩爾先生: 每個隊伍成員必須加入團隊, 隊長會註冊隊伍的名字.",0,0xf08080;
	end;

OnTimer12000:
	mapannounce "poring_w02","多佩爾先生: 你還有50秒. 加入該小組並註冊它的名稱然後進入戰鬥地圖.",0,0xf08080;
	end;

OnTimer32000:
	mapannounce "poring_w02","多佩爾先生: 你還有30秒. 加入該小組並註冊它的名稱然後進入戰鬥地圖.",0,0xf08080;
	end;

OnTimer62000:
	mapannounce "poring_w02","多佩爾先生: 時間到了. 戰鬥開始前將解釋相關規則.",0,0xf08080;
	end;

OnTimer70000:
	mapannounce "poring_w02","多佩爾先生: 準備好了沒? 讓我們來解釋下規則.",0,0xf08080;
	end;

OnTimer75000:
	mapannounce "poring_w02","多佩爾先生: 天使戰隊必須在基地保護2隻天使波利, 於此同時, 消滅惡魔戰隊基地中的惡魔波利, 反之亦然.",0,0xf08080;
	end;

OnTimer80000:
	mapannounce "poring_w02","多佩爾先生: 規則很簡單,如果你消滅敵人的團隊 2 怪物, 則獲勝.",0,0xf08080;
	end;

OnTimer85000:
	mapannounce "poring_w02","多佩爾先生: 不過天使波利和惡魔波利會在一定時間內復活, 所以動作要快.",0,0xf08080;
	end;

OnTimer90000:
	mapannounce "poring_w02","多佩爾先生: 還有一些不屬於任何團隊的波利, 會無差別的攻擊任何人.",0,0xf08080;
	end;

OnTimer95000:
	mapannounce "poring_w02","多佩爾先生: 因為波利視人類為天地, 這時你們所有人都需要合作擊潰它們.",0,0xf08080;
	end;

OnTimer100000:
	mapannounce "poring_w02","多佩爾先生: 在某些領域, 你們將需要通過消除其他團隊成員獲得的戰爭徽章. 好吧, 讓我們開始!",0,0xf08080;
	setmapflag "poring_w02",mf_partylock;
	setmapflag "poring_w02",mf_pvp;
	setmapflag "poring_w02",mf_pvp_noguild;
	setmapflag "poring_w02",mf_pvp_nocalcrank;
	donpcevent "#wop_warp_a::OnEnable";
	donpcevent "#wop_warp_d::OnEnable";
	donpcevent "#wop_angellium1::OnAngelingSpawn";
	donpcevent "#wop_angellium2::OnAngelingSpawn";
	donpcevent "#wop_devillium1::OnDevilingSpawn";
	donpcevent "#wop_devillium2::OnDevilingSpawn";
	end;

OnTimer700000:
	mapannounce "poring_w02","多佩爾先生: 時間到了. 你們馬上可以參與到頒獎了.",0,0xf08080;
	removemapflag "poring_w02",mf_partylock;
	removemapflag "poring_w02",mf_pvp;
	removemapflag "poring_w02",mf_pvp_noguild;
	removemapflag "poring_w02",mf_pvp_nocalcrank;
	donpcevent "#wop_angellium1::OnReset";
	donpcevent "#wop_angellium2::OnReset";
	donpcevent "#wop_devillium1::OnReset";
	donpcevent "#wop_devillium2::OnReset";
	end;

OnTimer703000:
	donpcevent "小惡魔#wop_endmaster::OnEnable";
	stopnpctimer;
	end;
}

poring_w02,51,82,0	script	#wop_warp_a	HIDDEN_WARP_NPC,1,1,{
	end;

OnInit:
	disablenpc "#wop_warp_a";
	end;

OnEnable:
	enablenpc "#wop_warp_a";
	specialeffect EF_MAPPILLAR2,AREA,"#wop_warp_a";
	end;

OnDisable:
	disablenpc "#wop_warp_a";
	end;

OnTouch:
	set wop_team,1;
	warp "poring_w02",57,82;
	end;
}

poring_w02,146,82,0	script	#wop_warp_d	HIDDEN_WARP_NPC,1,1,{
	end;

OnInit:
	disablenpc "#wop_warp_d";
	end;

OnEnable:
	enablenpc "#wop_warp_d";
	specialeffect EF_MAPPILLAR2,AREA,"#wop_warp_d";
	end;

OnDisable:
	disablenpc "#wop_warp_d";
	end;

OnTouch:
	set wop_team,2;
	warp "poring_w02",140,82;
	end;
}

// Poring Spawn
//============================================================
poring_w02,80,82,0	script	#wop_angellium1	-1,{
	end;

OnReset:
	killmonster "poring_w02","#wop_angellium1::OnMyMobDead";
	stopnpctimer;
	end;

OnAngelingSpawn:
	monster "poring_w02",80,82,"天使波利",1766,1,"#wop_angellium1::OnMyMobDead";
	end;

OnMyMobDead:
	set $@wop_deadcount_a,$@wop_deadcount_a + 1;
	if($@wop_deadcount_a == 1) {
		mapannounce "poring_w02","多佩爾先生: 在1st陣地的天使波利被殺了! 丟1分!!",0,0x00ff00;
		initnpctimer;
	}
	else {
		donpcevent "#wop_angellium1::OnReset";
		donpcevent "#wop_angellium2::OnReset";
		donpcevent "#wop_devillium1::OnReset";
		donpcevent "#wop_devillium2::OnReset";
		donpcevent "#wop_master::OnDevilingWin";
	}
	end;

OnTimer120000:
	set $@wop_deadcount_a,$@wop_deadcount_a - 1;
	donpcevent "#wop_angellium1::OnAngelingSpawn";
	mapannounce "poring_w02","多佩爾先生: 天使波利在1st陣地復活了! 增加1分!!",0,0x00ff00;
	stopnpctimer;
}

poring_w02,98,41,0	script	#wop_angellium2	-1,{
	end;

OnReset:
	killmonster "poring_w02","#wop_angellium2::OnMyMobDead";
	stopnpctimer;
	end;

OnAngelingSpawn:
	monster "poring_w02",98,41,"天使波利",1766,1,"#wop_angellium2::OnMyMobDead";
	end;

OnMyMobDead:
	set $@wop_deadcount_a,$@wop_deadcount_a + 1;
	if ($@wop_deadcount_a == 1) {
		mapannounce "poring_w02","多佩爾先生: 在2nd陣地的天使波利被殺了! 丟1分!!",0,0x00ff00;
		initnpctimer;
	}
	else {
		donpcevent "#wop_angellium1::OnReset";
		donpcevent "#wop_angellium2::OnReset";
		donpcevent "#wop_devillium1::OnReset";
		donpcevent "#wop_devillium2::OnReset";
		donpcevent "#wop_master::OnDevilingWin";
	}
	end;

OnTimer120000:
	set $@wop_deadcount_a,$@wop_deadcount_a - 1;
	donpcevent "#wop_angellium2::OnAngelingSpawn";
	mapannounce "poring_w02","多佩爾先生: 天使波利在2nd陣地復活了! 增加1分!!",0,0x00ff00;
	stopnpctimer;
	end;
}

poring_w02,116,82,0	script	#wop_devillium1	-1,{
	end;

OnReset:
	killmonster "poring_w02","#wop_devillium1::OnMyMobDead";
	stopnpctimer;
	end;

OnDevilingSpawn:
	monster "poring_w02",116,82,"惡魔波利",1767,1,"#wop_devillium1::OnMyMobDead";
	end;

OnMyMobDead:
	set $@wop_deadcount_d,$@wop_deadcount_d + 1;
	if($@wop_deadcount_d == 1) {
		mapannounce "poring_w02","多佩爾先生: 在1st陣地的惡魔波利被殺了! 丟1分!!",0,0x00ff00;
		initnpctimer;
	}
	else {
		donpcevent "#wop_devillium1::OnReset";
		donpcevent "#wop_devillium2::OnReset";
		donpcevent "#wop_devillium1::OnReset";
		donpcevent "#wop_devillium2::OnReset";
		donpcevent "#wop_master::OnAngelingWin";
	}
	end;

OnTimer120000:
	set $@wop_deadcount_d,$@wop_deadcount_d - 1;
	donpcevent "#wop_devillium1::OnDevilingSpawn";
	mapannounce "poring_w02","多佩爾先生: 惡魔波利在1st陣地復活了! 增加1分!!",0,0x00ff00;
	stopnpctimer;
	end;
}

poring_w02,98,124,0	script	#wop_devillium2	-1,{
	end;

OnReset:
	killmonster "poring_w02","#wop_devillium2::OnMyMobDead";
	stopnpctimer;
	end;

OnDevilingSpawn:
	monster "poring_w02",98,124,"惡魔波利",1767,1,"#wop_devillium2::OnMyMobDead";
	end;

OnMyMobDead:
	set $@wop_deadcount_d,$@wop_deadcount_d + 1;
	if ($@wop_deadcount_d == 1) {
		mapannounce "poring_w02","多佩爾先生: 在2nd陣地的惡魔波利被殺了! 丟1分!!",0,0x00ff00;
		initnpctimer;
	}
	else {
		donpcevent "#wop_devillium1::OnReset";
		donpcevent "#wop_devillium2::OnReset";
		donpcevent "#wop_devillium1::OnReset";
		donpcevent "#wop_devillium2::OnReset";
		donpcevent "#wop_master::OnAngelingWin";
	}
	end;

OnTimer120000:
	set $@wop_deadcount_d,$@wop_deadcount_d - 1;
	donpcevent "#wop_devillium2::OnDevilingSpawn";
	mapannounce "poring_w02","多佩爾先生: 惡魔波利在2nd陣地復活了! 增加1分!!",0,0x00ff00;
	stopnpctimer;
}

// Deviruchi
//============================================================
poring_w02,99,201,3	script	小惡魔#wop_endmaster	4_DEVIRUCHI,{
	if (MaxWeight-Weight < 2000 || checkweight(1201,1) == 0) {
		mes "- 稍等!! -";
		mes "- 檢查你的負重 -";
		mes "- 你帶的東西太多啦. -";
		mes "- 等你清理下負重後 -";
		mes "- 再來找我吧. -";
		close;
	}
	set .@a_point,$@wop_deadcount_a;
	set .@d_point,$@wop_deadcount_d;
	if(getcharid(1) == $@wop_team_a) {
		if(.@a_point < .@d_point) {
			mes "[小惡魔]";
			mes "幹的好!";
			mes "多虧了你,增加了我們的勝利的機會.";
			mes "請接受這些硬幣作為我們的心意.";
			next;
			if(wop_team == 1) {
				mes "[小惡魔]";
				mes "再見了, 我的人類朋友.";
				close2;
				getitem 7539,3; // Poring_Coin
				warp "prt_fild08",150,370;
				end;
			}
			else {
				mes "[小惡魔]";
				mes "稍等, 你看起來很可疑!!";
				mes "啊, 我知道了! 你是天使戰隊的人?!";
				mes "你來做間諜的吧!";
				mes "我不會忘記你的!";
				close2;
				percentheal 99,0;
				warp "prt_fild08",150,370;
				end;
			}
		}
		else if(.@a_point == .@d_point) {
			mes "[小惡魔]";
			mes "你沒有盡力啊.";
			mes "太恥辱了...";
			mes "不過還是感謝你為我們而戰, 收下這些硬幣吧.";
			next;
			if(wop_team == 1) {
				mes "[小惡魔]";
				mes "再見了, 我的人類朋友.";
				close2;
				getitem 7539,1; // Poring_Coin
				warp "prt_fild08",150,370;
				end;
			}
			else {
				mes "[小惡魔]";
				mes "稍等, 你看起來很可疑!!";
				mes "啊, 我知道了! 你是天使戰隊的人?!";
				mes "你來做間諜的吧!";
				mes "肯定是你配合我們戰鬥的!!";
				mes "我不會忘記你的!";
				close2;
				percentheal 99,0;
				warp "prt_fild08",150,370;
				end;
			}
		}
		else {
			if(wop_team == 1) {
				mes "[小惡魔]";
				mes "我猜我們運氣不好...";
				mes "我什麼都不能給你, 因為我失去了所有...";
				next;
				mes "[小惡魔]";
				mes "再見了, 我的人類朋友.";
				close2;
				warp "prt_fild08",150,370;
				end;
			}
			else {
				mes "[小惡魔]";
				mes "稍等... 這裡有個叛徒!";
				mes "我知道了! 你是天使陣營的人?!";
				mes "你來做間諜的吧, 天使陣營!";
				next;
				mes "[小惡魔]";
				mes "你應該感到羞愧!";
				close2;
				percentheal 99,0;
				warp "prt_fild08",150,370;
				end;
			}
		}
	}
	else if(getcharid(1) == $@wop_team_d) {
		if (.@a_point > .@d_point) {
			mes "[小惡魔]";
			mes "幹的好!";
			mes "謝謝你, 讓我們獲得勝利.";
			mes "請接受這些硬幣作為我們的心意.";
			next;
			if (wop_team == 2) {
				mes "[小惡魔]";
				mes "再見了, 我的人類朋友.";
				close2;
				getitem 7539,3; // Poring_Coin
				warp "prt_fild08",150,370;
				end;
			}
			else {
				mes "[小惡魔]";
				mes "稍等, 你看起來很可疑!!";
				mes "啊, 我知道了! 你是天使陣營的人?!";
				mes "你來做間諜的吧!!";
				mes "我不會忘記你的!";
				close2;
				percentheal 99,0;
				warp "prt_fild08",150,370;
				end;
			}
		}
		else if (.@a_point == .@d_point) {
			mes "[小惡魔]";
			mes "你盡力了.";
			mes "多虧了你,增加了我們的勝利的機會...";
			mes "請接受這些硬幣作為我們的心意.";
			next;
			if(wop_team == 2) {
				mes "[小惡魔]";
				mes "再見了, 我的人類朋友.";
				close2;
				getitem 7539,1; // Poring_Coin
				warp "prt_fild08",150,370;
				end;
			}
			else {
				mes "[Devi]";
				mes "[小惡魔]";
				mes "稍等, 你看起來很可疑!!";
				mes "啊, 我知道了! 你是天使陣營的人?!";
				mes "你來做間諜的吧!";
				mes "肯定是你配合我們戰鬥的!!";
				mes "我不會忘記你的!";
				close2;
				percentheal 99,0;
				warp "prt_fild08",150,370;
				end;
			}
		}
		else {
			if (wop_team == 2) {
				mes "[小惡魔]";
				mes "我猜我們運氣不好...";
				mes "我什麼都不能給你, 因為我失去了所有...";
				next;
				mes "[小惡魔]";
				mes "我將傳送你回人類世界.";
				close2;
				warp "prt_fild08",150,370;
				end;
			}
			else {
				mes "[Devi]";
				mes "稍等, 你看起來很可疑!!";
				mes "啊, 我知道了! 你是天使陣營的人?!";
				mes "你來做間諜的吧, 天使陣營!";
				next;
				mes "[小惡魔]";
				mes "你應該感到羞愧!";
				close2;
				percentheal 99,0;
				warp "prt_fild08",150,370;
				end;
			}
		}
	}
	else {
		mes "[小惡魔]";
		mes "這很奇怪......你的名字不被註冊.";
		mes "非常不好意思啦, 規定就是規定. 如果你的隊伍沒註冊我可幫不了你.";
		close;
	}
OnInit:
	disablenpc "小惡魔#wop_endmaster";
	end;

OnEnable:
	enablenpc "小惡魔#wop_endmaster";
	initnpctimer;
	end;

OnDisable:
	disablenpc "小惡魔#wop_endmaster";
	end;

OnTimer3000:
	mapwarp "poring_w02","poring_w02",99,196;
	end;

OnTimer5000:
	mapannounce "poring_w02","多佩爾先生: Allow me to offer a souvenir to the humans that fought bravely for the Porings! Please, speak to Devi.",0,0x00ff00;
	end;

OnTimer65000:
	mapannounce "poring_w02","多佩爾先生: Time is up! Let me teleport you.",0,0x00ff00;
	end;

OnTimer68000:
	mapwarp "poring_w02","poring_w01",112,138;
	end;

OnTimer68100:
	donpcevent "#wop_master::OnStop";
	donpcevent "#wop_devillium1::OnReset";
	donpcevent "#wop_devillium2::OnReset";
	donpcevent "#wop_angellium1::OnReset";
	donpcevent "#wop_angellium2::OnReset";
	set $@wop_team_a,0;
	set $@wop_team_d,0;
	set $@wop_deadcount_a,0;
	set $@wop_deadcount_d,0;
	set $@wop_teamcount,0;
	set $@wop_doorcount_a,0;
	set $@wop_doorcount_d,0;
	donpcevent "#wop_warp_a::OnDisable";
	donpcevent "#wop_warp_d::OnDisable";
	donpcevent "多佩爾先生#wop_team_a::OnDisable";
	donpcevent "多佩爾先生#wop_team_d::OnDisable";
	donpcevent "#wop_warp_rtry::OnDisable";
	donpcevent "波利#wop_door_a::OnEnable";
	donpcevent "波利#wop_door_d::OnEnable";
	donpcevent "波利#wop_door_all::OnEnable";
	stopnpctimer;
	end;
}

// Gate Guardian
//============================================================
poring_w02,99,52,0	script	天使守衛#wop_da	HIDDEN_NPC,{
	cutin "wop_emb0" + $@wop_doorcount_a,1;
	if ($@wop_doorcount_a < 4) {
		if (wop_team == 2) {
			mes "那裡有個使用戰鬥徽章的設備.";
			mes "我可以看到裝備徽章的空插槽.";
			next;
			switch(select("- 裝備戰鬥徽章.:- 取消.")) {
			case 1:
				if (countitem(7773)) {
					if ($@wop_doorcount_a < 4) {
						mes "戰鬥徽章裝備完成.";
						delitem 7773,1; // War_Badge
						set $@wop_doorcount_a,$@wop_doorcount_a + 1;
						cutin "wop_emb0" + $@wop_doorcount_a,1;
						if($@wop_doorcount_a == 4) {
							next;
							mes "大門已激活.";
							donpcevent "#aroom_ingate_wop::OnEnable";
							donpcevent "#aroom_outgate_wop::OnEnable";
						}
						close2;
					}
					else {
						mes "所有的戰鬥徽章都已經裝備完成.";
						mes "不需要更多了.";
						close2;
					}
				}
				else {
					mes "^4d4dff - 缺少戰鬥徽章. 你可以在戰鬥中通過擊殺對方成員來獲得. - ^000000";
					close2;
				}
				break;
			case 2:
				mes "取消.";
				close2;
			}
		}
		else {
			mes "這裡有一臺設備需要裝備戰鬥徽章.";
			mes "要小心, 別讓你的敵人獲得徽章.";
			close2;
		}
	}
	else {
		mes "所有的戰鬥徽章均已裝備完成.";
		close2;
	}
	cutin "",255;
	end;

OnInit:
	set $@wop_doorcount_a,0;
	end;
}

poring_w02,103,52,0	script	#aroom_ingate_wop	HIDDEN_WARP_NPC,1,1,{
	end;

OnInit:
	disablenpc "#aroom_ingate_wop";
	end;

OnEnable:
	enablenpc "#aroom_ingate_wop";
	end;

OnDisable:
	disablenpc "#aroom_ingate_wop";
	end;

OnTouch:
	warp "poring_w02",99,49;
	end;
}

poring_w02,93,51,0	script	#aroom_outgate_wop	HIDDEN_WARP_NPC,1,1,{
	end;

OnInit:
	disablenpc "#aroom_outgate_wop";
	end;

OnEnable:
	enablenpc "#aroom_outgate_wop";
	end;

OnDisable:
	disablenpc "#aroom_outgate_wop";
	end;

OnTouch:
	warp "poring_w02",99,54;
	end;
}

poring_w02,98,113,0	script	惡魔守衛#wop_dd	HIDDEN_NPC,{
	cutin "wop_emb0" + $@wop_doorcount_d,1;
	if ($@wop_doorcount_d < 4) {
		if (wop_team == 1) {
			mes "那裡有個使用戰鬥徽章的東西.";
			mes "我可以看到裝備徽章的空插槽.";
			next;
			switch(select("- 裝備戰鬥徽章.:- 取消.")) {
			case 1:
				if (countitem(7773)) {
					if ($@wop_doorcount_d < 4) {
						mes "戰鬥徽章裝備完成.";
						delitem 7773,1; // War_Badge
						set $@wop_doorcount_d,$@wop_doorcount_d + 1;
						cutin "wop_emb0" + $@wop_doorcount_d,1;
						if ($@wop_doorcount_d == 4) {
							next;
							mes "大門已激活.";
							donpcevent "#droom_ingate_wop::OnEnable";
							donpcevent "#droom_outgate_wop::OnEnable";
						}
						close2;
					}
					else {
						mes "所有的戰鬥徽章都已經裝備完成.";
						mes "不需要更多了.";
						close2;
					}
				}
				else {
					mes "^4d4dff - 缺少戰鬥徽章. 你可以在戰鬥中通過擊殺對方成員來獲得. - ^000000";
					close2;
				}
				break;
			case 2:
				mes "取消.";
				close2;
			}
		}
		else {
			mes "這裡有一臺設備需要裝備戰鬥徽章.";
			mes "要小心, 別讓你的敵人獲得徽章.";
			close2;
		}
	}
	else {
		mes "所有的戰鬥徽章均已裝備完成.";
		close2;
	}
	cutin "",255;
	end;

OnInit:
	set $@wop_doorcount_d,0;
	end;
}

poring_w02,93,113,0	script	#droom_ingate_wop	HIDDEN_WARP_NPC,1,1,{
	end;

OnInit:
	disablenpc "#droom_ingate_wop";
	end;

OnEnable:
	enablenpc "#droom_ingate_wop";
	end;

OnDisable:
	disablenpc "#droom_ingate_wop";
	end;

OnTouch:
	warp "poring_w02",98,116;
	end;
}

poring_w02,103,114,0	script	#droom_outgate_wop	HIDDEN_WARP_NPC,1,1,{
	end;

OnInit:
	disablenpc "#droom_outgate_wop";
	end;

OnEnable:
	enablenpc "#droom_outgate_wop";
	end;

OnDisable:
	disablenpc "#droom_outgate_wop";
	end;

OnTouch:
	warp "poring_w02",98,111;
	end;
}

// Other NPCs
//============================================================
-	script	Angeling Side Poring#wpa	-1,{
	end;
}

-	script	Deviling Side Marin#wpd	-1,{
	end;
}

poring_w01,79,102,5	duplicate(Angeling Side Poring#wpa)	天使陣營波利#wa1	1002
poring_w01,80,107,5	duplicate(Angeling Side Poring#wpa)	天使陣營波利#wa2	1002
poring_w01,84,108,5	duplicate(Angeling Side Poring#wpa)	天使陣營波利#wa3	1002
poring_w01,84,112,5	duplicate(Angeling Side Poring#wpa)	天使陣營波利#wa4	1002
poring_w01,88,112,5	duplicate(Angeling Side Poring#wpa)	天使陣營波利#wa5	1002

poring_w01,114,112,3	duplicate(Deviling Side Marin#wpd)	惡魔陣營波利#wd1	1242
poring_w01,117,111,3	duplicate(Deviling Side Marin#wpd)	惡魔陣營波利#wd2	1242
poring_w01,117,108,3	duplicate(Deviling Side Marin#wpd)	惡魔陣營波利#wd3	1242
poring_w01,122,108,3	duplicate(Deviling Side Marin#wpd)	惡魔陣營波利#wd4	1242
poring_w01,121,105,3	duplicate(Deviling Side Marin#wpd)	惡魔陣營波利#wd5	1242

poring_w01,84,108,3	script	#wop_ex_1	HIDDEN_WARP_NPC,3,3,{
	end;

OnTouch:
	mes "[波利]";
	mes "波~波波波 -!!!";
	mes "讓我們給他們上一課, 呤!";
	mes "為了垃圾, 賭上我們的生命, 呤呤!!!";
	next;
	mes "[波利]";
	mes "出發! 戰鬥! 必勝, 必勝, 必勝!!!";
	emotion ET_GO, getnpcid(0, "天使陣營波利#wa1");
	emotion ET_GO, getnpcid(0, "天使陣營波利#wa2");
	emotion ET_GO, getnpcid(0, "天使陣營波利#wa3");
	emotion ET_GO, getnpcid(0, "天使陣營波利#wa4");
	emotion ET_GO, getnpcid(0, "天使陣營波利#wa5");
	next;
	if(wop_team == 1) {
		mes "[波利]";
		mes "噢啊啊, 這是個為我們戰鬥的戰士!!";
		mes "我的天... 太好了, 呤!";
		mes "波利~ 波利~ 波波波波波~";
		mes "天使陣營~ 波波, 波利!";
	}
	else if(wop_team == 2) {
		mes "[波利]";
		mes "站住, 你! 你不是惡魔陣營的人嗎?!";
		mes "快滾! 離開這裡!!";
		mes "波波波波波~ 波波~ 波波波~";
		emotion ET_HUK, getnpcid(0, "天使陣營波利#wa1");
		emotion ET_FRET, getnpcid(0, "天使陣營波利#wa2");
		emotion ET_HUK, getnpcid(0, "天使陣營波利#wa3");
		emotion ET_FRET, getnpcid(0, "天使陣營波利#wa4");
		emotion ET_SURPRISE, getnpcid(0, "天使陣營波利#wa5");
	}
	else {
		mes "[波利]";
		mes "嘿, 人類. 和天使陣營一起感覺如何??!";
		emotion ET_HELP, getnpcid(0, "天使陣營波利#wa1");
		emotion ET_GO, getnpcid(0, "天使陣營波利#wa2");
		emotion ET_HELP, getnpcid(0, "天使陣營波利#wa3");
		emotion ET_GO, getnpcid(0, "天使陣營波利#wa4");
		emotion ET_GO, getnpcid(0, "天使陣營波利#wa5");
	}
	next;
	mes "[波利]";
	mes "天使陣營~ 波波波, 波利!";
	mes "勝利~ 勝利~ 波波波波波波利~";
	next;
	mes "- 波利們高高興興的慶祝勝利. -";
	close;
}

poring_w01,119,107,3	script	#wop_ex_2	HIDDEN_WARP_NPC,3,3,{
	end;

OnTouch:
	mes "[惡魔波利]";
	mes "唔啊啊啊啊!";
	mes "最後, 是時候給那幫蠢豬點教訓了!!";
	mes "那些波利都是低等的白痴!!";
	next;
	mes "[惡魔波利]";
	mes "出發! 戰鬥! 為了勝利!!!!";
	emotion ET_GO, getnpcid(0, "惡魔陣營波利#wd1");
	emotion ET_GO, getnpcid(0, "惡魔陣營波利#wd2");
	emotion ET_GO, getnpcid(0, "惡魔陣營波利#wd3");
	emotion ET_GO, getnpcid(0, "惡魔陣營波利#wd4");
	emotion ET_GO, getnpcid(0, "惡魔陣營波利#wd5");
	next;
	if (wop_team == 2) {
		mes "[惡魔波利]";
		mes "噢... 這裡有個為我們而戰的戰士!!";
		mes "太神奇了!";
		mes "這證明了人類認識到我們是真正的怪物!!";
	}
	else if (wop_team == 1) {
		mes "[惡魔波利]";
		mes "站住, 你! 你不是天使陣營的人嗎?!";
		mes "快滾! 離開這裡!!";
		mes "波波波波波~ 波~ 波波~";
		emotion ET_HUK, getnpcid(0, "惡魔陣營波利#wd1");
		emotion ET_FRET, getnpcid(0, "惡魔陣營波利#wd2");
		emotion ET_HUK, getnpcid(0, "惡魔陣營波利#wd3");
		emotion ET_FRET, getnpcid(0, "惡魔陣營波利#wd4");
		emotion ET_SURPRISE, getnpcid(0, "惡魔陣營波利#wd5");
	}
	else {
		mes "[惡魔波利]";
		mes "嘿, 人類. 要為惡魔戰隊戰鬥麼, 為了黑暗? 想說什麼?!";
		emotion ET_HELP, getnpcid(0, "惡魔陣營波利#wd1");
		emotion ET_GO, getnpcid(0, "惡魔陣營波利#wd2");
		emotion ET_HELP, getnpcid(0, "惡魔陣營波利#wd3");
		emotion ET_GO, getnpcid(0, "惡魔陣營波利#wd4");
		emotion ET_GO, getnpcid(0, "惡魔陣營波利#wd5");
	}
	next;
	mes "[惡魔波利]";
	mes "世界上最神奇的波利! 都在惡魔戰隊! 出發, 惡魔戰隊, Go-!!";
	next;
	mes "- 波利們高高興興地慶祝勝利. -";
	close;
}
