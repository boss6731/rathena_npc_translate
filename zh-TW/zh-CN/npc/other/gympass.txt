//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================

//===== rAthena Script ======================================= 
//= Gym Pass Cash Item NPC
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Exchange Gym Passes to learn "Enlarge Weight Limit"
//= iRO NPC situated in Payon beside Kafra Shop.
//===== Additional Comments: ================================= 
//= 1.0 First version [L0ne_W0lf]
//= 1.1 Fixed the NPC saying you "00" [L0ne_W0lf]
//= 1.2 Replaced effect numerics with constants. [Samuray22]
//= 1.3 Updated script, and fixed potential errors. [L0ne_W0lf]
//============================================================ 

payon,173,141,4	script	重力聖人#GymPass1::GymPass	899,{
	mes "[重力聖人]";
	mes "嘿. 現代人的身體";
	mes "早已沒以前那麼好了";
	mes "就算你每天為了生存而戰鬥";
	mes "你也有時會變得";
	mes "虛弱和肌肉鬆弛對吧";
	mes "知道我的意思吧?";
	next;
	mes "[重力聖人]";
	mes "我可以訓練你";
	mes "而且我可以保證你";
	mes "舉起更重的東西,擁有更高的負重";
	mes "簡直就開掛!吸收兩千米下的氮磷鉀!";
	mes "每次給我^FF0000健身房會員券^000000,";
	mes "我就給你金坷垃.";
	next;
	mes "[重力聖人]";
	mes "不過別高興的太早:";
	mes "無論你怎樣訓練";
	mes "金坷垃的副作用還是太大了";
	mes "你有沒有聽過";
	mes "誰吃金坷垃吃成春哥了?";
	mes "那是因為他們都吃死了,懂?";
	next;
	mes "[重力聖人]";
	mes "當然金坷垃絕對不安全";
	mes "就算你再怎麼訓練";
	mes "我也只能保證你";
	mes "提升^FF000010次^000000負重能力.";
	mes "So... 準備好了麼?";
	next;
	switch(select("- 媽媽的,快給我金坷垃:- 不,我還是自己訓練吧.:- 該死,我的訓練效果消失了.")) {
	case 1:
		if (gympassmemory < 10) {
			set .@add_carry,gympassmemory + 1;
			set .@remain_carry,10 - .@add_carry;
			if (countitem(7776) > 0) {
				mes "[重力聖人]";
				mes "喔, 太棒了, 我看見你";
				mes "帶來了你的健身房使用券.";
				mes "好的,跟著我做,";
				mes "並且感受這個力量吧.";
				mes "讓我們開始吧.";
				next;
				specialeffect2 EF_EARTHSPIKE;
				next;
				specialeffect2 EF_DEVIL;
				next;
				specialeffect2 EF_COIN;
				next;
				specialeffect2 EF_SIGHTRASHER;
				next;
				mes "[重力聖人]";
				mes "好了,吃了金坷垃你應該能";
				mes "帶更多的東西了.";
				mes "看吧,我可以增加你";
				mes "攜帶東西的能力";
				mes "^FF00000" + .@remain_carry + "^000000次,如果繼續的話";
				mes "就像這樣的一起訓練.";
				delitem 7776,1; //Max_Weight_Up_Scroll
				set gympassmemory,.@add_carry;
				skill "ALL_INCCARRY",.@add_carry,SKILL_PERM_GRANT;
				close;
			}
			else {
				mes "[重力聖人]";
				mes "嘿, 我要怎麼給你說?";
				mes "你要給我帶來";
				mes "^FF0000健身券給我^000000 如果你想要";
				mes "金坷垃的話";
				mes "它能鍛鍊你的肌肉.";
				close;
			}
		}
		else {
			mes "[重力聖人]";
			mes "嘿,我不認為你";
			mes "你的肌肉能承受";
			mes "更多的金坷垃了.";
			mes "對於你的身體來說";
			mes "這實在是太危險了,";
			mes "我說過了你要有節制的.";
			close;
		}
	case 2:
		mes "[重力聖人]";
		mes "這太悲劇了.";
		mes "好吧, 如果你改變了";
		mes "想法就回來吧.";
		mes "也和你的朋友說說我:";
		mes "如果你感到了肌肉無力, ";
		mes "我能幫你解決.";
		close;
	case 3:
		if (gympassmemory > 0) {
			mes "[重力聖人]";
			mes "咋啦兄弟,被人煮了?";
			mes "什麼?你肌肉萎縮了";
			mes "金坷垃失效了? ";
			mes "還好世界上有種東西";
			mes "叫肌肉記憶. 再稍微";
			mes "訓練下你的能力就會全回來的...";
			next;
			specialeffect2 EF_EARTHSPIKE;
			next;
			specialeffect2 EF_DEVIL;
			next;
			specialeffect2 EF_COIN;
			next;
			specialeffect2 EF_SIGHTRASHER;
			next;
			mes "[重力聖人]";
			mes "現在感覺怎麼樣了?";
			mes "你的能力應該";
			mes "全部回來了吧,";
			mes "就像那樣!試著不要";
			mes "再把你的能力弄丟了好了?";
			skill "ALL_INCCARRY",gympassmemory,SKILL_PERM_GRANT;
			close;
		}
		else {
			mes "[重力聖人]";
			mes "唔...";
			mes "我們以前";
			mes "並沒有一起訓練過吧.";
			mes "我很確信.";
			close;
		}
	}
}
prontera,160,136,3	duplicate(GymPass)	重力聖人#GymPass2	899
