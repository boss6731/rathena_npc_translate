//============================================================
//= rAthenaCN [國王獎勵任務]漢化腳本 
//============================================================


//===== Hercules Script ======================================
//= Event King Items
//===== By: ==================================================
//= $ephiroth
//===== Current Version: =====================================
//= 1.1
//===== Description: =========================================
//= Grand Circlet Quest combined with Excalibur Quest.
//===== Additional Comments: =================================
//= 2006/09/27: 1.0 Release and fully working. [$ephiroth]
//= 1.1 Optimized. [Euphy]
//============================================================

prt_castle,80,170,4	script	普隆德拉國王	1_M_PRON_KING,{
	mes "[普隆德拉國王]";
	mes "你好, 請問你能不能幫我?";
	next;
	if(select("- 當然可以, 我可以為你做些什麼?:- 對不起, 我很忙")==2) {
		mes "[普隆德拉國王]";
		mes "好的, 這太好了, 我敢肯定一定會有人幫到我的.";
		close; }
	mes "[普隆德拉國王]";
	mes "謝謝你. 好的, 在我旁邊的是我的女兒她有點吵鬧..";
	mes "如果你能給她這些東西, 我肯定她就會平靜下來.";
	mes "我將會送給你我的皇冠或者我的寶劍來當獎勵.";
	next;
	setarray .@items1[0],5007,742,30,741,50,753,20,732,1,710,20,629,20;
	setarray .@items2[0],1137,2254,1,2282,1,714,1;
	switch(select("- 你需要些什麼?:- 我把東西都帶齊了!:- 算了")) {
		case 1:
	for(set .@i,1; .@i<3; set .@i,.@i+1) {
				mes "[普隆德拉國王]";
				mes "如果你能給我：";
				for(set .@j,1; .@j<getarraysize(getd(".@items"+.@i)); set .@j,.@j+2)
					mes " ~ ^0055FF"+getd(".@items"+.@i+"["+(.@j+1)+"]")+"^000000x "+getitemname(getd(".@items"+.@i+"["+.@j+"]"));
				mes "我將送出我的 "+((.@i)?"- 寶劍.":"- 皇冠.");
				if (.@i==2) close;
				next; }
		case 2:
			mes "[普隆德拉國王]";
			mes "你想要哪個物品?";
			next;
			set .@i, select("寶劍:皇冠");
			for(set .@j,1; .@j<getarraysize(getd(".@items"+.@i)); set .@j,.@j+2)
				if (countitem(getd(".@items"+.@i+"["+.@j+"]")) < getd(".@items"+.@i+"["+(.@j+1)+"]")) set .@nr,1;
			mes "[普隆德拉國王]";
			if (.@nr) { 
				mes "你有些東西缺少了.";
				mes "等你收集好所有東西再回來吧.";
				close; }
			for(set .@j,1; .@j<getarraysize(getd(".@items"+.@i)); set .@j,.@j+2)
				delitem getd(".@items"+.@i+"["+.@j+"]"), getd(".@items"+.@i+"["+(.@j+1)+"]");
			getitem getd(".@items"+.@i+"[0]"),1;
			mes "非常感謝你!!";
			mes "這次肯定可以讓她冷靜下來的.";
			mes "作為承諾, 這是我的 "+((.@i==1)?"寶劍.":"皇冠.");
			close;
		case 3:
			mes "[普隆德拉國王]";
			mes "好的, 沒關係, 我確信一定會有其他人能幫到我的.";
			close;
	}
}

prt_castle,84,170,4	script	國王的女兒	1_M_SIZ,{
	mes "[國王的女兒]";
	mes "哼, 我現在討厭死我父王了!!";
	next;
	if(select("- 為什麼?:- 無所謂..")==2) {
		mes "[國王的女兒]";
		mes "那好吧! 哼!!";
		close; }
	mes "[國王的女兒]";
	mes "好吧, 他給不了我想要的!!";
	mes "他是普隆德拉王國的國王. 他應該能夠給我所有的東西!!!";
	close;
}
