//============================================================
//= rAthenaCN [聖誕戒指任務]漢化腳本 
//============================================================


//===== Hercules Script ======================================
//= Karachun Event (Xmas Rings Quest)
//===== By: ==================================================
//= Lupus (1.0)
//===== Current Version: =====================================
//= 1.2
//===== Description: =========================================
//= Custom event: Karachun (from 8 Dec till 8 Jan), X-Mas, HNY
//===== Additional Comments: =================================
//= 1.0 Tested, fully working. 1.0a a few typos fix
//= 1.1 Readded Mdef +1 and  Luk +1 to Santa's Hat [Lupus]
//= 1.2 Fixed double quotes not being escaped [brianluau]
//============================================================

prontera,156,174,4	script	雪諾爾	4_M_06,{
	mes "[雪諾爾]";
	if(Q_XMSRINGS==2){
		mes "哦...這是真的嗎? 你幫助了我的姐妹雪伊? 謝謝.";
	}else if(Q_XMSRINGS==3){
		mes "雪諾伊已經讓她的小鳥送信來告訴我你幫忙的事情了...";
		mes "讓我來告訴你一個魔戒的秘密.";
		emotion ET_BEST;
		next;
		mes "[雪諾爾]";
		mes "它們的魔力只有在12月8日到1月8日之間才存在.";
		next;
		mes "[雪諾爾]";
		mes "順便告訴你, 只要把聖誕帽帶上你就能發現它們真正的魔力了.";
		next;
		mes "[雪諾爾]";
		mes "如果你把兩個戒指都帶上, 他們的魔力也會加倍.";
		mes "祝你今天過的開心!";
		close;
	}else{
		mes "你好! 我和我的兩個姐妹正在為全部三個節日準備禮物!";
	}
	emotion ET_CRY;
	next;
	menu "- 你的姐妹在哪裡?",-, "- 什麼節日?",M_PR,"- 什麼禮物?",M_WRAP,"- 我可以幫什麼嗎?",M_QUEST;

	mes "[雪諾爾]";
	mes "我的妹妹雪伊去了一個遙遠的地方為那裡的窮孩子準備禮物了... 我也不知道在哪裡. 你可以去問問我的姐姐和她的小鳥們.";
	next;
	mes "[雪諾爾]";
	mes "我的姐姐? 她的名字是雪諾伊. 她航海去了某個綠色的小島... 她似乎提起過要為孤兒們準備一些禮物...";
	close;

M_PR:
	mes "[雪諾爾]";
	mes "那三個節日? 好吧, 它們一個接著一個!";
	mes "^000080聖誕節^000000, ^FF0000Karachun^000000 和 ^008000新年前夕^000000!";
	next;
	mes "[雪諾爾]";
	mes "^FF0000Karachun^000000? 嘿... 這是一個古老的異教徒的節日. 你知道, 曾經他們會在聖誕和新年到來之前慶祝它.";
	next;
	goto M_QUEST;

M_WRAP:
	mes "[雪諾爾]";
	mes "我的姐妹說所有的禮物都應該看起來一模一樣, 不然有些孩子可能會去搶那些漂亮的!";
	next;
	mes "[雪諾爾]";
	mes "你想要我幫你把你漂亮的禮物盒子用灰紙包起來嗎?";
	next;
	menu "- 請包起來吧!",-, "- 不用, 謝謝.",M_QUEST;

	set @prizes, countitem(664)+countitem(665)+countitem(666)+countitem(667);
	if( @prizes == 0){
		mes "[雪諾爾]";
		mes "我沒發現你身上有什麼禮物在漂亮盒子裡...";
		if(countitem(644))mes "你只有灰色的盒子.";
		close;
	}
	delitem 664,countitem(664);
	delitem 665,countitem(665);
	delitem 666,countitem(666);
	delitem 667,countitem(667);
	getitem 644,@prizes;

	mes "[雪諾爾]";
	mes "*晃了晃* 給你.";
	next;

M_QUEST:
	mes "[雪諾爾]";
	mes "希望你能儘快找到我的姐妹們, 他們需要你的幫助.";
	if(Q_XMSRINGS==0) set Q_XMSRINGS,1;
	close;

OnInit:
//Santa's Hat
	setitemscript 2236,"{ bonus bMdef,1; bonus bLuk,1; if(isequipped(2636,2637)){if(@xmr == gettime(2))end; set @xmr,gettime(2); misceffect 410; end;} if(isequipped(2636)){if(@xmr == gettime(2))end; set @xmr,gettime(2); misceffect 72;} if(isequipped(2637)){if(@xmr == gettime(2))end; set @xmr,gettime(2); misceffect 338;}}";
//Gold Xmas Ring
	setitemscript 2636,"{ bonus bLoseSPWhenUnequip,30; if(isequipped(2236)==0)end; if(getskilllv(\"AL_HEAL\")){skill \"TF_HIDING\",4+isequipped(2637);}else{skill \"AL_HEAL\",1+4*isequipped(2637);} }";
//Silver Xmas Ring
	setitemscript 2637,"{ bonus bDamageWhenUnequip,40; if(isequipped(2236)==0)end; if(getskilllv(\"AL_TELEPORT\")){skill \"MC_IDENTIFY\",1;}else{skill \"AL_TELEPORT\",1+isequipped(2636);} }";
	end;
}

louyang,224,249,4	script	雪伊	4_F_KID3,{
	mes "[雪伊]";
	if(Q_XMSRINGS==0){
		mes "你好! 我正在等我姐姐的小鳥送信過來...";
		emotion ET_CRY;
		close;
	}
	if(Q_XMSRINGS==1){
		mes "我真希望我還能有4個灰色禮物盒給當地的孩子們...";
		emotion ET_CRY;
		next;
		mes "[雪伊]";
		mes "噢, 你好. 我的哥哥雪諾爾把事情都告訴你了嗎?";
		next;
		menu "這是你的4個禮物盒",-, "我不認識什麼雪...什麼什麼",M_End;

		mes "[雪伊]";
		if(countitem(644)<4){
			mes "給我4個禮物送給窮孩子們吧... 等等, 你還不滿4個!";
			emotion ET_SORRY;
			close;
		}
		set Q_XMSRINGS,2;
		delitem 644,4;
		getnameditem 2637,strcharinfo(0);
		emotion ET_CHUPCHUP;
		mes "謝謝! 這是我送你的一點小小心意!";
		next;
		mes "[雪伊]";
		mes "對了, 如果你帶上2個不同的戒指, 你的節日等級提升會更容易!";
		close;
	}
	emotion ET_THANKS;
	mes "再次感謝! 所有的孩子們都會有節日禮物啦!";
	close;

M_End:
	mes "[雪伊]";
	mes "真可惜... 我有一個聖誕老人的魔力戒指... 我想用它來換4個禮物盒...";
	emotion ET_SWEAT;
	close;
}

izlu2dun,133,160,2	script	雪諾伊	4_F_06,{
	mes "[雪諾伊]";
	if(Q_XMSRINGS==0){
		mes "你好哦! 你認識我的弟弟和妹妹嗎? 不認識? 我的弟弟雪諾爾在普隆德拉很有名氣的!! 隨便找個那邊的孩子問問就知道了.";
		emotion ET_SCRATCH;
		close;
	}
	if(Q_XMSRINGS==1){
		mes "可惡... 窮孩子們怎麼可能比海軍們更重要呢.. 我錯了嗎?";
		next;
		mes "[雪諾伊]";
		mes "呃. 你好. 我的妹妹雪伊讓她的小鳥送來一封信. 可是信已經溼了裡面的內容都看不清了, 我只能分辨出她的地址的最後幾個字母...";
		next;
		mes "[雪諾伊]";
		mes "看吧, 最後好像是'楊'什麼的... 嗯... 她一定是把'洛陽'拼錯了...";
		close;
	}
	if(Q_XMSRINGS==2){
		mes "你幫助了我的妹妹? 太好了!";
		next;
		mes "[雪諾伊]";
		mes "但是我還需要3個禮物給我的海軍朋友們...";
		next;
		menu "拿下這些禮物!",-, "我需要一些小小的報酬",M_End;

		mes "[雪諾伊]";
		if(countitem(644)<3){
			mes "噢!! 你還沒有收集全3個灰色禮物盒.";
			emotion ET_SORRY;
			close;
		}
		set Q_XMSRINGS,3;
		delitem 644,3;
		getnameditem 2636,strcharinfo(0);
		emotion ET_CHUP;
		mes "謝謝! 收下這個魔力戒指吧!";
		close;
	}
	emotion ET_THANKS;
	mes "噢... 全體海軍隊員們都很感謝你! 他們非常喜歡這些禮物!!!";
	if(rand(2))mes "你不會不知道聖誕帽能夠顯現出戒指的真實魔力吧?";
	close;

M_End:
	mes "[雪諾伊]";
	mes "我想用一個金色的魔力戒指來交換3個禮物盒...";
	emotion ET_SWEAT;
	close;
}
