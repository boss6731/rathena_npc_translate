//============================================================
//= rAthenaCN [自動吃藥]漢化腳本 
//============================================================


//===== rAthena Script =======================================
//= Auto-Potion
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Provides an @autopot command to automatically use potions
//= when hit (i.e. 'autobonus2').
//===== Additional Comments: =================================
//= 1.0 First version. [Euphy]
//============================================================

-	script	#autopot	-1,{
OnInit:
	bindatcmd("autopot",strnpcinfo(0)+"::OnCommand");
	end;

L_Help:
	dispbottom "可用的指令:";
	dispbottom "    @autopot <藥物ID> {<最低HP % [1..100]> {<延遲 [50..1000]>}}";
	dispbottom "    @autopot <on|off>";
	dispbottom "    @autopot list";
	dispbottom "    @autopot info";
	dispbottom "    @autopot help";
	return;

L_Info:
	dispbottom "------ 自動吃藥相關信息 ------";
	dispbottom "藥物  :  " + getitemname(@autopot_id) + " (" + @autopot_id + ")";
	dispbottom "最低HP:   " + @autopot_min + " %";
	dispbottom "延遲  :    " + @autopot_delay + " ms";
	dispbottom "---------------------------------------------";
	return;

L_Start:
	.@potion = getarg(0);
	.@min    = getarg(1);
	.@delay  = getarg(2);
	if (.@min   < 1  || .@min   > 100)  .@min   = 90;
	if (.@delay < 50 || .@delay > 1000) .@delay = 50;  // lower values will increase server strain
	switch (.@potion) {
		case 501:
		case 507:
		case 545:
		case 569: .@effect = EF_POTION1; break;
		case 502: .@effect = EF_POTION2; break;
		case 503:
		case 508:
		case 546:
		case 579:
		case 11500: .@effect = EF_POTION3; break;
		case 504:
		case 509:
		case 547:
		case 11501:
		case 11503:
		case 11548: .@effect = EF_POTION4; break;
		case 512:
		case 513:
		case 515:
		case 516:
		case 548:
		case 549:
		case 550:
		case 582:
		case 607: .@effect = EF_POTION7; break;
		default: .@effect = EF_EXIT; break;
	}

	if (BaseLevel < getiteminfo(.@potion,12)) {
		message strcharinfo(0), "您的基本等級不夠無法使用 '" + getitemname(.@potion) + "'.";
		end;
	}

	@autopot_id    = .@potion;
	@autopot_min   = .@min;
	@autopot_delay = .@delay;
	@autopot_eff   = .@effect;
	@autopot_none  = 0;
	bonus_script "{ callfunc \"start_autopot\"; }",86400,8,0,SI_INCHEALRATE;

	message strcharinfo(0), "開始自動吃藥.";
	callsub L_Info;
	return;

OnCommand:
	if (!getarraysize(.@atcmd_parameters$)) {
		message strcharinfo(0), "語法錯誤.";
		callsub L_Help;
		end;
	}

	.@command$ = strtolower(.@atcmd_parameters$[0]);

	if (.@command$ == "on") {
		if (@autopot_min)
			message strcharinfo(0), "自動吃藥功能已處於激活狀態.";
		else if (@autopot_min_) {
			@autopot_min  = @autopot_min_;
			@autopot_min_ = 0;
			message strcharinfo(0), "自動吃藥功能已經激活.";
			callsub L_Info;
		} else {
			message strcharinfo(0), "自動吃藥功能尚未配置.";
			callsub L_Help;
		}
		end;
	} else if (.@command$ == "off") {
		if (!@autopot_min)
			message strcharinfo(0), "自動吃藥功能已處于禁用狀態.";
		else {
			@autopot_min_ = @autopot_min;
			@autopot_min  = 0;
			message strcharinfo(0), "自動吃藥功能已經禁用.";
		}
		end;
	} else if (.@command$ == "list") {  // credits to AnnieRuru
		getinventorylist;
		for (; .@i < @inventorylist_count; .@i++) {
			if (getiteminfo(@inventorylist_id[.@i],2) == IT_HEALING) {
				.@items[.@count] = @inventorylist_id[.@i];
				.@menu$ = .@menu$ + sprintf("~ ^0055FF%s^000000 (%dx):", getitemname(@inventorylist_id[.@i]), countitem(@inventorylist_id[.@i]));
				.@count++;
			}
		}
		if (.@count) {	// 'mes' window needed if player is hit during selection
			mes "[ Auto-Potion ]";
			mes "請選擇一個治療類道具.";
			.@select = select(.@menu$ + "   ^777777取消^000000") - 1;
			if (.@select != .@count)
				callsub L_Start, .@items[.@select], 0, 0;
			close2;
		} else
			message strcharinfo(0), "您的背包中沒有任何治療類道具.";
		end;
	} else if (.@command$ == "info") {
		if (@autopot_min) {
			message strcharinfo(0), "自動吃藥信息顯示如下.";
			callsub L_Info;
		} else
			message strcharinfo(0), "自動吃藥功能尚未啟用.";
		end;
	} else if (.@command$ == "help") {
		message strcharinfo(0), "功能的命令列表如下顯示.";
		callsub L_Help;
		end;
	} else {
		.@potion = atoi(.@atcmd_parameters$[0]);
		if (getiteminfo(.@potion,2) != IT_HEALING) {
			message strcharinfo(0), getitemname(.@potion) + " 不是治療道具.";
			end;
		}
		callsub L_Start, .@potion, atoi(.@atcmd_parameters$[1]), atoi(.@atcmd_parameters$[2]);
		end;
	}
}

function	script	start_autopot	{
	if (@autopot_active) end;
	@autopot_active = 1;
	while (Hp && Hp * 100 / MaxHp < @autopot_min) {
		if (!countitem(@autopot_id)) {
			if (@autopot_none <= gettimetick(2)) {
				@autopot_none = gettimetick(2) + 10;
				dispbottom "您背包中的 '" + getitemname(@autopot_id) + "' 已經用完.";
			}
			break;
		}
		if (getstatus(SC_BERSERK) || getstatus(SC_SATURDAYNIGHTFEVER) || getstatus(SC_GRAVITATION) ||
		    getstatus(SC_TRICKDEAD) || getstatus(SC_HIDING) || getstatus(SC__SHADOWFORM) || getstatus(SC__INVISIBILITY) ||
		    getstatus(SC__MANHOLE) || getstatus(SC_KAGEHUMI) || getstatus(SC_HEAT_BARREL_AFTER))
			break;
		if (getstatus(SC_STONE) || getstatus(SC_FREEZE) || getstatus(SC_STUN) || getstatus(SC_SLEEP))
			;
		else {
			delitem @autopot_id,1;
			consumeitem @autopot_id;
			specialeffect2 @autopot_eff;
		}
		sleep2 @autopot_delay;
	}
	@autopot_active = 0;
	autobonus2 "{}",10000,1,BF_WEAPON|BF_MAGIC;
	end;
}
