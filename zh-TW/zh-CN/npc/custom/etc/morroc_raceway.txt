//============================================================
//= rAthenaCN [極速天使]漢化腳本 
//============================================================


//===== Hercules Script ======================================
//= Morroc Raceway
//===== By: ==================================================
//= acky
//===== Current Version: =====================================
//= 1.3
//===== Description: =========================================
//= Lets players race around Morroc (pvp_y_1-4)
//===== Additional Comments: =================================
//= If there are more than 3 players, at least 3 people
//= must finish before a new race can be started.
//=
//= If there are less than 3 players, at least 1 person
//= must finish before a new race can be started.
//=
//= Removed permanent global variables
//= 1.2 Removed Duplicates [Silent]
//= 1.3 Replaced effect numerics with constants. [Samuray22]
//============================================================

//Warps you into race way
morocc,166,105,6	script	極速天使#01	4_F_KAFRA2,{
	mes "[極速天使]";
	mes "你想不想參加 ^0000FF夢羅克競速賽^000000?";
	next;
	menu "- 是",L_Warp,"- 否",-;
	mes "[極速天使]";
	mes "好吧, 等你什麼時候改變主要了隨時來找我.";
	close;
L_Warp:
	warp "pvp_y_1-4",165,256;
	close;
}

//Warps you out of raceway
pvp_y_1-4,169,265,5	script	極速天使#02	4_F_KAFRA2,{
	mes "[極速天使]";
	mes "歡迎來到夢羅克競速賽!";
	next;
	menu "- 情報",-,"- 離開",L_Warp,"- 取消",L_Cancel;
	mes "[極速天使]";
	mes "選手們必須點擊開始NPC才可以開啟競賽.";
	next;
	mes "[極速天使]";
	mes "當比賽開始後, 逆時針方向圍著夢羅克競速.";
	next;
	mes "[極速天使]";
	mes "你必須通過所有的關卡 - 不能作弊!";
	close;
L_Warp:
	warp "morocc",165,101;
L_Cancel:
	mes "[極速天使]";
	mes "再見!";
	close;
}

//Counts down and starts race
pvp_y_1-4,145,269,5	script	Starter	4_M_JOB_KNIGHT1,{
	if ($@race != 0) goto L_Started;
	if ($@counting != 0) goto L_Started;
	if ($@racecount == 1) goto L_Started;
L_Menu:
	mes "[比賽開始]";
	mes "請待在我的東面.";
	menu "- 開始競賽",L_Count,"- 取消",-;
	close;
L_Count:
	set $@counting,1;
	mes "倒計時...";
	addtimer 1000, "Starter::OnCount1000";
	addtimer 2000, "Starter::OnCount2000";
	addtimer 3000, "Starter::OnCount3000";
	addtimer 4000, "Starter::OnCount4000";
	announce strcharinfo(0) + "開始倒計時",1;
	announce "準備好比賽!",1;
	close;

OnCount1000:
	announce "[3]",1;
	end;
OnCount2000:
	announce "[2]",1;
	end;
OnCount3000:
	announce "[1]",1;
	end;
OnCount4000:
	emotion 27;
	specialeffect EF_CHIMTO;
	announce "[GO!]",1;
	set $@race,1;
	set $@position,0;
	set $@counting,0;
	set $@raceid,rand(100000,999999);
	end;

L_Started:
	if ((getmapusers("pvp_y_1-4") < 3) && ($@position > 0)) goto L_Menu;
	if ($@position > 2) goto L_Menu;
	mes "[Starter]";
	mes "比賽進行中";
	close;

OnInit:
	set $@race,0;
	set $@position,0;
	set $@racecount,0;
	end;
}

//Checkpoint 1
pvp_y_1-4,144,262,5	script	起點	HIDDEN_NPC,0,5,{
	end;
OnTouch:
	if (@raceid != $@raceid) goto L_Started;
	if (@race == 6) goto L_Finished;
	if ($@race == 1) goto L_Started;
	mes "比賽還沒有開始, 請回起跑線.";
	close;
L_Started:
	set @race,1;
	set @raceid,$@raceid;
	end;
L_Finished:
	mes "你已經完成了比賽.";
	close;
}

//Checkpoint 2
pvp_y_1-4,73,247,5	script	檢測點[1]	HIDDEN_NPC,6,6,{
	end;
OnTouch:
	if (@race != 1) goto L_Miss;
	set @race,2;
	announce "[" + strcharinfo(0) +"] 已經到達檢測點 [1]",1;
	end;
L_Miss:
	mes "你錯過了檢測點[1]. 請返回.";
	close;
}

//Checkpoint 3
pvp_y_1-4,77,44,5	script	檢測點[2]	HIDDEN_NPC,6,6,{
	end;
OnTouch:
	if (@race != 2) goto L_Miss;
	set @race,3;
	announce "[" + strcharinfo(0) +"] 已經到達檢測點 [2]",1;
	end;
L_Miss:
	mes "你錯過了檢測點[2]. 請返回.";
	close;
}

//Checkpoint 3
pvp_y_1-4,249,60,5	script	檢測點[3]	HIDDEN_NPC,6,6,{
	end;
OnTouch:
	if (@race != 3) goto L_Miss;
	set @race,4;
	announce "[" + strcharinfo(0) +"] 已經到達檢測點 [3]",1;
	end;
L_Miss:
	mes "你錯過了檢測點[3]. 請返回.";
	close;
}

//Checkpoint 4
pvp_y_1-4,255,256,5	script	檢測點[4]	HIDDEN_NPC,6,6,{
	end;
OnTouch:
	if (@race != 4) goto L_Miss;
	set @race,5;
	announce "[" + strcharinfo(0) +"] 已經到達檢測點 [4]",1;
	end;
L_Miss:
	mes "你錯過了檢測點[4]. 請返回.";
	close;
}

//Finish Line
pvp_y_1-4,174,244,5	script	終點	HIDDEN_NPC,6,6,{
	end;
OnTouch:
	if (@raceid != $@raceid) goto L_WrongRace;
	if (@race != 5) goto L_Miss;
	set @race,6;
	set $@position,$@position+1;
	announce "[" + strcharinfo(0) +"] 已經到達了終點! [排名: " + $@position + "]",1;
	end;
L_Miss:
	mes "你錯過了檢測點. 請返回.";
	close;
L_WrongRace:
	mes "你不在比賽中.";
	close;
}

//Check Point Marker Flags
pvp_y_1-4,144,267,4	script	起點#01	GUILD_FLAG,{
	end;
}
pvp_y_1-4,144,257,4	script	起點#02	GUILD_FLAG,{
	end;
}
pvp_y_1-4,70,252,3	script	檢測點[1]#01	GUILD_FLAG,{
	end;
}
pvp_y_1-4,77,243,3	script	檢測點[1]#02	GUILD_FLAG,{
	end;
}
pvp_y_1-4,81,48,1	script	檢測點[2]#01	GUILD_FLAG,{
	end;
}
pvp_y_1-4,72,40,1	script	檢測點[2]#02	GUILD_FLAG,{
	end;
}
pvp_y_1-4,244,65,7	script	檢測點[3]#01	GUILD_FLAG,{
	end;
}
pvp_y_1-4,252,57,7	script	檢測點[3]#02	GUILD_FLAG,{
	end;
}
pvp_y_1-4,259,260,5	script	檢測點[4]#01	GUILD_FLAG,{
	end;
}
pvp_y_1-4,251,252,5	script	檢測點[4]#02	GUILD_FLAG,{
	end;
}
pvp_y_1-4,174,249,4	script	終點#01	GUILD_FLAG,{
	end;
}
pvp_y_1-4,174,238,4	script	終點#02	GUILD_FLAG,{
	end;
}
