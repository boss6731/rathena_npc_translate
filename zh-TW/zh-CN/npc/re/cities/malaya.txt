//============================================================
//= rAthenaCN [城鎮人員]漢化腳本 
//============================================================


//===== Hercules Script ======================================
//= Port Malaya
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Description: =========================================
//= [Official Conversion]
//= Port Malaya Town Script
//===== Additional Comments: =================================
//= 0.1 Alberta sailors only. [Euphy]
//= 0.2 Adapted from Masao's conversion.
//= 1.0 Added remaining NPCs.
//= 1.1 Added Jeepney script. [DeadlySilence]
//============================================================

// Port Malaya Transportation
//============================================================
// Old coordinates: alberta (237,71)
alberta,196,202,3	script	船員 奧塔瑪拉#alberta	4W_SAILOR,{
	mes "[船員 奧塔瑪拉]";
	mes "嘿~這兒~";
	mes "10,000 Zeny就能去馬來西亞港口,有興趣麼?";
	next;
	switch(select("有關馬來西亞港口.:去往馬來西亞港口.:取消.")) {
	case 1:
		mes "[船員 奧塔瑪拉]";
		mes "馬來西亞是一個小村莊,從艾爾貝塔坐船向西南方向航行便能到達.";
		next;
		mes "[船員 奧塔瑪拉]";
		mes "之前一陣我都不去馬來西亞,因為那裡有些問題,不過似乎現在都已經解決了.";
		next;
		mes "[船員 奧塔瑪拉]";
		mes "我不是很確定啦,不過那裡就像是一個舒適又安逸的公園~";
		next;
		mes "[船員 奧塔瑪拉]";
		mes "我差點兒就待在那裡再也不回艾爾貝塔了!";
		next;
		mes "[船員 奧塔瑪拉]";
		mes "我相信,等你到了那裡也會有同樣感受的.";
		close;
	case 2:
		if (Zeny < 10000) {
			mes "[船員 奧塔瑪拉]";
			mes "噢!天哪!";
			mes "這不能免費啦,你不知道去馬來西亞有多遠!等你有了10,000 Zeny的時候再來找我吧!";
			close;
		}
		mes "[船員 奧塔瑪拉]";
		mes "太好了!";
		mes "方向馬來西亞港口~走起!~";
		set Zeny, Zeny - 10000;
		close2;
		warp "malaya",271,55;
		end;
	case 3:
		mes "[船員 奧塔瑪拉]";
		mes "冒險家們最近似乎都很忙的樣子.好吧,等你有時間的時候再回來找我一起去馬來西亞港口吧.";
		close;
	}
}

malaya,276,55,4	script	船員 奧塔瑪拉#malaya	4W_SAILOR,{
	if (malaya_hi < 10) {
		mes "[船員 奧塔瑪拉]";
		mes "這個小村莊現在依舊繁忙.你已經參觀完畢了嗎?這兒一直有返回艾爾貝塔的船隻.";
		next;
	} else if (malaya_hi < 20) {
		mes "[船員 奧塔瑪拉]";
		mes "我記得一位無名的冒險家幫助了村民,使他們放下心來.好了~你想回艾爾貝塔嗎?";
		next;
	} else {
		mes "[船員 奧塔瑪拉]";
		mes "你想和我一起返回艾爾貝塔嗎?";
		next;
	}
	switch(select("回去.:不回.")) {
	case 1:
		mes "[船員 奧塔瑪拉]";
		mes "好了!~我們起航返回艾爾貝塔!";
		close2;
		warp "alberta",239,68;
		end;
	case 2:
		mes "[船員 奧塔瑪拉]";
		mes "沒關係,等你想回去的時候再來找我吧.";
		close;
	}
}

// Generic Port Malaya NPCs
//============================================================
ma_in01,30,94,4	script	旅店老闆#ma	4_F_MALAYA,{
	if (malaya_hi < 10) {
		mes "[旅店老闆]";
		mes "俄..你是來旅行的?";
		mes "你來得真不是時候..目前這個村莊還有些混亂,我的旅館一直沒有營業.";
		close;
	} else if (malaya_hi < 20) {
		mes "[旅店老闆]";
		mes "你就是那個村民們正在談論的冒險家?!為了表示感謝,我的旅館為您馬上開業!";
		next;
	} else {
		mes "[旅店老闆]";
		mes "歡迎光臨.";
		mes "這兒是馬來西亞最棒的旅店---城中小屋~";
		next;
	}
	mes "[旅店老闆]";
	mes "需要在這裡休息嗎?住宿費用為 5,000 Zeny.";
	next;
	switch(select("儲存服務.:住宿--5,000 Zeny.:取消.")) {
	case 1:
		mes "[旅店老闆]";
		mes "已經成功儲存.下次再見~";
		savepoint "ma_in01",43,98;
		close;
	case 2:
		if (Zeny < 5000) {
			mes "[旅店老闆]";
			mes "不好意思,您的錢不夠.";
			close;
		}
		mes "[旅店老闆]";
		mes "住宿愉快~!";
		close2;
		set Zeny, Zeny - 5000;
		percentheal 100,100;
		warp "ma_in01",43,98;
		end;
	case 3:
		close;
	}
}

-	script	不明生物#i	-1,{
	end;
OnInit:
	switch(atoi(strnpcinfo(2))) {
		case 1: setarray .@npc$,"01","02","03","04"; break;
		case 5: setarray .@npc$,"05","06","07","08"; break;
		case 9: setarray .@npc$,"09","10","11"; break;
		default: end;
	}
	set .@size, getarraysize(.@npc$);
	set .@rand, rand(.@size);
	for(set .@i,0; .@i<.@size; set .@i,.@i+1)
		donpcevent "不明生物#"+.@npc$[.@i]+"::"+((.@i == .@rand)?"OnEnable":"OnDisable");
	end;
OnEnable:
	enablenpc strnpcinfo(0);
	end;
OnDisable:
	disablenpc strnpcinfo(0);
	end;
OnBingx2:
	switch(atoi(strnpcinfo(2))) {
		case 1: setarray .@npc$,"02","03","04"; break;
		case 5: setarray .@npc$,"06","07","08"; break;
		case 9: setarray .@npc$,"10","11"; break;
	}
	set .@size, getarraysize(.@npc$);
	set .@rand, rand(.@size);
	for(set .@i,0; .@i<.@size; set .@i,.@i+1)
		donpcevent "不明生物#"+.@npc$[.@i]+"::"+((.@i == .@rand)?"OnEnable":"OnDisable");
	donpcevent strnpcinfo(0)+"::OnDisable";
	end;
OnTouch:
	if (getcharid(1)) {
		if ($ma_name04$ == strcharinfo(0) || $ma_name05$ == strcharinfo(0) || $ma_name06$ == strcharinfo(0)) {
			donpcevent strnpcinfo(0)+"::OnBingx2";
			mes "不明生物瞥了你一眼,臉紅了下後消失了.";
			close;
		}
		if (rand(1,5)%2) {
			switch(atoi(strnpcinfo(2))) {
				case 1: set $ma_name04$,strcharinfo(0); break;
				case 5: set $ma_name05$,strcharinfo(0); break;
				case 9: set $ma_name06$,strcharinfo(0); break;
			}
		}
		donpcevent strnpcinfo(0)+"::OnBingx2";
		mes "不明生物瞥了你一眼後消失了.";
		close;
	}
	donpcevent strnpcinfo(0)+"::OnBingx2";
	mes "不明生物朝你看看,然後跑開了.";
	close;
}
ma_fild01,74,367,6	duplicate(不明生物#i)	不明生物#01	4_MAL_BUDIDAI,2,2
ma_fild02,282,41,4	duplicate(不明生物#i)	不明生物#05	4_MAL_BUDIDAI,2,2
ma_scene01,195,92,4	duplicate(不明生物#i)	不明生物#09	4_MAL_BUDIDAI,2,2

-	script	不明生物#j	-1,{
	end;
OnEnable:
	enablenpc strnpcinfo(0);
	end;
OnDisable:
	disablenpc strnpcinfo(0);
	end;
OnBingx2:
	switch(atoi(strnpcinfo(2))) {
		case 2: setarray .@npc$,"01","03","04"; break;
		case 3: setarray .@npc$,"01","02","04"; break;
		case 4: setarray .@npc$,"01","02","03"; break;
		case 6: setarray .@npc$,"05","07","08"; break;
		case 7: setarray .@npc$,"05","06","08"; break;
		case 8: setarray .@npc$,"05","06","07"; break;
		case 10: setarray .@npc$,"09","11"; break;
		case 11: setarray .@npc$,"09","10"; break;
	}
	set .@size, getarraysize(.@npc$);
	set .@rand, rand(.@size);
	for(set .@i,0; .@i<.@size; set .@i,.@i+1)
		donpcevent "不明生物#"+.@npc$[.@i]+"::"+((.@i == .@rand)?"OnEnable":"OnDisable");
	donpcevent strnpcinfo(0)+"::OnDisable";
	end;
OnTouch:
	set .@i, atoi(strnpcinfo(2));
	if (.@i%4 == 2) {
		if (getcharid(0) == getpartyleader(getcharid(1),2))
			set .@pass,1;
	} else if (.@i%4 == 3) {
		if (checkquest(4229,PLAYTIME) > -1 && checkquest(9223,PLAYTIME) > -1 && checkquest(12278,PLAYTIME) > -1)
			set .@pass,1;
	} else if (.@i%4 == 0) {
		if (rand(1,100)%10 == 7)
			set .@pass,1;
	}
	if (.@pass) {
		if ($ma_name04$ == strcharinfo(0) || $ma_name05$ == strcharinfo(0) || $ma_name06$ == strcharinfo(0)) {
			donpcevent strnpcinfo(0)+"::OnBingx2";
			mes "不明生物瞥了你一眼,臉紅了下後消失了.";
			close;
		}
		if (rand(1,5)%2) {
			switch(.@i) {
			case 2:
			case 3:
			case 4:
				set $ma_name04$,strcharinfo(0);
				break;
			case 6:
			case 7:
			case 8:
				set $ma_name05$,strcharinfo(0);
				break;
			case 10:
			case 11:
				set $ma_name06$,strcharinfo(0);
				break;
			}
		}
		donpcevent strnpcinfo(0)+"::OnBingx2";
		mes "不明生物瞥了你一眼後消失了.";
		close;
	}
	donpcevent strnpcinfo(0)+"::OnBingx2";
	mes "不明生物朝你看看,然後跑開了.";
	close;
}
ma_fild01,109,116,4	duplicate(不明生物#j)	不明生物#02	4_MAL_BUDIDAI,2,2
ma_fild01,280,150,6	duplicate(不明生物#j)	不明生物#03	4_MAL_BUDIDAI,2,2
ma_fild01,309,221,6	duplicate(不明生物#j)	不明生物#04	4_MAL_BUDIDAI,2,2
ma_fild02,246,324,4	duplicate(不明生物#j)	不明生物#06	4_MAL_BUDIDAI,2,2
ma_fild02,71,296,6	duplicate(不明生物#j)	不明生物#07	4_MAL_BUDIDAI,2,2
ma_fild02,32,263,4	duplicate(不明生物#j)	不明生物#08	4_MAL_BUDIDAI,2,2
ma_scene01,158,139,4	duplicate(不明生物#j)	不明生物#10	4_MAL_BUDIDAI,2,2
ma_scene01,167,112,6	duplicate(不明生物#j)	不明生物#11	4_MAL_BUDIDAI,2,2

malaya,227,311,4	script	婆婆#ma01	4_F_BARYO_OLD,{
	if (malaya_hi < 10) {
		mes "[婆婆]";
		mes "你不是這兒附近的人吧?自己小心點~!";
		next;
		mes "[婆婆]";
		mes "我的媳婦兒懷孕了,我有點擔心,希望別出什麼岔子.";
		close;
	} else if (malaya_hi < 20) {
		mes "[婆婆]";
		mes "我聽說一個外族人正在幫助我們的村莊.";
		next;
		mes "[婆婆]";
		mes "我想我的媳婦兒會平安的!";
		close;
	}
	if (rand(1,3) == 2) {
		mes "[婆婆]";
		mes "聽說"+getd("$ma_name0"+rand(1,6)+"$")+"一直跟隨在噩夢護士左右.";
		next;
		mes "[婆婆]";
		mes "我會祝福他們這種禁忌愛戀的.";
		close;
	}
	mes "[婆婆]";
	mes "我很擔心我懷孕的媳婦兒.希望噩夢護士不會來傷害她.";
	next;
	select("噩夢護士?");
	mes "[婆婆]";
	mes "你不是附近的村民?即使你是外鄉人,在這裡也得小心.";
	next;
	mes "[婆婆]";
	mes "噩夢護士在很久以前是一個居住在馬來西亞港口的女人,她嫁給了一個體面的男人後不久便懷孕了.";
	next;
	mes "[婆婆]";
	mes "可是,快樂總是短暫的.在一次意外中,她流產了..";
	next;
	mes "[婆婆]";
	mes "她失去孩子後無比悲傷,沒過幾天便離開了人世.";
	next;
	select("這真是個悲傷的故事.");
	mes "[婆婆]";
	mes "之後,噩夢護士的靈魂一直在村子裡徘徊,帶走村莊裡所有懷孕女人腹中的孩子.";
	next;
	mes "[婆婆]";
	mes "如果夜晚你還在馬來西亞港口行走..";
	next;
	mes "[婆婆]";
	mes "聽見tik-tik或者wak-wak的聲音,那你得小心了,那是噩夢護士在煽動翅膀.";
	close;
}

malaya,189,263,4	script	擊鼓的 年輕人#ma02	4_M_BARYO_MAN,{
	if (malaya_hi < 10) {
		emotion ET_HUK;
		mes "[擊鼓的 年輕人]";
		mes "呃..這就和以前那時候一樣...";
		next;
		emotion ET_HUK;
		mes "[擊鼓的 年輕人]";
		mes "當月亮被吞掉的時候..呃!!";
		close;
	} else if (malaya_hi < 20) {
		mes "[擊鼓的 年輕人]";
		mes "我記得當時村莊被一位無名的英雄所拯救.";
		next;
		emotion ET_OHNO;
		mes "[擊鼓的 年輕人]";
		mes "唔..我覺得食月暴龍又出現了!!";
		close;
	}
	if (rand(1,3) == 2) {
		mes "[擊鼓的 年輕人]";
		mes getd("$ma_name0"+rand(1,6)+"$")+"據說曾經阻止了食月暴龍吞下月亮.是不是很可怕?不過我不擔心~ ";
		close;
	}
	mes "[擊鼓的 年輕人]";
	mes "在馬來西亞港口,傳說食月暴龍是一隻能吞下月亮的怪物!";
	next;
	mes "[擊鼓的 年輕人]";
	mes "不過我還知道,食月暴龍還會吞食人類!!";
	next;
	select("靠!那不是很危險?!");
	emotion ET_KIK;
	mes "[擊鼓的 年輕人]";
	mes "哈哈哈,不過還是有辦法阻止食月暴龍吞食人類的~";
	next;
	mes "[擊鼓的 年輕人]";
	mes "即使是已經吞掉了6個月亮的怪物也是有弱點的嘛!~";
	next;
	select("哦?那弱點是什麼?");
	mes "[擊鼓的 年輕人]";
	mes "當它要吞食月亮的時候,對外界的噪音相當敏感.";
	next;
	mes "[擊鼓的 年輕人]";
	mes "這時候你的機會就來了!!不管是鍋碗瓢盆還是鼓~無論哪種噪音都可以!!";
	next;
	mes "[擊鼓的 年輕人]";
	mes "食月暴龍受到驚嚇後便會吐出月亮隨即逃走!~";
	next;
	mes "[擊鼓的 年輕人]";
	mes "隨身帶一個鼓吧,很方便的~!";
	close;
}

malaya,270,59,4	script	港口 守衛#ma03	4_MAL_SOLDIER,{
	if (malaya_hi < 10) {
		emotion ET_PROFUSELY_SWEAT;
		mes "[港口 守衛]";
		mes "村子裡最近不怎麼太平.我就這麼離開不好吧?";
		close;
	} else if (malaya_hi < 20) {
		emotion ET_STARE_ABOUT;
		mes "[港口 守衛]";
		mes "現在,村莊裡已經安定多了,不過即使這樣,夜晚出門的時候也請小心.";
		close;
	}
	if (rand(1,3) == 2) {
		mes "[港口 守衛]";
		mes getd("$ma_name0"+rand(1,6)+"$")+"據說曾經被寶箱巨鱷俘獲過,不過他逃脫了.他肯定是個厲害角色!";
		close;
	}
	emotion ET_HUK;
	mes "[港口 守衛]";
	mes "什麼?!港口解除警報!!";
	next;
	mes "[港口 守衛]";
	mes "咦?你不是隊長?靠!嚇死我了..好了,我接到了一份有關遊客的官方文件.";
	next;
	select("什麼文件?");
	mes "[港口 守衛]";
	mes "文件上說,遊客夜晚在村莊裡活動的時候一定要小心這裡的怪物出沒.";
	next;
	mes "[港口 守衛]";
	mes "寶箱巨鱷並不會經常在村子裡出沒,不過它們會將人誘拐進箱子並在夜晚帶走他們.";
	next;
	mes "[港口 守衛]";
	mes "俄..還有一種叫食屍鬼的怪物經常會在晚上出現,一定要小心別碰見.";
	next;
	switch(select("我知道了.:有什麼辦法阻止它們進村麼?")) {
	case 1:
		mes "[港口 守衛]";
		mes "晚上的時候要小心!";
		close;
	case 2:
		mes "[港口 守衛]";
		mes "聽說,如果你被關在寶箱巨鱷背著的箱子裡時,可以趁機在箱子裡攻擊它的弱點!";
		next;
		mes "[港口 守衛]";
		mes "如果你聽見食屍鬼從遠處飛翔而來的聲音,一定要立刻躲進最近的建築裡去.";
		next;
		emotion ET_SCRATCH;
		mes "[港口 守衛]";
		mes "不過要是聽不見聲音或者煽動翅膀的聲音並不是很大,那你就自求多福,隨時保持警惕吧!";
		close;
	}
}

malaya,88,252,4	script	小女孩#ma04	4_F_BARYO_GIRL,{
	if (malaya_hi < 10) {
		mes "[小女孩]";
		mes "我很害怕,不過我還是要去拜訪森林裡的精靈.";
		close;
	} else if (malaya_hi < 20) {
		mes "[小女孩]";
		mes "呵呵.爸爸說只要村子恢復平靜了我就能去森林拜訪精靈.";
		close;
	}
	if (rand(1,3) == 2) {
		set .@name$, getd("$ma_name0"+rand(1,6)+"$");
		mes "[小女孩]";
		mes "不久以前,"+.@name$+"來到這裡說了一句有趣的話~";
		next;
		mes "[小女孩]";
		mes "'我在院子裡種了一對梨樹~'";
		mes " 呵呵,這真有趣~不是麼?"+.@name$+"看起來是個很有趣的人~!";
		close;
	}
	mes "[小女孩]";
	mes "爸爸說,只要和森林裡的精靈成為朋友就不會得皮膚病了!";
	next;
	select("這是什麼精靈?");
	mes "[小女孩]";
	mes "他們是仙女!他們住在森林裡的大樹或巖石裡.仙女有女的,也有男的...";
	next;
	mes "[小女孩]";
	mes "不過,男仙女比較好看哦...為什麼呢...";
	close;
}

malaya,219,92,6	script	小孩#ma05	4_M_BARYO_BOY,{
	if (malaya_hi < 10) {
		mes "[小孩]";
		mes "媽媽說外面太危險了,不讓我出去玩.為什麼呢?";
		close;
	} else if (malaya_hi < 20) {
		mes "[小孩]";
		mes "媽媽只讓我在馬來西亞港口玩耍.";
		next;
		mes "[小孩]";
		mes "呵呵,不過我從來沒想過去馬來西亞港口以外的世界玩呢.";
		close;
	}
	if (rand(1,3) == 2) {
		mes "[小孩]";
		mes "我聽說哦,有人在神秘的村莊裡面拿走了食人波利的帽子..";
		next;
		mes "[小孩]";
		mes "為什麼會有人要偷怪物的帽子呢?"+getd("$ma_name0"+rand(1,6)+"$")+"肯定要絕望了.";
		next;
		mes "[小孩]";
		mes "哦!!等等.. 這應該是個秘密的.噓~~~~~你什麼都沒有聽見哦!";
		close;
	}
	mes "[小孩]";
	mes "我想有人偷走了村子裡所有的帽子!";
	next;
	mes "[小孩]";
	mes "鄰居們的帽子在幾天前就已經消失了,我媽昨天剛洗好的帽子也不見了.";
	next;
	mes "[小孩]";
	mes "唔...你覺得會不會是被食人波利拿走了?";
	next;
	mes "[小孩]";
	mes "我的朋友說,他曾經看見食人波利戴著這些消失的帽子...";
	close;
}

malaya,363,283,4	script	當地居民#ma06	4_M_MALAYA,{
	if (malaya_hi < 10) {
		mes "[當地居民]";
		mes "唔...女巫的詛咒又來了!";
		next;
		mes "[當地居民]";
		mes "外鄉人,一定要小心噩夢巫師手裡的針!";
		close;
	} else if (malaya_hi < 20) {
		mes "[當地居民]";
		mes "歡迎來到馬來西亞!";
		next;
		mes "[當地居民]";
		mes "村子裡最近有些不安全,如果你見到有誰的眼睛是紅色的,切忌不要盯著他們的眼睛看!!";
		close;
	}
	set .@name$, getd("$ma_name0"+rand(1,6)+"$");
	switch(rand(1,6)) {
	case 1:
		mes "[當地居民]";
		mes "夜晚出門的時候一定要小心,聽說"+.@name$+"被食屍鬼帶走了.";
		close;
	case 2:
		mes "[當地居民]";
		mes .@name$+"據說成功地馴服了一隻邪魔馬.這傢伙一定很勇敢!!真希望我能親眼見到這隻邪魔馬呢!";
		close;
	case 3:
		mes "[當地居民]";
		mes "你有見過獨眼怪麼?聽說"+.@name$+"抓捕到了一隻.";
		close;
	case 4:
		mes "[當地居民]";
		mes "如果你想去村莊的外面走走,一定要小心惡魔嬰兒."+.@name$+"住院了,聽說他被惡魔嬰兒騙了.";
		close;
	case 5:
	case 6:
		break;
	}
	mes "[當地居民]";
	mes "最近村子裡很混亂,看起來噩夢巫師又回來了..";
	next;
	select("噩夢巫師?");
	mes "[當地居民]";
	mes "那怪物是巫師,它們也穿著衣服,看起來就和人一樣.";
	next;
	mes "[當地居民]";
	mes "噩夢巫師會把它看見的人製做成娃娃並用針來詛咒.";
	next;
	select("有沒有辦法可以辨認出這種怪物?");
	mes "[當地居民]";
	mes "有一種辦法,所有的噩夢巫師都是紅色的眼睛.";
	next;
	mes "[當地居民]";
	mes "你可以通過它們的眼睛顏色辨別出,不過一定要切記一點!";
	next;
	mes "[當地居民]";
	mes "別盯著它們的眼睛看太久!不然你會被噩夢巫師俘獲的.";
	next;
	mes "[當地居民]";
	mes "記著!別盯著它們的眼睛看!!";
	close;
}

malaya,41,127,6	script	老人 #ma07	4_M_BARYO_OLD,{
	if (malaya_hi < 10) {
		mes "[老人]";
		mes "我們並不希望有外鄉人來,因為這裡最近不安全.";
		close;
	} else if (malaya_hi < 20) {
		mes "[老人]";
		mes "現在真不是來我們村莊旅遊的時候,我們不希望您前來.";
		close;
	}
	if (rand(1,3) == 2) {
		mes "[老人]";
		mes getd("$ma_name0"+rand(1,6)+"$")+"聽說在村子裡丟掉了傑勒比結晶. ";
		next;
		mes "[老人]";
		mes "嘖,嘖..肯定是個不值得擁有傑勒比結晶的人!";
		close;
	}
	mes "[老人]";
	mes "如果你要砍樹的話還必須注意是不是有噩夢死神住在那棵樹上.";
	next;
	mes "[老人]";
	mes "要是使用了噩夢死神居住過的樹木上的木頭建造房子,會發生奇怪的事情!";
	next;
	select("會發生什麼?");
	mes "[老人]";
	mes "對噩夢死神來說,這意味著它們失去了自己的家,你就等著被報復吧!";
	next;
	mes "[老人]";
	mes "當你在屋子裡想要入睡的時候,它們會對你惡作劇,讓你無法睡著.";
	next;
	mes "[老人]";
	mes "隨著時間的推移,復仇升級,他們會坐在你身上直到你沒氣了.";
	next;
	select("俄..天哪..");
	mes "[老人]";
	mes "哈哈哈!不過通常房梁裡面沒有噩夢死神的,別擔心會失眠.";
	close;
}

malaya,63,185,4	script	女人#ma08	4_F_MALAYA,{
	if (malaya_hi < 10) {
		mes "[女人]";
		mes "我沒什麼可以和你說的.";
		close;
	} else if (malaya_hi < 20) {
		mes "[女人]";
		mes "我很擔心那些孩子們.希望村子裡發生的可怕事件別給他們留下陰影.";
		close;
	}
	if (rand(1,3) == 2) {
		set .@name$, getd("$ma_name0"+rand(1,6)+"$");
		set .@rand, rand(1,10);
		if (.@rand < 5) {
			mes "[女人]";
			mes "我喜歡有趣的語句.不久之前,"+.@name$+"來到這裡告訴了我一個有趣的段子.";
			next;
		}
		switch(.@rand) {
		case 1:
			mes "[女人]";
			mes "'我的阿姨踩到了一隻螞蟻.'";
			next;
			break;
		case 2:
			mes "[女人]";
			mes "'你在一個寒冷的日子吃辣椒.'";
			next;
			break;
		case 3:
			mes "[女人]";
			mes "'你為什麼往花上撒麵粉?'";
			next;
			break;
		case 4:
			mes "[女人]";
			mes "在工具店裡, '我想去買針線,你卻買了乾草把針都藏了進去.'";
			next;
			mes "[女人]";
			mes .@name$+"說完後被扔出了工具店.";
			next;
			break;
		case 5:
			mes "[女人]";
			mes "我的一個卡普拉服務員朋友告訴了我有關一位客人的故事.";
			next;
			mes "[女人]";
			mes "'聰明如卡普拉,她們從不結婚,一旦他們結婚了,就再也不是卡普拉了.'";
			next;
			mes "[女人]";
			mes "呵呵~~我喜歡有趣的故事~不過要合我的口味有點困難~";
			close;
		default:
			mes "[女人]";
			mes "我喜歡有趣的語句,等一下~還有一個~ "+.@name$+", 愛情就像照片,你同意麼?";
			next;
			mes "[女人]";
			mes "愛情就像照片,需要黑暗來顯影感光.";
			next;
		}
		mes "[女人]";
		mes "呵呵~這不是很有趣嘛~?";
		close;
	}
	mes "[女人]";
	mes "如果你聽見從村外的樹林傳來嬰兒的哭聲,千萬不要靠近那裡.";
	next;
	select("為什麼?");
	mes "[女人]";
	mes "有一種叫惡魔嬰兒的怪物會在村外遊蕩,它們將自己變身成為啼哭的嬰兒吸引行人的注意.";
	next;
	mes "[女人]";
	mes "一旦你抱起了嬰兒,它就會立刻現出原形攻擊你.";
	next;
	select("聽起來真是個卑鄙的怪物.");
	mes "[女人]";
	mes "是的,這種怪物利用旅行者的好心真是太無恥了..但是..";
	next;
	mes "[女人]";
	mes "但是..傳聞這種怪物是一個未出生孩子的靈魂轉變的..哎..真是太悲傷了..";
	close;
}

// Jeepneys
//============================================================
function	script	F_Malaya_Jeepney	{
	.@mapname$        = getarg(0);
	.@passengers      = getarg(1);

	// set the other messages of varying amount)
	for (.@i = 5; .@i < getargcount(); .@i++) {
		setd(".@msgJeepneyInfo$[" + (.@i - 5) + "]", getarg(.@i));
	}

	if (malaya_hi < 10) {
		mes "[Jeepney Driver]";
		mes getarg(2);
		close;
	} else if ((malaya_hi >= 10) && (malaya_hi < 20)) {
		mes "[Jeepney Driver]";
		mes getarg(3);
		close;
	} else {
		mes "[Jeepney Driver]";
		mes getarg(4);
		next;
	}
	switch(select("Board [Passenger " + getmapusers(.@mapname$) + "/" + .@passengers +"]:Jeepney?:Are there any other Jeepneys?:Ah... Yes...")) {
	case 1:
		if (getmapusers(.@mapname$) >= .@passengers) {
			mes "[Jeepney Driver]";
			mes "I'm afraid the Jeepney is full.";
			mes "I'm sorry but how about some other Jeepney?";
			close;
		} else {
			mes "[Jeepney Driver]";
			mes "Have a nice day.";
			close2;
			warp .@mapname$,29,24;
			end;
		}
	case 2:
		// iterate through all the jeepney information for this specific NPC
		for (.@i = 0; .@i < getarraysize(.@msgJeepneyInfo$); .@i++) {
			mes "[Jeepney Driver]";
			mes getd(".@msgJeepneyInfo$[" + .@i + "]");

			if (.@i < (getarraysize(.@msgJeepneyInfo$) - 1)) {
				next;
			}
		}
		close;
	case 3:
		mes "[Jeepney Driver]";
		mes "Oh! Other Jeepneys are in operation, of course.";
		next;
		mes "[Jeepney Driver]";
		mes "In Port Malaya there are 12, 30 and 60 passenger Jeepneys with 3 each operating.";
		next;
		mes "[Jeepney Driver]";
		mes "Would you like to know the location of other cars?";
		next;
		switch (select("I'd like to know where the 12 passenger car is.:I'd like to know where the 30 passenger car is.:I'd like to know where the 60 passenger car is.")) {
		case 1:
			viewpoint 1, 237, 240, 1, 0xF7E009;
			viewpoint 1, 67 , 44 , 2, 0xF7E009;
			viewpoint 1, 282, 129, 3, 0xF7E009;
			set .@zif_in, 12;
			break;
		case 2:
			viewpoint 1, 134, 250, 4, 0xF7E009;
			viewpoint 1, 341, 153, 5, 0xF7E009;
			viewpoint 1, 293, 290, 6, 0xF7E009;
			set .@zif_in, 30;
			break;
		case 3:
			viewpoint 1, 242, 221, 7, 0xF7E009;
			viewpoint 1, 62 , 245, 8, 0xF7E009;
			viewpoint 1, 257, 58 , 9, 0xF7E009;
			set .@zif_in, 60;
			break;
		}
		mes "[Jeepney Driver]";
		mes .@zif_in+" passenger Jeepney's location has been marked on your map.";
		next;
		break;
	}
	mes "[Jeepney Driver]";
	mes "Have a nice trip.";
	close;
}

malaya,237,240,4	script	Jeepney Driver#01	4_M_MALAYA,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif01",
		12,

		// Messages for malaya_hi checks.
		"UUrgghhhh.... I'm scared... so scared... what happened to this place?",
		"The place is still a mess... I wonder if I can operate a jeepney here...",
		"Ha ha ha Welcome. I am ^1561EAVol^000000, operating the 12 man Jeepney here.",

		// Monologues of variable lengths.
		"Is this your first time in Port Malaya?",
		"Jeepney drivers here also promote Jeepneys, so let me explain them to you.",
		"Jeepneys were first developed to transport goods quickly, but are now used as public transport.",
		"Since everybody has different tastes, you can see those with cool and elaborate decorations.",
		"Of course you don't need to drive them yourselves, but you can ride them so please feel free to do so."
	);
}

malaya,67,44,4	script	Jeepney Driver#02	4_M_MALAYA,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif02",
		12,

		"Wha... What's... Going on in that hospital...",
		"I think the moaning coming from the hospital has lessened...",
		"I'm ^1561EAChui^000000, operating a 12 man Jeepney. Hi there~",

		"Is this your first time in Port Malaya?",
		"Jeepney drivers here also promote Jeepneys, so let me explain them to you.",
		"Jeepneys were first developed to transport goods quickly, but are now used as public transport.",
		"I modified my Jeepney myself...",
		"For safety reasons there's a limit to the number of passengers, and it's safe so no worries..."
	);
}

malaya,282,129,4	script	Jeepney Driver#03	4_M_MALAYA,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif03",
		12,

		"It's not good for outsiders to have a chat... Perhaps the Mumbaki Leader may have the solution for this.",
		"Are you the adventurer who recently met Mumbaki? No wait... Then you shouldn't be here... Yes it's a ghost... Arghhhh...",
		"The name's ^1561EATop^000000, operating one of the 3 12 man Jeepneys in town.",

		"First time in town?",
		"Jeepney drivers here also promote Jeepneys, so let me explain them to you.",
		"Jeepneys were first developed to transport goods quickly, but are now used as public transport.",
		"They all look different depending on their drivers...",
		"Now it is the fame of Port Malaya."
	);
}

malaya,134,250,6	script	Jeepney Driver#04	4_M_MALAYA,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif04",
		30,

		"G...Go... Go away...",
		"Arghh... Scared the devil out of me... Go away... Shoo...",
		"^1561EABrav^000000, 30 man Jeepney driver at your service, driving as safely as I can since I get scared easily.",

		"Is this your first time in Port Malaya?",
		"Jeepneys were first developed to transport goods quickly, but are now used as public transport.",
		"The modified cars are decorated by their operators.",
		"You come to Port Malaya, you must ride a Jeepney.",
		"Ha ha ha, of course you can't drive it yourself."
	);
}

malaya,341,153,4	script	Jeepney Driver#05	4_M_MALAYA,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif05",
		30,

		"G... Ghosts in town... Hey... Do you have one on you?",
		"I think there are fewer ghosts now... The town is slightly quieter...",
		"Hi I'm ^1561EALivil^000000 operator for the 30 man Jeepney. Safety is my priority.",

		"First time in town?",
		"I'll give you a quick guide as a service.",
		"Jeepneys were first developed to transport goods quickly, but are now used as public transport.",
		"For safety reasons, Jeepneys have a limit on the number of passengers.",
		"Some carried 180 people, but there aren't any of those in Port Malaya today."
	);
}

malaya,293,290,6	script	Jeepney Driver#06	4_M_MALAYA,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif06",
		30,

		"Gu... Guards... What are the guards doing...",
		"Did the guards finally do something? Phew... What a rush...",
		"I'm ^1561EAGad^000000, the trustworthy 30 man Jeepney driver.",

		"I see you're new to this town.",
		"To guide travelers is also my task!! Let me explain.",
		"Jeepneys were goods transport vehicles, now decorated and modified with Port Malaya's new technology!!",
		"This Jeepney is my very own handiwork. Ha ha ha... It's cool right? Ha ha ha",
		"Even though the customer is king, I can't let you drive it... Ha ha ha"
	);
}

malaya,242,221,4	script	Jeepney Driver#07	4_M_MALAYA,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif07",
		60,

		"Oh no... I think the whole village is bewitched by ghosts... Maybe... I should give the Mumbaki Leader a visit...",
		"Ghost... I'm sure the Mumbaki Leader knows how to get rid of these ghosts...",
		"^1561EAHott^000000, at your service, the safest 60 man Jeepney Operator.",

		"Jeepney is the public transportation of Port Malaya.",
		"Therefore they are everywhere. And it's free! I know they all have different passenger limits, but that's no problem because they're everywhere."
	);
}

malaya,62,245,6	script	Jeepney Driver#08	4_M_MALAYA,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif08",
		60,

		"Shoo~ Be gone if you're a ghost, and go away even if you're human~ Shoo~",
		"Hey... You... Are human right? Right? Huh? Say you are... Please...",
		"Hullo, I'm ^1561EAMuyan^000000, operator of this 60 man Jeepney. Nice to meet you.",

		"Jeepneys are public transport, so safety is our priority.",
		"My Jeepney received the best class of Port Malaya, a '60 man Jeepney' certificate.",
		"Meaning!! That it is perfectly safe!! Don't you worry about the ride."
	);
}

malaya,257,58,6	script	Jeepney Driver#09	4_M_MALAYA,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif09",
		60,

		"...You're an outsider... Don't go touching anything and be careful in town...",
		"The town is still quite dangerous. Best not touch anything until it is safe.",
		"Hi there. I'm ^1561EAGramma^000000, Operator of the 60 man Jeepney of Port Malaya.",

		"Is this your first time in Port Malaya?",
		"We Jeepney drivers here also promote Jeepneys, so let me explain them to you.",
		"Jeepneys were first developed to transport goods quickly, but are now used as public transport.",
		"Since everybody have different tastes, you can see those with cool and elaborate decorations.",
		"Of course you don't need to drive them yourselves, but you can ride them so please feel free to do so."
	);
}
