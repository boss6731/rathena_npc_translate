//============================================================
//= rAthenaCN [城鎮人員]漢化腳本 
//============================================================


//===== = ======================================
//= Malangdo Town
//===== By: ================================================== 
//= Muad_Dib
//===== Current Version: ===================================== 
//= 1.4
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Malangdo Town Script
//===== Additional Comments: ================================= 
//= 1.0 First version. [Euphy]
//= 1.1 Added entrance scripts.
//= 1.2 Moved "Odgnalam" coordinates.
//= 1.3 Fixed dialogues.
//= 1.4 Dialogue edits, source iRO.
//============================================================

// Malangdo Transportation :: malangdo_go
//============================================================
-	script	Dolangmal	-1,{
	if (checkquest(5091) == -1) {
		mes "[貓綿綿]";
		mes "嘿~！人類你好呀~我是貓綿綿。哇卡卡~";
		next;
		mes "[貓綿綿]";
		mes "我是被派來接你去貓咪的樂園^A2314B綿綿島^000000的。";
		next;
		mes "[貓綿綿]";
		switch(atoi(strnpcinfo(2))) {
			case 1: mes "怎麼樣？雖然人聲鼎沸的普隆德拉也不錯啦，但你有考慮過去我們貓咪的樂園^A2314B綿綿島^000000一探究竟嗎？"; break;
			case 2: mes "與其來充斥著死腦筋魔法師的吉芬，不如去美麗的貓咪樂園^A2314B綿綿島^000000吧！怎麼樣？"; break;
			case 3: mes "雖然斐楊的空氣很不錯，但是美麗的貓咪樂園^A2314B綿綿島^000000的空氣比這裡還好噢~要不要去？"; break;
			case 4: mes "比起充滿滴答滴答嘈雜鐘聲的艾爾帕蘭，美麗又安靜的貓咪樂園^A2314B綿綿島^000000更好吧！要不要去呀？"; break;
			case 5: mes "比起又熱又颳風沙的夢羅克，吹海風的貓咪樂園^A2314B綿綿島^000000更好啦！要不要去呀？"; break;
			case 6: mes "怎麼樣？有興趣離開這讓眼睛和腦袋都疼的到處都是書的朱諾，到我們貓咪的樂園^A2314B綿綿島^000000嗎？？"; break;
			case 7: mes "怎麼樣？有興趣離開靜到嚇人的拉赫，到我們熱鬧的貓咪樂園^A2314B綿綿島^000000嗎？"; break;
			case 8: mes "怎麼樣？有興趣離開滿街充斥著黑道的裡希塔樂鎮，到我們安全的貓咪樂園^A2314B綿綿島^000000嗎？"; break;
			case 9: mes "海洋的氣味比莫拉好，充滿草香味."; break;
		}
		next;
		if(select("不要:往^A2314B綿綿島^000000出發咯~ Go~ Go~") == 1) {
			mes "[貓綿綿]";
			mes "我擦！？ 你說你不想去^A2314B綿綿島^000000？嗟！";
			next;
			mes "[貓綿綿]";
			mes "你不去的話幹嘛要跟我搭話...幹嘛煩我...不過...";
			next;
			mes "[貓綿綿]";
			mes "^9E1837湯瑪斯司令^000000要我來綁架人類.. 不是.. 是邀請人類吧？";
			next;
			mes "[貓綿綿]";
			mes "這次放你一馬！想去的話隨時告訴我喔~";
			close;
		}
		mes "[貓綿綿]";
		mes "很好！我欣賞你的熱情奔放！";
		next;
		mes "[貓綿綿]";
		mes "在你前往^A2314B綿綿島^000000之前，有幾點注意事項要提醒你。";
		next;
		mes "[貓綿綿]";
		mes "第一點！！雖然這是一定要的，但抵達^A2314B綿綿島^000000後，必須先向^9E1837湯瑪斯司令^000000報到..";
		next;
		mes "[貓綿綿]";
		mes "不然的話，你可能會在昏暗的路上遭到可怕粗暴的貓咪大哥們的攻擊。";
		next;
		mes "[貓綿綿]";
		mes "第二點！！雖然這裡開放給人類造訪，但^A2314B綿綿島^000000100%是我們貓咪的地盤！";
		next;
		mes "[貓綿綿]";
		mes "要把我們當作一個人... 喔... 不... 我們是貓嘛~要把我們當作一隻貓對待！";
		next;
		mes "[貓綿綿]";
		mes "然後是...";
		next;
		mes "明明說注意事項只有幾點的貓綿綿，滔滔不絕地說了差不多100點的注意事項。";
		next;
		mes "[貓綿綿]";
		mes "很好！以上就是注意事項，都是些很容易遵守的事...怎麼樣？你有辦法遵守嗎？";
		next;
		if(select("我可以遵守:沒辦法遵守") == 2) {
			mes "[貓綿綿]";
			mes "什麼！？連這麼簡單的規則也沒辦法遵守？嗟！";
			next;
			mes "[貓綿綿]";
			mes "如果你沒辦法遵守就早說嘛~還讓我說得那麼累...";
			next;
			mes "[貓綿綿]";
			mes "^9E1837湯瑪斯司令^000000交代說要抓個人類.. 不對.. 是邀請人類的吧？";
			next;
			mes "[貓綿綿]";
			mes "這次放你一馬！想去的話隨時告訴我喔~";
			close;
		}
		mes "[貓綿綿]";
		mes "很好！說話算話，^9E1837湯瑪斯司令^000000就在^A2314B綿綿島中央的船^000000裡。";
		next;
		mes "[貓綿綿]";
		mes "別忘記要去報到喔~";
		next;
		mes "[貓綿綿]";
		mes "好啦，我不囉嗦了。";
		mes "讓我們一起前往貓咪的樂園^A2314B綿綿島^000000吧~";
		close2;
		setquest 5091;
		completequest 5091;
		warp "malangdo",217,85;
		end;
	}
	mes "[貓綿綿]";
	mes "嗯？你不是？上次...？沒錯！";
	mes "你去^A2314B綿綿島^000000回來拉？";
	next;
	if(select("再送我去一次^A2314B綿綿島^000000吧:嗯，我回來拉！") == 1) {
		mes "[貓綿綿]";
		mes "我也很想送你去呢！但是規則不允許嘛~我光送第一次去^A2314B綿綿島^000000的人就夠累的！";
		next;
		mes "[貓綿綿]";
		mes "而且你根本沒有聽完整我說的規則嘛...第53條規則...";
		next;
		mes "[貓綿綿]";
		mes "我再特別告訴你一次第53條規則吧！誰教我是心胸很寬大的貓咪呢~";
		next;
		mes "[貓綿綿]";
		mes "如果你想再次去^A2314B綿綿島^000000的話，就去依斯魯得島或是艾爾貝塔搭船。";
		next;
		mes "[貓綿綿]";
		mes "搭上船後，一直走就會抵達我們貓咪的樂園^A2314B綿綿島^000000啦。";
		next;
		mes "[貓綿綿]";
		mes "很簡單吧？那就祝你一路順風啦~瞄";
		close;
	}
	mes "[貓綿綿]";
	mes "怎麼樣？不錯吧？我就知道！！哇卡卡~";
	next;
	mes "[貓綿綿]";
	mes "對了！我怕你不知道……";
	next;
	mes "[貓綿綿]";
	mes "如果你想再次去^A2314B綿綿島^000000的話，就得去依斯魯得島或是艾爾貝塔搭船才行。";
	next;
	mes "[貓綿綿]";
	mes "嘛~你這麼聰明一定知道吧！";
	mes "那我們下次再見啦~";
	close;
}
prontera,114,77,6	duplicate(Dolangmal)	貓綿綿#1	553
geffen,109,61,6	duplicate(Dolangmal)	貓綿綿#2	553
payon,200,106,4	duplicate(Dolangmal)	貓綿綿#3	553
aldebaran,170,104,4	duplicate(Dolangmal)	貓綿綿#4	553
morocc,168,275,4	duplicate(Dolangmal)	貓綿綿#5	553
yuno,165,122,4	duplicate(Dolangmal)	貓綿綿#6	553
rachel,121,126,4	duplicate(Dolangmal)	貓綿綿#7	553
lighthalzen,254,83,5	duplicate(Dolangmal)	貓綿綿#8	553
mora,57,150,4	duplicate(Dolangmal)	貓綿綿#9	553

//Pre-RE: izlude (182,192)
izlude,182,218,4	script	貓奧克#Izlude	554,{
	mes "貓咪曬著 "+strnpcinfo(2)+" 溫暖的太陽,睡的很香.";
	next;
	if(select("讓貓咪繼續睡，不打擾它:和貓咪說話") == 1) close;
	mes "[貓奧克]";
	mes "喵？嗯？";
	next;
	if (ep13_yong1 == 0) {
		set .@price,1000;
		set .@s1$,"哼！！原來是煩人的人類！給我^0000FF1000金幣^000000我就送你去貓咪樂園^A2314B綿綿島^000000！";
		set .@s2$,"錢呢？錢在哪裡？沒有錢你還想去哪呀？人類臉皮真厚！";
	} else if (ep13_yong1 < 60) {
		set .@price,999;
		set .@s1$,"你和貓咪商隊還蠻有緣的嘛！看在我們這麼有緣的份上特地給你打個折，給我^0000FF999金幣^000000我就送你去貓咪樂園^A2314B綿綿島^000000！";
		set .@s2$,"錢在哪裡？你怎麼一點榮譽感都沒有？";
	} else if (ep13_yong1 > 59 && ep13_yong1 < 80) {
		set .@price,500;
		set .@s1$,"你曾經給過我們貓咪一些幫助。這次你只需要給我50%的費用(^0000FF即500金幣^000000)，我就送你去貓咪樂園^A2314B綿綿島^000000！";
		set .@s2$,"簡直不敢相信，你居然沒有錢支付已經打50%折扣的傳送費？";
	} else {
		mes "[貓奧克]";
		mes "你是貓咪商隊的英雄，你的事跡是我們的傳說。";
		next;
		mes "[貓奧克]";
		mes "因為你是貓咪商隊的英雄，所以我會免費送你去貓咪樂園^A2314B綿綿島^000000。";
		next;
		if(select("出發前往綿綿島！！:不想去了...") == 1) {
			mes "[貓奧克]";
			mes "祝你有個舒適的旅程...好啦 我們出發~";
			warp "malangdo",217,85;
		}
		close;
	}
	mes "[貓奧克]";
	mes .@s1$;
	next;
	if(select("出發前往綿綿島！！:不想去了...") == 2) close;
	if (Zeny < .@price) {
		mes "[貓奧克]";
		mes .@s2$;
		close;
	}
	set Zeny, Zeny - .@price;
	mes "[貓奧克]";
	mes "現在馬上就送你過去！出發~";
	close2;
	warp "malangdo",217,85;
	end;
}
alberta,200,151,4	duplicate(貓奧克#Izlude)	貓奧克#Alberta	554

malangdo,219,86,4	script	貓豆豆#malang	545,{
	mes "[貓豆豆]";
	mes "在綿綿島玩得還開心嗎？";
	mes "商隊有提供來往依斯魯得島和艾爾貝塔的便船。";
	next;
	if (ep13_yong1 == 0) {
		set .@price,1000;
		set .@s1$, "1000金幣";
		set .@s2$, "祝你今天在綿綿島上過得愉快~";
		set .@no_money$, "搭船費用很難做到全免，請諒解~";
		set .@yes_money$, "祝你旅途愉快~";
		mes "[貓豆豆]";
		mes "搭船需要1000金幣，你的目的地是哪裡？";
		next;
	} else if (ep13_yong1 < 60) {
		set .@price,999;
		set .@s1$,"999金幣";
		set .@s2$, "祝你在綿綿島上事事順利...";
		set .@no_money$, "即使你和貓咪商隊有緣，搭船費用很難做到全免，請諒解。";
		set .@yes_money$, "謝謝，祝你旅途愉快。";
		mes "[貓豆豆]";
		mes "看在你和貓咪商隊的緣分上，特別給你打折~船費只要999金幣，你想去哪裡？";
		next;
	} else if (ep13_yong1 > 59 && ep13_yong1 < 80) {
		set .@price,500;
		set .@s1$,"500金幣";
		set .@s2$, "祝你今天在綿綿島上擁有超級好運然後還能中大獎。";
		set .@no_money$, "我的天啊。你連500金幣都沒有？搭船費用很難做到全免，請諒解。";
		set .@yes_money$, "祝你旅途愉快，歡迎下次再來。";
		mes "[貓豆豆]";
		mes "哇噢~你為我們做了很多奉獻...好吧！！ 給你特別優惠價500金幣。你想去哪裡？";
		next;
	} else {
		set .@price,0;
		set .@s1$,"英雄免費！";
		set .@s2$, "沒有人會反對貓咪商隊的英雄停留在綿綿島，絕對沒有...";
		mes "[貓豆豆]";
		mes "啊...你是貓咪商隊的英雄，你是傳奇。";
		next;
		mes "[貓豆豆]";
		mes "我們不能收取貓咪商隊英雄的費用。 尊敬的您想去哪裡？";
		next;
	}
	set .@i, select("- 往依斯魯得 --- "+.@s1$+":- 往艾爾貝塔 --- "+.@s1$+":- 留在綿綿島");
	mes "[貓豆豆]";
	if (.@i == 3) {
		mes .@s2$;
		close;
	}
	if (ep13_yong1 > 80) {
		mes "祝有趟舒適的旅行.";
		if (.@i == 1)
			warp "izlude",195,212;
		else
			warp "alberta",192,150;
		close;
	}
	if (Zeny < .@price) {
		mes .@no_money$;
		close;
	}
	set Zeny, Zeny - .@price;
	mes .@yes_money$;
	close2;
	if (.@i == 1)
		warp "izlude",195,212;
	else
		warp "alberta",192,150;
	end;
}

// Town NPCs :: malang_tre
//============================================================
malangdo,147,117,3	script	旅館老闆#malang	554,{
	mes "發現有之貓咪整個癱在椅子上.";
	mes "雖然他是老闆，但似乎一副不想做生意的模樣.";
	next;
	switch(select("我可以在吊床上休息嗎?:我要在旅館記憶位置?:你好像很閒的樣子.")) {
	case 1:
		mes "[旅館老闆]";
		mes "你只要捐出 ^FF00005個綿綿島風味罐頭^000000,就可以好好消除勞阿.";
		next;
		switch(select("給他罐頭.:地板好像也不錯.")) {
		case 1:
			if (countitem(12636) > 4) {
				delitem 12636,5; //Malang_Sp_Can
				percentheal 100,100;
				specialeffect2 EF_HEALSP;
				mes "[旅館老闆]";
				mes "放鬆享受吧.";
				emotion ET_CHUP;
				next;
				warp "malangdo",140,121;
				close;
			}
			mes "[旅館老闆]";
			mes "罐頭不夠喔.";
			close;
		case 2:
			mes "[旅館老闆]";
			mes "想不到你這麼小氣.";
			close;
		}
	case 2:
		mes "[旅館老闆]";
		mes "記憶好囉！以後我們會常見面，要相親相愛喔.";
		savepoint "malangdo",142,118;
		close;
	case 3:
		mes "[旅館老闆]";
		mes "該忙的時候就忙，該偷閒的時候就偷閒呀.";
		next;
		mes "- 不知道究竟他有忙的時候嗎？ -";
		close;
	}
	close;
}

malangdo,151,120,3	script	流浪的宮廷樂師#mal	479,{
	mes "[流浪的宮廷樂師]";
	mes "不知道為什麼看著那個旅館老闆就會一直感到睡意襲來...";
	next;
	mes "[流浪的宮廷樂師]";
	mes "渾身發懶得感覺.";
	close;
}

malangdo,149,120,5	script	流浪的浪姬舞者#mal	485,5,1,{
	mes "[流浪的浪姬舞者]";
	mes "住在這裡的貓咪都好有活力喔.";
	mes "讓我也不知不覺手舞足蹈了起來呢~";
	close;
OnTouch:
	switch(rand(5)) {
	case 0:
		soundeffect "ring_of_nibelungen.wav",0;
		break;
	case 1:
		soundeffect "dont_forget_mET_STARE_ABOUTt.wav",0;
		break;
	case 2:
		soundeffect "humming.wav",0;
		break;
	case 3:
		soundeffect "assassin_of_sunset.wav",0;
		break;
	case 4: 
		emotion ET_DELIGHT;
		emotion ET_DELIGHT, getnpcid(0, "Wandering Minstrel#mal");
		break;
	}
	end;
}

malangdo,216,168,3	script	貓咪訓練師#mal1	558,0,8,{
	emotion ET_COOL;
	mes "[貓咪訓練師]";
	mes "白白胖胖的尾巴!還有屁股!";
	mes "受不了了, 這裡簡直是天堂~~!!";
	next;
	emotion ET_KIK;
	mes "[貓咪訓練師]";
	mes "我要用我的王牌 ^FF0000<獮猴桃果實>^000000將綿綿島上的貓咪都變成我的奴隸!";
	next;
	select("^FF0000<獮猴桃果實>^000000是什麼?");
	mes "[貓咪訓練師]";
	mes "獮猴桃果實是非常珍貴的東西!";
	mes "我也是好不容易才得到的.";
	mes "而且只要是貓的話沒有一隻不愛死它的!";
	next;
	select(" ^FF0000<獮猴桃果實>^000000去哪裡買呢?");
	mes "[貓咪訓練師]";
	mes "連我都是好不容易才得到的，你竟敢想要它?";
	mes "聽說這可是連^3131FF金幣也買不到的異界之物^000000!呢";
	next;
	emotion ET_KIK;
	mes "[貓咪訓練師]";
	mes "綿綿島的貓咪全都是我的了! 只許成功不準失敗!呦呼~";
	mes "^FF0000<獮猴桃果實>^000000!我要靠著它成為綿綿島的女王~ 呵呵呵!!!";
	close;
OnTouch:
	emotion ET_SPARK;
	end;
}

malangdo,164,203,3	script	貓咪訓練師#mal2	853,{
	emotion ET_COOL;
	mes "[貓咪訓練師]";
	mes "軟綿綿的尾巴!還有屁股!~";
	mes "受不了了~!!這裡簡直是極樂天堂嘛!!";
	next;
	emotion ET_KIK;
	mes "[貓咪訓練師]";
	mes "我要用我的王牌";
	mes "^FF0000獮猴桃果實000000，";
	mes "將綿綿島上的所有貓咪，";
	mes "都變成我的奴隸~!";
	next;
	select("^FF0000獮猴桃果實000000是什麼?");
	mes "[貓咪訓練師]";
	mes "貓咪訓練師是非常珍貴的東西.";
	mes "我也是好不容易才得到的!";
	mes "而且只是貓咪的話，沒有一隻不愛死它的!";
	next;
	select("^FF0000獮猴桃果實000000去哪裡買呢?");
	mes "[貓咪訓練師]";
	mes "連我都是好不容易才得到的，你竟敢想要它?";
	mes "聽說這可是連^3131FF金幣也買不到的異界之物^000000!呢";
	next;
	mes "[貓咪訓練師]";
	mes "啊，我差點將秘密都告訴你.";
	next;
	emotion ET_KIK;
	mes "[貓咪訓練師]";
	mes "綿綿島的貓咪全都是我的了! 只許成功不準失敗!呦呼~";
	mes "^FF0000<獮猴桃果實>^000000!我要靠著它成為綿綿島的女王~ 呵呵呵!!!";
	close;
}

// Utility NPCs :: cat_merchant
//============================================================
mal_in02,134,31,5	script	投資諮詢貓	505,{
	if (MaxWeight - Weight < 100 || checkweight(1201,1) == 0) {
		mes "[投資諮詢貓]";
		mes "You have a heavy load. Maybe you're a peddler? No peddlers in this area.";
		close;
	}
	if (ep13_yong1 < 10) {
		mes "[投資諮詢貓]";
		mes "喔!";
		mes "你是來諮詢投資的嗎?";
		mes "歡迎歡迎~快請近.";
		next;
		mes "[投資諮詢貓]";
		mes "你好~我的工作是.";
		mes "協助來投資貓咪商隊的人，";
		mes "提供諮詢服務!嘻~嘻~嘻~";
		next;
		mes "[投資諮詢貓]";
		mes "因為來諮詢的人太多，沒有辦法全都記住…";
		mes "查清楚你的底細... 先等一下喔.";
		next;
		mes "聞... 聞...";
		next;
		mes "[投資諮詢貓]";
		mes "什麼?!";
		mes "你不知道貓咪商隊喔?";
		mes "沒什麼貓味兒呢~";
		next;
		mes "[投資諮詢貓]";
		mes "非常好!!";
		mes "來投資貓咪商隊是非常睿智的選擇!";
		next;
		mes "[投資諮詢貓]";
		mes "首先，要開立貓咪商隊投資帳戶需要罐頭10個.";
		next;
		while(1) {
			switch(select("開立帳戶:詢問優惠內容:離開")) {
			case 1:
				if (countitem(12636) < 10) {
					mes "[投資諮詢貓]";
					mes "嗯!";
					mes "我也想免費幫你開立帳戶呀~";
					mes "不過…";
					mes "這是要招攬商隊的投資人, 所以很抱歉.";
					close;
				}
				delitem 12636,10; //Malang_Sp_Can
				set ep13_yong1,10;
				mes "[投資諮詢貓]";
				mes "非常好!";
				mes "請在這裡寫下資料~";
				mes "並且按押你的指紋,";
				mes "阿!~你叫什麼名字?";
				next;
				mes "[投資諮詢貓]";
				mes "你是~ strcharinfo(0)+";
				next;
				mes "[投資諮詢貓]";
				mes "姓名: "+strcharinfo(0);
				mes "投資帳戶數: 10";
				mes "帳戶開立好囉!";
				next;
				mes "[投資諮詢貓]";
				mes "你現在有帳戶了.";
				mes "如果有得到罐頭，記得下次要來做個有效投資喔.";
				next;
				mes "[投資諮詢貓]";
				mes "我希望能獲得許多投資，";
				mes "這樣你也能獲得更服務。";
				mes "並且，我也希望我們貓咪商隊，能賺更多的錢.";
				next;
				mes "[投資諮詢貓]";
				mes "希望下次能見到你來投資~";
				close;
			case 2:
				mes "[投資諮詢貓]";
				mes "你想知道優惠嗎?";
				mes "其實因為我們的商隊規模還不大，所以無法提供太多你們人類所說的優惠.";
				next;
				mes "[投資諮詢貓]";
				mes "經眾多貓咪商量一番後，決定根據帳戶餘額，提供從異世界傳送至米德加耳特，";
				mes "或是記錄位置的服務，費用方面當然也會打折!";
				next;
				mes "[投資諮詢貓]";
				mes "我答應你，如果商隊得到許多投資因而得以逐漸壯大的話，那我會提供更多服務作為報答.";
				next;
				break;
			case 3:
				mes "[投資諮詢貓]";
				mes "呃~ 你要走啦?";
				mes "哎喲~ 帳戶開完在走嘛.";
				close;
			}
		}
	} else if (ep13_yong1 < 20000) {
		mes "[投資諮詢貓]";
		mes "來諮詢的人太多，沒有辦法全都記住…";
		mes "查清楚你的底細... 先等一下喔.";
		mes "非常抱歉!";
		next;
		mes "聞... 聞...";
		next;
		if (ep13_yong1 < 10000) {
			set .@cans,10;
			mes "[投資諮詢貓]";
			mes "這是如此...!!";
			mes "熟悉的氣味!!";
			next;
			mes "[投資諮詢貓]";
			mes "你有在這裡投資過!!";
			mes "非常好!~~ 很高興見到你~";
			next;
			mes "[投資諮詢貓]";
			mes "從你氣味嗅出~ 你的投資帳戶低於 10,000個.";
			mes "你可以投資10個罐頭在獲得帳戶.";
		} else {
			set .@cans,20;
			mes "[投資諮詢貓]";
			mes "這麼強烈的氣味!!";
			mes "這一定是!!";
			next;
			mes "[投資諮詢貓]";
			mes "是 VIP 貴賓!!!!!!";
			mes "你來這裡投資嗎? 歡迎!";
			next;
			mes "[投資諮詢貓]";
			mes "VIP貴賓投資擁有超過 10,000 個帳戶";
			mes "只要在20個罐頭就能在投資另一個帳戶.";
			mes "非常好的投資!";
		}
		next;
		mes "[投資諮詢貓]";
		mes "你想做什麼呢?";
		next;
		switch(select("投資:查看投資帳戶數:離開")) {
		case 1:
			mes "[投資諮詢貓]";
			mes "哇~ 感謝你的投資~";
			mes "你一次最多能投資10個帳戶.";
			mes "是不是很棒呢?";
			mes "你想投資多少帳戶呢? 輸入0取消.";
			next;
			input .@input;
			if (.@input == 0) {
				mes "[投資諮詢貓]";
				mes "請下次在來投資喔~";
				close;
			} else if (.@input > 10) {
				mes "[投資諮詢貓]";
				mes "你最多可以投資10個帳戶~";
				close;
			}
			set .@required, .@input*.@cans;
			mes "[投資諮詢貓]";
			mes "總數 " + .@input + " 個投資帳戶, 對吧?";
			mes "你要投資的罐頭數量為 "+.@required+"罐.";
			mes "你確定了嗎?";
			next;
			if(select("是的:我在考慮一下") == 2) {
				mes "[投資諮詢貓]";
				mes "阿? 不對嗎? 真奇怪.";
				mes "請確認一下吧~";
				close;
			}
			if (countitem(12636) < .@required) {
				mes "[投資諮詢貓]";
				mes "阿!";
				mes "你沒有足夠的罐頭阿.";
				mes "我很忙碌的，你知道嗎!";
				close;
			}
			if (ep13_yong1 < 10000) {
				delitem 12636,.@required; //Malang_Sp_Can
				set ep13_yong1, ep13_yong1+.@input;
				mes "[投資諮詢貓]";
				mes .@input+" 個投資帳戶.";
				mes "謝謝您的幫忙~";
				next;
				mes "[投資諮詢貓]";
				mes "一旦我們獲得更多的罐頭投資,";
				mes "我們的交易商員將會增加更多.";
				mes "你就能得到更多的服務~";
				mes "這不是雙贏的局面嗎?";
				close;
			} else {
				if (.@input + ep13_yong1 > 20000) {
					delitem 12636,.@required; //Malang_Sp_Can
					set ep13_yong1,20000;
					mes "[投資諮詢貓]";
					mes "你對多可以有 20,000 個帳戶數";
					mes "依據投資商會的政策...";
					mes strcharinfo(0)+"的帳戶數增加為 20,000.";
					mes "非常感謝你的投資~";
					next;
					mes "[投資諮詢貓]";
					mes "現在, "+strcharinfo(0)+", 你無法進一步投資";
					mes "但你仍然能捐出一些罐頭給投資商會.";
					mes "太謝謝你了~";
					close;
				} else {
					delitem 12636,.@required; //Malang_Sp_Can
					set ep13_yong1, ep13_yong1+.@input;
					mes "[投資諮詢貓]";
					mes .@input+" 已投資帳戶.";
					mes "謝謝您的幫助~";
					next;
					mes "[投資諮詢貓]";
					mes "經由你大量的投資罐頭,";
					mes "讓我們能夠早點實現目標.";
					mes "再次感謝您.";
					close;
				}
			}
		case 2:
			mes "[投資諮詢貓]";
			mes "你要查看帳戶嗎? 嗯, 我們來看一下吧~";
			next;
			mes "[投資諮詢貓]";
			mes "姓名: "+strcharinfo(0);
			mes "投資帳戶數: "+ep13_yong1;
			next;
			mes "[投資諮詢貓]";
			if (ep13_yong1 < 10000)
				mes "清楚了嗎?";
			else
				mes "你已經投資非常多了. 謝謝你.";
			close;
		case 3:
			mes "[投資諮詢貓]";
			mes "請下次在來投資~";
			close;
		}
	} else if (ep13_yong1 >= 20000) {
		mes "[投資諮詢貓]";
		mes strcharinfo(0) + ", 歡迎~";
		mes strcharinfo(0) + "~ 您的到來嗅出了一股出不尋常的氣味.";
		next;
		mes "[投資諮詢貓]";
		mes "想捐出嗎?";
		next;
		if(select("捐出:還不想") == 2) {
			mes "[投資諮詢貓]";
			mes "呵呵~ 願你在這玩的開心~";
			close;
		}
		mes "[投資諮詢貓]";
		mes "哇, 謝謝你, "+strcharinfo(0)+"~!";
		next;
		mes "[投資諮詢貓]";
		mes strcharinfo(0)+"~ with your help";
		mes "這些罐頭將會光榮的在";
		mes "米德加耳特地區分配給貓~";
		next;
		mes "[投資諮詢貓]";
		mes "可以捐贈的數量從最少的 20罐 到最多的 10,000罐.";
		next;
		input .@input;
		if (.@input < 20 || .@input > 10000) {
			mes "[投資諮詢貓]";
			mes "可以捐贈的數量從最少的 20罐 到最多的 10,000罐.";
			close;
		} else if (countitem(12636) < .@input) { // Malang_Sp_Can
			mes "[投資諮詢貓]";
			mes "嗯? 你沒有足夠的罐頭.";
			close;
		} else {
			delitem 12636,.@input; //Malang_Sp_Can
			mes "[投資諮詢貓]";
			mes "哦! 謝謝你~";
			next;
			mes "[投資諮詢貓]";
			mes "親愛的 "+strcharinfo(0)+" ~";
			mes "我只能送你加速術";
			mes "和天使之賜福.";
			mes "感謝你.";
			npcskill "AL_INCAGI",10,0,0;
			npcskill "AL_BLESSING",10,0,0;
			close;
		}
	}
}

malangdo,184,139,4	script	守倉員#mal	560,{
	mes "[守倉員]";
	mes "啊, 看你好像很重的樣子，";
	mes "你要開倉庫嗎?是否免費嗎?";
	next;
	mes "[守倉員]";
	mes "哈哈哈哈~ 開玩笑的，當然!!要付費啦!.";
	mes "我又不是慈善家~";
	next;
	if (ep13_yong1 > 999) {
		set .@cans,1;
		set .@zeny,1500;
		set .@i, select("以罐頭結帳 ----- 1個:以金幣結帳 ------- 1,500z");
	} else {
		set .@cans,2;
		set .@zeny,3000;
		set .@i, select("以罐頭結帳 ----- 2個:以金幣結帳 ------- 3,000z");
	}
	if (getskilllv("NV_BASIC") < 6) {
		mes "[守倉員]";
		mes "Ah, hold on!!";
		mes "你必需要有 ^FF0000基本技能等級 6^000000 才能使用倉庫服務喔!!";
		close;
	}
	switch(.@i) {
	case 1:
		if (countitem(12636) < .@cans) {
			mes "[守倉員]";
			mes "你沒有足夠的罐頭啦...";
			close;
		}
		delitem 12636,.@cans; //Malang_Sp_Can
		break;
	case 2:
		if (Zeny < .@zeny) {
			mes "[守倉員]";
			mes "你沒有足夠的金幣啦...";
			close;
		}
		set Zeny, Zeny-.@zeny;
		break;
	}
	mes "[守倉員]";
	mes "感謝您使用本服務.";
	close2;
	openstorage;
	end;
}
