//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================

//===== rAthena Script =======================================
//= Blessed Refiner
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Refiners that use Blessed ores to refine equipment.
//= Upon failure, the equipment is not destroyed. The success
//= rate is identical to that for Enriched ores.
//= - "Blacksmith Dister" only refines from +6~12.
//= NOTE: This NPC is currently disabled on official servers.
//===== Additional Comments: =================================
//= 1.0 First version. [Euphy]
//============================================================

// Main NPC :: new_smelting612
//============================================================
-	script	::BlacksmithDister	-1,{
	disable_items;
	mes "[鐵匠迪斯特]";
	mes "在這個競爭激烈的社會, 我們必須為了生存!";
	mes "我只負責精煉 +6 ~ 12 的裝備.";
	next;
	mes "[鐵匠迪斯特]";
	mes "我需要 ^ff9999受祝福的武器礦石^000000 來精煉武器, 或者 ^ff9999受祝福的防具礦石^000000 來精煉防具.";
	mes "精煉失敗 ^FF0000不會破壞或減少精煉級^000000.";
	next;
	mes "[鐵匠迪斯特]";
	mes "如何? 要不要精煉?";
	next;
	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for(set .@i,1; .@i<=10; set .@i,.@i+1)
		set .@menu$, .@menu$ + ( getequipisequiped(.@indices[.@i]) ? getequipname(.@indices[.@i]) : F_getpositionname(.@indices[.@i]) +" - [未裝備]" ) +":";
	set .@part, .@indices[ select(.@menu$) ];
	if (!getequipisequiped(.@part)) {
		mes "[鐵匠迪斯特]";
		switch(.@part) {
		case 1:
			mes "我是一個鐵匠, 不是美容師.";
			break;
		case 2:
			mes "我的錘子, 會讓你成為天空中的一顆明星.";
			break;
		case 3:
		case 4:
			mes "用空手製作是不是我的專長.";
			break;
		case 5:
			mes "我無法精煉脫掉的裝備!";
			break;
		case 6:
			mes "這個奇怪的氣味來自哪裡?";
			break;
		case 7:
		case 8:
			mes "裝飾品在哪裡?";
			break;
		case 9:
			mes "你想讓我精煉什麼?";
			break;
		case 10:
			mes "呼? 你想讓我怎麼做?";
			break;
		}
		close;
	}
	if (!getequipisenableref(.@part)) {
		mes "[鐵匠迪斯特]";
		mes "這個物品無法精煉.";
		close;
	}
	set .@equip_refine, getequiprefinerycnt(.@part);
	if (.@equip_refine < 6 || .@equip_refine > 12) {
		mes "[鐵匠迪斯特]";
		mes "這個裝備已經被精煉到 "+.@equip_refine+". 我只能夠精煉 +6 到 +12的裝備!";
		close;
	}
	set .@equip_lv, getequipweaponlv(.@part);
	switch(.@equip_lv) {
	default:
	case 0:
		set .@price,20000;
		set .@material,6439; //受祝福的防具礦石
		set .@type$,"防具";
		break;
	case 1:
		set .@price,1000;
		set .@material,6438; //受祝福的武器礦石
		set .@type$,"武器";
		break;
	case 2:
		set .@price,2000;
		set .@material,6438; //受祝福的武器礦石
		set .@type$,"武器";
		break;
	case 3:
		set .@price,20000;
		set .@material,6438; //受祝福的武器礦石
		set .@type$,"武器";
		break;
	case 4:
		set .@price,40000;
		set .@material,6438; //受祝福的武器礦石
		set .@type$,"武器";
		break;
	}
	set .@ore$,"^ff9999受賜福的"+.@type$+" 礦石^000000";
	mes "[鐵匠迪斯特]";
	mes "這個 "+.@type$+" 已經被精煉到 "+.@equip_refine+". 如果想繼續精煉, 我需要 "+.@ore$+" 和 "+(.@price/1000)+",000 zeny 的精煉費用.";
	mes "你想繼續?";
	next;
	if(select("- 是:- 否") == 2) {
		mes "[鐵匠迪斯特]";
		mes "我很忙, 別和我開玩笑!";
		close;
	}
	if (getequippercentrefinery(.@part) < 100) {
		mes "[鐵匠迪斯特]";
		mes "這個 "+.@type$+" 已經被多次精煉. 雖然它不會失敗時損毀, 但是 "+.@ore$+" 會消失!";
		next;
		mes "[鐵匠迪斯特]";
		mes "當然, 精煉費將不予退還! 你想繼續?";
		next;
		if(select("- 是:- 否") == 2) {
			mes "[鐵匠迪斯特]";
			mes "很好. 如果失敗我也會很傷心的.";
			close;
		}
	}
	if (countitem(.@material) == 0 || Zeny < .@price) {
		mes "[鐵匠迪斯特]";
		mes "材料不足.";
		mes "要精煉 "+((.@equip_lv)?"為等級 "+.@equip_lv+" 武器":"防具")+", 我需要 "+.@ore$+" 和 "+(.@price/1000)+",000 zeny 的精煉費用.";
		close;
	}
	delitem .@material,1;
	set Zeny, Zeny-.@price;
	mes "[鐵匠迪斯特]";
	mes "西塔! 西塔! 西塔!";
	if (getequippercentrefinery(.@part) > rand(100) || getequippercentrefinery(.@part) > rand(100)) {
		specialeffect EF_BLESSING;
		successrefitem .@part;
		next;
		mes "[鐵匠迪斯特]";
		mes "哈哈哈哈哈!!!";
		next;
		mes "[鐵匠迪斯特]";
		mes "精煉非常成功~ 哈哈哈!";
		close;
	}
	specialeffect EF_CURSEATTACK;
	specialeffect2 EF_SUI_EXPLOSION;
	next;
	emotion (!rand(5))?ET_ANGER:ET_HUK;
	mes "[鐵匠迪斯特]";
	mes "天吶!!!!";
	next;
	mes "[鐵匠迪斯特]";
	mes "噢! 雖然 "+.@ore$+" 上被賜予了祝福...";
	next;
	mes "[鐵匠迪斯特]";
	mes "當我失敗的時候心情很糟!!";
	close;
}
prt_in,52,56,3	duplicate(BlacksmithDister)	鐵匠迪斯特#prt	826
payon,148,172,3	duplicate(BlacksmithDister)	鐵匠迪斯特#pay	826
alberta_in,20,56,3	duplicate(BlacksmithDister)	鐵匠迪斯特#alb	826
yuno_in01,175,18,3	duplicate(BlacksmithDister)	鐵匠迪斯特#yuno	826
ein_in01,26,82,3	duplicate(BlacksmithDister)	鐵匠迪斯特#ein	826
lhz_in02,280,15,3	duplicate(BlacksmithDister)	鐵匠迪斯特#lhz	826
