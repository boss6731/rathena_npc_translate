//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================

//===== rAthena Script =======================================
//= Falcon & Wolf Flute Trader
//===== By: ================================================== 
//= Masao, Muad_Dib, Ziu
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Sells Falcon Flute and Wolf Flute.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Masao]
//= 1.1 Added Baby Hunter and Baby Ranger check. bugreport:5728 [Masao]
//= 1.2 Added Wolf Flute trader, optimized. [Euphy]
//============================================================

-	script	Falcon Flute Trader::fflute	-1,{
	mes "[鷹笛商人]";
	if (BaseJob == Job_Hunter || Class == Job_Ranger || Class == Job_Ranger_T || Class == Job_Baby_Ranger) {
		mes "你需要一隻獵鷹嗎?";
		mes "有個神奇的笛子, 不管你身處何處, 只要使用, 就能馬上呼喚獵鷹飛來!";
		next;
		mes "[鷹笛商人]";
		mes "是不是非常的了不起? 哈哈哈!";
		next;
		mes "[鷹笛商人]";
		mes "為何我會販售鷹笛?";
		mes "因為我不知如何使用.";
		mes "哈哈哈!";
		next;
		mes "[鷹笛商人]";
		mes "當然, 你還必須學會 ^ff0000馴鷹術^000000技能.";
		mes "我不會這項技能, 所以我無法呼喚獵鷹.";
		mes "我可能一直都沒機會能擁有獵鷹.";
		next;
		mes "[鷹笛商人]";
		mes "我無法免費給您!";
		mes "我還得賺錢生活!";
		next;
		mes "[鷹笛商人]";
		mes "如果你需要鷹笛, 販售價 12,500 zeny. 會覺得太貴嗎?";
		next;
		if (BaseJob == Job_Hunter)
			set .@price,12500;
		else {
			mes "[鷹笛商人]";
			mes "如果是遊俠的話, 我會給予優惠價格 10,000 zeny!";
			set .@price,10000;
			next;
		}
		if(select("不買:購買鷹笛") == 1) {
			mes "[鷹笛商人]";
			mes "真是遺憾. 如果你有需要, 請回來找我!";
			close;
		}
		if (Zeny >= .@price) {
			if (countitem(12848) >= 1) {
				mes "[鷹笛商人]";
				mes "你已經有鷹笛了!";
				mes "這位朋友, 別擔心, 使用鷹笛不會消耗, 一但有了就不需要再買";
				mes "鷹笛使用狀況不是非常好嗎?";
				close;
			}
			mes "[鷹笛商人]";
			mes "這, 鷹笛拿去吧.";
			mes "有了它你能在任何地方吹奏鷹笛呼喚獵鷹飛來.";
		  	set Zeny, Zeny - .@price;
			getitem 12848,1; // Falcon Flute
			next;
			mes "[鷹笛商人]";
			mes "在次提醒.";
			mes "你需要學會技能 ^ff0000馴鷹術^000000 來使用它!";
			close;
		}
		mes "[鷹笛商人]";
		mes "抱歉, 你沒有足夠的金錢, 等你有足夠的金錢在來買鷹笛!";
		close;
	}
	if (Class == Job_Baby_Archer || Class == Job_Archer) {
		mes "哈囉！年輕人!";
		mes "目前幫不上你,";
		mes "何不等你成為很棒的獵人並且學會駕馭獵鷹在回來找我?";
		close;
	}
	mes "今天是不是美好的一天?";
	close;
}
hu_in01,386,306,3	duplicate(fflute)	鷹笛商人#fft	51
pay_arche,91,134,3	duplicate(fflute)	鷹笛商人#fft2	51

tur_dun01,89,170,5	script	狼笛工匠	59,{
	if (Class == Job_Ranger || Class == Job_Ranger_T || Class == Job_Baby_Ranger) {
		if (countitem(6124)) {
			mes "[狼笛工匠]";
			mes "我能使用材料來製作狼笛.";
			mes "如果你不小心遺失了狼笛可以來找我!";
			next;
			mes "[狼笛工匠]";
			mes "狼笛僅有我能製作.";
			mes "製作狼笛對我來說是小意思!!";
			close;
		}
		mes "[狼笛工匠]";
		mes "你還需要什麼?";
		mes "一個新狼笛?";
		next;
		switch(select("請給我新的狼笛.:不需要.")) {
		case 1:
			if (countitem(7150) && Zeny >= 100000) {
				mes "[狼笛工匠]";
				mes "你已經帶齊所有材料了嗎?";
				mes "哇! 材料品質都還不錯!";
				mes "非常棒.";
				next;
				mes "[狼笛工匠]";
				mes "這是新的狼笛.";
				mes "希望你準備好使用它.";
				delitem 7150,1; //竹筒
				set Zeny, Zeny-100000;
				getitem 6124,1; //狼笛
				close;
			}
			mes "[狼笛工匠]";
			mes "如果你想製作新狼長笛你需要給我 ^4d4dff1 竹筒 和 100,000 zeny.^000000";
			next;
			mes "[狼笛工匠]";
			mes "^4d4dff竹筒 能從 釣魚河童 或 傘妖 身上獲得.^000000";
			close;
		case 2:
			mes "[狼笛工匠]";
			mes "如果不需要狼, 你就不需要狼笛.";
			mes "如果你改變了想法 , 歡迎隨時來找我.";
			mes "如何? 好好考慮ㄧ下吧?";
			close;
		}
	}
	mes "[狼笛工匠]";
	mes "啥? 你不是遊俠?";
	mes "這笛子對你沒什麼用處!";
	close;
}
