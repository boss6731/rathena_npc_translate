//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================


//===== rAthena Script =======================================
//= Advanced Refiner
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Refiner that uses Enriched ores to increase upgrade success.
//= After a conversation with Doddler, it's been established that
//= the advanced refiner works similar the the "Bubble Gum" item.
//= The success percentage is not "increased" however, if it fails
//= You get a second try. This tries twice at the same time,
//= effectively giving you a re-roll on your attempt.
//===== Additional Comments: =================================
//= 1.0 Added Malangdo Refiner "Holink". [Euphy]
//============================================================

// Main NPC :: mal_jerun
//============================================================
malangdo,221,174,6	script	霍林克#mal_cash	559,{
	disable_items;
	mes "[霍林克]";
	mes "我是 '喵~' 鐵匠霍林克~";
	mes "精煉界的大師, 霍林克~";
	mes "我是一隻特殊的貓, 師承夢羅克的精煉大師~";
	mes "我的女兒是如此為我驕傲, 霍林克~";
	mes "無所不能的霍林克~ 今天想精煉嗎?";
	next;

	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for (set .@i,1; .@i<=10; set .@i,.@i+1)
		set .@menu$, .@menu$ + ( getequipisequiped(.@indices[.@i]) ? getequipname(.@indices[.@i]) : F_getpositionname(.@indices[.@i]) +" - [未裝備]" ) +":";
	set .@part, .@indices[ select(.@menu$) ];

	if (!getequipisequiped(.@part)) {
		mes "[霍林克]";
		switch(.@part) {
		case 1:
			mes "我的師傅龍仙儒曾經說過, 喵~";
			mes "天生我才必有用...";
			break;
		case 2:
			mes "這裡沒什麼好看的, 喵!!";
			break;
		case 3:
			mes "喵? 你想讓我怎麼處理這件裝備在左手的物品...?";
			break;
		case 4:
			mes "喵? 你想讓我怎麼處理這件裝備在右手的物品...?";
			break;
		case 5:
			mes "喵? 你竟然什麼都沒穿.";
			break;
		case 6:
			mes "Kyang~! 別想瞞過我敏銳的嗅覺, 喵~.";
			break;
		case 7:
		case 8:
			mes "喵? 裝飾品在哪裡?";
			break;
		case 9:
		case 10:
			mes "喵? 你想知道頭部其他位置的精煉方法, 喵?~";
			break;
		}
		close;
	}
	if (!getequipisenableref(.@part)) {
		mes "[霍林克]";
		mes "即使我師傅龍仙儒也無法精煉這個東西, 喵.";
		close;
	}
	if (getequiprefinerycnt(.@part) >= 10) {
		mes "[霍林克]";
		mes "喵~ 完美的精煉. 龍仙儒都做不到, 喵?~";
		close;
	}
	mes "[霍林克]";
	switch(getequipweaponlv(.@part)) {
	default:
	case 0: // Armor
		set .@price,15000;
		set .@material,7619; //Enriched_Elunium
		set .@type$,"armor";
		mes "你想要精煉身上的鎧甲, 喵~";
		break;
	case 1: // Level 1 Weapon
		set .@price,500;
		set .@material,7620; //Enriched_Oridecon
		set .@type$,"weapon";
		mes "呃, 一個1級武器...?";
		break;
	case 2: // Level 2 Weapon
		set .@price,2000;
		set .@material,7620; //Enriched_Oridecon
		set .@type$,"weapon";
		mes "喵, 一個2級武器...?";
		break;
	case 3: // Level 3 Weapon
		set .@price,20000;
		set .@material,7620; //Enriched_Oridecon
		set .@type$,"weapon";
		mes "喵 喵~~ 一個3級武器~~";
		break;
	case 4: // Level 4 Weapon
		set .@price,50000;
		set .@material,7620; //Enriched_Oridecon
		set .@type$,"weapon";
		mes "咪-喵!... 一個4級武器...!";
		mes "我只見過2次這玩意.";
		mes "還是從龍師傅那裡學來的... 咪-喵!!";
		break;
	}
	mes "這次精煉你得付 ^ff9999"+getitemname(.@material)+"^000000 和 ^ff9999"+.@price+"^000000 Zeny, 喵~";
	mes "Want to continue, 喵?~";
	next;
	if(select("Yes!!:No!!") == 2) {
		mes "[霍林克]";
		mes "Kyack!!";
		mes "你竟然不相信精煉大師霍林克, 喵?~";
		close;
	}
	if (getequippercentrefinery(.@part) < 100) {
		mes "[霍林克]";
		mes "喵!!";
		if (.@type$ == "armor")
			mes "這件鎧甲已經不知道被精煉過多少次了, 喵.";
		else {
			mes "危險. 危險~";
			mes "這把武器小小的精煉過幾次, 喵~";
			next;
			mes "[霍林克]";
		}
		mes "如果你想繼續追求這件裝備的高精煉值的話.";
		mes "那很有可能會在精煉過程中飲恨, 喵.";
		next;
		mes "[霍林克]";
		mes "如果 "+.@type$+" 給精煉壞了, ";
		mes "你可以就再也用不了了, 喵. 還有... ";
		mes "^ff0000卡片和附魔^000000也會一起玩完.";
		mes "後果無法預料, 你還想繼續嘗試下去嗎, 喵~?";
		next;
		if(select("- 是的我要繼續精煉!!:- 好吧我再想想!!") == 2) {
			mes "[霍林克]";
			mes "喵! 明智的選擇, 喵.";
			mes "不過!!";
			mes "我對於你如此輕視我精煉大師霍林克而提出抗議, 喵~";
			close;
		}
	}
	if (countitem(.@material) == 0 || Zeny < .@price) {
		mes "[霍林克]";
		mes "你的材料沒帶夠啊.";
		mes "你需要為此支付 ^ff9999"+getitemname(.@material)+"^000000 和 ^ff9999"+.@price+"^000000 Zeny, 喵~";
		mes "搞定這些再來吧, 喵~";
		close;
	}
	delitem .@material,1;
	set Zeny, Zeny-.@price;
	if (getequippercentrefinery(.@part) > rand(100) || getequippercentrefinery(.@part) > rand(100)) {
		successrefitem .@part;
		mes "[霍林克]";
		mes "咪~ 咪~ 喵! 快樂 興奮 精煉中~";
		next;
		emotion ET_CHUP;
		mes "[霍林克]";
		mes "完美!! 無敵, 喵!!";
		mes "我是龍仙儒的高徒~";
		mes "霍林克!!";
		mes "又是一個成功的精煉日, 喵!!";
		close;
	}
	failedrefitem .@part;
	mes "[霍林克]";
	mes "喵~ 喵~ Kyaaak!!";
	next;
	switch(rand(1,5)) {
		case 1: emotion ET_CRY; break;
		case 2: emotion ET_PROFUSELY_SWEAT; break;
		case 3: emotion ET_KEK; break;
		case 4: emotion ET_SCRATCH; break;
		case 5: emotion ET_BIGTHROB; break;
	}
	mes "[霍林克]";
	mes "喵!! Aaaaakk~~!!!!";
	mes "Kyaaak!! 我失敗了, 喵!!";
	next;
	mes "[霍林克]";
	mes "......";
	mes "......";
	mes "所有~ 一切~ 都玩完了, 喵...";
	next;
	mes "[霍林克]";
	mes "喵.... 我師傅龍仙儒曾經說過.";
	mes "失敗乃成功之母...";
	mes "人類, 要記住, 這次失敗會是你今後成功的開始.";
	close;
}
