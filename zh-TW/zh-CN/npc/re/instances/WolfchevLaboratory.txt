//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================


//===== rAthena Script =======================================
//= Wolfchev's Laboratory
//===== By: ==================================================
//= Kisuka
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Defeat Wolfchev's human experimentations.
//===== Additional Comments: =================================
//= 1.0 First version, edited. [Euphy]
//= 1.1 Moved the Enchant (Sorcerer#Bio4Reward) and Reward
//=		(Weird old man#Bio4Reward) NPC in merchant folder. [Capuche]
//============================================================

// Giacomo Girolam
//============================================================
lighthalzen,303,303,6	script	賈科莫吉羅拉莫#kiup2	4_M_KHMAN,3,3,{
	if ((MaxWeight - Weight) < 1000 || !checkweight(1201, 1)) {
		mes "^FF0000 檢查你的包裹, 可能太重或者道具已滿. ^000000";
		close;
	}
	if (lhz_boss > 30) {
		if (!lght_duk01) {
			if (Sex) {
				emotion ET_HNG, "Giacomo Girolamo#kiup2";
				mes "[賈科莫吉羅拉莫]";
				mes "....一個人....?";
				next;
				mes "[賈科莫吉羅拉莫]";
				mes "啊!嘿!男人!";
				next;
				select("這是怎麼回事?");
				emotion ET_QUESTION, 1;
				mes "[賈科莫吉羅拉莫]";
				mes "你有沒有聽說過的可怕的人體實驗, 其總部設在裡希塔樂鎮";
				next;
				select("什麼?人體實驗!!");
				emotion ET_HUK, 1;
				mes "[賈科莫吉羅拉莫]";
				mes "是啊!人體實驗!";
				mes "有很多傳言說實際上是進行可怕的人體實驗, 但這些人在貧民窟....真不敢相信";
				next;
				emotion ET_PROFUSELY_SWEAT, "Giacomo Girolamo#kiup2";
				mes "[賈科莫吉羅拉莫]";
				mes "好吧!而那些有錢的人, 似乎知道某些事情, 但他們從來沒有提及";
				next;
				mes "[賈科莫吉羅拉莫]";
				mes "啊!這是一個秘密, 只是有些人知道.";
				next;
				emotion ET_SCRATCH, "Giacomo Girolamo#kiup2";
				mes "[賈科莫吉羅拉莫]";
				mes "反正!名為沃爾夫科學家..突然被解僱了.他必須與這個傳聞有關, 我覺得...";
				next;
				select("你的意思是...... 狼騎士?");
				emotion ET_SURPRISE, "Giacomo Girolamo#kiup2";
				mes "[賈科莫吉羅拉莫]";
				mes "哦, 是的!狼騎士!這個名字是狼騎士..瘋狂的科學家..";
				next;
				emotion ET_OK, "Giacomo Girolamo#kiup2";
				mes "[賈科莫吉羅拉莫]";
				mes "我想, 那麼快你就可以明白故事, 我相信他參與了謠言.我發誓.";
				next;
				mes "[賈科莫吉羅拉莫]";
				mes "順便說一句......你呢?這有什麼, 你知道他嗎?";
				next;
				if (select("假裝你什麼都不知道", "告訴他你知道的.") == 1) {
					mes "["+ strcharinfo(0) +"]";
					mes "我聽說過他的名字, 但我不知道他什麼..";
					next;
					mes "[賈科莫吉羅拉莫]";
					mes "嗯....你聽起來可疑, 夥計!是吧?";
					next;
				} else {
					mes "["+ strcharinfo(0) +"]";
					mes "他偏偏去研究生命體瞞著公司, 然後有一個人告訴給了公司, 所以他被解僱了.";
					next;
					emotion ET_BEST, "Giacomo Girolamo#kiup2";
					mes "[賈科莫吉羅拉莫]";
					mes "哦!男人..!你似乎知道相當多!";
					next;
					emotion ET_THINK;
					mes "["+ strcharinfo(0) +"]";
					mes "... .... ...";
					next;
				}
				emotion ET_SCRATCH, "Giacomo Girolamo#kiup2";
				mes "[賈科莫吉羅拉莫]";
				mes "不要擔心.我並沒有什麼惡意.";
				next;
				mes "[賈科莫吉羅拉莫]";
				mes "好吧!我只是好奇, 只是因為有些人在貧民窟和一些冒險家失蹤,";
				mes "而且我聽說Rekenber在做人體實驗與那些失蹤的人.希望你不要誤會我的意思.";
				next;
				lght_duk01 = 1;
				setquest 5109;
				mes "其實, 這是不是一個有趣的傳聞.";
				mes "應該調查那裡.";
				close;
			} else {
				emotion ET_THROB, "Giacomo Girolamo#kiup2";
				mes "[賈科莫吉羅拉莫]";
				mes "哦!多麼美麗的小姐!";
				next;
				emotion ET_QUESTION, "Giacomo Girolamo#kiup2";
				mes "[賈科莫吉羅拉莫]";
				mes "為什麼一個美女在這裡";
				next;
				switch(select("散步..", "外出..", "購物..")) {
				case 1:
					mes "[賈科莫吉羅拉莫]";
					mes "啊......夫人, 你是那麼的優雅.....";
					next;
					break;
				case 2:
					mes "[賈科莫吉羅拉莫]";
					mes "小姐, 即使你在貧民窟也是一個高端淑女...";
					next;
					break;
				case 3:
					mes "[賈科莫吉羅拉莫]";
					mes "啊...夫人...在貧民窟購物!!這裡有像樣的商店嗎?";
					next;
					break;
				}
			}
			select("... .... ... ....");
			mes "[賈科莫吉羅拉莫]";
			mes "啊, 我怎麼敢對這麼高端的淑女感興趣呢?";
			next;
			emotion ET_OK, "Giacomo Girolamo#kiup2";
			mes "[賈科莫吉羅拉莫]";
			mes "我只希望你相信這個小鎮的危險傳言..";
			next;
			select("危險的謠言?");
			emotion ET_QUESTION, 1;
			mes "[賈科莫吉羅拉莫]";
			mes "啊......夫人可能沒有聽說過的傳聞.";
			next;
			mes "[賈科莫吉羅拉莫]";
			mes "最近有人從這個小鎮, 實際上是從貧民窟......和一些冒險家失蹤...";
			next;
			mes "[賈科莫吉羅拉莫]";
			mes "然後謠言就上來了, 因為失蹤案件發生相當頻繁, 傳言說有人可能在進行可怕的人體實驗";
			next;
			select("你說什麼?!人體實驗?");
			emotion ET_HUK, 1;
			mes "[賈科莫吉羅拉莫]";
			mes "是啊!人體實驗!";
			mes "有很多傳言說實際上是進行可怕的人體實驗, 但這些人在貧民窟....";
			next;
			mes "[賈科莫吉羅拉莫]";
			mes "好吧!有錢的人要麼不願相信..我想......啊!前幾個星期.嗯.他叫什麼名字?狼騎士?";
			next;
			mes "[賈科莫吉羅拉莫]";
			mes "反正!!他被實驗室開除了!";
			next;
			mes "[賈科莫吉羅拉莫]";
			mes "所以, 小姐.請不要在這樣的地方";
			next;
			lght_duk01 = 1;
			setquest 5109;
			mes "............ 這似乎是一個相當嚴重的問題.";
			mes "應該調查那裡的實驗";
			close;
		} else {
			mes "[賈科莫吉羅拉莫]";
			if (Sex) {
				emotion ET_WRAP, "Giacomo Girolamo#kiup2";
				mes "好好照顧自己";
			} else {
				emotion ET_BIGTHROB, "Giacomo Girolamo#kiup2";
				mes "要小心, 夫人.";
			}
			close;
		}
	} else {
		if (Sex) {
			emotion ET_DELIGHT, "Giacomo Girolamo#kiup2";
			mes "[賈科莫吉羅拉莫]";
			mes "只有那些美麗的女士是我生命的理由~!";
			mes "我, 賈科莫吉羅拉莫從來都對女生相當有興趣!!";
			next;
			emotion ET_FRET, "Giacomo Girolamo#kiup2";
			mes "[賈科莫吉羅拉莫]";
			mes "但我可以聽你的......你想說什麼";
			next;
			switch(select("沒什麼..?", "關於女生..", "你這個好色之徒", "羨慕你...", "有人闢謠嗎?")) {
			case 1:
				emotion ET_SCRATCH, "Giacomo Girolamo#kiup2";
				mes "[賈科莫吉羅拉莫]";
				mes "嘿, 你, 走自己的路..";
				close;
			case 2:
				emotion ET_FRET, "Giacomo Girolamo#kiup2";
				mes "[賈科莫吉羅拉莫]";
				mes "我不喜歡談論女生.";
				close;
			case 3:
				mes "[賈科莫吉羅拉莫]";
				mes "哎..對不起!但我不是一個'好色之徒', 我只是一個崇拜女性的人";
				next;
				emotion ET_OK, "Giacomo Girolamo#kiup2";
				mes "[賈科莫吉羅拉莫]";
				mes "當然.....是啊...如果我是一個好色之徒, 地球上所有的男人都是!.";
				close;
			case 4:
				emotion ET_QUESTION, "Giacomo Girolamo#kiup2";
				mes "[賈科莫吉羅拉莫]";
				mes "哈啊......你...... ?你沒有姐妹或者母親嗎?";
				next;
				mes "[賈科莫吉羅拉莫]";
				mes "... .... ...";
				next;
				emotion ET_PROFUSELY_SWEAT, "Giacomo Girolamo#kiup2";
				mes "[賈科莫吉羅拉莫]";
				mes "哦......對不起......";
				close;
			case 5:
				mes "[賈科莫吉羅拉莫]";
				mes "你......是不是喜歡你怎麼想?有意思.";
				next;
				mes "[賈科莫吉羅拉莫]";
				mes "啊, 其實最近有很多來自貧民窟的人, 有些冒險家失蹤了, 我不知道為什麼.";
				next;
				mes "[賈科莫吉羅拉莫]";
				mes "你得小心";
				close;
			}
		} else {
			emotion ET_QUESTION, "Giacomo Girolamo#kiup2";
			mes "[賈科莫吉羅拉莫]";
			mes "嘿, 小姐?";
			next;
			mes "[賈科莫吉羅拉莫]";
			switch(rand(1,10)) {
			case 1:
				mes "是不是很痛苦, 當你愛上從天上降下來, 我的天使?";
				next;
				break;
			case 2:
				mes "你一定很累了.因為你總是徘徊我的腦海裡.";
				next;
				break;
			case 3:
				mes "你是不是昨晚在我的夢裡走來走去?";
				next;
				break;
			case 4:
				mes "對不起.但我覺得我失去了我的心臟.能否請你借我你的嗎?";
				next;
				break;
			case 5:
				mes "你相信一見鍾情?或者, 我們可以重新開始..?";
				next;
				break;
			case 6:
				mes "我很抱歉, 但是, 你可以親吻一個剛認識的人?是嗎?那好吧, 讓我介紹一下自己.";
				next;
				break;
			case 7:
				mes "我在這裡失去了第一次, 你知道嗎?";
				next;
				break;
			case 8:
				mes "請給我一張地圖, ..我談過你的眼睛.";
				next;
				break;
			case 9:
				mes "你的眼睛....我想永遠在那裡.";
				next;
				break;
			case 10:
				mes "我承諾, 我會成為你的一切在任何情況下, 在任何困難的情況.";
				next;
				break;
			}
			emotion ET_BIGTHROB, "Giacomo Girolamo#kiup2";
			mes "[賈科莫吉羅拉莫]";
			mes "今天是我們相識的第一天, 但是我愛你, 夫人.";
			next;
			mes "[賈科莫吉羅拉莫]";
			mes "而且我告訴你, 因為我只是擔心你.你需要走出這個充滿失蹤案件的小鎮.";
			close;
		}
	}

OnTouch:
	if (!lght_duk01)
		emotion (Sex ? ET_HNG : ET_BIGTHROB), "Giacomo Girolamo#kiup2";
	end;
}

// Warp portal to the 4th Fl.
//============================================================
lhz_dun03,239,78,1	script	lhz_dun03_lhz_dun04	WARPNPC,1,1,{
	end;

OnTouch:
	if (lght_duk01 > 0 && lght_duk01 < 6) {
		mes "為了研究人體實驗, 我不得不走下來, 而我不想因為可怕的聲音";
		close2;
		if (lght_duk01 < 3) {
			warp "que_lhz", 245, 56;
		} else if (lght_duk01 == 3) {
			warp "que_lhz", 96, 136;
		} else {
			warp "que_lhz", 148, 215;
		}
	} else {
		if (lhz_boss < 31) {
			mes "- 呼嘯聲 -";
			mes "從下面傳來, 可怕的風聲.";
			close2;
		}
		warp "lhz_dun04", 245, 56;
	}
	end;
}

// Warp Portal to the 3rd Fl.
//============================================================
lhz_dun04,244,61,1	warp	lhz_dun04_lhz_dun03	1,1,lhz_dun03,240,75

// Inside the Lab
//============================================================
que_lhz,242,50,0	script	#01Startpoint	-1,3,3,{
	end;

OnInit:
	initnpctimer;
	end;

OnTimer60000:
	mapannounce "que_lhz", "男人的聲音：請..請..救救我..我不想死..", bc_map, "0x7DCBF0";
	end;

OnTimer120000:
	mapannounce "que_lhz", "女人的聲音: .. 救命.. 啊啊.. 我們...", bc_map, "0x7DCBF0";
	end;

OnTimer180000:
	mapannounce "que_lhz", "孩子的呢喃: 爸爸爸~ 我在哪裡? 媽媽......啊啊啊啊....", bc_map, "0x7DCBF0";
	end;

OnTimer240000:
	mapannounce "que_lhz", "老人的聲音: 咳咳...幫助...幫助我......", bc_map, "0x7DCBF0";
	end;

OnTimer300000:
	stopnpctimer;
	initnpctimer;
	end;

OnTouch:
	if (lght_duk01 == 1) {
			mes "我到了一個研究設施, 但這裡似乎有些時日沒有人了.";
		next;
			mes "也有一些被破壞的部分, 但基本設施良好.";
		next;
		lght_duk01 = 2;
			mes "還是避開一點吧";
		close;
	}
	end;
}

que_lhz,206,74,4	script	研究員#02	4_LGTSCIENCE,3,3,{
	end;

OnTouch:
	if (lght_duk01 == 2) {
		emotion ET_HUK, 1;
		mes "["+ strcharinfo(0) +"]";
		  mes "你!!!是查夫?";
		next;
		emotion ET_KIK, "Researcher#02";
		  mes "[查夫]";
		  mes "一個人類!哈哈哈, 人們往往討厭這個地方, 你為什麼在這裡?";
		next;
		mes "["+ strcharinfo(0) +"]";
		  mes "你!我聽說你進行了人體實驗!!";
		next;
		mes "["+ strcharinfo(0) +"]";
		  mes "你竟然還在進行著!!";
		next;
		emotion ET_KIK, "Researcher#02";
		  mes "[查夫]";
		  mes "我可以看到, 你知道我的一些事.";
		  mes "你怎麼還敢過來";
		next;
		  mes "[查夫]";
		  mes "我覺得我很看好你";
		  mes "在很長一段時間, 但我得走了.";
		next;
		  mes "- 查夫跑開了. -";
		next;
		erasequest 5109;
		setquest 5110;
		lght_duk01 = 3;
		emotion ET_HUK, 1;
		mes "["+ strcharinfo(0) +"]";
		  mes "哇!查夫, 停下來!";
		close2;
		warp "que_lhz", 96, 136;
	}
	end;
}

que_lhz,94,119,4	script	研究員#03	4_LGTSCIENCE,{
	if ((MaxWeight - Weight) < 1000 || !checkweight(1201, 1)) {
		mes "^FF0000 無法完成任務因為你的東西太多或者已滿. ^000000";
		close;
	}
	if (lght_duk01 == 3) {
		emotion ET_PROFUSELY_SWEAT, "Researcher#03";
		mes "[查夫]";
		mes "(喘氣) 算了吧......你還是跟著我.嘿, 為什麼你們會不停跟著我?";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "查夫.你之前說的.你正在做為了大家好的研究.";
		next;
		mes "[查夫]";
		mes "我可能會說....等什麼呢?";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "但是, 您的實驗看起來真的很危險.怎麼來的, 這是每個人都想要的東西?";
		next;
		emotion ET_KIK, "Researcher#03";
		mes "[查夫]";
		mes "人人有更強的願望, 並希望支配別人, 所以與其服從他人還不如自己做, 難道你沒有這樣的欲望嗎?";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "... .... ...";
		next;
		mes "[查夫]";
		mes "如果你還想留著自己的命就不要跟著我了.";
		next;
		mes "查夫再次用一種奇怪的聲音逃跑.";
		next;
		lght_duk01 = 4;
		mes "["+ strcharinfo(0) +"]";
		mes "哇嗚...嗯!你!停下!";
		close2;
		warp "que_lhz", 148, 215;
	}
	end;
}

que_lhz,147,224,4	script	研究員#04	4_LGTSCIENCE,{
	if ((MaxWeight - Weight) < 1000 || !checkweight(1201, 1)) {
		mes "^FF0000 無法完成任務因為你的東西太多或者已滿. ^000000";
		close;
	}
	if (lght_duk01 == 4) {
		mes "[查夫]";
		mes "你很執著, 不是嗎?或者我想.......你喜歡我, 是吧?.";
		next;
		emotion ET_THINK;
		mes "["+ strcharinfo(0) +"]";
		mes "... .... ...";
		next;
		specialeffect EF_SIGHTRASHER;
		mes "查夫嘲笑你, 按下一個按鈕";
		next;
		for(.@i = 1; .@i < 9; .@i++)
			donpcevent "人類豚鼠#0"+.@i+"::OnEnable";
		mes "[查夫]";
		mes "再次..是時候說再見了.";
		next;
		emotion ET_SURPRISE, "Researcher#04";
		mes "[查夫]";
		mes "好吧!!或許....";
		next;
		mes "[查夫]";
		mes "如果你來我的實驗室, 我可以更好地對待你.";
		next;
		donpcevent "#Eventctrl::OnEnable";
		progressbar "FF00FF", 5;
		emotion ET_HUK, "Researcher#04";
		mes "[查夫]";
		mes "哦, 我,  ......發生什麼事了?";
		next;
		donpcevent "#Eventctrl::OnEnable2";
		for(.@i = 1; .@i < 9; .@i++)
			donpcevent "人類豚鼠#0"+.@i+"::OnDisable";
		mes "[查夫]";
		mes "不!不要來找我!別!!";
		next;
		lght_duk01 = 5;
		mes "首先, 我得處理這些豚鼠!";
		close;
	} else if (lght_duk01 == 5) {
		.@i = rand(1,3);
		if (.@i == 2) {
			mes "查夫是一頭霧水, 不知道現在因為那些怪物正在攻擊他了.";
			next;
			specialeffect EF_STUNATTACK;
			specialeffect EF_STUNATTACK;
			mes "- 打他把他叫醒 -";
			next;
			specialeffect EF_DARKBREATH;
			emotion ET_STARE_ABOUT;
			mes "["+ strcharinfo(0) +"]";
			mes "嘿, 老兄!無論如何, 首先讓我們離開這裡!";
			next;
			mes "[查夫]";
			mes "我....我的實驗室....我的實驗室...";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "您的實驗室?好了, 在哪呢?";
			next;
			mes "[查夫]";
			mes "在北面!";
			next;
			erasequest 5110;
			setquest 5111;
			lght_duk01 = 6;
			mes "去'^0000FF查夫的實驗室^000000' 在12點鐘方向...";
			close;
		} else {
			emotion ET_CONFUSE, "Researcher#04";
			mes "查夫是一頭霧水, 不知道現在因為那些怪物正在攻擊他了";
			close;
		}
	} else if (lght_duk01 == 6) {
		mes "去'^0000FF查夫的實驗室^000000'在12點鐘方向";
		close;
	}
	end;
}

// Warp to lhz_dun04 from Wolfchev's Lab
//============================================================
que_lhz,148,251,0	script	#Move2lab	-1,12,0,{
	end;

OnTouch:
	if (lght_duk01 == 6) {
			mes "我到了'^0000FF查夫的實驗室^000000'.";
		close2;
		warp "lhz_dun04", 148, 269;
		end;
	} else {
			mes "我和查夫還有點事情";
		close;
	}
}

// Event Controller
//============================================================
que_lhz,1,1,0	script	#Eventctrl	-1,{
	end;

OnEnable:
	initnpctimer;
	end;

OnEnable2:
	monster "que_lhz", 139, 232, "人類豚鼠", 2244, 1, "#Eventctrl::OnMyMobDead";
	monster "que_lhz", 147, 232, "人類豚鼠", 2244, 1, "#Eventctrl::OnMyMobDead";
	monster "que_lhz", 156, 232, "人類豚鼠", 2244, 1, "#Eventctrl::OnMyMobDead";
	monster "que_lhz", 156, 224, "人類豚鼠", 2244, 1, "#Eventctrl::OnMyMobDead";
	monster "que_lhz", 156, 215, "人類豚鼠", 2244, 1, "#Eventctrl::OnMyMobDead";
	monster "que_lhz", 147, 215, "人類豚鼠", 2244, 1, "#Eventctrl::OnMyMobDead";
	monster "que_lhz", 139, 215, "人類豚鼠", 2244, 1, "#Eventctrl::OnMyMobDead";
	monster "que_lhz", 139, 224, "人類豚鼠", 2244, 1, "#Eventctrl::OnMyMobDead";
	end;

OnTimer1000:
	specialeffect EF_SUI_EXPLOSION,AREA, "#01";
	specialeffect EF_BEGINASURA, "人類豚鼠#01";
	end;

OnTimer2000:
	specialeffect EF_SUI_EXPLOSION,AREA, "#02";
	specialeffect EF_BEGINASURA, "人類豚鼠#02";
	end;

OnTimer3000:
	specialeffect EF_SUI_EXPLOSION,AREA, "#03";
	specialeffect EF_BEGINASURA, "人類豚鼠#03";
	end;

OnTimer4000:
	specialeffect EF_SUI_EXPLOSION,AREA, "#04";
	specialeffect EF_BEGINASURA, "人類豚鼠#04";
	end;

OnTimer5000:
	specialeffect EF_SUI_EXPLOSION,AREA, "#05";
	specialeffect EF_BEGINASURA, "人類豚鼠#05";
	end;

OnTimer6000:
	specialeffect EF_SUI_EXPLOSION,AREA, "#06";
	specialeffect EF_BEGINASURA, "人類豚鼠#06";
	end;

OnTimer7000:
	specialeffect EF_SUI_EXPLOSION,AREA, "#07";
	specialeffect EF_BEGINASURA, "人類豚鼠#07";
	end;

OnTimer8000:
	specialeffect EF_SUI_EXPLOSION,AREA, "#08";
	specialeffect EF_BEGINASURA, "人類豚鼠#08";
	stopnpctimer;
	end;

OnMyMobDead:
	if (mobcount("que_lhz", "#Eventctrl::OnMyMobDead") < 4)
		killmonster "que_lhz", "#Eventctrl::OnMyMobDead";
}
que_lhz,139,232,0	script	#01	-1,{ end; }
que_lhz,147,232,0	script	#02	-1,{ end; }
que_lhz,156,232,0	script	#03	-1,{ end; }
que_lhz,156,224,0	script	#04	-1,{ end; }
que_lhz,156,215,0	script	#05	-1,{ end; }
que_lhz,147,215,0	script	#06	-1,{ end; }
que_lhz,139,215,0	script	#07	-1,{ end; }
que_lhz,139,224,0	script	#08	-1,{ end; }

// Human Guinea pigs
//============================================================
que_lhz,140,231,1	script	人類豚鼠#01	2224,{
	end;

OnInit:
	disablenpc strnpcinfo(0);
	end;

OnEnable:
	enablenpc strnpcinfo(0);
	end;

OnDisable:
	specialeffect EF_FLASHER;
	disablenpc strnpcinfo(0);
	end;
}
que_lhz,147,231,0	duplicate(人類豚鼠#01)	Human Guinea pig#02	2224
que_lhz,155,231,7	duplicate(人類豚鼠#01)	Human Guinea pig#03	2224
que_lhz,155,224,6	duplicate(人類豚鼠#01)	Human Guinea pig#04	2224
que_lhz,155,216,5	duplicate(人類豚鼠#01)	Human Guinea pig#05	2224
que_lhz,147,216,4	duplicate(人類豚鼠#01)	Human Guinea pig#06	2224
que_lhz,140,216,3	duplicate(人類豚鼠#01)	Human Guinea pig#07	2224
que_lhz,140,224,2	duplicate(人類豚鼠#01)	Human Guinea pig#08	2224

// Instance Creation
//============================================================
lhz_dun04,151,276,3	script	研究員#memo	4_LGTSCIENCE,{
	if ((MaxWeight - Weight) < 1000 || !checkweight(1201, 1)) {
		mes "^FF0000無法繼續任務因為你身上太重或者東西太多.^000000";
		close;
	}
	.@quest_time = checkquest(5112, PLAYTIME);
	.@killed_bosses = true;
	for(.@quest_id = 5113; .@quest_id <= 5125; .@quest_id++) {
		if (checkquest(.@quest_id, HUNTING) <= 1)
			.@killed_bosses = false;
	}
	if (lght_duk01 < 6) {
		mes "[研究員]";
		mes "什麼!你有什麼事情嗎?";
		next;
		if (select("我有!!", "呃, 沒有") == 1) {
			mes "[研究員]";
			mes "為什麼不停的盯著對方的實驗室";
			close;
		}
		mes "[研究員]";
		mes "不要在這裡停留.";
		close;
	} else if (lght_duk01 == 6) {
		mes "看來實驗動物不能走近實驗室, 就像查夫說的";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "嘿, 有沒有辦法阻止這些生物?你應該知道的東西, 你做這些事情!";
		next;
		mes "[查夫]";
		mes "我沒想到他們失去控制";
		next;
		mes "[查夫]";
		mes "是什麼失誤導致了這些事?";
		next;
		mes "[查夫]";
		mes "我想他們會毀了我的整個實驗室!";
		next;
		emotion ET_BLABLA, 1;
		mes "["+ strcharinfo(0) +"]";
		mes "嘿! 查夫!!";
		next;
		mes "[查夫]";
		mes "你!?嘿, 請阻止他們!";
		mes "生物是^9f6077與相同工作領域有機地連接起來^000000.";
		next;
		mes "[查夫]";
		mes "有一個特殊的生物是他們的 ^46B951老大^000000...";
		mes "如果你可以殺死該生物, 所有的^A1BB44生物^000000 走在這裡會停止移動的時候了";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "老大?";
		mes "嘿!查夫!^46B951老大^000000在哪裡?";
		next;
		mes "[查夫]";
		mes "實驗室裡面, 應該現在睡醒.這是可悲的, 破壞了我的工作.. ...";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "我怎樣才能獲得進入查夫的實驗室?";
		next;
		mes "[查夫]";
		mes "噢, 等等!";
		mes "我會幫你進入的";
		next;
		mes "[查夫]";
		mes "好消息是, 老大的手下不會一次都醒來.";
		next;
		erasequest 5111;
		lght_duk01 = 7;
		for(.@i = 5113; .@i <= 5125; .@i++)
			setquest .@i;
		mes "[查夫]";
		mes "但是, 它仍然是非常危險的, 因為我們不知道他們什麼時候會逃離那裡";
		mes "所以, 請準備好你的隊伍, 把這些生物殺死, 我不知道這個地方能堅持多久.";
		close;
	} else if (lght_duk01 > 6) {
		if (getcharid(1) > 0) {
			mes "[查夫]";
			mes "就這些人了嗎?如果你是隊長, 你可以選擇進入了.";
			next;
			switch(select("有何提示?", "進入實驗室", "不想進入")) {
			case 1:
				mes "[查夫]";
				mes "我不知道按動緊急報警會導致這樣的問題.管理系統似乎被改變為緊急模式.";
				next;
				mes "[查夫]";
				mes "有這麼多的系統設置實驗室裡面, 所以之前我不知道會造成什麼後果.";
				next;
				mes "[查夫]";
				mes "啊!如果緊急避險系統激活了, 隊長應該有能力領導一切, 這壓力會很大, 明白了嗎?";
				next;
				mes "[查夫]";
				mes "只是讓你知道, 請不要出於好奇的碰任何東西, 我不想你受到傷害.";
				close;
			case 2:
				if (.@quest_time == 0) {
					mes "[查夫]";
					mes "我很抱歉, 但似乎仍無法進入實驗室呢.下次再來吧.";
					close;
				} else {
					//if (.@quest_time == 2)
						//recall_completequest 5112;
					if (isbegin_quest(5112))
						erasequest 5112;
					if (!.@killed_bosses) {
						if (lght_duk01 == 9) {
							mes "[查夫]";
							mes "你回來的正是時候, 哈哈哈";
							next;
							mes "[查夫]";
							mes "你走了以後, 我修復了一些系統部件.然後突然奇怪的事情發生了.";
							next;
							mes "["+ strcharinfo(0) +"]";
							mes "那是什麼?";
							next;
							mes "[查夫]";
							mes "那些老大魔物開始更新換代了.";
							next;
							mes "["+ strcharinfo(0) +"]";
							mes "什麼......這怎麼可能...... ?";
							next;
							mes "[查夫]";
							mes "我也徹底失去了在這裡.但是, 這不應該發生..";
							next;
							mes "["+ strcharinfo(0) +"]";
							mes "... .... ...";
							next;
							mes "[查夫]";
							mes "正如你所看到的, 你是我可以信任的唯一的一個.請問您和您的同伴能再次幫助我嗎?";
							next;
							if(select("我不知道", "為什麼不呢") == 1) {
								mes "[查夫]";
								mes "我明白.我明白為什麼你還在猶豫, 我不想強迫你.但是, 我仍然不知道為什麼你想要去那裡.";
								next;
							} else {
								for(.@i = 5113; .@i <= 5125; .@i++) {
									setquest .@i;
								}
								lght_duk01 = 10;
								mes "[查夫]";
								mes "非常感謝你. "+strcharinfo(PC_NAME)+"";
								mes "我會幫助你的.";
								next;
							}
						} else {
							mes "[查夫]";
							mes "嗯..這些老闆生物再次再生..";
							next;
							mes "[查夫]";
							mes "你能不能再幫助我一次呢?";
							next;
							if (select("我不知道", "為什麼不呢") == 1) {
								mes "[查夫]";
								mes "我明白.我明白為什麼你還在猶豫, 我不想強迫你.但是, 我仍然不知道為什麼你想要去那裡.";
								next;
							} else {
								for(.@i = 5113; .@i <= 5125; .@i++) {
									setquest .@i;
								}
								lght_duk01 = 10;
								mes "[查夫]";
								mes "謝謝你. "+strcharinfo(0)+"";
								mes "你想要快速的進入, 是不是?";
								next;
							}
						}
					}
				}
				if (getpartyleader(getcharid(1),2) == getcharid(0)) {
					if (instance_create("Wolfchev's Laboratory") < 0) {
						mes "[查夫]";
						mes "嗯..實驗室進入系統有點怪異.等我來檢查系統?";
						close;
					}
					mes "[查夫]";
					mes "請等等..";
					next;
					mes "查夫開始控制某些齒輪.";
					next;
					progressbar "FF00FF", 2;
						mes "[查夫]";
						mes "好了, 我已經寫了系統進入的代碼了";
						mes "^0000ff"+ strcharinfo(1) +"^000000 隊伍的隊長是 ^0000ff"+ strcharinfo(PC_NAME) +"^000000!!.";
						next;
						mes "[查夫]";
						mes "現在, 你方可以自由地通過入口.";
					close;
				} else {
					mes "[查夫]";
					mes "你不是 ^0000ff隊長^000000, 是嗎? 請去找隊長來和我說話.";
					close;
				}
			case 3:
				mes "[查夫]";
				mes "需要更多的準備?拜託, 我們沒有更多的時間了.";
				close;
			}
		} else {
			mes "[查夫]";
			mes "你們不是要一起去嗎?這是一個自殺!.";
			close;
		}
	} else {
		mes "[查夫]";
		mes "如果繼續發生, 也許我們應該嘗試其他方式..";
		close;
	}
}

lhz_dun04,147,279,0	script	實驗室入口#memo	CLEAR_NPC,{
	if ((MaxWeight - Weight) < 1000 || !checkweight(1201, 1)) {
		mes "^FF0000無法繼續因為你身上東西太多或者太重了.^000000";
		close;
	}
	.@lhz_time = checkquest(5112, PLAYTIME);
	if (lght_duk01 < 7) {
		mes "它說,";
		mes "'^0000FF****唯一的實驗室成員^000000'";
		close;
	}
	if (.@lhz_time == -1) {
		mes "這是查夫的實驗室的入口.有東西在入口處嚎叫.";
		next;
		if(select("往裡走", "需要更多的時間") == 2) {
			mes "你已經停止進入到查夫的實驗室.";
			close;
		}
		if (instance_enter("Wolfchev's Laboratory") != 0) {  // probably missing failure cases
			mes "^FF0000警告^000000";
			mes ""+ strcharinfo(0) +". . .";
			mes "^FF0000未註冊人員^000000";
			next;
			pushpc 0, 10;
			percentheal -5,0;
			specialeffect EF_AUTOCOUNTER;
			specialeffect2 EF_BASH;
			mes "實驗室入口對你進行了攻擊.";
			close;
		}
			mapannounce "lhz_dun04","實驗室系統入口 ["+ strcharinfo(1) +"] 隊伍 ["+ strcharinfo(PC_NAME) +"] 授予訪問權限.",bc_map,"0x0DF297";
		setquest 5112;
		//warp "1@lhz", 45, 148;
		close;
	} else if (.@lhz_time == 0) {
		mes "滴滴滴滴滴";
		next;
		mes ""+ strcharinfo(0) +". . .";
		mes "^FF0000拒絕進入^000000";
		next;
		mes "該系統拒絕了您的訪問.";
		close;
	} else if (.@lhz_time >= 1) {
		mes "滴滴滴滴滴";
		next;
		//if (.@lhz_time == 2)
			//recall_completequest 5112;
		erasequest 5112;
		mes ""+ strcharinfo(0) +". . .";
		mes "^FF0000拒絕訪問已停用.^000000";
		close;
	} else {
		mes "^FF0000錯誤!錯誤!^000000";
		mes "^FF0000請再次嘗試.^000000";
		close;
	}
}

// The Lab No.1 : System 1_1
//============================================================
1@lhz,35,108,4	script	實驗室1#1_1	CLEAR_NPC,{
	end;

OnInstanceInit:
	initnpctimer;
	end;

OnDisable:
	stopnpctimer;
	end;

OnType_02:
	donpcevent instance_npcname("手動表#1_1")+"::OnEnable";
	donpcevent instance_npcname("閥#1_2")+"::OnEnable";
	donpcevent instance_npcname("閥#1_3")+"::OnEnable";
	mapannounce instance_mapname("1@lhz"),"[安全系統] 有關人士訪問與緊急訪問方法",bc_map,"0xff9977";
	end;

OnTimer1000:
	mapannounce instance_mapname("1@lhz"),"[安全系統] 這是一個警告通知!警告提示!入侵檢測.",bc_map,"0xff9977";
	end;

OnTimer2000:
	mapannounce instance_mapname("1@lhz"),"[安全系統] 在5秒內, 該實驗室1號的安全系統將被激活.",bc_map,"0xff9977";
	end;

OnTimer7000:
	donpcevent instance_npcname("怪物控制1#mob01")+"::OnEnable";
	end;
}

// The Lab No.1 Monster Control
//============================================================
1@lhz,37,108,4	script	怪物控制1#mob01	CLEAR_NPC,{
	end;

OnEnable:
	.@map$ = instance_mapname("1@lhz");
	mapannounce .@map$,"[安全系統] 安全系統激活!實驗動物被釋放.有關人員必須撤離.",bc_map,"0xff9977";
	areamonster .@map$,35,159,50,169,"飢餓的動物實驗",MD_DESERT_WOLF,rand(10,15),instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	initnpctimer;
	end;

OnDisable:
	killmonster instance_mapname("1@lhz"),instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	end;

OnTimer180000:
	stopnpctimer;
	if (mobcount(instance_mapname("1@lhz"), instance_npcname(strnpcinfo(0))+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("實驗室1#1_1")+"::OnType_02";
		mapannounce instance_mapname("1@lhz"),"[安全系統] 該實驗室1號的安全系統已經停止.",bc_map,"0xff9977";
	} else {
		initnpctimer;
	}
	end;

OnMyMobDead:
	if (mobcount(instance_mapname("1@lhz"), instance_npcname(strnpcinfo(0))+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("實驗室1#1_1")+"::OnType_02";
		mapannounce instance_mapname("1@lhz"),"[安全系統] 該實驗室1號的安全系統已經停止.",bc_map,"0xff9977";
		stopnpctimer;
	}
	end;
}

// Emergency Access Valve Manual
//============================================================
1@lhz,39,168,0	script	手動閥#1_1	CLEAR_NPC,{
	mes "還有散落在地板上閥手動表.";
	next;
	if (getpartyleader(getcharid(1),2) == getcharid(0)) {
		mes "你開始閱讀使用說明書.";
		next;
		mes "[緊急通道手動閥]";
		switch(lght_duk02) {
		case 0:
			switch(rand(1,6)) {
			case 1:
				lght_duk02 = 1;
				mes "打開左側閥門順時針兩次, 一次逆時針, 順時針一次";
				break;
			case 2:
				lght_duk02 = 2;
				mes "打開左側閥門順時針一次, 兩次逆時針, 順時針一次";
				break;
			case 3:
				lght_duk02 = 3;
				mes "旋轉左邊的閥門一次順時針, 逆時針一次, 兩次順時針";
				break;
			case 4:
				lght_duk02 = 4;
				mes "打開右側閥逆時針兩次, 一次順時針, 逆時針一次";
				break;
			case 5:
				lght_duk02 = 5;
				mes "打開右側閥逆時針一次, 兩次順時針, 逆時針一次";
				break;
			case 6:
				lght_duk02 = 6;
				mes "打開右側閥逆時針一次, 一次順時針, 逆時針兩次";
				break;
			default:
				mes "這是一個未知的錯誤.如果您嘗試一次後得到了同樣的問題, 聯繫GM ";
				break;
			}
			break;
		case 1:
			mes "打開左側閥門順時針兩次, 一次逆時針, 順時針一次";
			break;
		case 2:
			mes "打開左側閥門順時針一次, 兩次逆時針, 順時針一次";
			break;
		case 3:
			mes "旋轉左邊的閥門一次順時針, 逆時針一次, 兩次順時針";
			break;
		case 4:
			mes "打開右側閥逆時針兩次, 一次順時針, 逆時針一次";
			break;
		case 5:
			mes "打開右側閥逆時針一次, 兩次順時針, 逆時針一次";
			break;
		case 6:
			mes "打開右側閥逆時針一次, 一次順時針, 逆時針兩次";
			break;
		default:
			mes "這是一個未知的錯誤.如果您嘗試一次後得到了同樣的問題, 聯繫GM.";
			break;
		}
	} else {
		mes "讓你們的隊長來控制閥門.";
	}
	close;

OnInstanceInit:
	disablenpc instance_npcname("手動閥#1_1");
	end;

OnEnable:
	specialeffect EF_LIGHTSPHERE;
	enablenpc instance_npcname("手動閥#1_1");
	end; 
}

// Left Valve
//============================================================
1@lhz,41,172,0	script	閥#1_2	CLEAR_NPC,{
	.@open_portal = 0;
	mes "這是左閥的緊急訪問";
	if (getpartyleader(getcharid(1),2) == getcharid(0)) {
		next;
		mes "你要向哪個方向轉?";
		next;
		if (lght_duk02 < 1 || lght_duk02 > 3) {
			for(.@i = 1; .@i <= 4; .@i++) {
				select("順時針", "逆時針");
				mes "-金屬的聲音-";
				if (.@i != 4)
					mes "然後向哪個方向?";
				next;
			}
		} else {
			// lght_duk02 == 1 : Clockwise, Clockwise, Counterclockwise, Clockwise
			// lght_duk02 == 2 : Clockwise, Counterclockwise, Counterclockwise, Clockwise
			// lght_duk02 == 3 : Clockwise, Counterclockwise, Clockwise, Clockwise
			for(.@i = 1; .@i <= 4; .@i++) {
				switch(select("順時針", "逆時針")) {
				case 1:
					if ((.@i == 1 || .@i == 4) || (.@i == 2 && lght_duk02 == 1) || (.@i == 3 && lght_duk02 == 3)) {
						.@open_portal += 1;
					}
				case 2:
					if ((.@i == 2 && lght_duk02 != 1) || (.@i == 3 && lght_duk02 != 3)) {
						.@open_portal += 1;
					}
				}
				mes "-金屬的聲音-";
				if (.@i != 4)
					mes "然後向哪個方向?";
				next;
			}
		}
		if (.@open_portal == 4) {
			donpcevent instance_npcname("#potal_01")+"::OnEnable";
			lght_duk02 = 0;
			mes "-門開了-";
			next;
			mes "它連接到2號實驗室的門現在是開放的";
		} else {
			lght_duk02 = 0;
			mes "什麼都沒有發生.";
			next;
			mes "你需要重新來過";
		}
	}
	close;

OnInstanceInit:
	disablenpc instance_npcname("閥#1_2");
	end;

OnEnable:
	specialeffect EF_LIGHTSPHERE;
	enablenpc instance_npcname("閥#1_2");
	end;
}

// Right Valve
//============================================================
1@lhz,52,172,0	script	閥#1_3	CLEAR_NPC,{
	.@open_portal = 0;
	mes "這是左閥的緊急訪問";
	if (getpartyleader(getcharid(1),2) == getcharid(0)) {
		next;
		mes "你要向哪個方向轉?";
		next;
		if (lght_duk02 < 4 || lght_duk02 > 6) {
			for(.@i = 1; .@i <= 4; .@i++) {
				select("順時針", "逆時針");
				mes "-金屬的聲音-";
				if (.@i != 4)
					mes "然後向哪個方向?";
				next;
			}
		} else {
			// lght_duk02 == 4 : Counterclockwise, Counterclockwise, Clockwise, Counterclockwise
			// lght_duk02 == 5 : Counterclockwise, Clockwise, Clockwise, Counterclockwise
			// lght_duk02 == 6 : Counterclockwise, Clockwise, Counterclockwise, Counterclockwise
			for(.@i = 1; .@i <= 4; .@i++) {
				switch(select("順時針", "逆時針")) {
				case 1:
					if ((.@i == 2 && lght_duk02 != 4) || (.@i == 3 && lght_duk02 != 6))
						.@open_portal += 1;
					break;
				case 2:
					if ((.@i == 1 || .@i == 4) || (.@i == 2 && lght_duk02 == 4) || (.@i == 3 && lght_duk02 == 6))
						.@open_portal += 1;
					break;
				}
				mes "-金屬的聲音-";
				if (.@i != 4)
					mes "然後向哪個方向?";
				next;
			}
		}
		if (.@open_portal == 4) {
			donpcevent instance_npcname("#potal_01")+"::OnEnable";
			lght_duk02 = 0;
			mes "-門開了-";
			next;
			mes "它連接到2號實驗室的門現在是開放的";
		} else {
			lght_duk02 = 0;
			mes "什麼都沒發生.";
			next;
			mes "你需要重新來過.";
		}
	}
	close;

OnInstanceInit:
	disablenpc instance_npcname("閥#1_3");
	end;

OnEnable:
	specialeffect EF_LIGHTSPHERE;
	enablenpc instance_npcname("閥#1_3");
	end;
}


// The Lab No.1 >> The Lab No.2 Warp portal
//============================================================
1@lhz,45,173,0	script	#potal_01	WARPNPC,2,2,{
	end;

OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#potal_01");
	end;

OnEnable:
	mapannounce instance_mapname("1@lhz"),"[管理系統] 它連接到2號實驗室的門現在是開放的.",bc_map,"0xff9977";
	enablenpc instance_npcname("#potal_01");
	end;

OnTouch:
	warp instance_mapname("1@lhz"), 151, 29;
	end;
}

// The Lab No.2
//============================================================
1@lhz,151,29,0	script	#2_0	-1,1,1,{
	end;

OnEnable:
	enablenpc instance_npcname("#2_0");
	end;

OnDisable:
	disablenpc instance_npcname("#2_0");
	end;

OnTouch:
	donpcevent instance_npcname("實驗室2#2_1")+"::OnEnable";
	donpcevent instance_npcname("#2_0")+"::OnDisable";
	end;
}

// The Lab No.2 2_1 System - Control of Monster Wave and Pipe right before exploding
//============================================================
1@lhz,35,106,4	script	實驗室2#2_1	CLEAR_NPC,{
	end;

OnInstanceInit:
OnDisable:
	stopnpctimer;
	end;

OnEnable:
	initnpctimer;
	end;

OnTimer1000:
	mapannounce instance_mapname("1@lhz"),"[安全系統] 這是一個警告通知!警告提示!2號實驗室入侵檢測中!!",bc_map,"0xff9977";
	end;

OnTimer3000:
	mapannounce instance_mapname("1@lhz"),"[安全系統] 在5秒內, 實驗室2號安全系統將開始運行.",bc_map,"0xff9977";
	end;

OnTimer5000:
	mapannounce instance_mapname("1@lhz"),"[管理系統] 警告!第二實驗室管道壓力過高.",bc_map,"0xff99ff";
	end;

OnTimer8000:
	mapannounce instance_mapname("1@lhz"),"[安全系統] 第一安全系統啟動!有關人員必須撤離",bc_map,"0xff9977";
	donpcevent instance_npcname("怪物控制2#mob02")+"::On01";
	end;

OnTimer10000:
	mapannounce instance_mapname("1@lhz"),"[管理系統] 警告!管網壓力過高.打開閥門.",bc_map,"0xff99ff";
	donpcevent instance_npcname("實驗室2#2_2")+"::OnPipePressure";
	end;

OnTimer190000:
	mapannounce instance_mapname("1@lhz"),"[管理系統] 警告!管網壓力過高.打開閥門.",bc_map,"0xff99ff";
	donpcevent instance_npcname("實驗室2#2_2")+"::OnPipePressure";
	end;

OnTimer303000:
	mapannounce instance_mapname("1@lhz"),"[安全系統] 在5秒內, 第二安全系統將開始運行.",bc_map,"0xff9977";
	end;

OnTimer308000:
	mapannounce instance_mapname("1@lhz"),"[安全系統] 第二安全系統被激活!有關人員必須撤離.",bc_map,"0xff9977";
	donpcevent instance_npcname("怪物控制2#mob02")+"::On02";
	end;

OnTimer370000:
	mapannounce instance_mapname("1@lhz"),"[管理系統] 警告!管網壓力過高.打開閥門",bc_map,"0xff99ff";
	donpcevent instance_npcname("實驗室2#2_2")+"::OnPipePressure";
	end;

OnTimer550000:
	mapannounce instance_mapname("1@lhz"),"[管理系統] 警告!管網壓力過高.打開閥門.",bc_map,"0xff99ff";
	donpcevent instance_npcname("實驗室2#2_2")+"::OnPipePressure";
	end;

OnTimer603000:
	mapannounce instance_mapname("1@lhz"),"[安全系統] 在5秒內, 第三安全系統將開始工作.",bc_map,"0xff9977";
	end;

OnTimer608000:
	mapannounce instance_mapname("1@lhz"),"[安全系統] 第三安全系統激活!有關人員必須撤離.",bc_map,"0xff9977";
	donpcevent instance_npcname("怪物控制2#mob02")+"::On03";
	end;

OnTimer730000:
	mapannounce instance_mapname("1@lhz"),"[管理系統] 警告!管網壓力過高.打開閥門.",bc_map,"0xff99ff";
	donpcevent instance_npcname("實驗室2#2_2")+"::OnPipePressure";
	end;

OnTimer903000:
	mapannounce instance_mapname("1@lhz"),"[安全系統] 在5秒內, 第四安全系統將開始運行",bc_map,"0xff9977";
	end;

OnTimer908000:
	mapannounce instance_mapname("1@lhz"),"[安全系統] 第四安全系統啟動!有關人員必須撤離.",bc_map,"0xff9977";
	donpcevent instance_npcname("怪物控制2#mob02")+"::On04";
	end;

OnTimer910000:
	mapannounce instance_mapname("1@lhz"),"[管理系統] 警告!管網壓力過高.打開閥門.",bc_map,"0xff99ff";
	donpcevent instance_npcname("實驗室2#2_2")+"::OnPipePressure";
	end;

OnTimer1090000:
	mapannounce instance_mapname("1@lhz"),"[管理系統] 警告!管網壓力過高.打開閥門.",bc_map,"0xff99ff";
	donpcevent instance_npcname("實驗室2#2_2")+"::OnPipePressure";
	end;

OnTimer1203000:
	mapannounce instance_mapname("1@lhz"),"[安全系統] 在5秒內, 第五安全系統將開始工作.",bc_map,"0xff9977";
	end;

OnTimer1208000:
	mapannounce instance_mapname("1@lhz"),"[安全系統] 第五安防系統啟動!有關人員必須撤離.",bc_map,"0xff9977";
	donpcevent instance_npcname("怪物控制2#mob02")+"::On05";
	end;

OnTimer1270000:
	mapannounce instance_mapname("1@lhz"),"[管理系統] 警告!管網壓力過高.打開閥門",bc_map,"0xff99ff";
	donpcevent instance_npcname("實驗室2#2_2")+"::OnPipePressure";
	stopnpctimer;
	end;
}

// The Lab No.2 2_2 System - Pipe Operation Control
// When 3 pipes explode, system will shut down.
//============================================================
1@lhz,37,106,4	script	實驗室2#2_2	CLEAR_NPC,{
	end;

OnInstanceInit:
	'broken_pipes = 0;
	stopnpctimer;
	end;

OnPipePressure:
	switch(rand(1,6)) {
	case 1:
		donpcevent instance_npcname("閥#2_1")+"::OnEnable";
		donpcevent instance_npcname("怪物控制2#mob02")+"::On2_01";
	case 2:
		donpcevent instance_npcname("閥#2_2")+"::OnEnable";
		donpcevent instance_npcname("怪物控制2#mob02")+"::On2_02";
	case 3:
		donpcevent instance_npcname("閥#2_3")+"::OnEnable";
		donpcevent instance_npcname("怪物控制2#mob02")+"::On2_03";
	case 4:
		donpcevent instance_npcname("閥#2_4")+"::OnEnable";
		donpcevent instance_npcname("怪物控制2#mob02")+"::On2_04";
	case 5:
		donpcevent instance_npcname("閥#2_5")+"::OnEnable";
		donpcevent instance_npcname("怪物控制2#mob02")+"::On2_05";
	case 6:
		donpcevent instance_npcname("閥#2_6")+"::OnEnable";
		donpcevent instance_npcname("怪物控制2#mob02")+"::On2_06";
	}
	initnpctimer;
	end;

OnDisableValve1:
	disablenpc instance_npcname("閥#2_1");
	stopnpctimer;
	end;

OnDisableValve2:
	disablenpc instance_npcname("閥#2_2");
	stopnpctimer;
	end;

OnDisableValve3:
	disablenpc instance_npcname("閥#2_3");
	stopnpctimer;
	end;

OnDisableValve4:
	disablenpc instance_npcname("閥#2_4");
	stopnpctimer;
	end;

OnDisableValve5:
	disablenpc instance_npcname("閥#2_5");
	stopnpctimer;
	end;

OnDisableValve6:
	disablenpc instance_npcname("閥#2_6");
	stopnpctimer;
	end;

OnTimer63000:
	for(.@i = 1; .@i < 7; .@i++) {
		specialeffect EF_SUI_EXPLOSION, AREA, instance_npcname("閥#2_"+.@i+"");
		disablenpc instance_npcname("閥#2_"+.@i+"");
	}
	'broken_pipes += 1;
	if ('broken_pipes == 3) {
		donpcevent instance_npcname("#2_3")+"::OnEnable";
		stopnpctimer;
	} else {
		mapannounce instance_mapname("1@lhz"),"[管理系統] 爆管檢測! 如果實驗室被更多的突破, 系統將停止.",bc_map,"0xff99ff";
	}
	end;
}

// The Lab No.2 2_3 System
//============================================================
1@lhz,151,48,0	script	#2_3	-1,20,20,{
	end;

OnInstanceInit:
	disablenpc instance_npcname("#2_3");
	end;

OnEnable:
	initnpctimer;
	end;

OnTimer1000:
	mapannounce instance_mapname("1@lhz"),"[管理系統] 系統死機!系統死機!10秒內從實驗室裡撤離!!",bc_map,"0xff99ff";
	end;

OnTimer11000:
	mapannounce instance_mapname("1@lhz"),"[管理系統] 疏散!!",bc_map,"0xff99ff";
	enablenpc instance_npcname("#2_3");
	end;

OnTouch:
	warp "lhz_dun04", 147, 273;
	end;
}

// The Lab No.2 Monster Control
//============================================================
1@lhz,39,106,4	script	怪物控制2#mob02	CLEAR_NPC,{
	end;

On01:
	.@map$ = instance_mapname("1@lhz");
	.@label$ = instance_npcname("怪物控制2#mob02")+"::OnMyMobDead";
	areamonster .@map$,139,37,164,58,"飢餓的動物實驗",MD_DESERT_WOLF,10,.@label$;
	end;

On02:
	.@map$ = instance_mapname("1@lhz");
	.@label$ = instance_npcname("怪物控制2#mob02")+"::OnMyMobDead";
	areamonster .@map$,139,37,164,58,"飢餓的動物實驗",MD_DESERT_WOLF,20,.@label$;
	end;

On03:
	.@map$ = instance_mapname("1@lhz");
	.@label$ = instance_npcname("怪物控制2#mob02")+"::OnMyMobDead";
	areamonster .@map$,139,37,164,58,"飢餓的動物實驗",MD_DESERT_WOLF,16,.@label$;
	end;

On04:
	.@map$ = instance_mapname("1@lhz");
	.@label$ = instance_npcname("怪物控制2#mob02")+"::OnMyMobDead";
	areamonster .@map$,139,37,164,58,"飢餓的動物實驗",MD_DESERT_WOLF,19,.@label$;
	end;

On05:
	.@map$ = instance_mapname("1@lhz");
	.@label$ = instance_npcname("怪物控制2#mob02")+"::OnMyMobDead";
	areamonster .@map$,139,37,164,58,"飢餓的動物實驗",MD_DESERT_WOLF,20,.@label$;
	end;

On2_01:
	.@label$ = instance_npcname("怪物控制2#mob02")+"::OnMyMobDead";
	areamonster instance_mapname("1@lhz"),155,43,161,49,"飢餓的動物實驗",MD_DESERT_WOLF_B,5,.@label$;
	end;

On2_02:
	.@label$ = instance_npcname("怪物控制2#mob02")+"::OnMyMobDead";
	areamonster instance_mapname("1@lhz"),137,40,143,46,"飢餓的動物實驗",MD_DESERT_WOLF_B,5,.@label$;
	end;

On2_03:
	.@label$ = instance_npcname("怪物控制2#mob02")+"::OnMyMobDead";
	areamonster instance_mapname("1@lhz"),137,51,143,57,"飢餓的動物實驗",MD_DESERT_WOLF_B,5,.@label$;
	end;

On2_04:
	.@label$ = instance_npcname("怪物控制2#mob02")+"::OnMyMobDead";
	areamonster instance_mapname("1@lhz"),143,56,149,62,"飢餓的動物實驗",MD_DESERT_WOLF_B,5,.@label$;
	end;

On2_05:
	.@label$ = instance_npcname("怪物控制2#mob02")+"::OnMyMobDead";
	areamonster instance_mapname("1@lhz"),160,48,166,54,"飢餓的動物實驗",MD_DESERT_WOLF_B,5,.@label$;
	end;

On2_06:
	.@label$ = instance_npcname("怪物控制2#mob02")+"::OnMyMobDead";
	areamonster instance_mapname("1@lhz"),135,44,141,50,"飢餓的動物實驗",MD_DESERT_WOLF_B,5,.@label$;
	end;

OnDisable:
	killmonster instance_mapname("1@lhz"),instance_npcname("怪物控制2#mob02")+"::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount(instance_mapname("1@lhz"),instance_npcname("怪物控制2#mob02")+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("#potal_02")+"::OnEnable";
		donpcevent instance_npcname("Lab2#2_1")+"::OnDisable";
		mapannounce instance_mapname("1@lhz"),"[安全系統] 2號實驗室的安全系統已經停止.",bc_map,"0xff9977";
	}
	end;
}

// The Lab No.2 Valves
//============================================================
-	script	bio4FLab2Valve	-1,{
	if (getpartyleader(getcharid(1),2) == getcharid(0)) {
		progressbar "0xffff00", 20;
		stopnpctimer;
		donpcevent instance_npcname("實驗室2#2_2")+"::OnDisableValve1";
		disablenpc instance_npcname(strnpcinfo(0));
		end;
	} else {
		mes "讓你們的隊長來控制這個閥門.";
		close;
	}

OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;

OnEnable:
	enablenpc instance_npcname(strnpcinfo(0));
	initnpctimer;
	end;

OnTimer3000:
	specialeffect EF_DRAGONSMOKE,AREA,instance_npcname(strnpcinfo(0));
	stopnpctimer;
	initnpctimer;
	end;
}
1@lhz,158,46,0	duplicate(bio4FLab2Valve)	閥#2_1	CLEAR_NPC
1@lhz,138,43,0	duplicate(bio4FLab2Valve)	閥#2_2	CLEAR_NPC
1@lhz,139,54,0	duplicate(bio4FLab2Valve)	閥#2_3	CLEAR_NPC
1@lhz,145,60,0	duplicate(bio4FLab2Valve)	閥#2_4	CLEAR_NPC
1@lhz,165,51,0	duplicate(bio4FLab2Valve)	閥#2_5	CLEAR_NPC
1@lhz,137,47,0	duplicate(bio4FLab2Valve)	閥#2_6	CLEAR_NPC

// The Lab No.2 >> The Lab No.3 Warp portal(General)
//============================================================
1@lhz,151,64,0	script	#potal_02	WARPNPC,2,2,{
	end;

OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#potal_02");
	end;

OnEnable:
	mapannounce instance_mapname("1@lhz"),"[管理系統] 連接到3號實驗室的門已經打開",bc_map,"0xff99ff";
	enablenpc instance_npcname("#potal_02");
	end;

OnTouch:
	warp instance_mapname("1@lhz"), 84, 28;
	end;
}

// The Lab No.3
//============================================================
1@lhz,84,28,0	script	#3_0	-1,1,1,{
	end;

OnEnable:
	enablenpc instance_npcname("#3_0");
	end;

OnDisable:
	disablenpc instance_npcname("#3_0");
	end;

OnTouch:
	donpcevent instance_npcname("實驗室3#3_1")+"::OnEnable";
	donpcevent instance_npcname("#3_0")+"::OnDisable";
	end;
}

// The Lab No.3 3_1 System - Monster wave control
//============================================================
1@lhz,35,104,4	script	實驗室3#3_1	CLEAR_NPC,{
	end;

OnEnable:
	initnpctimer;
	end;

OnDisable:
	stopnpctimer;
	end;

OnTimer1000:
	mapannounce instance_mapname("1@lhz"),"[安全系統] 這是一個警告通知!警告提示!",bc_map,"0xff9977";
	donpcevent instance_npcname("怪物控制3#mob03")+"::OnDisable";
	end;

OnTimer5000:
	mapannounce instance_mapname("1@lhz"),"[安全系統] 3號實驗室入侵檢測中!!",bc_map,"0xff9977";
	end;

OnTimer7000:
	mapannounce instance_mapname("1@lhz"),"[安全系統] 在5秒內, 3號實驗室的安全系統將開始工作",bc_map,"0xff9977";
	end;

OnTimer12000:
	mapannounce instance_mapname("1@lhz"),"[安全系統] 3號實驗室的第一安全系統啟動!有關人員必須撤離.",bc_map,"0xff9977";
	donpcevent instance_npcname("怪物控制3#mob03")+"::On01";
	end;

OnTimer192000:
	mapannounce instance_mapname("1@lhz"),"[安全系統] 3號實驗室的第二安全系統啟動!有關人員必須撤離.",bc_map,"0xff9977";
	donpcevent instance_npcname("怪物控制3#mob03")+"::On02";
	end;

OnTimer372000:
	mapannounce instance_mapname("1@lhz"),"[安全系統] 3號實驗室的第三安全系統啟動!有關人員必須撤離.",bc_map,"0xff9977";
	donpcevent instance_npcname("怪物控制3#mob03")+"::On03";
	end;

OnTimer552000:
	mapannounce instance_mapname("1@lhz"),"[安全系統] 3號實驗室的第四安全系統啟動!有關人員必須撤離.",bc_map,"0xff9977";
	donpcevent instance_npcname("怪物控制3#mob03")+"::On04";
	end;

OnTimer732000:
	mapannounce instance_mapname("1@lhz"),"[安全系統] 3號實驗室的第五安全系統啟動!有關人員必須撤離.",bc_map,"0xff9977";
	donpcevent instance_npcname("怪物控制3#mob03")+"::On05";
	stopnpctimer;
	end;
}

// The Lab No.3 Monster Control
//============================================================
1@lhz,37,104,4	script	怪物控制3#mob03	CLEAR_NPC,{
	end;

On01:
	.@map$ = instance_mapname("1@lhz");
	.@label$ = instance_npcname("怪物控制3#mob03")+"::OnMyMobDead";
	monster .@map$,80,60,"飢餓的動物",2242,1,.@label$;
	monster .@map$,81,58,"飢餓的動物",2242,1,.@label$;
	monster .@map$,82,56,"飢餓的動物",2242,1,.@label$;
	monster .@map$,84,60,"飢餓的動物",2242,1,.@label$;
	monster .@map$,85,58,"飢餓的動物",2242,1,.@label$;
	monster .@map$,82,60,"飢餓的動物",2243,1,.@label$;
	monster .@map$,83,58,"飢餓的動物",2243,1,.@label$;
	monster .@map$,85,56,"飢餓的動物",2243,1,.@label$;		
	monster .@map$,86,60,"飢餓的動物",2243,1,.@label$;
	monster .@map$,87,58,"飢餓的動物",2243,1,.@label$;
	end;

On02:
	.@map$ = instance_mapname("1@lhz");
	.@amount1 = rand(1,5);
	.@amount2 = rand(1,5);
	.@label$ = instance_npcname("怪物控制3#mob03")+"::OnMyMobDead";
	monster .@map$,80,60,"飢餓的動物",2242,.@amount1,.@label$;
	monster .@map$,82,60,"飢餓的動物",2242,.@amount2,.@label$;
	monster .@map$,84,60,"飢餓的動物",2243,.@amount1,.@label$;
	monster .@map$,86,60,"飢餓的動物",2242,.@amount2,.@label$;
	monster .@map$,81,58,"飢餓的動物",2242,.@amount1,.@label$;
	monster .@map$,83,58,"飢餓的動物",2243,.@amount2,.@label$;
	monster .@map$,85,58,"飢餓的動物",2242,.@amount1,.@label$;
	monster .@map$,87,58,"飢餓的動物",2242,.@amount2,.@label$;
	monster .@map$,82,56,"飢餓的動物",2243,.@amount1,.@label$;
	monster .@map$,85,56,"飢餓的動物",2242,.@amount2,.@label$;
	end;

On03:
	.@map$ = instance_mapname("1@lhz");
	.@amount1 = rand(1,5);
	.@amount2 = rand(1,5);
	.@amount3 = rand(1,5);
	.@label$ = instance_npcname("怪物控制3#mob03")+"::OnMyMobDead";
	monster .@map$,80,60,"飢餓的動物",2242,.@amount1,.@label$;
	monster .@map$,82,60,"飢餓的動物",2242,.@amount2,.@label$;
	monster .@map$,84,60,"飢餓的動物",2242,.@amount3,.@label$;
	monster .@map$,86,60,"飢餓的動物",2243,.@amount1,.@label$;
	monster .@map$,81,58,"飢餓的動物",2242,.@amount2,.@label$;
	monster .@map$,83,58,"飢餓的動物",2242,.@amount3,.@label$;
	monster .@map$,85,58,"飢餓的動物",2242,.@amount1,.@label$;
	monster .@map$,87,58,"飢餓的動物",2243,.@amount2,.@label$;
	monster .@map$,82,56,"飢餓的動物",2242,.@amount3,.@label$;
	monster .@map$,85,56,"飢餓的動物",2242,.@amount1,.@label$;
	end;

On04:
	.@map$ = instance_mapname("1@lhz");
	.@amount1 = rand(1,5);
	.@amount2 = rand(1,5);
	.@amount3 = rand(1,5);
	.@amount4 = rand(1,5);
	.@label$ = instance_npcname("怪物控制3#mob03")+"::OnMyMobDead";
	monster .@map$,80,60,"飢餓的動物",2242,.@amount1,.@label$;
	monster .@map$,82,60,"飢餓的動物",2242,.@amount2,.@label$;
	monster .@map$,84,60,"飢餓的動物",2242,.@amount3,.@label$;
	monster .@map$,86,60,"飢餓的動物",2242,.@amount4,.@label$;
	monster .@map$,81,58,"飢餓的動物",2243,.@amount1,.@label$;
	monster .@map$,83,58,"飢餓的動物",2242,.@amount2,.@label$;
	monster .@map$,85,58,"飢餓的動物",2242,.@amount3,.@label$;
	monster .@map$,87,58,"飢餓的動物",2242,.@amount4,.@label$;
	monster .@map$,82,56,"飢餓的動物",2243,.@amount1,.@label$;
	monster .@map$,85,56,"飢餓的動物",2242,.@amount2,.@label$;
	end;

On05:
	.@map$ = instance_mapname("1@lhz");
	.@amount1 = rand(1,5);
	.@amount2 = rand(1,5);
	.@amount3 = rand(1,5);
	.@amount4 = rand(1,5);
	.@amount5 = rand(1,5);
	.@label$ = instance_npcname("怪物控制3#mob03")+"::OnMyMobDead";
	monster .@map$,80,60,"飢餓的動物",2243,.@amount1,.@label$;
	monster .@map$,82,60,"飢餓的動物",2242,.@amount2,.@label$;
	monster .@map$,84,60,"飢餓的動物",2242,.@amount3,.@label$;
	monster .@map$,86,60,"飢餓的動物",2243,.@amount4,.@label$;
	monster .@map$,81,58,"飢餓的動物",2242,.@amount5,.@label$;
	monster .@map$,83,58,"飢餓的動物",2242,.@amount1,.@label$;
	monster .@map$,85,58,"飢餓的動物",2243,.@amount2,.@label$;
	monster .@map$,87,58,"飢餓的動物",2242,.@amount3,.@label$;
	monster .@map$,82,56,"飢餓的動物",2242,.@amount4,.@label$;
	monster .@map$,85,56,"飢餓的動物",2243,.@amount5,.@label$;
	end;

OnDisable:
	killmonster instance_mapname("1@lhz"),instance_npcname("怪物控制3#mob03")+"::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount(instance_mapname("1@lhz"),instance_npcname("怪物控制3#mob03")+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("#potal_03")+"::OnEnable";
		donpcevent instance_npcname("實驗室3#3_1")+"::OnDisable";
		mapannounce instance_mapname("1@lhz"),"[安全系統] 3號實驗室的安全系統已經停止!",bc_map,"0xff9977";
	}
	end;
}

// The Lab No.3 >> Boss's Room Warp portal
//============================================================
1@lhz,83,62,0	script	#potal_03	WARPNPC,2,2,{
	end;

OnInstanceInit:
	disablenpc instance_npcname("#potal_03");
	end;

OnEnable:
	mapannounce instance_mapname("1@lhz"),"[管理系統] 連接到4號實驗室的門已經打開",bc_map,"0x7799ff";
	enablenpc instance_npcname("#potal_03");
	end;

OnDisable:
	disablenpc instance_npcname("#potal_03");
	end;

OnTouch:
	warp instance_mapname("1@lhz"), 137, 100;
	end;
}

// The Lab No.4
//============================================================
1@lhz,137,100,0	script	#4_0	-1,1,1,{
	end;

OnEnable:
	enablenpc instance_npcname("#4_0");
	end;

OnDisable:
	disablenpc instance_npcname("#4_0");
	end;

OnTouch:
	donpcevent instance_npcname("實驗室4#4_1")+"::OnEnable";
	donpcevent instance_npcname("#4_0")+"::OnDisable";
	end;
}

// The Lab No.4 4_1 System - Summon of Boss Monster
//============================================================
1@lhz,35,102,4	script	實驗室4#4_1	CLEAR_NPC,{
	end;

OnEnable:
	initnpctimer;
	end;

OnDisable:
	stopnpctimer;
	end;

OnTimer1000:
	mapannounce instance_mapname("1@lhz"),"耳語: 你是誰....",bc_map,"0x7DCBF0";
	specialeffect EF_SPHERE,AREA,instance_npcname("Seyren Windsor#boss01");
	end;

OnTimer2000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Cecile Damon#boss02");
	end;

OnTimer3000:
	mapannounce instance_mapname("1@lhz"),"耳語: 不要靠近...",bc_map,"0x7DCBF0";
	specialeffect EF_SPHERE,AREA,instance_npcname("Gertie Wie#boss03");
	end;

OnTimer4000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Eremes Guile#boss04");
	end;

OnTimer5000:
	mapannounce instance_mapname("1@lhz"),"耳語: 這是一個非常危險的地方..",bc_map,"0x7DCBF0";
	specialeffect EF_SPHERE,AREA,instance_npcname("Trentini#boss05");
	end;

OnTimer6000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Chen Liu#boss06");
	end;

OnTimer7000:
	mapannounce instance_mapname("1@lhz"),"耳語: 你不會活著出去....",bc_map,"0x7DCBF0";
	specialeffect EF_SPHERE,AREA,instance_npcname("Alphochio Basil#boss07");
	end;

OnTimer8000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Flamel Emul#boss08");
	end;

OnTimer9000:
	mapannounce instance_mapname("1@lhz"),"耳語: 如果你不希望成為我們的一部分....",bc_map,"0x7DCBF0";
	specialeffect EF_SPHERE,AREA,instance_npcname("Randel Lawrence#boss09");
	end;

OnTimer10000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Celia Alde#boss10");
	end;

OnTimer11000:
	mapannounce instance_mapname("1@lhz"),"耳語: 現在離開....",bc_map,"0x7DCBF0";
	specialeffect EF_SPHERE,AREA,instance_npcname("Kathryne Keyron#boss11");
	end;

OnTimer12000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Margaretha Sorin#boss12");
	end;

OnTimer13000:
	mapannounce instance_mapname("1@lhz"),"耳語: 之前這個地方是徹底充滿了悲傷和痛苦....",bc_map,"0x7DCBF0";
	specialeffect EF_SPHERE,AREA,instance_npcname("Howard Alt-Eisen#boss13");
	end;

OnTimer14000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Type_**Gren#boss14");
	end;

OnTimer15000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Type_**Roke#boss15");
	end;

OnTimer16000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Type_**Dree#boss16");
	end;

OnTimer17000:
	mapannounce instance_mapname("1@lhz"),"耳語: 啊....這....這是為時已晚......",bc_map,"0x7DCBF0";
	donpcevent instance_npcname("怪物控制4#mob04")+"::OnEnable";
	stopnpctimer;
	end;
}

// Area No. 4 - Boss Summon Control
//============================================================
1@lhz,37,102,4	script	怪物控制4#mob04	CLEAR_NPC,{
	end;

OnEnable:
	switch(rand(1,13)) {
	case 1:
		donpcevent instance_npcname("Seyren Windsor#boss01")+"::OnEnable";
		end;
	case 2:
		donpcevent instance_npcname("Cecile Damon#boss02")+"::OnEnable";
		end;
	case 3:
		donpcevent instance_npcname("Gertie Wie#boss03")+"::OnEnable";
		end;
	case 4:
		donpcevent instance_npcname("Eremes Guile#boss04")+"::OnEnable";
		end;
	case 5:
		donpcevent instance_npcname("Trentini#boss05")+"::OnEnable";
		end;
	case 6:
		donpcevent instance_npcname("Chen Liu#boss06")+"::OnEnable";
		end;
	case 7:
		donpcevent instance_npcname("Alphochio Basil#boss07")+"::OnEnable";
		end;
	case 8:
		donpcevent instance_npcname("Flamel Emul#boss08")+"::OnEnable";
		end;
	case 9:
		donpcevent instance_npcname("Randel Lawrence#boss09")+"::OnEnable";
		end;
	case 10:
		donpcevent instance_npcname("Celia Alde#boss10")+"::OnEnable";
		end;
	case 11:
		donpcevent instance_npcname("Kathryne Keyron#boss11")+"::OnEnable";
		end;
	case 12:
		donpcevent instance_npcname("Margaretha Sorin#boss12")+"::OnEnable";
		end;
	case 13:
		donpcevent instance_npcname("Howard Alt-Eisen#boss13")+"::OnEnable";
		end; 
	}

OnDisable:
	//stopnpctimer;
	end;
}

// Random Experiments in Tubes
//============================================================
1@lhz,156,148,3	script	Type_**Gren#boss14	4_M_03,{ end; }
1@lhz,152,152,3	script	Type_**Roke#boss15	4_M_HUMAN_01,{ end; }
1@lhz,148,156,3	script	Type_**Dree#boss16	4_M_DWARF,{ end; }

// Bosses
//============================================================
-	script	bio4FBoss	-1,{
	end;

OnEnable:
	.@map$ = instance_mapname("1@lhz");
	.@label$ = instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	specialeffect EF_SUI_EXPLOSION;

	if (strnpcinfo(0) == "Seyren Windsor#boss01")
		monster .@map$,129,154,"暗●騎士領主 賽依連",1646,1,.@label$;
	else if (strnpcinfo(0) == "Cecile Damon#boss02")
		monster .@map$,125,150,"暗●神射手 迪文",1650,1,.@label$;
	else if (strnpcinfo(0) == "Gertie Wie#boss03")
		monster .@map$,121,146,"闇●神行太保 格蒂",2239,1,.@label$;
	else if (strnpcinfo(0) == "Eremes Guile#boss04")
		monster .@map$,117,142,"闇●十字刺客 艾勒梅斯",1647,1,.@label$;
	else if (strnpcinfo(0) == "Trentini#boss05")
		monster .@map$,117,125,"闇●冷豔舞姬 特蘭提妮",2241,1,.@label$;
	else if (strnpcinfo(0) == "Chen Liu#boss06")
		monster .@map$,121,121,"闇●武術宗師 陳",2238,1,.@label$;
	else if (strnpcinfo(0) == "Alphochio Basil#boss07")
		monster .@map$,125,117,"闇●搞笑藝人 阿弗克希奧",2240,1,.@label$;
	else if (strnpcinfo(0) == "Flamel Emul#boss08")
		monster .@map$,129,113,"闇●創造者 弗拉梅爾",2236,1,.@label$;
	else if (strnpcinfo(0) == "Randel Lawrence#boss09")
		monster .@map$,146,113,"闇●聖殿十字軍 蘭德爾",2235,1,.@label$;
	else if (strnpcinfo(0) == "Celia Alde#boss10")
		monster .@map$,150,117,"闇●智者 希莉亞",2237,1,.@label$;
	else if (strnpcinfo(0) == "Kathryne Keyron#boss11")
		monster .@map$,154,121,"闇●超魔導師 凱特莉娜",1651,1,.@label$;
	else if (strnpcinfo(0) == "Margaretha Sorin#boss12")
		monster .@map$,158,125,"闇●神官 瑪嘉雷特",1649,1,.@label$;
	else if (strnpcinfo(0) == "Howard Alt-Eisen#boss13")
		monster .@map$,158,142,"闇●神工匠 哈沃得",1648,1,.@label$;

	end;

OnDisable:
	killmonster instance_mapname("1@lhz"),instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	disablenpc instance_npcname(strnpcinfo(0));
	end;

OnMyMobDead:
	if (mobcount(instance_mapname("1@lhz"), instance_npcname(strnpcinfo(0))+"::OnMyMobDead") < 1) {
		.@map$ = instance_mapname("1@lhz");

		if (strnpcinfo(0) == "Seyren Windsor#boss01")
			mapannounce .@map$,"呀, 在哪裡都是我的同伴....",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Cecile Damon#boss02")
			mapannounce .@map$,"任何..之一, 請大家幫忙..請.....",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Gertie Wie#boss03")
			mapannounce .@map$,"我的行為..太貿然..",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Eremes Guile#boss04")
			mapannounce .@map$,"啊.....我絕對打敗....",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Trentini#boss05")
			mapannounce .@map$,"啊..曾經可以......舞蹈和唱歌.... ...開心....",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Chen Liu#boss06")
			mapannounce .@map$,"我...我..對不起....",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Alphochio Basil#boss07")
			mapannounce .@map$,"...新的歌......是....對.....她....",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Flamel Emul#boss08")
			mapannounce .@map$,"哈...我想向大家展示....新的藥水....",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Randel Lawrence#boss09")
			mapannounce .@map$,"每個人都被打敗了.....?",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Celia Alde#boss10")
			mapannounce .@map$,"這個實驗是.",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Kathryne Keyron#boss11")
			mapannounce .@map$,"噢..是大家....?",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Margaretha Sorin#boss12")
			mapannounce .@map$,"我的好孩子......不要哭...啊....我們偉大的奧丁..請救救我們......",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Howard Alt-Eisen#boss13")
			mapannounce .@map$,"哦...不....不能呼吸....和我的妹妹...... ?",bc_map,"0xFD3B02";

		donpcevent instance_npcname("查夫#last")+"::OnEnable";
		donpcevent instance_npcname(strnpcinfo(0))+"::OnDisable";
	}
	end;
}
1@lhz,127,156,5	duplicate(bio4FBoss)	Seyren Windsor#boss01	1646
1@lhz,123,152,5	duplicate(bio4FBoss)	Cecile Damon#boss02	1650
1@lhz,119,148,5	duplicate(bio4FBoss)	Gertie Wie#boss03	2239
1@lhz,115,144,5	duplicate(bio4FBoss)	Eremes Guile#boss04	1647
1@lhz,115,123,7	duplicate(bio4FBoss)	Trentini#boss05	2241
1@lhz,119,119,7	duplicate(bio4FBoss)	Chen Liu#boss06	2238
1@lhz,123,115,7	duplicate(bio4FBoss)	Alphochio Basil#boss07	2240
1@lhz,127,111,7	duplicate(bio4FBoss)	Flamel Emul#boss08	2236
1@lhz,148,111,1	duplicate(bio4FBoss)	Randel Lawrence#boss09	2235
1@lhz,152,115,1	duplicate(bio4FBoss)	Celia Alde#boss10	2237
1@lhz,156,119,1	duplicate(bio4FBoss)	Kathryne Keyron#boss11	1651
1@lhz,160,123,1	duplicate(bio4FBoss)	Margaretha Sorin#boss12	1649
1@lhz,160,144,3	duplicate(bio4FBoss)	Howard Alt-Eisen#boss13	1648

// Wolfchev - Final encounter
//============================================================
1@lhz,137,156,7	script	查夫#last	4_LGTSCIENCE,{
	if ((MaxWeight - Weight) < 1000 || !checkweight(1201, 1)) {
		mes "^FF0000 無法繼續任務因為你的東西太多或者太重了. ^000000";
		close;
	}
	.@all_quests_complete = true;
	for(.@i = 5113; .@i <= 5125; .@i++) {
		if (checkquest(.@i, HUNTING) != 2)
			.@all_quests_complete = false;
	}
	if (.@all_quests_complete) {
		// Quests Complete. Get Rewards.
		if (lght_duk01 == 8) {
			// First time beating the instance.
			mes "[查夫]";
			mes "你終於打敗了我的Boss級的怪物";
			next;
			mes "[查夫]";
			mes "終於.是這樣的結果?對他們來說, 沒有我實驗?他們有沒有騙我?";
			next;
			mes "[查夫]";
			mes "按鈕本來是用於緊急情況下可按下...但..";
			next;
			mes "[查夫]";
			mes "嘿..你和我遇到了一個不好的連接....";
			next;
			mes "[查夫]";
			mes "但是...... , 你幫我......你怎麼能這樣做呢?";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "當我第一次看見你, 你似乎瘋了, 但可悲的, 你還是進行了這些殘忍的實驗.";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "但實際上, 因為你太孤獨了";
			next;
			mes "[查夫]";
			mes "哈哈哈哈.寂寞......你說什麼?";
			next;
			mes "[查夫]";
			mes "它可以被看作像你.但是, 事實並非如此.孤獨是失敗者的只是一個良好的和容易的藉口";
			next;
			mes "[查夫]";
			mes "我很高興做完美的實驗, 我從來沒有感到孤獨.";
			next;
			mes "[查夫]";
			mes "無論如何, 因為我意識到,  Rekenber背叛了我, 我不能只讓他們做他們想要的東西.";
			next;
			mes "[查夫]";
			mes "現在, 我將離開他們獨自一人, 但我會讓他們哭泣和後悔解僱查夫";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "... .... ... ....";
			next;
			lght_duk01 = 9;
			callsub L_Reward;
			mes "[查夫]";
			mes "啊啊啊啊啊, "+ strcharinfo(0) +" 太謝謝你了, 殺死了我的試驗品.不是一個大禮物, 但我希望你不否認, 我不需要這個了.";
			next;
			mes "[查夫]";
			mes "而且, 如果你能......希望你回來的時候, 尤其是與您的朋友.";
			next;
			mes "我能感覺到, 他不再迷戀..";
			close2;
			warp "lhz_dun04", 147, 273;
			end;
		} else {
			mes "[查夫]";
			mes "你們這樣做, 打敗所有的生物.再次感謝您.";
			next;
			callsub L_Reward;
			mes "[查夫]";
			mes "我再次想說, "+ strcharinfo(0) +" 太謝謝你了, 殺死了我的試驗品.不是一個大禮物, 但我希望你不否認, 我不需要這個了.";
			next;
			mes "[查夫]";
			mes "再見.你想離開這裡嗎?";
			next;
			if (select("不", "是的") == 1) {
				mes "[查夫]";
				mes "等你想離開的時候告訴我";
				close;
			}
			mes "[查夫]";
			mes "那麼我將送你出去.";
			close2;
			warp "lhz_dun04", 147, 273;
			end;
		}
	} else {
		if (!.@all_quests_complete) {
			mes "[查夫]";
			mes "我一直在尋找, 不知道是誰做這個.....但它是如此複雜";
			next;
		} else {
			mes "[查夫]";
			mes "謝謝";
			next;
		}
		if (lght_duk01 == 7) {
			lght_duk01 = 8;
			mes "[查夫]";
			mes "當你苦苦掙扎...";
			next;
			mes "[查夫]";
			mes "我想通了一些東西.. , 有一個系統錯誤, 不能讓我們來去自由, 所以只能72個小時之後再回去了.在這之前你可以休息休息.";
			next;
			mes "[查夫]";
			mes "啊..我可以送你去我的實驗室.你想要什麼?";
			next;
		} else {
			mes "[查夫]";
			mes "你想離開這裡嗎?";
			next;
		}
		if (select("不", "是的") == 1) {
			mes "[查夫]";
			mes "等你想離開的時候告訴我";
			close;
		}
		mes "[查夫]";
		mes "那麼我將送你出去.";
		close2;
		warp "lhz_dun04", 147, 273;
		end;
	}

L_Reward:
	for(.@i = 5113; .@i <= 5125; .@i++) {
		erasequest .@i;
	}
	.@r = rand(1, 1000);
	if      (.@r < 84)  getitem 2582,  1; //Salvage_Cape
	else if (.@r < 167) getitem 18570, 1; //Ancient_Gold_Deco
	else if (.@r < 250) getitem 1490,  1; //Giant_Lance
	else if (.@r < 333) getitem 16017, 1; //Bloody_Cross
	else if (.@r < 416) getitem 1291,  1; //Guillotine_Katar
	else if (.@r < 499) getitem 1584,  1; //Chilly_Spell_Book
	else if (.@r < 582) getitem 6471, 10; //Goast_Chill
	else if (.@r < 665) getitem 6470, 10; //Blood_Thirst
	else if (.@r < 748) getitem 6469, 10; //Will_Of_Warrior
	else if (.@r < 831) getitem 6471, 20; //Goast_Chill
	else if (.@r < 914) getitem 6470, 20; //Blood_Thirst
	else                getitem 6469, 20; //Will_Of_Warrior
	return;

OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("查夫#last");
	end;

OnEnable:
	enablenpc instance_npcname("查夫#last");
	end;
}
