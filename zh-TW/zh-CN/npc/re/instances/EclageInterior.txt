//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================

//===== rAthena Script =======================================
//= Eclage Instance Script
//===== By: ================================================== 
//= Dastgir
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Eclage Interior Instance
//===== Additional Comments: ================================= 
//= 1.0 Converted from the official Aegis script. [Dastgir]
//= 1.1 Converted from Hercules script to rAthena script & Small bug fix. [Skorm]
//============================================================ 

ecl_hub01,132,12,3	script	參謀長#tl02	435,{
	.@party_id = getcharid(1);
	.@md_name$ = "Eclage Interior";

	if (.@party_id == 0 || getpartyleader(.@party_id,2) != getcharid(0)){
		mes "-! 警告 !-";
		mes "當前的任務將在紀念地牢裡進行.";
		mes "只有組隊隊長可以進入. 請創建你的隊伍.";
		close;
	}
	if (ep14_2_mylord == 29) {
		mes "[神艾美]";
		mes "這個地方設置了能量球照明.";
		mes "如果你喜歡這裡, 你應該在這裡保持謹慎.";
		next;
		switch (select("進入:放棄")) {
		case 1:
			if (instance_create(.@md_name$) < 0) {
				mes "[神艾美]";
				mes "不久之前有一些士兵由市長派遣出去了.";
				mes "我應該相信你不是那個他們在尋找的人, 對嗎?";
				close;
			}
			ecl_interior_time = gettimetick(2) + 20 * 60;
			mes "[神艾美]";
			mes "由於內部安全問題較為緊張, 你自己應該有所準備.";
			mes "在這裡等一會兒.";
			close;
		case 2:
			mes "[神艾美]";
			mes "這可不是一個簡單的任務.";
			close;
		}
	}
	mes "[參謀長]";
	mes "嗯? 這個房間?";
	mes "沒關係. 不要擔心.";
	close;
}

ecl_hub01,130,15,0	script	它是關閉的.	844,{
	if (ep14_2_mylord == 29) {
		mes "這扇門連接到廣場的天花板.";
		mes "能量球照明設置的地方看上去更漂亮.";
		next;
		if (select("進入:放棄") == 1) {
			.@party_id = getcharid(1);
			.@md_name$ = "Eclage Interior";
			if (.@party_id == 0 || getpartyleader(.@party_id,2) != getcharid(0)) {
				mes "-!警告 !-";
				mes "當前的任務將在紀念地牢裡進行.";
				mes "只有組隊隊長可以進入. 請創建你的隊伍.";
				close;
			}
			if (gettimetick(2) >= ecl_interior_time){
				mes "[神艾美]";
				mes "裡面的士兵看上去真的很緊張.";
				mes "引導一個像你這樣的局外人是相當困難的.";
				close;
			}
			switch (instance_enter(.@md_name$)) {
			case 3:
				mes "一個未知的錯誤出現.";
				close;
			case 2:
				mes "關閉?";
				close;
			case 0:
				mapannounce "ecl_hub01",getpartyname(.@party_id) + " 組隊隊長 " + strcharinfo(0) + " 進入 " + .@md_name$,bc_map,"0x00ff99";
				end;
			default:
			}
		} else
			close;
	}
	mes "這扇門連接到廣場的天花板.";
	mes "能量球照明設置的地方看上去更漂亮.";
	mes "目前關閉中.";
	close;
}

1@ecl,58,69,3	script	神艾美#ecl01	435,{
	mes "[神艾美]";
	mes "哦, 這裡發生什麼事了?";
	mes "他們破壞了市長派出去的軍隊?";
	next;
	select("什麼...?:我打了怪物!");
	mes "[神艾美]";
	mes "沒關係.";
	mes "你的工作就此為止.";
	mes "現在你是一個背叛者.";
	mes "別了!";
	specialeffect EF_BEGINSPELL3;
	next;
	mes "[" + strcharinfo(0) + "]";
	mes "沒...門!!!";
	specialeffect2 EF_LORD;
	percentheal -99,0;
	soundeffect "wander_man_move.wav",1;
	sc_start SC_BLIND,60000,0;
	close2;
	warp instance_mapname("1@ecl"),146,95;
	end;

OnInstanceInit:
	hideonnpc instance_npcname(strnpcinfo(0));
	end;

OnEnable:
	hideoffnpc instance_npcname(strnpcinfo(0));
	end;
}

1@ecl,51,64,1	script	可疑的傢伙#1	2375,{
	end;
}
1@ecl,43,67,3	duplicate(可疑的傢伙#1)	可疑的傢伙#2	2375,0,0
1@ecl,55,80,3	duplicate(可疑的傢伙#1)	可疑的傢伙#3	2375,0,0
1@ecl,60,75,3	duplicate(可疑的傢伙#1)	可疑的傢伙#4	2375,0,0

1@ecl,42,80,5	script	可疑的傢伙#5	2376,{
	end;
OnInstanceInit:
	hideonnpc instance_npcname(strnpcinfo(0));
	end;
}
1@ecl,48,86,3	duplicate(可疑的傢伙#5)	可疑的傢伙#6	2376,0,0
1@ecl,58,82,3	duplicate(可疑的傢伙#5)	可疑的傢伙#7	2376,0,0
1@ecl,56,88,3	duplicate(可疑的傢伙#5)	可疑的傢伙#8	2376,0,0

1@ecl,53,83,0	script	#sndmaster	HIDDEN_WARP_NPC,10,10,{
	end;

OnInstanceInit:
	'mob_summoned = 0;
	disablenpc instance_npcname(strnpcinfo(0));
	end;

OnEnable:
	sleep 1000;
    mapannounce instance_mapname("1@ecl"),"警告: 在照明室有異常現象. 在照明室有異常現象.",bc_map,"0xffff00"; //FW_NORMAL 12 0 0
	sleep 2000;
    mapannounce instance_mapname("1@ecl"),"可疑生物: 呵....哇哇!!",bc_map,"0xffff00"; //FW_NORMAL 12 0 0
	hideoffnpc instance_npcname("可疑的傢伙#5");
	hideoffnpc instance_npcname("可疑的傢伙#6");
	hideoffnpc instance_npcname("可疑的傢伙#7");
	hideoffnpc instance_npcname("可疑的傢伙#8");
	sleep 3000;
	hideonnpc instance_npcname("#sndmaster");
	end;

OnMyMobDead:
	if (mobcount(instance_mapname("1@ecl"),instance_npcname("#sndmaster")+"::OnMyMobDead") == 0) {
		donpcevent instance_npcname("神艾美#ecl01")+"::OnEnable";
		mapannounce instance_mapname("1@ecl"),"神艾美: 好的, 我及時趕到了.",bc_map,"0xffff00"; //FW_NORMAL 12 0 0
	}
	end;

OnTouch:
	if ('mob_summoned > 1) end;
    mes "在能量球附近消除入侵者, 其他奇怪的生物又出現了.";
    next;
    mes "我們無法與他們溝通, 其中一個因為發現他們的夥伴戰敗了看上去有點憤怒 .";
    next;
    mes "[" + strcharinfo(0) + "]";
    mes "他們自認為他們是陌若可的化身.";
    mes "能量球的後面是多蒙的國王嗎?";
    mes "或者只是巧合?";
    next;
    mes "[多蒙國王的下屬]";
    mes "啊...啊!!!";
    mes "哇!";
    next;
    mes "一場戰鬥在所難免!";
    mes "他們會殺了我的!";
	close2;
	hideonnpc instance_npcname("可疑的傢伙#5");
	hideonnpc instance_npcname("可疑的傢伙#6");
	hideonnpc instance_npcname("可疑的傢伙#7");
	hideonnpc instance_npcname("可疑的傢伙#8");
	disablenpc instance_npcname("可疑的傢伙#5");
	disablenpc instance_npcname("可疑的傢伙#6");
	disablenpc instance_npcname("可疑的傢伙#7");
	disablenpc instance_npcname("可疑的傢伙#8");
	.@eclmap$ = instance_mapname("1@ecl");
	.@sndmaster$ = instance_npcname("#sndmaster");
	monster .@eclmap$,42,80,"多蒙國王的下屬",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,48,86,"多蒙國王的下屬",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,59,82,"多蒙國王的下屬",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,56,88,"多蒙國王的下屬",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,39,72,"多蒙國王的下屬",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,42,69,"多蒙國王的下屬",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,47,64,"多蒙國王的下屬",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,50,60,"多蒙國王的下屬",2376,1,.@sndmaster$+"::OnMyMobDead";
	'mob_summoned = 2;
    end;
}

1@ecl,57,65,0	script	#event	139,5,5,{
	end;
OnTouch:
	if ('mob_summoned > 0) end;
	.@eclmap$ = instance_mapname("1@ecl");
	.@eventname$ = instance_npcname("#event");
	mes "房間空無一物.";
	mes "燈光從房間中央的一個圓形設備散發出來.";
	next;
	mes "[可疑生物]";
	mes "呵呵......";
	next;
	mes "一群未知生物正在逼近.";
	mes "他們看上去很吃驚,雖然彩虹橋被保留著, 但是很難去聯繫.";
	next;
	mes "[可疑生物]";
	mes "啊...咔...哇!!";
	next;
	mes "似乎他們馬上要發起進攻了.";
	mes "我們忍不住要去他們戰鬥了!";
	close2;
	hideonnpc instance_npcname("可疑的傢伙#1");
	hideonnpc instance_npcname("可疑的傢伙#2");
	hideonnpc instance_npcname("可疑的傢伙#3");
	hideonnpc instance_npcname("可疑的傢伙#4");
	disablenpc instance_npcname("可疑的傢伙#1");
	disablenpc instance_npcname("可疑的傢伙#2");
	disablenpc instance_npcname("可疑的傢伙#3");
	disablenpc instance_npcname("可疑的傢伙#4");
	monster .@eclmap$,51,64,"可疑的傢伙",2375,1,.@eventname$+"::OnMyMobDead";
	monster .@eclmap$,43,67,"可疑的傢伙",2375,1,.@eventname$+"::OnMyMobDead";
	monster .@eclmap$,55,80,"可疑的傢伙",2375,1,.@eventname$+"::OnMyMobDead";
	monster .@eclmap$,60,75,"可疑的傢伙",2375,1,.@eventname$+"::OnMyMobDead";
	'mob_summoned = 1;
	end;

OnMyMobDead:
	if (mobcount(instance_mapname("1@ecl"),instance_npcname("#event")+"::OnMyMobDead") == 0) {
		enablenpc instance_npcname("#sndmaster");
		donpcevent instance_npcname("#sndmaster")+"::OnEnable";
	}
	end;
}

1@ecl,148,97,3	script	阿凡塔#	618,{
	mes "[阿凡塔]";
	mes "嘿, 看看誰在這兒!";
	mes "你是我兒子的朋友, 對嗎?";
	mes "多麼神奇的緣分把我們一起帶到了這裡!";
	cutin "avant01",1;
	next;
	mes "[阿凡塔]";
	mes "你發生了什麼事嗎?";
	mes "那裡有一個微妙和神奇的回聲.";
	mes "你馬上被扔在這裡了.";
	next;
	if (select("都是因為你!:神艾美說...") == 1) {
		mes "[阿凡塔]";
		mes "我不太明智你所說的一個單詞.";
		mes "你不能整理下你的故事嗎? 你不知道 '5個W 和 1個H' 規則嗎?";
		next;
	}
	mes "[阿凡塔]";
	mes "哦...這就是發生的事情.";
	mes "正如你所說的, 神艾美在能量球的後面?";
	next;
	mes "[阿凡塔]";
	mes "呵呵...哈哈";
	mes "好的, 好的...他也回家了?";
	mes "我, 所有人的阿凡塔?! 哈哈!";
	cutin "avant02",1;
	next;
	select("你什麼意思?");
	mes "[阿凡塔]";
	mes "我在做一個關於能量球的研究.";
	mes "但神艾美一直慫恿我.";
	cutin "avant01",1;
	next;
	mes "[阿凡塔]";
	mes "我從能量球的審美開始去思考, 然後對於能量球內部累計的能量感到疑惑...";
	mes "因為我們都受益於它, 所以我開始探索能量球的原則.";
	next;
	mes "[阿凡塔]";
	mes "所有的拉菲都認為能量球和空氣，水一樣是很自然的事物, 但他是第一個讓我感覺到不同並且想追根溯源.";
	mes "是的...神艾美. 他故意激發了我的好奇心.";
	next;
	mes "[阿凡塔]";
	mes "刺激一個不了解空氣和水的人，讓他認識並進行調查!";
	mes "好, 好...那不是很有趣!";
	cutin "avant02",1;
	next;
	mes "[阿凡塔]";
	mes "哈...哈哈...";
	mes "...我真的很生氣, 我意識到他是如何利用我的，但更糟糕的是，我做了一些對大家可怕的事情.";
	mes "我現在真的非常生氣.";
	next;
	select("你有良心嗎?");
	mes "[阿凡塔]";
	mes "你認為我是一個怎樣的人, 說真的!";
	mes "這是因為我的兒子嗎?";
	mes "好的, 那麼, 我不能過於苛責你.";
	cutin "avant01",1;
	next;
	mes "[阿凡塔]";
	mes "...哈.";
	mes "這可不好笑.";
	mes "它不再有趣了。我不再對你感興趣了.";
	next;
	mes "[阿凡塔]";
	mes "......";
	mes "似乎有位訪客.";
	next;
	cutin "minuel01",4;
	mes "[敏奴艾]";
	mes "" + strcharinfo(0) + "Sir,";
	mes "因為懷疑最近在照明室的事件與你有關, 所以你被拘留了";
	mes "有目擊證人可以證明你的清白.";
	next;
	mes "[敏奴艾]";
	mes "細節我不清楚,";
	mes "但我相信你是無罪的.";
	mes "傷口似乎傷地很嚴重, 我帶你去治療室.";
	next;
	mes "[敏奴艾]";
	mes "我想著不是什麼大不了的事情.";
	mes "...希望吧.";
	mes "我們走吧. 我會引導你.";
	close2;
	cutin "",255;
	warp "1@ecl",146,29;
	end;
}
	
1@ecl,148,29,3	script	洛基#ecl01	512,{
	mes "[洛基]";
	mes "我旁邊的這個傢伙似乎有很多話要說.";
	mes "為什麼你不先和他解釋下呢?";
	close;
}	
	
1@ecl,147,30,3	script	守護者努德霍格#ecl01	510,{
	mes "[守護者努德霍格]";
	mes "我想你應該現和海西談談.";
	close;
}
	
1@ecl,144,27,5	script	海西#ecl01	623,{
	cutin "hisie01",0;
	mes "[海西]";
	mes "你能告訴我到底發生了什麼嗎?";
	next;
	select("我與怪獸作戰.");
	cutin "hisie03",0;
	mes "[海西]";
	mes "怪獸? 夢羅克的屬下?";
	mes "拉菲, 為何你想要殺死我們的士兵!!";
	mes "地球上到底發生了什麼?";
	npcskill "AL_HEAL",9,99,80;
	next;
	cutin "",255;
	mes "[洛基]";
	mes "是因為這個原因.";
	mes "" + strcharinfo(0) + "它是身體內部 -";
	mes "現在神艾美是多麼強大啊.";
	next;
	mes "洛基的手掌上, 有一根小而乾燥的刺.";
	next;
	cutin "hisie03",0;
	mes "[海西]";
	mes "神艾美, 隊員們的頭?";
	mes "為什麼他會牽扯到這個事情上去?";
	npcskill "AL_HEAL",9,99,80;
	next;
	cutin "",255;
	mes "[洛基]";
	mes "神艾美是夢克羅的屬下之一.";
	mes "通過他身體的一部分, 他的能量能夠直接或者間接地控制其他人.";
	next;
	select("現在讓我考慮下...");
	mes "[" + strcharinfo(0) + "]";
	mes "來這裡之前, 神艾美伸出他的手給我. 當我握住他的手, 我感覺到了火花.";
	mes "我想著只是我的幻覺...也許是當他...";
	next;
	mes "[努德霍格]";
	mes "你也許是對的.";
	mes "他們用了卑鄙的手段.";
	mes "這根刺也許會混淆你的判斷.";
	next;
	cutin "hisie03",0;
	mes "[海西]";
	mes "你的意思是他欺騙了我們大家?";
	mes "和我們在一起那麼久的拉菲是隊員們的頭! 他不可能是...";
	npcskill "AL_HEAL",9,99,80;
	next;
	cutin "",255;
	mes "[洛基]";
	mes "他可能是在良好的環境下.";
	mes "不管怎樣, 這傢伙是無罪的.";
	mes "我能夠證明.";
	next;
	mes "[努德霍格]";
	mes "我以宇宙樹監護人的名義發誓.";
	mes "" + strcharinfo(0) + "- 絕不會做這樣的事.";
	next;
	cutin "hisie02",0;
	mes "[海西]";
	mes "哈...";
	mes "但是事情已經脫離掌控了.";
	mes "這是我沒有料到的.";
	next;
	select(" 能量球發生了什麼?");
	mes "[海西]";
	mes "內核被偷了.";
	mes "沒人知道能量球的內核不見了,";
	mes "那就是為什麼卡爾沒有成功的原因.";
	npcskill "AL_HEAL",9,99,80;
	next;
	mes "[海西]";
	mes "還有, 一些士兵因為你受傷了.";
	mes "幸好沒有陣亡, 但大部分傷勢比較嚴重.";
	mes "目前情況不妙.";
	next;
	cutin "",255;
	mes "[洛基]";
	mes "有人得擔負這個責任.";
	mes "所有可能性," + strcharinfo(0) + "會是你.";
	next;
	cutin "hisie02",0;
	mes "[海西]";
	mes "......有2位男士付清了有關能量球方面的所有費用,";
	mes "但你和卡爾在所難免要承擔你們的責任.";
	mes "你會趕上神艾美的.";
	npcskill "AL_HEAL",9,99,80;
	next;
	cutin "hisie01",0;
	mes "[海西]";
	mes "你似乎已經休息夠了. 讓我們出發吧.";
	mes "卡爾在等你呢.";
	mes "你們兩個要做什麼?";
	next;
	cutin "",255;
	mes "[洛基]";
	mes "不用為我們擔心.";
	mes "我們的目標是魔族國王夢克羅.";
	mes "太好了, 我們在這裡發現了一下證據.";
	next;
	cutin "hisie01",0;
	mes "[海西]";
	mes "感謝你的幫助.";
	mes "現在我們要離開了.";
	mes "我們走吧." + strcharinfo(0) + ".";
	ep14_2_mylord = 30;
	changequest 7450,7451;
	close2;
	cutin "",255;
	warp "ecl_in03",57,76;
	instance_destroy;
	end;
}
