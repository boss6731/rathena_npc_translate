//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================

//===== rAthena Script =======================================
//= Bangungot Hospital 2F
//===== By: ================================================== 
//= Euphy
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Save the hospital in Port Malaya from Bangungot.
//= Part of the "Nurse in Port Malaya" quest.
//===== Additional Comments: ================================= 
//= 1.0 First version. [Euphy]
//= 1.1 Added GM management function. [Euphy]
//============================================================ 

// Instance Creation
//============================================================
ma_dun01,147,10,5	script	護士#ma_dun01	569,{
	if (BaseLevel < 100) end;

	set .@party_id,getcharid(1);
	cutin "malaya_nurseB",2;
	if (checkquest(9223) == -1) {
		set .@quest, checkquest(9222);
		if (.@quest == -1) {
			if (!.@party_id) {
				if (malaya_bang == 30)
					callsub L_Closed,1;
				else if (malaya_bang == 31)
					callsub L_Closed,0;
				else
					goto L_NoParty;
			}
			if (checkquest(11309,HUNTING) < 2) {
				set .@playtime, checkquest(9224,PLAYTIME);
				if (.@playtime == -1) {
					if (malaya_bang == 30)
						callsub L_Closed,1;
					else if (malaya_bang == 31)
						callsub L_Closed,0;
					else if (malaya_bang > 39)
						callsub L_Enter,(getcharid(0) == getpartyleader(.@party_id,2))?1:0,0,0;
					else {
						mes "[護士瑪恩尼]";
						mes "..........";
						close2;
						cutin "",255;
						end;
					}
				} else if (.@playtime == 0 || .@playtime == 1) {
					if (checkquest(11309) == -1) {
						mes "[護士瑪恩尼]";
						mes "這是一個嚴重的情況.";
						close2;
						cutin "",255;
						end;
					} else {
						mes "[護士瑪恩尼]";
						mes "現在通向二樓的大門";
						mes "已經關閉.";
						mes "請稍候嘗試.";
						next;
						mes "[護士瑪恩尼]";
						mes "如果想再次進入二樓,";
						mes "需要在冷卻時間過後,";
						mes "因此";
						mes "請稍後再來.";
						close2;
						cutin "",255;
						end;
					}
				} else {
					set .@quest2, checkquest(11309);
					if (.@quest2 == -1) {
						callsub L_Closed,0;
					} else if (.@quest2 == 0 || .@quest2 == 1) {
						callsub L_Complete;
						completequest 9224;
						erasequest 9224;
						close2;
						cutin "",255;
						end;
					} else {
						//if (!.@party_id) goto L_NoParty; //unneeded
						if (malaya_bang > 39)
							callsub L_Enter,(getcharid(0) == getpartyleader(.@party_id,2))?1:0,1,1;
						else {
							mes "[護士瑪恩尼]";
							mes ".......";
							close2;
							cutin "",255;
							end;
						}
					}
				}
			} else {
				mes "[護士瑪恩尼]";
				mes "你擺脫了夢魘!"; //?
				mes "護士有可能會在醫院門口等著你.";
				mes "請去試試!";
				close2;
				cutin "",255;
				end;
			}
		} else if (.@quest == 0 || .@quest == 1) {
			if (!.@party_id) goto L_NoParty;
			if (malaya_bang > 39) {
				set .@hunting, checkquest(9222,HUNTING);
				if (.@hunting == 0 || .@hunting == 1) {
					set .@playtime, checkquest(9224,PLAYTIME);
					if (.@playtime == 0 || .@playtime == 1) {
						mes "[護士瑪恩尼]";
						mes "你想再次去到二樓?";
						next;
						callsub L_Enter,0,0;
					} else if (.@playtime == 2) {
						//if (!.@party_id) goto L_NoParty; //unneeded
						if (malaya_bang > 39)
							callsub L_Enter,(getcharid(0) == getpartyleader(.@party_id,2))?1:0,1,1;
						else {
							mes "[護士瑪恩尼]";
							mes ".......";
							close2;
							cutin "",255;
							end;
						}
					} else {
						mes "[護士瑪恩尼]";
						mes "這是一個致命的情況.";
						mes "你不能去二樓.";
						close2;
						cutin "",255;
						end;
					}
				} else {
					if (checkweight(607,1) == 0) {
						mes "[護士瑪恩尼]";
						mes "你有太多的東西.";
						mes "請減少你攜帶的物品,";
						mes "然後再來.";
						close2;
						cutin "",255;
						end;
					}
					if (checkweight(607,11) == 0) {
						mes "[護士瑪恩尼]";
						mes "你的負重不夠.";
						mes "請控制你攜帶的物品,";
						mes "然後再來.";
						close2;
						cutin "",255;
						end;
					}
					callsub L_Complete;
					completequest 9222;
					erasequest 9222;
					completequest 9224;
					erasequest 9224;
					setquest 9223;
					getexp 500000,0;
//					if (IsPremiumPcCafe == 10)
						getitem 6499,7;	getitem 10707,1;
//					else
//						getitem 6499,5; //Ancient_Grudge
					close2;
					cutin "",255;
					end;
				}
			} else {
				mes "[護士瑪恩尼]";
				mes ".....?";
				mes "現在, 你無法進入二樓.";
				mes "請稍後再來.";
				close2;
				cutin "",255;
				end;
			}
		} else {
			callsub L_Complete;
			completequest 9222;
			erasequest 9222;
			completequest 9224;
			erasequest 9224;
			setquest 9223;
			close2;
			cutin "",255;
			end;
		}
	} else {
		if (checkquest(9223,PLAYTIME) == 0 || checkquest(9223,PLAYTIME) == 1) {
			callsub L_Complete;
			close2;
			cutin "",255;
			end;
		} else {
			if (!.@party_id) goto L_NoParty;
			if (malaya_bang > 39)
				callsub L_Enter,(getcharid(0) == getpartyleader(.@party_id,2))?1:0,1,1;
			else {
				mes "[護士瑪恩尼]";
				mes ".......";
				close2;
				cutin "",255;
				end;
			}
		}
	}

L_Closed:
	mes "[護士瑪恩尼]";
	mes "- 通向一樓的大門關閉了";
	mes "當見到護士出現的時候,";
	mes "我在她身後叫了她一聲.";
	mes "但是她卻沒有任何反應. -";
	if (getarg(0)) {
		set malaya_bang,31;
		changequest 11302,11303;
	}
	close2;
	cutin "",255;
	end;

L_NoParty:
	mes "- 我想...";
	mes "我不能單獨和他說話.";
	mes "等他組了一支隊伍後";
	mes "再讓我告訴他一些情報吧. -";
	close2;
	cutin "",255;
	end;

L_Enter:
	set .@md_name$,"Bangungot Hospital 2F";
	if (getarg(2) == 0) {
		mes "[護士瑪恩尼]";
		mes "我已經聽說了那個故事";
		mes "所以我非常了解.";
		mes "接下來你準備怎麼做?";
		mes "你準備";
		mes "上二樓嗎?";
	} else {
		mes "[護士瑪恩尼]";
		mes "你回來了啊...";
		mes "有個壞消息.";
		mes "我聽說";
		mes "噩夢死神";
		mes "又出現了.";
		next;
		mes "[護士瑪恩尼]";
		mes strcharinfo(0) + "的幫助.";
		mes "請再給我一次機會,";
		mes "好嗎?";
	}
	next;
	switch(select(((getarg(0))?"準備進入二樓.":"")+":進入二樓.:離開.")) {
	case 1:
		if (instance_create(.@md_name$) < 0) {
			mes "[護士瑪恩尼]";
			mes "發生了一件非常嚴重的事情.";
			mes "你不能到二樓去.";
			close2;
			cutin "",255;
			end;
		}
		mes "[護士瑪恩尼]";
		mes "我們";
		mes "準備到二樓去了.";
		mes "那兒有一個說不清楚的傳言.";
		mes "死去的人,並沒有真正的死亡.";
		mes "請記住這句話吧.";
		mes "...";
		next;
		mes "[護士瑪恩尼]";
		mes "當你都準備好了的時候,";
		mes "請告訴我一聲.";
		mes "我會非常感謝的.";
		close2;
		cutin "",255;
		end;
	case 2:
		switch(instance_enter(.@md_name$)) {
		case 3:
			mes "[護士瑪恩尼]";
			mes "發生了一件非常嚴重的事情.";
			mes "你不能到二樓去.";
			close2;
			cutin "",255;
			end;
		case 2:
			mes "[護士瑪恩尼]";
			mes "現在";
			mes "你還不能到二樓去.";
			close2;
			cutin "",255;
			end;
		case 1:
			mes "[護士瑪恩尼]";
			mes "一個人去二樓";
			mes "太危險了.";
			mes "至少組織一個人和你一起";
			mes "再過來吧.";
			close2;
			cutin "",255;
			end;
		case 0:
			mapannounce "ma_dun01", getpartyname(getcharid(1))+" 小隊成員 "+strcharinfo(0)+" 進入了 "+.@md_name$+".",bc_map,"0x00ff99";
			if (getarg(1)) {
				if (checkquest(9223) > -1) {
					completequest 9223;
					erasequest 9223;
				}
				if (checkquest(9222) > -1)
					erasequest 9222;
				if (checkquest(9224) > -1)
					erasequest 9224;
				setquest 9222;
			}
			setquest 9224;
			cutin "",255;
			//warp "1@ma_h",40,157;
			end;
		}
	case 3:
		mes "[護士瑪恩尼]";
		mes "好吧.";
		mes "如果你有新的想法了,再來找我吧.";
		close2;
		cutin "",255;
		end;
	}

L_Complete:
	mes "[護士瑪恩尼]";
	mes "你竟然從二樓安全的回來啦!!";
	mes "不過";
	mes "這並沒有真正的結束.";
	next;
	mes "[護士瑪恩尼]";
	mes "我非常肯定";
	mes "事情絕不會這麼輕易的結束.";
	mes "關於噩夢死神";
	mes "我不知道接下來還會有什麼情況發生";
	mes "所以,請過一會兒";
	mes "再來找我.";
	return;
}


sec_in02,27,30,0	script	夢魘之門	406,{
	mes "密碼";
	next;
	if (callfunc("F_GM_NPC","1854",1) == 0) {
		mes "祝你好運.";
		close;
	}
	switch(select("Receive 9222:Receive 9223:Erase 9222:Erase 9223:Receive 9224:Erase 9224:Receive malaya_bang 30:Receive malaya_bang 40:Receive 11309:Erase 11309:Cancel")) {
		case 1: setquest 9222; close;
		case 2: setquest 9223; close;
		case 3: erasequest 9222; close;
		case 4: erasequest 9223; close;
		case 5: setquest 9224; close;
		case 6: erasequest 9224; close;
		case 7: set malaya_bang,30; close;
		case 8: set malaya_bang,40; close;
		case 9: setquest 11309; close;
		case 10: completequest 11309; close;
		case 11: close;
	}
}

// Instance Scripts
//============================================================
1@ma_h,43,157,0	script	#Memorial Start	139,2,2,{
	end;
OnTouch:
	set .@map$, instance_mapname("1@ma_h");
	mapannounce .@map$,"噩夢死神: 你這個蠢貨 "+strcharinfo(0)+" ....",bc_map,"0xFF82FF";
	mapannounce .@map$,"噩夢死神: 在還沒死之前, 快點滾出這裡...",bc_map,"0xFF82FF";
	areamonster .@map$,44,156,46,158,"噩夢死神的噩夢巫師",2339,1,instance_npcname("#Memorial Start")+"::OnMyMobDead";
	donpcevent instance_npcname("#Memorial Start_time")+"::OnEnable";
	end;
OnMyMobDead:
	initnpctimer;
	end;
OnTimer1000:
	set .@map$, instance_mapname("1@ma_h");
	if (mobcount(.@map$,instance_npcname("#Memorial Start")+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("#Ward1 Entry")+"::OnEnable1";
		donpcevent instance_npcname("#Memorial Start_time")+"::OnDisable";
		mapannounce .@map$,"噩夢死神: 這裡, 就是你噩夢的開始...",bc_map,"0xFF82FF";
		disablenpc instance_npcname("#Memorial Start");
	}
	stopnpctimer;
	end;
}

1@ma_h,43,157,0	script	#Memorial Start_time	139,{ //2,2
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Memorial Start_time");
	end;
OnEnable:
	specialeffect EF_BAT2,AREA,instance_npcname("#Memorial Start"); 
	initnpctimer;
	end;
OnDisable:
	stopnpctimer;
	end;
OnTimer5000:
	mapannounce instance_mapname("1@ma_h"),"噩夢死神: 一點兒作用也沒有 ... 你不可能打敗我.",bc_map,"0xFF82FF";
	end;
OnTimer10000:
	mapannounce instance_mapname("1@ma_h"),"噩夢死神: 哈哈哈哈哈哈!!!!",bc_map,"0xFF82FF";
	stopnpctimer;
	end;
}

1@ma_h,46,165,0	script	#Ward1 Entry	139,{ //3,3
	end;
OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnEnable1:
	set .@map$, instance_mapname("1@ma_h");
	set .@label$, instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	enablenpc instance_npcname(strnpcinfo(0));
	specialeffect EF_BAT2;
	switch(atoi(charat(strnpcinfo(2),4))) {
	case 1:
		areamonster .@map$,49,171,51,173,"噩夢死神的噩夢巫師",2339,1,.@label$;
		areamonster .@map$,51,171,53,173,"噩夢死神的噩夢巫師",2339,1,.@label$;
		end;
	case 2:
		areamonster .@map$,49,137,51,139,"噩夢死神的噩夢巫師",2339,1,.@label$;
		areamonster .@map$,51,137,53,139,"噩夢死神的噩夢巫師",2339,1,.@label$;
		end;
	case 3:
		areamonster .@map$,69,171,71,173,"噩夢死神的噩夢巫師",2339,1,.@label$;
		areamonster .@map$,71,171,73,173,"噩夢死神的噩夢巫師",2339,1,.@label$;
		areamonster .@map$,73,171,75,173,"噩夢死神的噩夢巫師",2339,1,.@label$;
		end;
	case 4:
		areamonster .@map$,71,139,73,141,"噩夢死神的噩夢巫師",2339,1,.@label$;
		areamonster .@map$,73,139,75,141,"噩夢死神的噩夢巫師",2339,1,.@label$;
		areamonster .@map$,75,139,77,141,"噩夢死神的噩夢巫師",2339,1,.@label$;
		end;
	case 5:
		areamonster .@map$,93,173,95,175,"噩夢死神的噩夢巫師",2339,1,.@label$;
		areamonster .@map$,95,173,97,175,"噩夢死神的噩夢巫師",2339,1,.@label$;
		areamonster .@map$,97,173,99,175,"噩夢死神的噩夢巫師",2339,1,.@label$;
		areamonster .@map$,93,175,95,177,"噩夢死神的噩夢巫師",2339,1,.@label$;
		end;
	}
/*
// Not called in official scripts.
OnEnable2:
	areamonster instance_mapname("1@ma_h"),140,38,142,40,"噩夢死神的噩夢巫師",2339,1,instance_npcname("#Ward1 Entry")+"::OnMyMobDead";
	end;
*/
OnEnable3:
	set .@i, atoi(charat(strnpcinfo(2),4))+1;
	donpcevent instance_npcname("#Ward"+.@i+" Entry")+"::OnEnable1";
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnMyMobDead:
	initnpctimer;
	end;
OnTimer1000:
	set .@map$, instance_mapname("1@ma_h");
	if (mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMyMobDead") < 1) {
		specialeffect EF_BAT2;
		donpcevent instance_npcname(strnpcinfo(0))+"::OnEnable3";
		switch(atoi(charat(strnpcinfo(2),4))) {
			case 1: set .@str$,"噩夢死神: 我會讓你知道不甘與痛苦的滋味, 那是多麼的真實......"; break;
			case 2: set .@str$,"噩夢死神: 扭曲的信仰, 是我的生命和力量的源泉..."; break;
			case 3: set .@str$,"噩夢死神: 喊吧, 叫吧, 釋放出你的痛苦..."; break;
			case 4: set .@str$,"噩夢死神: 痛嗎, 再痛苦一些..."; break;
			case 5: set .@str$,"噩夢死神: 恐懼吧...吃驚了嗎."; break;
		}
		mapannounce .@map$,.@str$,bc_map,"0xFF82FF";
	}
	stopnpctimer;
	end;
}
1@ma_h,57,147,0	duplicate(#Ward1 Entry)	#Ward2 Entry	139 //3,3
1@ma_h,67,165,0	duplicate(#Ward1 Entry)	#Ward3 Entry	139 //3,3
1@ma_h,79,147,0	duplicate(#Ward1 Entry)	#Ward4 Entry	139 //3,3
1@ma_h,90,165,0	duplicate(#Ward1 Entry)	#Ward5 Entry	139 //3,3

1@ma_h,101,147,0	script	#Ward6 Entry	139,{ //2,2
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Ward6 Entry");
	end;
OnEnable1:
	set .@map$, instance_mapname("1@ma_h");
	set .@label$, instance_npcname("#Ward6 Entry")+"::OnMyMobDead";
	enablenpc instance_npcname("#Ward6 Entry");
	specialeffect EF_BAT2;
	areamonster .@map$,93,137,95,139,"噩夢死神的噩夢巫師",2339,1,.@label$;
	areamonster .@map$,95,137,97,139,"噩夢死神的噩夢巫師",2339,1,.@label$;
	areamonster .@map$,97,137,99,139,"噩夢死神的噩夢巫師",2339,1,.@label$;
	areamonster .@map$,93,139,95,141,"噩夢死神的噩夢巫師",2339,1,.@label$;
	areamonster .@map$,95,139,97,141,"噩夢死神的噩夢巫師",2338,1,.@label$;
	end;
/*
// Not called in official scripts.
OnEnable2:
	areamonster instance_mapname("1@ma_h"),140,38,142,40,"噩夢死神的噩夢巫師",2339,1,instance_npcname("#Ward6 Entry")+"::OnMyMobDead";
	end;
*/
OnMyMobDead:
	initnpctimer;
	end;
OnTimer3000:
	set .@map$, instance_mapname("1@ma_h");
	if (mobcount(.@map$,instance_npcname("#Ward6 Entry")+"::OnMyMobDead") < 1) {
		specialeffect EF_BAT2;
		donpcevent instance_npcname("#Summon Boss")+"::OnEnable";
		mapannounce .@map$,"噩夢死神的噩夢巫師: 雖然你可以選擇來這裡, 但你是不是能夠出去卻是由我來決定的...",bc_map,"0xFF82FF";
	}
	stopnpctimer;
	end;
}

1@ma_h,110,177,0	script	#Summon Boss	139,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Summon Boss");
	end;
OnEnable:
	enablenpc instance_npcname("#Summon Boss");
	monster instance_mapname("1@ma_h"),143,158,"噩夢死神",2317,1,instance_npcname("#Summon Boss")+"::OnMyMobDead";
	donpcevent instance_npcname("#Boss Room Entry")+"::OnEnableEntry";
	donpcevent instance_npcname("#Boss Room Entry")+"::OnEnable";
	end;
OnMyMobDead:
	initnpctimer;
	end;
OnTimer1000:
	if (mobcount(instance_mapname("1@ma_h"),instance_npcname("#Summon Boss")+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("#Summon Pillar")+"::OnEnable1";
		mapannounce instance_mapname("1@ma_h"),"噩夢死神: 我...不會...就這樣...罷休的.",bc_map,"0xFF82FF";
		disablenpc instance_npcname("#Summon Boss");
	}
	stopnpctimer;
	end;
}

1@ma_h,147,143,0	script	#Boss Room Entry	139,10,10,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Boss Room Entry");
	end;
OnTouch:
	specialeffect EF_GHOST;
	donpcevent instance_npcname("#Boss Room Entry")+"::OnDisableEntry";
	donpcevent instance_npcname("#Boss Room Entry_time")+"::OnEnable";
	end;
OnEnable:
	enablenpc instance_npcname("#Boss Room Entry");
	enablenpc instance_npcname("#Boss Room Door");
	enablenpc instance_npcname("#Boss Room Door1");
	end;
OnSpawn:
	areamonster instance_mapname("1@ma_h"),123,152,137,166,"噩夢死神的噩夢護士",2338,1,instance_npcname("#Boss Room Entry")+"::OnMyMobDead";
	end;
OnReset:
	killmonster instance_mapname("1@ma_h"),instance_npcname("#Boss Room Entry")+"::OnMyMobDead";
	end;
OnMyMobDead:
	initnpctimer;
	end;
OnTimer1000:
	set .@map$, instance_mapname("1@ma_h");
	if (mobcount(.@map$,instance_npcname("#Boss Room Entry")+"::OnMyMobDead") < 1)
		mapannounce .@map$,"噩夢死神: 哈哈哈... 這還不夠, 我會更加強大....",bc_map,"0xFF82FF";
	stopnpctimer;
	end;

// Unofficial labels (to avoid duplicating code).
OnEnableEntry:
	enablenpc instance_npcname("#Boss Room Entry");
	enablenpc instance_npcname("#Boss Room Entry_a");
	enablenpc instance_npcname("#Boss Room Entry_b");
	enablenpc instance_npcname("#Boss Room Entry_c");
	enablenpc instance_npcname("#Boss Room Entry_d");
	enablenpc instance_npcname("#Boss Room Entry_e");
	enablenpc instance_npcname("#Boss Room Entry_f");
	enablenpc instance_npcname("#Boss Room Entry_g");
	enablenpc instance_npcname("#Boss Room Entry_h");
	end;
OnDisableEntry:
	disablenpc instance_npcname("#Boss Room Entry");
	disablenpc instance_npcname("#Boss Room Entry_a");
	disablenpc instance_npcname("#Boss Room Entry_b");
	disablenpc instance_npcname("#Boss Room Entry_c");
	disablenpc instance_npcname("#Boss Room Entry_d");
	disablenpc instance_npcname("#Boss Room Entry_e");
	disablenpc instance_npcname("#Boss Room Entry_f");
	disablenpc instance_npcname("#Boss Room Entry_g");
	disablenpc instance_npcname("#Boss Room Entry_h");
	end;
}

1@ma_h,147,143,0	script	#Boss Room Entry_time	139,{ //10,10
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Boss Room Entry_time");
	end;
OnEnable:
	mapannounce instance_mapname("1@ma_h"),"噩夢死神: 螻蟻般的存在. 來參觀我的噩夢世界吧...",bc_map,"0xFF82FF";
	initnpctimer;
	end;
OnTimer25000:
	mapannounce instance_mapname("1@ma_h"),"噩夢死神: 我會讓你明白...",bc_map,"0xFF82FF";
	for(set .@i,10; .@i<=22; set .@i,.@i+1)
		disablenpc instance_npcname("#Patternwarp"+.@i);
	end;
OnTimer35000:
	mapannounce instance_mapname("1@ma_h"),"噩夢死神: 真正的痛楚! 你會明白的!!",bc_map,"0xFF82FF";
	for(set .@i,25; .@i<=33; set .@i,.@i+1)
		disablenpc instance_npcname("#Patternwarp"+.@i);
	end;
OnTimer45000:
	mapannounce instance_mapname("1@ma_h"),"噩夢死神: 你聽過病人的尖叫嗎?",bc_map,"0xFF82FF";
	end;
OnTimer55000:
	mapannounce instance_mapname("1@ma_h"),"噩夢死神: 那麼,來享受一下吧!!",bc_map,"0xFF82FF";
	enablenpc instance_npcname("#Summon Ward Mob");
	donpcevent instance_npcname("#Summon Ward Mob")+"::OnEnable7";
	end;
OnTimer60000:
	for(set .@i,1; .@i<=8; set .@i,.@i+1)
		enablenpc instance_npcname("#Patternwarp"+.@i);
	for(set .@i,34; .@i<=38; set .@i,.@i+1)
		enablenpc instance_npcname("#Patternwarp"+.@i);
	end;
OnTimer60500:
	for(set .@i,41; .@i<=49; set .@i,.@i+1)
		enablenpc instance_npcname("#Patternwarp"+.@i);
	end;
OnTimer90000:
	mapannounce instance_mapname("1@ma_h"),"噩夢死神: 看到你的痛苦是我最大的樂趣.",bc_map,"0xFF82FF";
	donpcevent instance_npcname("#Boss Room Entry")+"::OnSpawn";
	end;
OnTimer110000:
	mapannounce instance_mapname("1@ma_h"),"噩夢死神: 留在這裡, 詛咒疾病.....",bc_map,"0xFF82FF";
	donpcevent instance_npcname("#Boss Room Entry")+"::OnSpawn";
	end;
OnTimer125000:
	mapannounce instance_mapname("1@ma_h"),"噩夢死神: 你想和我逛逛嗎 ......?",bc_map,"0xFF82FF";
	end;
OnTimer128000:
	for(set .@i,1; .@i<=8; set .@i,.@i+1)
		disablenpc instance_npcname("#Patternwarp"+.@i);
	for(set .@i,34; .@i<=38; set .@i,.@i+1)
		disablenpc instance_npcname("#Patternwarp"+.@i);
	end;
OnTimer128500:
	for(set .@i,41; .@i<=49; set .@i,.@i+1)
		disablenpc instance_npcname("#Patternwarp"+.@i);
	end;
OnTimer129000:
	donpcevent instance_npcname("#Boss Room Entry")+"::OnEnableEntry";
	end;
OnTimer129500:
	donpcevent instance_npcname("#Boss Room Entry")+"::OnSpawn";
	for(set .@i,10; .@i<=22; set .@i,.@i+1)
		enablenpc instance_npcname("#Patternwarp"+.@i);
	end;
OnTimer130000:
	for(set .@i,25; .@i<=33; set .@i,.@i+1)
		enablenpc instance_npcname("#Patternwarp"+.@i);
	stopnpctimer;
	end;
OnStopTimer:
	stopnpctimer;
	end;
}

1@ma_h,111,177,0	script	#Boss Room Entry_2	45,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Boss Room Entry_2");
	end;
OnEnable:
	mapannounce instance_mapname("1@ma_h"),"噩夢死神: 你還有點兒厲害...",bc_map,"0xFF82FF";
	initnpctimer;
	end;
OnTimer3000:
	mapannounce instance_mapname("1@ma_h"),"噩夢死神: 但是你能...",bc_map,"0xFF82FF";
	end;
OnTimer6000:
	mapannounce instance_mapname("1@ma_h"),"噩夢死神: ...還想擊敗我...?",bc_map,"0xFF82FF";
	end;
OnTimer9000:
	for(set .@i,1; .@i<=8; set .@i,.@i+1)
		disablenpc instance_npcname("#Patternwarp"+.@i);
	for(set .@i,34; .@i<=38; set .@i,.@i+1)
		disablenpc instance_npcname("#Patternwarp"+.@i);
	end;
OnTimer9500:
	for(set .@i,41; .@i<=49; set .@i,.@i+1)
		disablenpc instance_npcname("#Patternwarp"+.@i);
	end;
OnTimer10000:
	mapannounce instance_mapname("1@ma_h"),"噩夢死神: 傻瓜 ......",bc_map,"0xFF82FF";
	for(set .@i,10; .@i<=22; set .@i,.@i+1)
		enablenpc instance_npcname("#Patternwarp"+.@i);
	end;
OnTimer10500:
	for(set .@i,25; .@i<=33; set .@i,.@i+1)
		enablenpc instance_npcname("#Patternwarp"+.@i);
	stopnpctimer;
	end;
}

1@ma_h,118,63,0	script	#Summon Ward Mob	139,4,4,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Summon Ward Mob");
	end;
OnTouch:
	donpcevent instance_npcname("#Summon Ward Mob_time")+"::OnEnable";
	specialeffect EF_CURSEATTACK;
	end;
OnEnable1:
	set .@map$, instance_mapname("1@ma_h");
	set .@label$, instance_npcname("#Summon Ward Mob")+"::OnMyMobDead";
	areamonster .@map$,47,77,49,79,"噩夢死神的惡魔嬰兒",2340,1,.@label$;
	areamonster .@map$,49,77,51,79,"噩夢死神的惡魔嬰兒",2340,1,.@label$;
	areamonster .@map$,51,77,53,79,"噩夢死神的惡魔嬰兒",2340,1,.@label$;
	areamonster .@map$,47,79,49,81,"噩夢死神的惡魔嬰兒",2340,1,.@label$;
	areamonster .@map$,49,79,51,81,"噩夢死神的噩夢巫師",2339,1,.@label$;
	areamonster .@map$,47,81,49,83,"噩夢死神的噩夢巫師",2339,1,.@label$;
	areamonster .@map$,49,81,51,83,"噩夢死神的噩夢巫師",2339,1,.@label$;
	end;
OnEnable2:
	set .@map$, instance_mapname("1@ma_h");
	set .@label$, instance_npcname("#Summon Ward Mob")+"::OnMyMobDead";
	areamonster .@map$,51,45,53,47,"噩夢死神的噩夢巫師",2339,1,.@label$;
	areamonster .@map$,53,45,55,47,"噩夢死神的噩夢巫師",2339,1,.@label$;
	areamonster .@map$,49,47,51,49,"噩夢死神的噩夢巫師",2339,1,.@label$;
	areamonster .@map$,53,47,55,49,"噩夢死神的惡魔嬰兒",2340,1,.@label$;
	areamonster .@map$,49,49,51,51,"噩夢死神的惡魔嬰兒",2340,1,.@label$;
	areamonster .@map$,51,49,53,51,"噩夢死神的惡魔嬰兒",2340,1,.@label$;
	areamonster .@map$,53,49,55,51,"噩夢死神的惡魔嬰兒",2340,1,.@label$;
	end;
OnEnable3:
	set .@map$, instance_mapname("1@ma_h");
	set .@label$, instance_npcname("#Summon Ward Mob")+"::OnMyMobDead";
	areamonster .@map$,69,75,71,77,"噩夢死神的惡魔嬰兒",2340,1,.@label$;
	areamonster .@map$,71,75,73,77,"噩夢死神的惡魔嬰兒",2340,1,.@label$;
	areamonster .@map$,73,75,75,77,"噩夢死神的惡魔嬰兒",2340,1,.@label$;
	areamonster .@map$,69,77,71,79,"噩夢死神的惡魔嬰兒",2340,1,.@label$;
	areamonster .@map$,71,77,73,79,"噩夢死神的噩夢巫師",2339,1,.@label$;
	areamonster .@map$,69,79,71,81,"噩夢死神的噩夢巫師",2339,1,.@label$;
	areamonster .@map$,71,79,73,81,"噩夢死神的噩夢巫師",2339,1,.@label$;
	end;
OnEnable4:
	set .@map$, instance_mapname("1@ma_h");
	set .@label$, instance_npcname("#Summon Ward Mob")+"::OnMyMobDead";
	areamonster .@map$,73,45,75,47,"噩夢死神的噩夢巫師",2339,1,.@label$;
	areamonster .@map$,75,45,77,47,"噩夢死神的噩夢巫師",2339,1,.@label$;
	areamonster .@map$,71,47,73,49,"噩夢死神的噩夢巫師",2339,1,.@label$;
	areamonster .@map$,75,47,77,49,"噩夢死神的惡魔嬰兒",2340,1,.@label$;
	areamonster .@map$,71,49,73,51,"噩夢死神的惡魔嬰兒",2340,1,.@label$;
	areamonster .@map$,73,49,75,51,"噩夢死神的惡魔嬰兒",2340,1,.@label$;
	areamonster .@map$,75,49,77,51,"噩夢死神的惡魔嬰兒",2340,1,.@label$;
	end;
OnEnable5:
	set .@map$, instance_mapname("1@ma_h");
	set .@label$, instance_npcname("#Summon Ward Mob")+"::OnMyMobDead";
	areamonster .@map$,93,77,95,79,"噩夢死神的惡魔嬰兒",2340,1,.@label$;
	areamonster .@map$,95,77,97,79,"噩夢死神的惡魔嬰兒",2340,1,.@label$;
	areamonster .@map$,97,77,99,79,"噩夢死神的惡魔嬰兒",2340,1,.@label$;
	areamonster .@map$,93,79,95,81,"噩夢死神的惡魔嬰兒",2340,1,.@label$;
	areamonster .@map$,95,79,97,81,"噩夢死神的噩夢巫師",2339,1,.@label$;
	areamonster .@map$,93,81,95,83,"噩夢死神的噩夢巫師",2339,1,.@label$;
	areamonster .@map$,95,81,97,83,"噩夢死神的噩夢巫師",2339,1,.@label$;
	end;
OnEnable6:
	set .@map$, instance_mapname("1@ma_h");
	set .@label$, instance_npcname("#Summon Ward Mob")+"::OnMyMobDead";
	areamonster .@map$,93,43,95,45,"噩夢死神的噩夢巫師",2339,1,.@label$;
	areamonster .@map$,95,43,97,45,"噩夢死神的噩夢巫師",2339,1,.@label$;
	areamonster .@map$,91,45,93,47,"噩夢死神的噩夢巫師",2339,1,.@label$;
	areamonster .@map$,95,45,97,47,"噩夢死神的惡魔嬰兒",2340,1,.@label$;
	areamonster .@map$,91,47,93,49,"噩夢死神的惡魔嬰兒",2340,1,.@label$;
	areamonster .@map$,93,47,95,49,"噩夢死神的惡魔嬰兒",2340,1,.@label$;
	areamonster .@map$,95,47,97,49,"噩夢死神的惡魔嬰兒",2340,1,.@label$;
	end;
OnEnable7:
	killmonster instance_mapname("1@ma_h"),instance_npcname("#Summon Ward Mob")+"::OnMyMobDead";
	end;
OnMyMobDead:
	initnpctimer;
	end;
OnTimer1000:
	if (mobcount(instance_mapname("1@ma_h"),instance_npcname("#Summon Ward Mob")+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("#Boss Room Entry_2")+"::OnEnable";
		donpcevent instance_npcname("#Boss Room Entry")+"::OnEnableEntry";
		donpcevent instance_npcname("#Boss Room Entry_time")+"::OnStopTimer";
	}
	stopnpctimer;
	end;
}

1@ma_h,118,64,0	script	#Summon Ward Mob_time	139,{ //4,4
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Summon Ward Mob_time");
	end;
OnEnable:
	initnpctimer;
	disablenpc instance_npcname("#Summon Ward Mob");
	end;
OnTimer2000:
	mapannounce instance_mapname("1@ma_h"),"噩夢死神: 我的靈魂常駐你被詛咒的軀體...",bc_map,"0xFF82FF";
	end;
OnTimer4000:
	mapannounce instance_mapname("1@ma_h"),"噩夢死神: 我的小弟...!! 去吧!! 去折磨這個世上的人!!",bc_map,"0xFF82FF";
	end;
OnTimer6000:
	mapannounce instance_mapname("1@ma_h"),"- 噩夢死神的小弟正在侵濁病人!! 如果你不清除它們, 夢魘會變的更加強壯. -",bc_map,"0xFF8200";
	end;
OnTimer10000:
	setarray .@npc$[1],
		"呻吟的病人","尖叫的病人","痛苦的病人","稀爛的病人","噩夢中的病人","萎靡的病人",
		"驚駭的病人","悲傷的病人","受折磨的病人","蠕動的病人","單薄的病人","嚎叫的病人";
	enablenpc instance_npcname("#Summon Ward Mob");
	set .@rand, rand(1,6);
	donpcevent instance_npcname("#Summon Ward Mob")+"::OnEnable"+.@rand;
	set .@id, (.@rand*2)-rand(2);
	donpcevent instance_npcname(.@npc$[.@id]+"#"+.@id)+"::OnEnable";
	stopnpctimer;
	end;
}

1@ma_h,112,177,0	script	#Summon Pillar	139,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Summon Pillar");
	end;
OnEnable1:
	enablenpc instance_npcname("#Summon Pillar");
	donpcevent instance_npcname("#Boss Room Entry_time")+"::OnStopTimer";
	donpcevent instance_npcname("#Summon Pillar_time")+"::OnEnable";
	monster instance_mapname("1@ma_h"),131,157,"靈魂分身",2327,1,instance_npcname("#Summon Pillar")+"::OnMyMobDead";
	end;
OnEnable3:
	enablenpc instance_npcname("#Boss Room Exit");
	disablenpc instance_npcname("#Summon Pillar");
	end;
OnReset:
	killmonster instance_mapname("1@ma_h"),instance_npcname("#Summon Pillar")+"::OnMyMobDead";
	end;
OnMyMobDead:
	initnpctimer;
	end;
OnTimer1000:
	set .@map$, instance_mapname("1@ma_h");
	if (mobcount(.@map$,instance_npcname("#Summon Pillar")+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("#Summon Pillar_time")+"::OnStopTimer";
		donpcevent instance_npcname("#Summon Pillar")+"::OnEnable3";
		donpcevent instance_npcname("#Boss Room Entry")+"::OnReset";
		mapannounce .@map$,"噩夢死神: 噢....不能相信我竟然失敗了...",bc_map,"0xFF4500";
		mapannounce .@map$,"噩夢死神: 不過我覺得還沒結束!!",bc_map,"0xFF4500";
		mapannounce .@map$,"噩夢死神: 我會回來的!!!!!",bc_map,"0xFF4500";
	}
	stopnpctimer;
	end;
}

1@ma_h,112,178,0	script	#Summon Pillar_time	139,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Summon Pillar_time");
	end;
OnEnable:
	initnpctimer;
	end;
OnStopTimer:
	stopnpctimer;
	end;
OnTimer1000:
	mapannounce instance_mapname("1@ma_h"),"- 噩夢死神的靈魂分身正在對他進行治療. 如果你還不出手, 他馬上就會康復!! -",bc_map,"0xFF8200";
	specialeffect EF_MAPPILLAR2,AREA,instance_npcname("#Patternwarp45");
	end;
OnTimer7000:
	mapannounce instance_mapname("1@ma_h"),"噩夢死神: 我不可能死!!!!",bc_map,"0xFF82FF";
	specialeffect EF_MAPPILLAR2,AREA,instance_npcname("#Patternwarp45");
	end;
OnTimer14000:
	mapannounce instance_mapname("1@ma_h"),"噩夢死神: 夠膽就殺了我!! 我會馬上再回來的!!!",bc_map,"0xFF82FF";
	specialeffect EF_MAPPILLAR2,AREA,instance_npcname("#Patternwarp45");
	end;
OnTimer21000:
	mapannounce instance_mapname("1@ma_h"),"噩夢死神: 絕不能這樣結束...!!!!",bc_map,"0xFF82FF";
	specialeffect EF_MAPPILLAR2,AREA,instance_npcname("#Patternwarp45");
	end;
OnTimer30000:
	donpcevent instance_npcname("#Summon Pillar")+"::OnReset";
	end;
OnTimer31000:
	mapannounce instance_mapname("1@ma_h"),"噩夢死神: 哈哈哈哈!! 靈魂分身讓我的力量開始恢復了!!!",bc_map,"0xFF82FF";
	donpcevent instance_npcname("#Summon Pillar")+"::OnReset";
	end;
OnTimer36000:
	donpcevent instance_npcname("#Boss Room Entry")+"::OnEnableEntry";
	donpcevent instance_npcname("#Summon Boss")+"::OnEnable";
	stopnpctimer;
	end;
}

1@ma_h,118,171,0	script	#Boss Room Entry_a	139,10,10,{
	end;
OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnTouch:
	specialeffect EF_GHOST;
	donpcevent instance_npcname("#Boss Room Entry")+"::OnDisableEntry";
	donpcevent instance_npcname("#Boss Room Entry_time")+"::OnEnable";
	end;
}
1@ma_h,132,171,0	duplicate(#Boss Room Entry_a)	#Boss Room Entry_b	139,10,10
1@ma_h,147,171,0	duplicate(#Boss Room Entry_a)	#Boss Room Entry_c	139,10,10
1@ma_h,118,157,0	duplicate(#Boss Room Entry_a)	#Boss Room Entry_d	139,10,10
1@ma_h,132,157,0	duplicate(#Boss Room Entry_a)	#Boss Room Entry_e	139,10,10
1@ma_h,147,157,0	duplicate(#Boss Room Entry_a)	#Boss Room Entry_f	139,10,10
1@ma_h,118,143,0	duplicate(#Boss Room Entry_a)	#Boss Room Entry_g	139,10,10
1@ma_h,132,143,0	duplicate(#Boss Room Entry_a)	#Boss Room Entry_h	139,10,10

1@ma_h,131,62,0	script	#Boss Effect	139,{ //4,4
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Boss Effect");
	initnpctimer;
	end;
OnTimer3000:
	specialeffect EF_BARRIER;
	stopnpctimer;
	initnpctimer;
	end;
}

1@ma_h,43,86,4	script	呻吟的病人#1	584,{
	end;
OnInstanceInit:
	monster instance_mapname("1@ma_h"),131,62,"噩夢死神",2318,1;
	end;
OnEnable:
	mapannounce instance_mapname("1@ma_h"),"呻吟的病人: 唔......",bc_map,"0xFF0000";
	initnpctimer;
	end;
OnTimer5000:
OnTimer8000:
OnTimer11000:
OnTimer14000:
OnTimer17000:
OnTimer20000:
OnTimer23000:
OnTimer26000:
	specialeffect EF_POTION_BERSERK;
	specialeffect EF_CURSEATTACK;
	end;
OnTimer30000:
	specialeffect EF_POTION_BERSERK;
	specialeffect EF_CURSEATTACK;
	stopnpctimer;
	end;
}

1@ma_h,60,87,4	script	尖叫的病人#2	585,{
	end;
OnEnable:
	switch(atoi(strnpcinfo(2))) {
		case 2: set .@str$,"尖叫的病人: 啊!!"; break;
		case 3: set .@str$,"痛苦的病人: 啊,我要死了!!!"; break;
		case 4: set .@str$,"稀爛的病人: ........"; break;
		case 5: set .@str$,"噩夢中的病人: 救命啊，救救我!!!"; break;
		case 6: set .@str$,"萎靡的病人: 唔...啊"; break;
		case 7: set .@str$,"驚駭的病人: 走開!! 求你了......"; break;
		case 8: set .@str$,"悲傷的病人: 哭吧哭吧......"; break;
		case 9: set .@str$,"受折磨的病人: 不要了... 求求你..."; break;
		case 10: set .@str$,"蠕動的病人: 哇..."; break;
		case 11: set .@str$,"單薄的病人: 救命.....救命...."; break;
		case 12: set .@str$,"嚎叫的病人: 啊~~~~~~~~~~~~!!"; break;
	}
	mapannounce instance_mapname("1@ma_h"),.@str$,bc_map,"0xFF0000";
	initnpctimer;
	end;
OnTimer5000:
OnTimer8000:
OnTimer11000:
OnTimer14000:
OnTimer17000:
OnTimer20000:
OnTimer23000:
OnTimer26000:
	specialeffect EF_POTION_BERSERK;
	specialeffect EF_CURSEATTACK;
	end;
OnTimer30000:
	specialeffect EF_POTION_BERSERK;
	specialeffect EF_CURSEATTACK;
	stopnpctimer;
	end;
}
1@ma_h,60,39,7	duplicate(尖叫的病人#2)	痛苦的病人#3	584
1@ma_h,43,39,7	duplicate(尖叫的病人#2)	腐爛的病人#4	585
1@ma_h,77,86,4	duplicate(尖叫的病人#2)	噩夢中的病人#5	584
1@ma_h,69,87,4	duplicate(尖叫的病人#2)	萎靡的病人#6	585
1@ma_h,73,39,7	duplicate(尖叫的病人#2)	驚駭的病人#7	584
1@ma_h,65,51,7	duplicate(尖叫的病人#2)	悲傷的病人#8	585
1@ma_h,87,86,4	duplicate(尖叫的病人#2)	受折磨的病人#9	584
1@ma_h,104,86,4	duplicate(尖叫的病人#2)	蠕動的病人#10	585
1@ma_h,99,39,7	duplicate(尖叫的病人#2)	單薄的病人#11	584
1@ma_h,87,39,7	duplicate(尖叫的病人#2)	嚎叫的病人#12	585

1@ma_h,43,181,4	script	呻吟的病人#b	584,{ end; }
1@ma_h,60,182,4	script	尖叫的病人#b	585,{ end; }
1@ma_h,60,132,7	script	痛苦的病人#b	584,{ end; }
1@ma_h,43,132,7	script	腐爛的病人#b	585,{ end; }
1@ma_h,77,182,4	script	噩夢中的病人#b	584,{ end; }
1@ma_h,69,182,4	script	萎靡的病人#b	585,{ end; }
1@ma_h,73,132,7	script	驚駭的病人#b	584,{ end; }
1@ma_h,65,144,7	script	悲傷的病人#b	585,{ end; }
1@ma_h,87,181,4	script	受折磨的病人#b	584,{ end; }
1@ma_h,104,181,4	script	蠕動的病人#b	585,{ end; }
1@ma_h,99,132,7	script	單薄的病人#b	584,{ end; }
1@ma_h,87,132,7	script	嚎叫的病人#b	585,{ end; }

// Warp Portals
//============================================================
1@ma_h,35,156,0	warp	#Boss Room Exit	2,2,ma_dun01,150,9
1@ma_h,105,157,0	warp	#Boss Room Door	2,2,1@ma_h,116,157
1@ma_h,112,157,0	warp	#Boss Room Door1	2,2,1@ma_h,100,157
1@ma_h,104,63,0	warp	#Boss Room Door2	2,2,1@ma_h,117,63
1@ma_h,112,63,0	warp	#Boss Room Door3	2,2,1@ma_h,100,63
1@ma_h,36,157,0	warp	#Patternwarp1	7,7,1@ma_h,117,63
1@ma_h,46,157,0	warp	#Patternwarp2	7,7,1@ma_h,117,63
1@ma_h,57,157,0	warp	#Patternwarp3	7,7,1@ma_h,117,63
1@ma_h,67,157,0	warp	#Patternwarp4	7,7,1@ma_h,117,63
1@ma_h,79,157,0	warp	#Patternwarp5	7,7,1@ma_h,117,63
1@ma_h,89,157,0	warp	#Patternwarp6	7,7,1@ma_h,117,63
1@ma_h,100,157,0	warp	#Patternwarp7	7,7,1@ma_h,117,63
1@ma_h,51,173,0	warp	#Patternwarp8	10,10,1@ma_h,117,63
1@ma_h,52,140,0	warp	#Patternwarp34	10,10,1@ma_h,117,63
1@ma_h,73,173,0	warp	#Patternwarp35	10,10,1@ma_h,117,63
1@ma_h,73,140,0	warp	#Patternwarp36	10,10,1@ma_h,117,63
1@ma_h,95,173,0	warp	#Patternwarp37	10,10,1@ma_h,117,63
1@ma_h,95,140,0	warp	#Patternwarp38	10,10,1@ma_h,117,63
//1@ma_h,70,170,0	warp	#Patternwarp39	10,10,1@ma_h,117,63
//1@ma_h,77,169,0	warp	#Patternwarp40	10,10,1@ma_h,117,63
1@ma_h,117,171,0	warp	#Patternwarp41	10,10,1@ma_h,117,63
1@ma_h,131,171,0	warp	#Patternwarp42	10,10,1@ma_h,117,63
1@ma_h,146,171,0	warp	#Patternwarp43	10,10,1@ma_h,117,63
1@ma_h,117,157,0	warp	#Patternwarp44	10,10,1@ma_h,117,63
1@ma_h,131,157,0	warp	#Patternwarp45	10,10,1@ma_h,117,63
1@ma_h,146,157,0	warp	#Patternwarp46	10,10,1@ma_h,117,63
1@ma_h,117,143,0	warp	#Patternwarp47	10,10,1@ma_h,117,63
1@ma_h,131,143,0	warp	#Patternwarp48	10,10,1@ma_h,117,63
1@ma_h,146,143,0	warp	#Patternwarp49	10,10,1@ma_h,117,63
1@ma_h,36,63,0	warp	#Patternwarp10	7,7,1@ma_h,116,157
1@ma_h,46,63,0	warp	#Patternwarp11	7,7,1@ma_h,116,157
1@ma_h,56,63,0	warp	#Patternwarp12	7,7,1@ma_h,116,157
1@ma_h,67,63,0	warp	#Patternwarp13	7,7,1@ma_h,116,157
1@ma_h,78,63,0	warp	#Patternwarp14	7,7,1@ma_h,116,157
1@ma_h,89,63,0	warp	#Patternwarp15	7,7,1@ma_h,116,157
1@ma_h,100,63,0	warp	#Patternwarp16	7,7,1@ma_h,116,157
1@ma_h,51,79,0	warp	#Patternwarp17	10,10,1@ma_h,116,157
1@ma_h,51,46,0	warp	#Patternwarp18	10,10,1@ma_h,116,157
1@ma_h,73,80,0	warp	#Patternwarp19	10,10,1@ma_h,116,157
1@ma_h,73,46,0	warp	#Patternwarp20	10,10,1@ma_h,116,157
1@ma_h,95,79,0	warp	#Patternwarp21	10,10,1@ma_h,116,157
1@ma_h,95,46,0	warp	#Patternwarp22	10,10,1@ma_h,116,157
//1@ma_h,193,82,0	warp	#Patternwarp23	7,7,1@ma_h,116,157
//1@ma_h,193,82,0	warp	#Patternwarp24	7,7,1@ma_h,116,157
1@ma_h,117,77,0	warp	#Patternwarp25	10,10,1@ma_h,116,157
1@ma_h,131,77,0	warp	#Patternwarp26	10,10,1@ma_h,116,157
1@ma_h,146,77,0	warp	#Patternwarp27	10,10,1@ma_h,116,157
1@ma_h,117,63,0	warp	#Patternwarp28	10,10,1@ma_h,116,157
1@ma_h,131,63,0	warp	#Patternwarp29	10,10,1@ma_h,116,157
1@ma_h,146,63,0	warp	#Patternwarp30	10,10,1@ma_h,116,157
1@ma_h,117,49,0	warp	#Patternwarp31	10,10,1@ma_h,116,157
1@ma_h,131,49,0	warp	#Patternwarp32	10,10,1@ma_h,116,157
1@ma_h,146,49,0	warp	#Patternwarp33	10,10,1@ma_h,116,157

// Disable select warps.
// Custom, but saves processing and lines.
1@ma_h,1,1,0	script	#ma_hos_warp_init	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Boss Room Exit");
	disablenpc instance_npcname("#Boss Room Door");
	disablenpc instance_npcname("#Boss Room Door1");
	for(set .@i,1; .@i<=8; set .@i,.@i+1)
		disablenpc instance_npcname("#Patternwarp"+.@i);
	for(set .@i,34; .@i<=38; set .@i,.@i+1)
		disablenpc instance_npcname("#Patternwarp"+.@i);
	// Skip 39-40 since they're disabled in the official script.
	for(set .@i,41; .@i<=49; set .@i,.@i+1)
		disablenpc instance_npcname("#Patternwarp"+.@i);
	disablenpc instance_npcname("#ma_hos_warp_init");
	end;
}
