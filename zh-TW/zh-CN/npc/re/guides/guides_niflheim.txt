//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================

//===== rAthena Script =======================================
//= Niflheim Guide
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Guide for the city of Niflheim.
//===== Additional Comments: ================================= 
//= 1.0 First version, Renewal guide.
//= 1.1 Optimized. [Euphy]
//============================================================ 

niflheim,201,187,3	script	尼芙菲姆導遊#01nif	849,{
	mes "[尼芙菲姆導遊]";
	mes "歡迎光臨.. 死亡之都^8B4513尼芙菲姆^000000...";
	mes "需要為您介紹嗎?";
	while (1) {
		next;
		switch(select("[ 道具店 ]:[ 酒店 ]:[ 武器店 ]:[ 魔女城 ]:刪除小地圖的標記:結束對話")) {
		case 1:
			callsub L_Mark,"^0000FF道具店";
			viewpoint 1,218,196,0,0x0A82FF;
			break;
		case 2:
			callsub L_Mark,"^006400酒店";
			viewpoint 1,189,208,1,0xAAFF00;
			break;
		case 3:
			callsub L_Mark,"^800080武器店";
			viewpoint 1,218,170,2,0xDA70D6;
			break;
		case 4:
			callsub L_Mark,"^FF1493魔女城";
			viewpoint 1,255,195,3,0xFF1493;
			break;
		case 5:
			mes "[尼芙菲姆導遊]";
			mes "標記已刪除,";
			mes "還需要其它服務嗎?";
			viewpoint 2,1,1,0,0xFFFFFF;
			viewpoint 2,1,1,1,0xFFFFFF;
			viewpoint 2,1,1,2,0xFFFFFF;
			viewpoint 2,1,1,3,0xFFFFFF;
			break;
		case 6:
			mes "[尼芙菲姆導遊]";
			mes "......再見~!..";
			close;
		}
	}
	end;
L_Mark:
	mes "[尼芙菲姆導遊]";
	mes "讓我在你的小地圖上標記一下";
	mes getarg(0)+"^000000的位置.";
	mes "你還需要確認其他建築物的位置嗎?";
	return;
}
