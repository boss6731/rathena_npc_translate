//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================

//===== rAthena Script ======================================= 
//= Magic Book Seller
//===== By: ================================================== 
//= Masao
//= Credits to Muad_Dib for the translation.
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena
//===== Description: ========================================= 
//= [Official Conversion]
//= An NPC which sells you different kinds of Magic Books.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Masao]
//= 1.1 Optimized. [Euphy]
//============================================================ 

geffen_in,176,105,4	script	魔法書管理者維洛夫	64,{

	if (checkweight(1201,1) == 0) {
		mes "你身上帶了太多東東了，去整理一下再來吧。";
		close;
	}
	if ((MaxWeight - Weight) < 1000) {
		mes "你超重了哦，清理一下減輕重量再來吧。";
		close;
	}
	if ((Upper == 2) && (Class != Job_Baby_Warlock)) {
		mes "[維洛夫]";
		mes "小盆友你是誰呀？";
		mes "你是不是迷路了啊？我可不是照顧小盆友的保姆。";
		close;
	}
	if ((Class != Job_Warlock) && (Class != Job_Warlock_T) && (Class != Job_Baby_Warlock)) {
		mes "[維洛夫]";
		mes "哈哈，你也在尋求新的知識麼？";
		next;
		mes "[維洛夫]";
		mes "我很抱歉，我的知識恐怕不太不適合你呀。";
		close;
	}
	if (getskilllv(2230) < 1) {
		mes "[維洛夫]";
		mes "好吧，你滿足了所有條件除了...你還需要學習「釋放」咒語~";
		next;
		mes "[維洛夫]";
		mes "先學釋放咒語吧，我有很多可以教你。";
		close;
	}
	if ((getskilllv(2230) > 0) && (Class == Job_Warlock) || (Class == Job_Warlock_T) || (Class == Job_Baby_Warlock)) {
		mes "[維洛夫]";
		mes "祝賀你，我的朋友！";
		mes "歡迎來到全新的魔法世界！";
		next;
		mes "[維洛夫]";
		mes "我們魔法師經常渴求新的知識和能量。";
		next;
		mes "[維洛夫]";
		mes "我們願意歷經千辛萬險去實現理想";
		mes "當我們達成所願之時，那種成就感一定妙不可言";
		mes "這也是你和我都選擇魔法之路的原因。";
		next;
		mes "[維洛夫]";
		mes "對啦，今天來找我有什麼事情啊？";
		next;
		switch (select("沒什麼事情...:你知道閱讀魔法書麼？:你知道魔法保存麼？:我可以看一下魔法書麼？:我不用魔法保存，還可以使用魔法書麼？:有關終極魔法書的事情")) {
		case 1:
			mes "[維洛夫]";
			mes "哈！真是個傻孩子。";
			close;
		case 2:
			mes "[維洛夫]";
			mes "閱讀魔法書顧名思義就是「讀一本有魔法咒語的書」啊，你知道怎麼來使用魔法書麼？";
			next;
			switch (select("我知道啊:我完全不知道")) {
			case 1:
				mes "[維洛夫]";
				mes "好吧，看來不需要我再費唇舌介紹啦";
				close;
			case 2:
				mes "[維洛夫]";
				mes "魔法書是魔法捲軸的高端形式，魔法捲軸上的咒語用過一次就會失效，但魔法書可以反覆使用多次。";
				next;
				mes "[維洛夫]";
				mes "讓我們把魔法捲軸比喻成一根普通導線，當你用這根導線去開門或是做其他什麼事情的時候，導線便會彎曲或是毀壞，失去了原有的形狀。而魔法書就好比有形狀記憶功能的合金。";
				next;
				mes "[維洛夫]";
				mes "哈，看來你不知道什麼是擁有記憶功能的合金呀~合金線可以反覆改變形狀並多次使用，你記住這個就可以啦~";
				next;
				mes "[維洛夫]";
				mes "就像合金一樣，魔法書上的咒語使用過一次之後仍然還會存在，儘管再次使用也是需要一些時間。";
				next;
				mes "[維洛夫]";
				mes "...噢,我不想耽誤太多時間解釋這些。不管怎樣，你可以使用魔法書去念咒，這個概念和普通念咒是不一樣的。";
				next;
				select("有什麼不同呢？");
				mes "[維洛夫]";
				mes "為什麼你要把已經有的咒語寫到魔法書中呢？";
				next;
				mes "[維洛夫]";
				mes "這個問題很好，這個涉及到魔法保存。";
				next;
				mes "[維洛夫]";
				mes "你已經學會了「釋放」魔法，那麼你想想看，在魔法釋放之前，是什麼承載了四種不同自然元素的能量？";
				next;
				mes "[維洛夫]";
				mes "這就是魔法保存和釋放過程的連結，接下來我給你詳細說說。";
				next;
				mes "[維洛夫]";
				mes "正如你所知曉的，如何在合適的時機念魔法咒語，";
				mes "將四大自然元素的能量釋放出來，這些都是基於魔法保存的基礎。";
				next;
				mes "[維洛夫]";
				mes "四大元素是指火，水，風和地。魔法的精髓就在於自由支配這些自然元素的能量。";
				next;
				mes "[維洛夫]";
				mes "我們人類利用火炬來作為火的載體，利用扇子來產生風，利用碗來盛水，利用粘土來製作陶器。";
				next;
				mes "[維洛夫]";
				mes "這些行為雖然跟魔法不同，但同樣都是運用自然元素的方法。";
				next;
				mes "[維洛夫]";
				mes "當你運用更加複雜的魔法去呼風喚雨，電閃雷鳴，甚至焚燒四野，這都需要消耗很多精神層面和物質層面的能量。";
				next;
				mes "[維洛夫]";
				mes "如果你的念咒不成功導致你沒法控制這些能量，那麼你的身心都將受到重創。";
				next;
				mes "[維洛夫]";
				mes "這就是為什麼我們要借魔法書的保存功能來減輕這樣的傷害，魔法書可以幫助你預念咒語，使你更加專注於魔法釋放的過程。";
				next;
				mes "[維洛夫]";
				mes "當然，魔法書也有一個缺點，就是無法幫你預念沒有學過的咒語。";
				next;
				mes "[維洛夫]";
				mes "正如我之前所說，魔法書裡面的咒語，仍然是咒語本身。";
				next;
				mes "[維洛夫]";
				mes "作為一個魔法師，你沒法改變魔法書中的法術，或是在魔法書中創造新的法術。";
				next;
				mes "[維洛夫]";
				mes "就算你勉強創造了一個，強大的魔力也會讓你覺得很難控制。";
				next;
				mes "[維洛夫]";
				mes "記住啊，為了學習閱讀魔法書，你必須先學會魔法保存。";
				close;
			}
		case 3:
			mes "[維洛夫]";
			mes "魔法保存是為了是魔法擁有載體可以釋放，幫助在魔法組合中更順暢的念咒。";
			next;
			mes "[維洛夫]";
			mes "當你將魔法保存在魔法書中，如果你不選擇釋放，魔法是不會自動釋放出來的。";
			next;
			mes "[維洛夫]";
			mes "當然，魔法保存也會消耗你的魔力，但是比起傳統念咒要的消耗少得多。";
			next;
			mes "[維洛夫]";
			mes "當你使用更加高級強大的魔法咒語時，魔法保存的難度會提升。";
			next;
			mes "[維洛夫]";
			mes "對於那些不太厲害的咒語來說，在一次魔法組合中，你可以保存兩到三個。但是對於比較強大的咒語來說，一兩個已經是保存的極限了。";
			next;
			mes "[維洛夫]";
			mes "這都完全取決於你控制咒語的能力，在你學會之後，你必須經過多次的練習才能將魔法保存的功效發揮出來。";
			close;
		case 4:
			mes "[維洛夫]";
			mes "我很抱歉，我這裡只有一些給初學者看的魔法書，顯然這些不太適合你，你還是想要看麼？";
			next;
			switch (select("當然，我還是想看:哪裡我可以看到更加高階的魔法書？")) {
			case 1:
				mes "[維洛夫]";
				mes "你可以去找旁邊的 [莉亞]，告訴她你想閱讀我存在那裡的魔法書。";
				next;
				mes "[維洛夫]";
				mes "特別提醒你一下，她不會平白無故的就給你看的，除非你能給她點什麼！";
				if (wm_book < 1) set wm_book, 1;
				close;
			case 2:
				mes "[維洛夫]";
				mes "我很抱歉，魔法協會的管理者最近頒布了一個限制令。由於一些不法之徒會利用魔法去做些壞事情，所以協會決定限制魔法書借閱的權限。";
				next;
				mes "[維洛夫]";
				mes "如果你努力去找肯定是能找到一本半本的，但是我這裡真沒有。";
				next;
				mes "[維洛夫]";
				mes "如果魔法協會的管理者取消了這個限制，我會第一個告訴你的，放心吧";
				close;
			}
		case 5:
			mes "[維洛夫]";
			mes "當你想保存魔法但卻不使用魔法書的話，什麼也不會發生，因為沒有載體去承載魔法。";
			next;
			mes "[維洛夫]";
			mes "就像你需要碗去盛水一樣。";
			next;
			mes "[維洛夫]";
			mes "問題在於，當你一意孤行非要運用一個你沒有學過的咒語時，你會走火入魔的。";
			next;
			mes "[維洛夫]";
			mes "當然，所謂的走火入魔也並不嚴重，不過是睡著了或是陷入暈眩狀態罷了。產生這種情況的原因是由於你在使用一個沒有學過的咒語時，你的魔力和魔法書之間的傳導出現了問題。";
			next;
			mes "[維洛夫]";
			mes "千萬別覺得好玩去嘗試！ 念咒語的時候睡著了？ 這對於我們魔法師來說是多麼大的諷刺和恥辱啊！";
			close;
		case 6:
			if ((BaseLevel > 139) && (getskilllv(2217) > 0) || (getskilllv(2213) > 0)) {
				if(mac_book < 1){
					mes "[維洛夫]";
					mes "抱歉，你可以問其他人麼？我現在有點忙。";
					close;
				}
				if (mac_book == 1) {
					mes "[維洛夫]";
					mes "哈？你在找終極魔法書啊？誰讓你來找我的？";
					next;
					select("莉亞女士");
					mes "[維洛夫]";
					mes "抱歉啊，關於終極魔法書，我這裡也沒有太多的信息";
					next;
					mes "[維洛夫]";
					mes "這個王國的一些學者試圖去整理並複製遠古魔法書，但是...";
					next;
					select("但是什麼？");
					mes "[維洛夫]";
					mes "對了，我哥也許知道點什麼";
					next;
					select("我就知道問你準沒錯！");
					mes "[維洛夫]";
					mes "我哥跟你很像，他對終極魔法書也有極大的興趣。";
					next;
					mes "[維洛夫]";
					mes "他的名字是加爾夫斯";
					mes "他正在饒有興致的研究著古代魔法書呢";
					next;
					mes "[維洛夫]";
					mes "原本我想著他會很快有成果的，所以我沒有給他任何幫助。";
					next;
					mes "[維洛夫]";
					mes "如果他能發現有趣的東西，也許會對你有幫助。";
					next;
					select("那現在加爾夫斯在哪裡？");
					mes "[維洛夫]";
					mes "我聽說他在遠徵軍營，也就是時空縫隙附近，尋找遠古魔法書的蹤跡。";
					next;
					mes "[維洛夫]";
					mes "如果你想去找他，你可要做好長期旅行的準備啊";
					set mac_book,2;
					close;
				}
				if (mac_book > 1) {
					mes "[維洛夫]";
					mes "加爾夫斯在遠徵軍營那邊，也就是時空縫隙附近。";
					next;
					mes "[維洛夫]";
					mes "如果你想去找他，你可要做好長期旅行的準備啊";
					close;
				}
			}
			mes "[維洛夫]";
			mes "哈哈，也許你該專注於強身健體，而不是天天想著創造魔法書。";
			close;
		}
	}
	mes "[維洛夫]";
	mes "哈哈，你也在尋找新知識麼?";
	next;
	mes "[維洛夫]";
	mes "我很抱歉，我的知識不適合你。";
	close;
}

geffen_in,175,112,4	script	莉亞	123,{

	if (checkweight(1201,1) == 0) {
		mes "你身上帶了太多東東了，";
		mes "去整理一下再來吧。";
		close;
	}
	if ((MaxWeight - Weight) < 1000) {
		mes "你的負重量已經超標了哦，";
		mes "清理一下減輕重量再來吧。";
		close;
	}
	if ((wm_book > 0) && (Class == Job_Warlock) || (Class == Job_Warlock_T) || (Class == Job_Baby_Warlock)) {
		mes "[莉亞]";
		mes "你只能在這裡閱讀魔法書，如果你想借閱的話，必須有魔法協會管理者和高級魔法師的許可。";
		next;
		mes "[莉亞]";
		mes "我有什麼可以幫你？";
		next;
		select("維洛夫讓我來閱讀他存放在這裡的書");
		mes "[莉亞]";
		mes "這麼說你是來借書的啦？";
		next;
		switch (select("讓我考慮一下...:我想要初級魔法書:我想要中級魔法書:我想要高級魔法書:我想要終極魔法書")) {
		case 1:
			mes "[莉亞]";
			mes "好的，你慢慢考慮。";
			close;
		case 2:
			mes "[莉亞]";
			mes "我們現在遇到了點麻煩，很多人都來借閱魔法書。";
			next;
			mes "[莉亞]";
			mes "糟糕的是，好多外借的魔法書都沒有按時歸還，所以我們決定收取一定的抵押金。";
			next;
			select("收取抵押金？");
			mes "[莉亞]";
			mes "是的，最少10000金幣，或者你也可以用2本^0000FF古魔法書^000000或是50張^0000FF破舊書頁^000000來換。";
			mes "你支付過押金後，我才會讓你借書。";
			next;
			set .@Payment, select("如何找到古魔法書和破舊書頁？:我還是付現金好了:我用古魔法書來換:我用破舊書頁來換")-1;
			mes "[莉亞]";
			if (!.@Payment) {
				mes "我聽說飛行魔書、死亡魔書、巫婆等怪物會掉落古魔法書和破舊書頁。不過你千萬別為了找這些東西去拚命。";
				next;
				mes "[莉亞]";
				mes "生命永遠都是最重要的，明白麼？";
				close;
			}
			if ((.@Payment == 1 && Zeny > 10000) || (.@Payment == 2 && countitem(1006) > 1) || (.@Payment == 3 && countitem(1097) > 49)) {
				mes "你想要借閱哪種書？";
				next;
				set .@i, select("讓我考慮一下...:魔法書(火箭術):魔法書(冰箭術):魔法書(雷擊術)")-1;
				mes "[莉亞]";
				if (!.@i) {
					mes "沒問題，你慢慢考慮。";
					close;
				}
				if (countitem(6188+.@i)) {
					mes "你已經有了那本魔法書了，你不能重複借同一本。";
					next;
					mes "[莉亞]";
					mes "你知道多少人想看你現在借去的這本書麼？如果你不歸還反而要再借一本，那就太自私了。";
					close;
				}
				if ((.@Payment == 1 && Zeny > 10000) || (.@Payment == 2 && countitem(1006) > 1) || (.@Payment == 3 && countitem(1097) > 49)) {
					mes "魔法書背面封皮的借書卡上，寫明了歸還日期。";
					next;
					mes "[莉亞]";
					mes "請不要弄丟或是損壞，否則會罰款的。";
					if (.@Payment == 1) set Zeny, Zeny - 10000;
					else if (.@Payment == 2) delitem 1006,2; //Old_Magic_Book
					else delitem 1097,50; //Worn_Out_Page
					getitem 6188+.@i,1; //Magic_Book_FB, Magic_Book_CB, Magic_Book_LB
					close;
				}
			}
			mes "抱歉，你所準備的抵押金或者抵押物品貌似數量不夠啊。";
			close;
		case 3:
			mes "[莉亞]";
			mes "你要借中級魔法書啊，請你支付5萬金幣，或是2個神秘箱子，或是9個鋁。";
			next;
			set .@Payment, select("如何可以找到神秘箱子和鋁？:我付5萬金幣好了:我用神秘箱子來交換:我用鋁來交換")-1;
			mes "[莉亞]";
			if (!.@Payment) {
				mes "神秘箱子不太常見,但是世界上很多怪物都會掉落比如邪惡禮盒、邪骸食人魚、邪惡箱、夢魘、克萊本、諾可伊、九尾狐、廢氣魔和漂流浪人。";
				mes "如果你資金充足的話，也可以在路上買到神秘箱子。";
				next;
				mes "[莉亞]";
				mes "掉落鋁的魔物包括妖君、腐屍戰俘、玩偶熊、黑耀石和銀月魔女。或者你可以考慮找城裡的鐵匠，把鋁原石提煉成鋁，這樣會比直接收集鋁稍微容易些。";
				close;
			}
			if ((.@Payment == 1 && Zeny > 50000) || (.@Payment == 2 && countitem(603) > 1) || (.@Payment == 3 && countitem(985) > 8)) {
				mes "你想要借閱什麼樣的魔法書？";
				next;
				set .@i, select("讓我考慮一下...:魔法書(暴風雪):魔法書(怒雷強擊):魔法書(隕石術):魔法書(雷爆術):魔法書(雷鳴術):魔法書(水球術):魔法書(崩裂術):魔法書(地震術)")-1;
				setarray .@Books[0],6192,6193,6194,6197,6198,6199,6200,6201;
				mes "[莉亞]";
				if (!.@i) {
					mes "沒問題，你慢慢考慮。";
					close;
				}
				if (countitem(.@Books[.@i-1])) {
					mes "你已經有了那本魔法書了，你不能重複借同一本。";
					next;
					mes "[莉亞]";
					mes "你知道多少人想看你現在借去的這本書麼？如果你不歸還反而要再借一本，那就太自私了。";
					close;
				}
				if ((.@Payment == 1 && Zeny > 50000) || (.@Payment == 2 && countitem(603) > 1) || (.@Payment == 3 && countitem(985) > 8)) {
					mes "謝謝你支付抵押金。魔法書背面封皮的借書卡上，寫明了歸還日期。請不要弄丟或是損壞，否則會罰款的。";
					next;
					mes "[莉亞]";
					mes "此外提醒一下，必須歸還了中級和高級魔法書之後才能借閱終極魔法書。你要記住，這些書只是借給你的，你要好好愛惜。";
					if (.@Payment == 1) set Zeny, Zeny - 50000;
					else if (.@Payment == 2) delitem 603,2; //Old_Blue_Box
					else delitem 985,9; //Elunium
					getitem .@Books[.@i-1],1; //Magic_Book_SG, Magic_Book_LOV, Magic_Book_MS, Magic_Book_TS, Magic_Book_JT, Magic_Book_WB, Magic_Book_HD, Magic_Book_ES
					close;
				}
			}
			mes "抱歉，你所準備的抵押金或者抵押物品貌似數量不夠啊。";
			close;
		case 4:
			mes "[莉亞]";
			mes "你想借高級魔法書啊？提醒你一下，借閱高級魔法書有很多條件和限制，你確定你要借麼？";
			next;
			select("是的，我堅決要借");
			mes "[莉亞]";
			mes "好吧，既然你這麼堅定的要借閱，那你選一種支付押金的方式吧。";
			next;
			mes "[莉亞]";
			mes "[12個未知的碎片另加100,000金幣]，或者";
			mes "[7個神之金屬另加100,000金幣]，或者";
			mes "[1個神秘紫箱另加100,000金幣]。";
			mes "你想怎麼支付呢？";
			next;
			set .@Payment, select("我在哪裡可以找到那些東西？:讓我考慮一下...:我選擇12個未知的碎片另加100,000金幣:我選擇7個神之金屬另加100,000金幣:我選擇1個神秘紫箱另加100,000金幣")-1;
			mes "[莉亞]";
			if (!.@Payment) {
				mes "未知的碎片聽說可以從優配擂斯謎一樣的奇怪機械怪物身上獲得，那種碎片是古代文明的源泉.....啊.....光是想想就讓人激動。";
				next;
				mes "[莉亞]";
				mes "神之金屬可以從行刑者、獅鷲獸、裘卡、深淵騎士等身上獲得，但是同樣也是收集神之金屬原石會更快一些。";
				next;
				mes "[莉亞]";
				mes "收集到神之金屬原石後，你可以在城裡的精煉房進行提煉。";
				next;
				mes "[莉亞]";
				mes "神秘紫箱聽說可以從邪惡箱、邪惡摩艾、獸人酋長、冰雹騎士、俄塞裡斯身上獲得。";
				next;
				mes "[莉亞]";
				mes "自從這些神秘箱子用來測試薛丁格的貓後，它們就成了值得研究的對象。";
				close;
			}
			if (.@Payment == 1) {
				mes "如果你無法搜集到這些物品來支付押金，那麼我給你另一條路走。";
				next;
				mes "[莉亞]";
				mes "支付200,000金幣怎麼樣？";
				next;
				if(select("抱歉，我沒那麼多錢:聽起來不錯") == 1) {
					mes "[莉亞]";
					mes "好的，那麼請下次再來吧";
					close;
				}
				mes "[莉亞]";
			}
			if ((.@Payment == 1 && Zeny > 199999) || (Zeny > 99999 && ((.@Payment == 2 && countitem(7094) > 11) || (.@Payment == 3 && countitem(984) > 6) || (.@Payment == 4 && countitem(617))))) {
				mes "你想要借閱什麼樣的魔法書？";
				next;
				set .@i, select("讓我考慮一下.:魔法書(地牛翻身):魔法書(連鎖電擊):魔法書(碧血隕石):魔法書(吸星大法)")-1;
				mes "[莉亞]";
				if (!.@i) {
					mes "好的，你慢慢考慮。";
					close;
				}
				if (countitem(6201+.@i)) {
					mes "你已經有了那本魔法書了，你不能重複借同一本。";
					next;
					mes "[莉亞]";
					mes "你知道多少人想看你現在借去的這本書麼？如果你不歸還反而要再借一本，那就太自私了。";
					close;
				}
				if ((.@Payment == 1 && Zeny > 199999) || (Zeny > 99999 && ((.@Payment == 2 && countitem(7094) > 11) || (.@Payment == 3 && countitem(984) > 6) || (.@Payment == 4 && countitem(617))))) {
					mes "謝謝你支付押金。魔法書背面封皮的借書卡上，寫明了歸還日期。請不要弄丟或是損壞，否則會罰款的。";
					next;
					mes "[莉亞]";
					mes "此外提醒一下，必須歸還了中級和高級魔法書之後才能借閱終極魔法書。你要記住，這些書只是借給你的，你要好好愛惜。";
					if (.@Payment == 1) set Zeny, Zeny - 200000;
					else if (.@Payment == 2) delitem 7094,12; //Mystery_Piece
					else if (.@Payment == 3) delitem 984,7; //Oridecon
					else delitem 617,1; //Old_Violet_Box
					if (.@Payment > 1) set Zeny, Zeny - 100000;
					getitem 6201+.@i,1; //Magic_Book_ES_, Magic_Book_CL, Magic_Book_CR, Magic_Book_DL
					close;
				}
			}
			mes "抱歉，你所準備的抵押金或者抵押物品貌似數量不夠啊。";
			close;
		case 5:
			if ((BaseLevel > 139) && (mac_book < 1) && ((getskilllv(2217) > 0) || (getskilllv(2213) > 0))) {
				mes "[莉亞]";
				mes "你...你是在說終極魔法書麼？";
				next;
				mes "[莉亞]";
				mes "噢，我的天啊...";
				mes "我簡直不敢相信有人要借那種書...";
				next;
				select("請別說你這裡不能借閱");
				mes "[莉亞]";
				mes "說實話，我從沒見過終極魔法書。";
				next;
				mes "[莉亞]";
				mes "如果你想知道更多信息，你應該去問維洛夫。";
				next;
				mes "[莉亞]";
				mes "抱歉，我真的無能為力。";
				set mac_book,1;
				close;
			}
			if ((BaseLevel > 139) && (mac_book > 0) && ((getskilllv(2217) > 0) || (getskilllv(2213) > 0))) {
				mes "[莉亞]";
				mes "如果你想知道更多信息，你應該去問維洛夫。";
				close;
			}
			mes "[莉亞]";
			mes "要不要現在就練習咒語呢？";
			next;
			mes "[莉亞]";
			mes "如果你想借閱終極魔法書，你必須達到140級，並且學會^0000FF元素漩渦^000000和^0000FF毀滅慧星^000000這兩個技能。";
			next;
			mes "[莉亞]";
			mes "那種書是不會借給泛泛之輩的，你明白麼？";
			close;
		}
	}
	mes "[莉亞]";
	mes "歡迎來到魔法檔案室，這裡提供有條件借閱魔法書的服務。";
	close;
}

mid_camp,255,244,4	script	加爾夫斯	735,{

	if (checkweight(1201,1) == 0) {
		mes "你身上帶了太多東東了，";
		mes "去整理一下再來吧。";
		close;
	}
	if ((MaxWeight - Weight) < 1000) {
		mes "你的負重量已經超標了哦，";
		mes "清理一下減輕重量再來吧。";
		close;
	}
	if (Upper == 2) {
		mes "[加爾夫斯]";
		mes "小盆友你是誰呀？";
		mes "你是不是迷路了啊？我可不是照顧小盆友的保姆。";
		close;
	}
	if ((Class != Job_Warlock) && (Class != Job_Warlock_T) && (Class != Job_Baby_Warlock)) {
		mes "[加爾夫斯]";
		mes "我左臂覺得熱得不行右臂又覺得冷得不行，我真是受不了這詭異的天氣啊";
		close;
	}
	if (getskilllv(2230) < 1) {
		mes "[加爾夫斯]";
		mes "如果我繼續呆在這的話簡直是自找麻煩，唉，我已經很麻煩了...";
		close;
	}
	if ((BaseLevel < 140) || (getskilllv(2217) < 1) && (getskilllv(2213) < 1)) {
		mes "[加爾夫斯]";
		mes "不好意思，我不會跟沒準備好的人說這些";
		close;
	}
	if (mac_book < 2) {
		mes "[加爾夫斯]";
		mes "你看起來很強，但是似乎跟我沒啥關係嘛";
		close;
	}
	if (mac_book == 2) {
		mes "[加爾夫斯]";
		mes "你來找我幹啥？";
		next;
		select("我聽說你在研究終極魔法書");
		mes "[加爾夫斯]";
		mes "你也感興趣麼？";
		mes "我不覺得真的會有人對這種書感興趣唉~";
		mes "實話告訴你吧，我確實是在研究，不過你怎麼知道的啊？";
		next;
		select("你是維洛夫的哥哥吧？");
		mes "[加爾夫斯]";
		mes "是的，我叫加爾夫斯，維洛夫是我弟弟。";
		next;
		mes "[加爾夫斯]";
		mes "等等，難道是維洛夫";
		mes "那個老傢伙推薦你來找我的？";
		next;
		select("就是這樣！");
		mes "[加爾夫斯]";
		mes "哇哦!";
		mes "真是意外啊，";
		mes "他從來不覺得我在正兒八經的做研究，";
		mes "我真高興這個老頑固也有改變的一天啊。";
		next;
		mes "[加爾夫斯]";
		mes "哈哈，你來的時機真準，我剛好在找人做個測試！";
		next;
		select("找人做測試？");
		mes "[加爾夫斯]";
		mes "老實說，我只是對創造終極魔法書有興趣。";
		next;
		mes "[加爾夫斯]";
		mes "其實我的魔法水平不高，更別提跟人戰鬥了。不過無所謂，我從魔法書中找到了絕妙有趣的事情。";
		next;
		mes "[加爾夫斯]";
		mes "但問題在於，即使我做出了一本魔法書，但我沒法用啊~所以我需要有人來幫我測試魔法書。";
		next;
		mes "[加爾夫斯]";
		mes "這也是我一直以來等待像你這樣法力高強的人來做測試的原因。";
		next;
		select("(這丫說啥呢？)");
		mes "[加爾夫斯]";
		mes "我的研究差不多有成果了，";
		mes "但我還差一個問題沒解決。";
		next;
		mes "[加爾夫斯]";
		mes "要做成遠古終極魔法書，我需要有個觸媒來誘發這些咒語。";
		next;
		mes "[加爾夫斯]";
		mes "根據遠古的資料，我已經成功的把咒語寫進魔法書了，但是我還需要知道必要的魔法觸媒信息。";
		next;
		mes "[加爾夫斯]";
		mes "啊！但那有一根銀線!";
		next;
		select("老兄，扯遠了吧...！");
		mes "[加爾夫斯]";
		mes "哈哈...";
		mes "在邪惡巨人的首都埃迪斯特斯，有很多檔案，記載著世界上全部的魔法知識。";
		next;
		mes "[加爾夫斯]";
		mes "也許那裡能找到有關終極魔法書觸媒的信息。";
		next;
		mes "[加爾夫斯]";
		mes "你知道該做啥了吧?去埃迪斯特斯找有關於魔法書觸媒的檔案，盡全力去找！";
		next;
		mes "[加爾夫斯]";
		mes "把那個觸媒帶回來，之後我才能最終完成終極魔法書的製作。";
		set mac_book,3;
		close;
	}
	if (mac_book == 3) {
		mes "[加爾夫斯]";
		mes "你知道該做啥了吧？去埃迪斯特斯找有關於魔法書觸媒的檔案，盡全力去找！";
		next;
		mes "[加爾夫斯]";
		mes "把那個觸媒帶回來，之後我才能最終完成終極魔法書的製作。";
		close;
	}
	if (mac_book == 4) {
		mes "[加爾夫斯]";
		mes "噢~有什麼發現麼？";
		next;
		set .@i, select("我發現了關於毀滅彗星的一切:我完全掌握了元素漩渦的極理要義");
		mes "[加爾夫斯]";
		if ((.@i == 1 && countitem(6195)) || (.@i == 2 && countitem(6196))) {
			mes "你在耍我吧，你已經有那本書了呀！";
			close;
		}
		if ((.@i == 1 && checkquest(12218) == 2) || (.@i == 2 && checkquest(12219) == 2 && checkquest(12220) == 2 && checkquest(12221) == 2 && checkquest(12222) == 2)) {
			mes "哦~我從你身上感覺到了不可思議的魔力！太好啦，我們開始製作終極魔法書吧！";
			next;
			mes "[加爾夫斯]";
			mes "沒問題，但是...我太窮了，買不起製作魔法書所需要的材料T。T";
			next;
			mes "[加爾夫斯]";
			mes "我需要至少^0000aa1,000,000金幣^000000，這樣才夠買齊所有製作魔法書所需的材料。你有那麼多錢麼？";
			next;
			if(select("囧，我沒有那麼多錢:當然，我別的沒有，錢多得是") == 1) {
				mes "[加爾夫斯]";
				mes "啊，你沒有啊，太失望了，那我就無能為力了...";
				mes "等你有錢再來找我吧。";
				close;
			}
			mes "[加爾夫斯]";
			if (Zeny >= 1000000) {
				mes "太好啦，我們開始吧！";
				next;
				specialeffect2 EF_DISPELL;
				progressbar "ffff00",4;
				specialeffect2 EF_LORD;
				set Zeny, Zeny - 1000000;
				getitem 6194+.@i,1; //Magic_Book_CM, Magic_Book_TV
				mes "[加爾夫斯]";
				mes "這是我傾盡全力製造出來的魔法書！給你！";
				next;
				mes "[加爾夫斯]";
				mes "謝謝你幫我實現夢想，如果你還想要這類的書，請隨時回來找我。";
				close;
			}
			mes "我不是告訴過你了麼，沒錢我就沒法準備製作魔法書的材料。";
			close;
		}
		mes "你確定你發現什麼了嘛？似乎你沒找到我要的信息呀。";
		close;
	}
	mes "[加爾夫斯]";
	mes "你還需要什麼別的嗎？";
	close;
}

dic_in01,25,190,0	script	神秘的文件資料	844,{

	if (isequipped(2782) < 1) {
		mes "我最好別忘了裝備一下我的智慧王之戒。";
		close;
	}
	if (ep13_3_invite < 5) {
		mes "當你要碰這些文件資料的時候，有個貌似是文檔管理員的沙帕就要去找守衛加爾頓過來，最好還是儘快離開這裡。";
		close;
	}
	if (checkquest(12165,PLAYTIME) == 1) {
		mes "這些文件資料讓你覺得噁心，還是等五臟廟平復了再回來吧。";
		close;
	}
	if (checkquest(12165,PLAYTIME) == 2) {
		mes "你不再覺得噁心了，看來可以繼續了。";
		erasequest 12165;
		close;
	}
	if ((BaseLevel < 140) || (getskilllv(2217) < 1) && (getskilllv(2213) < 1)) {
		mes "這些文件似乎很難懂，也沒啥必要去讀懂。";
		close;
	}
	specialeffect2 EF_DISPELL;
	progressbar "ffff00",4;
	if (mac_book < 3) {
		mes "你發現了關於魔法，加速，粒子波的記錄。";
		close;
	}
	if (mac_book == 3) {
		mes "你發現了關於魔法，加速，粒子波的記錄。也許這些正是加爾夫斯要找的。";
		set mac_book, 4;
		close;
	}
	if (mac_book == 4) {
		if (rand(1,10) == 1) {
			if (getskilllv(2213) > 0) {
				if (checkquest(12218) == 1) {
					mes "你已經有擁有這些資料了。";
					close;
				}
				mes "^660066為了提煉熔巖精華，你必須在圖爾火山二層最熱的地方去提煉40個燃燒的心臟。^000000";
				mes "^660066在那裡你必須傾盡全力才能提煉成功。自古以來，人們用這種方法來研究魔法粒子的加速現象。^000000";
				next;
				mes "你發現了^990099熔巖精華提煉方法^000000。";
				specialeffect2 EF_STEAL;
				close2;
				setquest 12218;
				end;
			}
			mes "這些資料沒啥有用的內容。";
			close;
		}
		if (rand(1,10) == 2) {
			if (getskilllv(2217) > 0) {
				if (checkquest(12219) == 1) {
					mes "你已經有擁有這些資料了。";
					close;
				}
				mes "^660066火焰精華的提取，必須在圖爾火山一層的平穩的面上進行。10個火種是提煉火焰精華必須的原料。^000000";
				mes "^660066根據人類的記載，火焰是四大自然元素之一。^000000";
				next;
				mes "你發現了^990099火焰精華提煉方法^000000。";
				specialeffect2 EF_STEAL;
				close2;
				setquest 12219;
				end;
			}
			mes "這些資料沒啥有用的內容。";
			close;
		}
		if (rand(1,10) == 3) {
			if (getskilllv(2217) > 0) {
				if (checkquest(12220) == 1) {
					mes "你已經有擁有這些資料了。";
					close;
				}
				mes "^660066我在冰洞三層成功提煉過冰河精華，如果你想嘗試，就帶著10個冰塊心臟去那個地方。^000000";
				next;
				mes "你發現了^990099冰川精華提煉方法^000000。";
				specialeffect2 EF_STEAL;
				close2;
				setquest 12220;
				end;
			}
			mes "這些資料沒啥有用的。";
			close;
		}
		if (rand(1,10) == 4) {
			if (getskilllv(2217) > 0) {
				if (checkquest(12221) == 1) {
					mes "你已經有擁有這些資料了。";
					close;
				}
				mes "^660066在艾音貝赫礦山二層我已經從古老的巖石提煉出化石精華。目前為止，那個地方是所有測試過的地方中最穩定也是最適合提煉的。";
				mes "如果你想試試，準備10個朱畢雷去那邊吧，祝你好運！^000000";
				next;
				mes "你發現了^990099化石精華提煉方法^000000。";
				specialeffect2 EF_STEAL;
				close2;
				setquest 12221;
				end;
			}
			mes "這些資料沒啥有用的。";
			close;
		}
		if (rand(1,10) == 5) {
			if (getskilllv(2217) > 0) {
				if (checkquest(12222) == 1) {
					mes "你已經有擁有這些資料了。";
					close;
				}
				mes "^660066為了提煉暴風精華，我去了崑崙的棋盤考察，那裡是風精靈的棲息地。";
				mes "提煉的過程很容易，但是我費了好大勁才找到了催化劑-舞獅的假牙。^000000";
				next;
				mes "你發現了^990099暴風精華提煉方法^000000。";
				specialeffect2 EF_STEAL;
				close2;
				setquest 12222;
				end;
			}
			mes "這些資料沒啥有用的。";
			close;
		}
		if (rand(1,10) == 10) {
			mes "突然你覺得很噁心，這種感覺似乎沒那麼容易消退，你需要晚點再來找有用的資料。";
			setquest 12165;
			close;
		}
		mes "這裡沒什麼特別的。";
		close;
	}
	mes "你不再需要繼續搜查這個地方了。";
	close;
}

function	script	Magic_Book_Essence	{
	if (MaxWeight - Weight < 1000) {
		mes "請去收拾你的行李，減輕些負重。";
		close;
	}
	if (checkquest(getarg(0)) == 1 && countitem(getarg(1)) >= getarg(2)) {
		specialeffect2 EF_BLIND;
		specialeffect2 EF_BEGINSPELL;
		progressbar "ffff00",4;
		delitem getarg(1),getarg(2);
		if (rand(2)) {
			mes "你使用了"+getarg(2)+"個"+getitemname(getarg(1))+"， 提煉出了^999900"+getarg(3)+"精華^000000。";
			next;
			mes getarg(3)+"精華是永久性的， 除非有啥意外發生。^000000";
			completequest getarg(0);
			specialeffect2 EF_LORD;
			close;
		}
		mes "^008888你使用了"+getarg(2)+"個"+getitemname(getarg(1))+"進行提煉， 可惜提煉失敗了。^000000";
		close;
	}
	if (checkquest(getarg(0)) == 2) {
		mes "你已經提煉了 "+getarg(3)+" 精華。";
		close;
	}
	mes "有人已經使用過這裡了。";
	close;
}

thor_v02,163,104,0	script	充滿熔巖能量的地方#33	844,{ callfunc "Magic_Book_Essence",12218,7097,40,"熔巖"; end; }
thor_v01,185,91,0	script	充滿火焰能量的地方#33	844,{ callfunc "Magic_Book_Essence",12219,7098,10,"火焰"; end; }
ice_dun03,137,148,0	script	充滿冰川能量的地方	844,{ callfunc "Magic_Book_Essence",12220,7561,10,"冰川"; end; }
ein_dun02,181,124,0	script	充滿化石能量的地方	844,{ callfunc "Magic_Book_Essence",12221,7312,10,"化石"; end; }
gon_dun02,252,198,0	script	充滿暴風能量的地方#33	844,{ callfunc "Magic_Book_Essence",12222,7266,10,"暴風"; end; }
