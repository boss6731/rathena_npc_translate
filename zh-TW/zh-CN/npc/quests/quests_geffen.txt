//===== Hercules Script ======================================
//= Quest NPCs related to Geffen
//===== By: ==================================================
//= kobra_k88 & Samuray22
//===== Current Version: =====================================
//= 1.5
//===== Description: =========================================
//= Welding Mask and Headset Quests.
//===== Additional Comments: =================================
//= 1.1 Fixed exploits [Lupus]
//= 1.2 Removed Duplicates [Silent]
//= 1.3 Updated "Welding Mask" Quest to the Official one. [Samuray22]
//= 1.3a replaced item "names" with item id. Added missing [Lupus]
//= 1.4 Updated welding mask and headset quests. [L0ne_W0lf]
//= 1.5 Added Number Match Game. [Euphy]
//============================================================

// Blacksmith: 'Welding Mask' Quest
//============================================================
geffen_in,144,166,4	script	鐵匠	1_M_SMITH,{
	mes "[鐵匠]";
	mes "你沒有聽說過我?";
	mes "為什麼, 我是這裡的資深鐵匠.";
	mes "我在這熱與血腥的地獄之火奮鬥了30年, ";
	mes "我有無法彎曲的鋼鐵意志!";
	next;
	mes "[鐵匠]";
	mes "但是...";
	mes "我的力量隨著我年齡的老去而消失著.";
	mes "所以我請來了一些年輕的, 熱血的, ";
	mes "上進的鐵匠, 希望他們掌握我的技術...";
	next;
	switch(select("這是什麼?:...")) {
	case 1:
		mes "[鐵匠]";
		mes "我們鐵匠經常在極度高溫的環境下打鐵, 這些炎熱的氣溫和融化的鋼鐵不是一般人可以忍耐的.";
		next;
		mes "[鐵匠]";
		mes "為了我們的藝術, 我們的美麗, 我們的臉部時常處於危險中...";
		next;
		mes "[鐵匠]";
		mes "但隨著一個非凡的技術誕生, 這些問題再也沒有需要擔憂!";
		mes "看, 這是就是我發明的^3355FF鋼鐵面罩^000000";
		next;
		mes "[鐵匠]";
		mes "它由可以覆蓋臉部的厚金屬板構成.";
		mes "事實上, 這是所有人都想要的道具!";
		mes "如果我不是自己要用, 給一些正在學習打鐵的朋友也不失為一個好主意.";
		next;
		mes "[鐵匠]";
		mes "這樣如何?如果你給我^2244FF50個鋼鐵^000000和^4422FF2000金幣^000000, 我會馬上為你做一個~";
		next;
		switch(select("嗯...不錯的樣子, 好吧:這是怎麼工作的能再說一遍麼?")) {
		case 1:
			if ((countitem(999) > 49) && (Zeny > 1999)){
				mes "[鐵匠]";
				mes "啊, 我發現你很欣賞鐵匠的工作! 很好, 我馬上做給你";
				next;
				delitem 999,50;  //Steel
				set Zeny, Zeny - 2000;
				getitem 2292,1; // Welding_Mask
				mes "[鐵匠]";
				mes "哈哈哈~!好好用它哦!";
				close;
			}
			else {
				mes "[鐵匠]";
				mes "嗯...很抱歉~";
				mes "你沒有我需要的道具.";
				next;
				mes "[鐵匠]";
				mes "正常的鐵匠會把這當成是不尊重的信號, 但我並沒有覺得你對鐵匠有不敬的感覺.如果你還想我做面具的話就別忘了我需要的東西.";
				close;
			}
		case 2:
			mes "[鐵匠]";
			mes "^3355FF鋼鐵面具^000000用覆蓋你臉部的厚鐵板來保護你的臉.本發明是我在鐵錘和鐵砧中無休止地工作30年的結果.";
			close;
		}
	case 2:
		mes "[鐵匠]";
		mes "啊...!";
		mes "我相信你第一次遇到我這麼好的鐵匠, ";
		mes "所以敬畏得說不出話.";
		mes "當你準備好的時候請找個時間告訴我.";
		next;
		mes "^3355FF這傢伙似乎有點自滿~^000000";
		close;
	}
}

// Eric: 'Headset' Quest
//============================================================
geffen_in,30,71,4	script	鄭何意嘉	4_M_01,{
	mes "[鄭何意嘉]";
	mes "你要不要聽聽我爺爺說的故事呢?";
	next;
	switch (select("繼續對話:詢問材料:製作看看:取消")) {
	case 1:
		mes "[鄭何意嘉]";
		mes "前幾年剛過世的爺爺";
		mes "在我的生命中是個相當重要的人物";
		mes "比起我那忙碌的父親";
		mes "我可以感覺到爺爺依舊在我身邊守候著我";
		next;
		mes "[鄭何意嘉]";
		mes "爺爺的書房更是最令我感到有趣的地方";
		mes "就像是' 世外桃源 '一般";
		mes "每當我想念我爺爺的時候";
		mes "我就會到書房去找些書來看";
		next;
		mes "[鄭何意嘉]";
		mes "在那裡";
		mes "我可以透過書籍學習到許多的知識";
		mes "因為爺爺對於鍊金術和音樂都相當的喜愛";
		mes "所以書房裡頭幾乎滿滿的都是相關的書籍";
		next;
		mes "[鄭何意嘉]";
		mes "直到有一天 ..";
		mes "我像往常一樣進到爺爺的書房裡";
		mes "在那裡我偶然發現了一張相當神奇的設計圖";
		next;
		mes "[鄭何意嘉]";
		mes "在設計圖中有一樣東西, 如果將它像耳罩一樣戴上";
		mes "如果將它像耳罩一樣戴, 透過一條細線的牽引";
		mes "將可以聽到像留聲機中一樣的聲音";
		mes "這就是設計圖的內容";
		next;
		mes "[鄭何意嘉]";
		mes "我 ..一定要完成爺爺的遺願";
		mes "做出這樣東西來";
		mes "所以我到處去將這設計圖給其他鍊金者看";
		mes "請求他們幫忙, 但是所有的人都說不知道這是什麼東西";
		next;
		mes "[鄭何意嘉]";
		mes "所以我才會想要親自動手來做做看";
		mes "經過我多年的苦心研究..";
		mes "我已經可以完全理解那圖上的結構了";
		mes "不過 .. 我還沒能收集到所需要的材料";
		next;
		mes "[鄭何意嘉]";
		mes "如果現在手邊有材料的話";
		mes "我就可以馬上依照設計圖動手製作了";
		mes "如果 .. 你感興趣的話";
		mes "是不是可以幫我找到所需要的材料呢?";
		next;
		mes "[鄭何意嘉]";
		mes "我只是想要完成爺爺的遺願";
		mes "做出設計圖上的東西而已";
		mes "完成後如果你要的話, 給你也沒關係";
		close;
	case 2:
		mes "[鄭何意嘉]";
		mes "想完成爺爺的遺願, 我需要這些東西：";
		mes "^3355FF鋼鐵^000000 ^FF555540^000000 片";
		mes "^3355FF神之金屬^000000 ^FF55551^000000 個";
		mes "^3355FF酒精^000000 ^FF55551^000000 瓶";
		mes "^3355FF煤礦^000000 ^FF55551^000000 塊";
		close;
	case 3:
		if ((countitem(999) > 39) && (countitem(984) > 0) && (countitem(970) > 0) && (countitem(1003) > 0)) {
			delitem 999,40; //Steel
			delitem 984,1; //Oridecon
			delitem 970,1; //Alchol
			delitem 1003,1; //Coal
			mes "滴答..滴答..滴答..";
			next;
			getitem 5001,1; //Headset
			mes "[鄭何意嘉]";
			mes "真是太感謝你了";
			mes "託你的福我才能完成爺爺的遺願";
			mes "如果爺爺能看到的話一定會很開心的";
			next;
			mes "[鄭何意嘉]";
			mes "誠如我之前說過的, 完成品請你帶走吧!";
			mes "既然已經完成了, 我就感到很高興了";
			close;
		}
		else {
			mes "[鄭何意嘉]";
			mes "謝謝你的好意 .. 但是材料似乎不足";
			mes "請你備齊了再來吧!";
			close;
		}
	case 4:
		mes "[鄭何意嘉]";
		mes "不想聽嗎?";
		mes "好吧...或許你有你自己的想法~";
		mes "請慢走";
		close;
	}
}

// Number Match Game :: gef_yagu
//============================================================
geffen,122,77,4	script	Nia#yagu	8_F_GIRL,{
	mes "[Nia]";
	mes "Hello, I'm Nia, the fifth";
	mes "disciple of Mawong, the";
	mes "gaming mastermind. Now,";
	mes "how would you like to play a";
	mes "Number Match game with me?";
	next;
	switch(select("Number Match Game?:Practice Number Match:Play Number Match:Record Holder:Cancel")) {
	case 1:
		mes "[Nia]";
		mes "Ah, well, the objective of";
		mes "the Number Match game is to";
		mes "guess the number I'm thinking";
		mes "of. Of course, there are a few";
		mes "rules that limit the numbers";
		mes "that I can choose from.";
		next;
		mes "[Nia]";
		mes "All the numbers that I make";
		mes "up must be 3 digits, I can't";
		mes "use 0 for any of the digits,";
		mes "and I can't use the same";
		mes "number for any of the digits";
		mes "more than once.";
		next;
		mes "[Nia]";
		mes "So, for example, I can't";
		mes "use the numbers 103, 112,";
		mes "252, or 701. Those numbers";
		mes "either have a 0, or they use";
		mes "the same number in the digits";
		mes "more than once. Got it now?";
		next;
		mes "[Nia]";
		mes "Now, you only get 5 guesses,";
		mes "and after each guess, I'll tell";
		mes "you how many digits you guessed";
		mes "correctly, followed by the number of correctly guessed digits in";
		mes "the correct digit place.";
		next;
		mes "[Nia]";
		mes "For example, let's say the";
		mes "number I come up with is 168.";
		mes "If your first guess is 678, then you guessed two of the digits,";
		mes "6 and 8, correctly. However,";
		mes "only 8 is in the correct place.";
		next;
		mes "[Nia]";
		mes "You know, it'd be a good idea";
		mes "if you try the Practice Mode";
		mes "first. Then, when you're more";
		mes "comfortable, play the actual";
		mes "Number Match game. Don't forget that speed counts for your score!";
		next;
		mes "[Nia]";
		mes "Ah, if you happen to";
		mes "make a new record in the";
		mes "Number Match game, you'll";
		mes "have your name recorded,";
		mes "so don't miss this chance for";
		mes "fame and maybe even fortune~";
		close;
	case 2:
		mes "[Nia]";
		mes "Great, let's do a practice";
		mes "Number Match game. Please";
		mes "try to guess my number, okay?";
		mes "If you want to cancel, then please enter a number greater than 1,000.";
		set .@practice,1;
		break;
	case 3:
		mes "[Nia]";
		mes "Great, let's play Number Match!";
		mes "You will have 5 chances to guess the number that I'm thinking of.";
		mes "If you want to cancel, then please enter a number greater than 1,000.";
		set .@practice,0;
		break;
	case 4:
		set .@name_record$, $050908_minus1_yagu$;
		if (.@name_record$ == "") //custom
			set .@name_record$,"(null)";
		set .@score_record, $050908_minus1_yagu;
		set .@score_min, .@score_record / 60;
		set .@score_sec, .@score_record % 60;
		mes "[Nia]";
		mes "^ff0000" + .@name_record$ + "^000000 is the";
		mes "record holder for the Number";
		mes "Match game with a time of";
		mes "^ff0000" + .@score_min + " minutes, " + .@score_sec + " seconds^000000.";
		close;
	case 5:
		mes "[Nia]";
		mes "Take care of yourself,";
		mes "adventurer. If you ever";
		mes "feel like playing a Number";
		mes "Match game, come and talk";
		mes "to me, alright? See you~";
		close;
	}
	next;

	// Generate 3 digits of a number.
	while(1) {
		set .@yagu100, rand(1,9);
		set .@yagu10, rand(1,9);
		set .@yagu1, rand(1,9);
		if ((.@yagu100 != .@yagu10) && (.@yagu100 != .@yagu1) && (.@yagu10 != .@yagu1)) {
			// Store the actual number (unofficial).
			set .@yagu, (100*.@yagu100) + (10*.@yagu10) + .@yagu1;
			break;
		}
	}

	// The official time calculation is based on ServerTime,
	// an int HHMMSS equivalent to atoi(gettimestr("%H%M%S",7)).
	// UNIX time is really a better way to do this.
	set .@start_time, gettimetick(2);

	while(1) {

		// Guess a number that meets all conditions.
		while(1) {
			if (input(.@input,100,999) != 0) {
				mes "[Nia]";
				mes "You've canceled the";
				mes "Number Match game.";
				close;
			}
			set .@input100, .@input / 100;
			set .@input10, (.@input % 100) / 10;
			set .@input1, .@input % 10;
			if ((.@input100 > 0) && (.@input10 > 0) && (.@input1 > 0)) {
				if ((.@input100 != .@input10) && (.@input100 != .@input1) && (.@input10 != .@input1))
					break;
			}
			mes "[Nia]";
			mes "You entered one of the";
			mes "digits as 0, or input the";
			mes "same numerical value in";
			mes "the digits more than once.";
			mes "Please enter another guess.";
			next;
		}

		set .@retry, .@retry+1;
		mes "[Nia]";
		mes "^ff0000"+strcharinfo(0)+"^000000,";
		mes "your guess is ^0000ff"+.@input+"^000000.";
		mes "Give me a moment to";
		mes "come up with your results.";
		next;

		// Correct digits in correct locations.
		set .@strike,0;
		if (.@yagu100 == .@input100)
			set .@strike, .@strike+1;
		if (.@yagu10 == .@input10)
			set .@strike, .@strike+1;
		if (.@yagu1 == .@input1)
			set .@strike, .@strike+1;

		// Correct digits in incorrect locations.
		set .@ball,0;
		if ((.@yagu100 == .@input10) || (.@yagu100 == .@input1))
			set .@ball, .@ball+1;
		if ((.@yagu10 == .@input100) || (.@yagu10 == .@input1))
			set .@ball, .@ball+1;
		if ((.@yagu1 == .@input100) || (.@yagu1 == .@input10))
			set .@ball, .@ball+1;

		if (.@strike == 3) {
			if (.@practice) {
				mes "[Nia]";
				mes "Yes, the number that";
				mes "I guessed was ^ff0000"+.@yagu+"^000000!";
				mes "Congratulations, you";
				mes "just won this practice";
				mes "game! Now you should be";
				mes "ready for the real thing~";
				close;
			} else {

				// Calculate final time and check against the previous record.
				set .@end_time, gettimetick(2);
				if (.@end_time < .@start_time)
					set .@end_time, .@end_time + 3600;
				set .@total_time, .@end_time - .@start_time;
				set .@total_min, .@total_time / 60;
				set .@total_sec, .@total_time % 60;
				set .@name_record$, $050908_minus1_yagu$;
				if (.@name_record$ == "") //custom
					set .@name_record$,"(null)";
				set .@score_record, $050908_minus1_yagu;
				set .@score_min, .@score_record / 60;
				set .@score_sec, .@score_record % 60;

				if (.@score_record >= .@total_time || !.@score_record) { //custom (second condition)
					set $050908_minus1_yagu$, strcharinfo(0);
					set $050908_minus1_yagu, .@total_time;
					mes "[Nia]";
					mes "Great! You guessed";
					mes "my number, which was";
					mes "^ff0000" + .@yagu + "^000000. You finished in";
					mes "^ff0000" + .@total_min + " minutes, " + .@total_sec + " seconds^000000,";
					mes "breaking the previous record";
					mes "of ^ff0000" + .@score_min + " minutes, " + .@score_sec + " seconds^000000.";
					next;
					mes "[Nia]";
					mes "^ff0000" + .@name_record$ + "^000000";
					mes "set that old record,";
					mes "but now you are the";
					mes "new person to beat in the";
					mes "Match Game. Congratulations";
					mes "for setting a new record!";
					close;
				}
				mes "[Nia]";
				mes "Nice work, you guessed";
				mes "my number! The answer, of";
				mes "course, was ^ff0000" + .@yagu + "^000000. You managed";
				mes "to guess this number correctly";
				mes "in ^ff0000" + .@total_min + " minutes, " + .@total_sec + " seconds^000000.";
				next;
				mes "[Nia]";
				mes "^ff0000" + .@name_record$ + "^000000 is";
				mes "the current Number Match";
				mes "record holder with a time";
				mes "of ^ff0000" + .@score_min + " minutes, " + .@score_sec + " seconds^000000.";
				mes "Good luck, and hopefully you";
				mes "can break this record someday~";
				close;
			}
		} else {
			if (.@practice) {
				mes "[Nia]";
				mes "Your guess has ^ff0000" + .@ball + "^000000 of the";
				mes "correct numbers. ^ff0000" + .@strike + "^000000 of the";
				mes "digits in your guess have";
				mes "the correct number in the";
				mes "correct digit placement.";
				mes "Alright, try it again~";
				next;
			} else {
				mes "[Nia]";
				mes "Your last guess had ^ff0000" + .@ball + "^000000";
				mes "of the correct numbers,";
				mes "and ^ff0000" + .@strike + "^000000 digits in your guess";
				mes "had the correct number in";
				mes "the correct digit placement.";
				switch(.@retry) {
					case 1: mes "You have four guesses left."; break;
					case 2: mes "You have three guesses left."; break;
					case 3: mes "You have two guesses left."; break;
					case 4: mes "You only have one guess left..."; break;
					case 5: mes "The answer was ^ff0000" + .@yagu + "^000000."; close;
				}
				next;
			}
		}
	}
}
