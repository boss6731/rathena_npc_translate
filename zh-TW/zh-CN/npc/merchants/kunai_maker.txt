//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================


//===== rAthena Script =======================================
//= Kunai Merchant Kashin
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.3a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Trades a few shurikens + ninja stones for elemental kunai.
//===== Additional Comments: =================================
//= 1.0 Added the npc. It uses a function that sends the item 
//=     id of the 2 required items plus the amount. Can trade
//=     up to 500 units (5,000 kunais) at once. [erKURITA]
//= 1.1 Officialized script [Playtester]
//= 1.2a Optimized/cleaned up a bit [ultramage]
//= 1.3 Updated to match AEGIS script. [Kisuka]
//= 1.3a Kagerou/Oboro support (BaseJob -> BaseClass) [Euphy]
//============================================================

que_ng,72,29,3	script	飛刀商人 卡新	83,{
	if(BaseClass == Job_Ninja) {
		mes "[卡新]";
		mes "我是忍者飛刀的兌換者卡新。";
		mes "隨便看看吧，有興趣沒？";
		next;
		switch(select("我想兌換 劇毒飛刀 10把:我想兌換 垂冰飛刀 10把:我想兌換 強風飛刀 10把:我想兌換 黑土飛刀 10把:我想兌換 火焰飛刀 10把:結束對話")) {
		case 1: callfunc "Kunai_Trade",13250,20,7524,1,13259; break;
		case 2: callfunc "Kunai_Trade",13251,8,7522,2,13255; break;
		case 3: callfunc "Kunai_Trade",13252,4,7523,2,13257; break;
		case 4: callfunc "Kunai_Trade",13253,2,7524,1,13256; break;
		case 5: callfunc "Kunai_Trade",13254,1,7521,2,13258; break;
		case 6:
			mes "[卡新]";
			mes "好勒，有需要再來找我吧。";
			mes "我 - 風之卡新會一直待在這裡。";
			mes "薩喲哪啦！";
			close;
		}
	}
	mes "[卡新]";
	mes "我是忍者飛刀的兌換者卡新。";
	mes "如果你有忍者朋友的話，";
	mes "請幫忙轉告一下他，";
	mes "在我這裡能兌換到好用的飛刀。";
	close;
}

function	script	Kunai_Trade	{
	setarray .@item_req[0], getarg(0), getarg(2);
	setarray .@req_amount[0], getarg(1), getarg(3);
	.@item_id = getarg(4);

	mes "[卡新]";
	mes "你可以交換";
	mes .@req_amount[0] +" "+ getitemname(.@item_req[0]) +" 和";
	mes .@req_amount[1] +" "+ getitemname(.@item_req[1]) +" 對於每一個";
	mes "獲得10個 "+ getitemname(.@item_id) +".";
	next;
	mes "[卡新]";
	mes "我一次最多只能給你500把飛刀。";
	mes "如果不想兌換的話，就在訂單中輸入0。";
	mes "你想要多少把？";
	next;
	input .@amount;
	if(.@amount < 1 || .@amount > 500) {
		mes "[卡新]";
		mes "啊？對不起，";
		mes "我不能給你那麼多，";
		mes "我一次最多只能給你500把。";
		close;
	}
	if (countitem(.@item_req[0]) < .@amount*.@req_amount[0] || countitem(.@item_req[1]) < .@amount*.@req_amount[1]) {
		mes "[卡新]";
		mes "哎呀哎呀……";
		mes "你好像沒有足夠的材料，";
		mes "兌換不到你想要的那麼多東西。";
		mes "等準備好了之後再來找我吧。";
		close;
 	}
	if (checkweight(.@item_id, .@amount*10) == 0) {
		mes "[卡新]";
		mes "呃……";
		mes "你的背包好像滿了。";
		mes "你最好先清理一下再來找我。";
		close;
	}
	mes "[卡新]";
	mes "噢~很好！";
	mes "物品數量確認完畢！";
	mes "我想你肯定會對這些飛刀很滿意的。";
	delitem .@item_req[0],.@req_amount[0]*.@amount;
	delitem .@item_req[1],.@req_amount[1]*.@amount;
	getitem .@item_id,10*.@amount;
	close;
}
