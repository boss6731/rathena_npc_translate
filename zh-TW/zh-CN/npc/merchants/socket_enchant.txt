//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================


//===== rAthena Script =======================================
//= Episode 11.1 Socket Enchant NPC
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 2.0a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Adds slots to selected weapons and armor.
//===== Additional Comments: =================================
//= 0.1a added missing L_No: to 2 funcs [Lupus]
//= 0.1b Removed duplicates [Toms]
//= 0.2 Added missing next;'s [Evera]
//= 0.3 Fixed Zweihander[+] id [Lupus]
//= 1.0 Fixed ingredients check [Lupus]
//= 1.1 Updated to official. [SinSloth]
//= 1.1b Optimized/cleaned [ultramage]
//= 1.2 Fixed some typos and a nasty bug. [SinSloth]
//= 1.3 Fixed a wrong item ID with Sphinx Hat. [SinSloth]
//= 1.4 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 1.5 Removed Ontouch trigger area from NPCs. [L0ne_W0lf]
//= 1.6 Replaced effect numerics with constants. [Samuray22]
//= 1.7 Adjusted prices on some armor and weapons. [L0ne_W0lf]
//= 1.8 Added missing Gae Bolg and Dragon Slayer. [Kisuka]
//= 1.9 Moved Episode 12 items to separate NPC.
//=     Some cleanup & optimization. [Gepard]
//= 2.0 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//= 2.0a Added 'disable_items' command. [Euphy]
//============================================================

-	script	Seiyablem#dummy::SocketEnchant	-1,{
	if (checkweight(1201,1) == 0)
	{
		mes "- 等一下!! -";
		mes "- 目前你攜帶的物品已經超重了 -";
		mes "- 請減少一下負重之後 -";
		mes "- 再來找我吧 -";
		close;
	}
	mes "[世亞布連]";
	mes "我是在一般武器或防具上面進行打孔的打孔師";
	mes "依照裝備的不同，手續費以及所需的材料，製作的成功的機率也會有所不同。";
	next;
	mes "[世亞布連]";
	mes "雖然有人認為裝備打孔看起來很簡單，可是事實上這遠比想像中的還要複雜！證據就是：除了我之外無人擁有此技術..";
	mes "如果你對打孔有興趣,我可以說明給你聽,讓你知道";
	next;
	switch(select("為武器打孔:為防具打孔:再聽聽說明:取消"))
	{
		case 1:
			mes "[世亞布連]";
			mes "你想為武器打孔嗎?";
			mes "根據打孔的難易程度來看，大約分為4種級別，C級是最容易的，S級是最困難的。";
			mes "首先挑選難易度水準吧";
			next;
			switch(select("C級:B級:A級:S級"))
			{
				case 1:
					mes "[世亞布連]";
					mes "C級難度？";
					mes "這是最簡單的類別了。";
					mes "你希望我為哪一種C級武器打孔呢？";
					next;
					switch(select("德瑞伊長戟:鞭子:小提琴"))
					{
						case 1: callfunc "Func_Socket",1460,1461,40,66,200,1010,10;
						case 2: callfunc "Func_Socket",1950,1951,40,66,200,1010,10;
						case 3: callfunc "Func_Socket",1901,1902,40,66,200,1010,10;
					}
				case 2:
					mes "[世亞布連]";
					mes "B級難度？";
					mes "這是普通難度的類別";
					mes "你希望我為哪一種B級武器打孔呢？";
					next;
					switch(select("鏈錘:長柄短劍:角弓:步兵長矛:海東劍:魯特琴:鋼鐵鞭子:虎爪拳套:亞伯雷弓"))
					{
						case 1: callfunc "Func_Socket",1519,1520,40,61,300,1010,10;
						case 2: callfunc "Func_Socket",1219,1220,40,61,300, 984, 1,999,5;
						case 3: callfunc "Func_Socket",1714,1716,40,61,300, 984, 2,999,5;
						case 4: callfunc "Func_Socket",1407,1408,40,61,200,1010,10;
						case 5: callfunc "Func_Socket",1123,1128,40,61,300, 984, 2,999,5;
						case 6: callfunc "Func_Socket",1905,1906,40,61,300,1011,10;
						case 7: callfunc "Func_Socket",1954,1955,40,61,300,1011,10;
						case 8: callfunc "Func_Socket",1801,1802,40,61,300,1010,10;
						case 9: callfunc "Func_Socket",1713,1715,40,61,300, 984, 2,999,5;
					}
				case 3:
					mes "[世亞布連]";
					mes "哦！A級難度？？";
					mes "這個類別的風險會很高呢！";
					mes "你希望我為哪一種A級武器打孔呢？";
					next;
					switch(select("獵人之弓:生存的魔杖(INT):巨大雙手劍:水紋之劍:刺殺拳刃:貝利斯塔強弩:昏迷之錘:秘拳套 閃光:十字巨劍"))
					{
						case 1: callfunc "Func_Socket",1718,1726,40,61,500,984,2,999,10;
						case 2: callfunc "Func_Socket",1619,1620,40,61,500,984,5,999,10;
						case 3: callfunc "Func_Socket",1168,1171,40,61,800,984,5,999,10;
						case 4: callfunc "Func_Socket",1129,1149,40,61,500,984,2,999,10;
						case 5: callfunc "Func_Socket",1261,1266,40,61,700,984,5,999,10;
						case 6: callfunc "Func_Socket",1722,1727,40,61,500,984,5,999,10;
						case 7: callfunc "Func_Socket",1522,1532,40,61,500,984,2,999,10;
						case 8: callfunc "Func_Socket",1814,1816,40,61,500,984,5,999,10;
						case 9: callfunc "Func_Socket",1163,1172,40,61,500,984,2,999,10; // NPC says "5 Oridecon" (Aegis) but takes only 2
					}
				case 4:
					mes "[世亞布連]";
					mes "哇,哇,哇...！";
					mes "S等級?你確定？....";
					mes "如果我真的能做成這件事，這將是一個傳奇。";
					mes "你希望我為哪一種S級武器打孔呢？";
					next;
					switch(select("剛尼爾長矛:巨毒短劍:破甲短錐:沙塵短劍:卡卡普劍:狂擊之劍:死神之鐮:生存的魔杖(DEX)"))
					{
						case 1: callfunc "Func_Socket", 1413, 1418,40,51,1000,984,5,999,10;
						case 2: callfunc "Func_Socket", 1239,13016,40,51,1000,984,5,999,10;
						case 3: callfunc "Func_Socket", 1230,13017,40,51,2000,984,5,999,10;
						case 4: callfunc "Func_Socket", 1236,13018,40,51,1000,984,5,999,10;
						case 5: callfunc "Func_Socket",13002,13019,40,51,1000,984,5,999,10;
						case 6: callfunc "Func_Socket", 1135,13400,40,51,1000,984,5,999,10;
						case 7: callfunc "Func_Socket", 1466, 1476,40,51,1000,984,5,999,10;
						case 8: callfunc "Func_Socket", 1617, 1618,40,51,2000,984,5,999,10;
					}
			}
		case 2:
			mes "[世亞布連]";
			mes "防具嘛..?";
			mes "當然,沒問題";
			mes "根據打孔的難易程度來看，大約分為4種級別，C級是最容易的，S級是最困難的。";
			mes "你要選擇什麼等級防具來嘗試打孔嗎?";
			next;
			switch(select("C級:B級:A級:S級"))
			{
				case 1:
					mes "[世亞布連]";
					mes "C等級防具,嗯?";
					mes "聽起來很合理。";
					mes "你想嘗試為什麼防具打孔?";
					next;
					switch(select("大衣;長大衣;頭窟;聖職之帽"))
					{
						case 1: callfunc "Func_Socket",2307,2308,40,66,200,999,3;
						case 2: callfunc "Func_Socket",2309,2310,40,66,200,999,3;
						case 3: callfunc "Func_Socket",2232,2233,40,66,200,999,3;
						case 4: callfunc "Func_Socket",2216,2217,40,66,200,999,3;
					}
				case 2:
					mes "[世亞布連]";
					mes "你選擇B等級防具.";
					mes "那麼,要選擇什麼樣的防具呢?";
					next;
					switch(select("鏡盾:鋼鐵鎖子甲:聖職之衣:絲質外袍:戰士長靴:長靴:披肩:鐵盾:圓盾:鋼盾:妖道帽"))
					{
						case 1:  callfunc "Func_Socket",2107,2108,40,61,250,999,5;
						case 2:  callfunc "Func_Socket",2314,2315,40,61,250,999,5;
						case 3:  callfunc "Func_Socket",2325,2326,40,61,300,999,5;
						case 4:  callfunc "Func_Socket",2321,2322,40,61,300,999,5;
						case 5:  callfunc "Func_Socket",2405,2406,40,61,300,999,5;
						case 6:  callfunc "Func_Socket",2403,2404,40,61,300,999,5;
						case 7:  callfunc "Func_Socket",2503,2504,40,61,300,999,5;
						case 8:  callfunc "Func_Socket",2101,2102,40,61,300,999,5;
						case 9:  callfunc "Func_Socket",2103,2104,40,61,300,999,5;
						case 10: callfunc "Func_Socket",2105,2106,40,61,250,999,5;
						case 11: callfunc "Func_Socket",5046,5168,40,61,250,999,5;
					}
				case 3:
					mes "[世亞布連]";
					mes "哦! A 等級.";
					mes "這是一個很高風險的挑戰!";
					mes "好吧,想要我在哪種防具上嘗試打孔？";
					next;
					switch(select("寶石頭盔:軟泥帽:謊言紀錄本:緊身便衣:秘銀金屬鎧甲:鋼鐵鎧甲:盜賊之衣:重靴:修女的帽子:鬥蓬:金屬頭盔:忍服 血櫻:獸人頭盔:伯爵鬥蓬:神聖之帽:黃金帽:敏捷別針:殭屍帽"))
					{
						case 1:  callfunc "Func_Socket",2230,2231,40,61,400,985,1;
						case 2:  callfunc "Func_Socket",5114,5120,40,61,400,985,1;
						case 3:  callfunc "Func_Socket",2109,2121,40,61,400,985,1;
						case 4:  callfunc "Func_Socket",2330,2331,40,61,400,985,1;
						case 5:  callfunc "Func_Socket",2341,2342,40,61,400,985,1;
						case 6:  callfunc "Func_Socket",2316,2317,40,61,400,985,1;
						case 7:  callfunc "Func_Socket",2335,2336,40,61,400,985,1;
						case 8:  callfunc "Func_Socket",2411,2412,40,61,400,985,1;
						case 9:  callfunc "Func_Socket",5092,5093,40,61,400,985,1;
						case 10: callfunc "Func_Socket",2505,2506,40,61,400,985,1;
						case 11: callfunc "Func_Socket",2228,2229,40,61,400,985,1;
						case 12: callfunc "Func_Socket",2337,2359,40,61,400,985,1;
						case 13: callfunc "Func_Socket",2299,5157,40,61,400,985,1;
						case 14: callfunc "Func_Socket",2507,2525,40,61,400,985,1;
						case 15: callfunc "Func_Socket",2251,5158,40,61,400,985,1;
						case 16: callfunc "Func_Socket",2246,5159,40,61,400,985,1;
						case 17: callfunc "Func_Socket",2605,2625,40,61,400,985,1;
						case 18: callfunc "Func_Socket",2264,5167,40,61,300,985,1;
					}
				case 4:
					mes "[世亞布連]";
					mes "呃....S等級?";
					mes "你確定嗎?";
					mes "告訴我你要對什麼防具打孔,我會讓我努力嘗試，給你最好的成品的!";
					next;
					switch(select("山羊頭盔:尖角發圈:骨制頭盔:海盜船長帽:王冠:后冠:人面獅身帽:創作者外袍:智力耳環:力量戒指:神射手套"))
					{
						case 1:  callfunc "Func_Socket",2256,5160,40,51,2000,985,2;
						case 2:  callfunc "Func_Socket",2258,5161,40,51,2000,985,2;
						case 3:  callfunc "Func_Socket",5017,5162,40,51,2000,985,2;
						case 4:  callfunc "Func_Socket",5019,5163,40,51,2000,985,2;
						case 5:  callfunc "Func_Socket",2235,5165,40,51,2000,985,2;
						case 6:  callfunc "Func_Socket",2234,5164,40,51,2000,985,2;
						case 7:  callfunc "Func_Socket",5053,5166,40,51,1000,985,2;
						case 8:  callfunc "Func_Socket",2343,2360,40,51,1000,985,2;
						case 9:  callfunc "Func_Socket",2602,2622,40,51,1000,985,2;
						case 10: callfunc "Func_Socket",2601,2621,40,51,1000,985,2;
						case 11: callfunc "Func_Socket",2619,2671,40,51,1000,985,2;
					}
			}
		case 3:
			mes "[世亞布連]";
			mes "你又想打聽什麼...若要給你一個提示的話，由於我是流浪的打孔師，所以曾週遊世界累積了豐富的經驗之後才從事此工作的，總覺得在普隆德拉精煉值越高的裝備，打洞的成功率則越高";
			next;
			mes "[世亞布連]";
			mes "雖然這點差異很微妙，但是總覺得越是低精煉的裝備越容易打孔失敗..";
			mes "當然，如果一但失敗的話，裝備也會全部泡湯...不過單看成功率的話，還是值得一試的。";
			next;
			mes "[世亞布連]";
			mes "另外，還有要注意的一點是，嗯...";
			mes "舉例說：當你身上帶著+7鬥篷和沒精煉的鬥篷，然後向我委託給鬥篷打孔的話，你覺得兩者間我會打哪一個呢？";
			next;
			mes "[世亞布連]";
			mes "答案是——我隨意的...";
			mes "也就是說，有著同樣名稱的裝備放在身上時，我是不會理會你的想法，而是隨意選擇其中一個裝備進行打孔的！";
			next;
			mes "[世亞布連]";
			mes "^FF0000你若不是想對一批裝備進行打孔的話，只要帶一個同名稱的裝備在身上就行了。名稱相同而又不想打孔的裝備，最好放到你的倉庫，然後再來找我！^000000";
			next;
			mes "[世亞布連]";
			mes "如果沒有妥善處理的話，萬一在你不希望打孔的裝備上進行打孔，而結果不如意的話，我可是不負責的喔..。";
			mes "記住請再三進行檢查";
			next;
			mes "[世亞布連]";
			mes "我不用再說明了吧？";
			mes "好了，百聞不如一見！";
			mes "要不要先來挑戰一下打孔呢？";
			mes "我也要餬口過日子啊";
			close;
		case 4:
			mes "[世亞布連]";
			mes "放輕鬆,冒險家。";
			mes "如果你想要嘗試給裝備打孔,那麼請將你的裝備帶回來給我看看";
			mes "再見~";
			close;
	}
}

function	script	Func_Socket	{
	.@item_id = getarg(0);
	.@zeny_req = getarg(4);
	setarray .@item_req[0], getarg(5), getarg(7,0);
	setarray .@req_amount[0], getarg(6), getarg(8,0);

	disable_items;
	mes "[世亞布連]";
	mes "你想要嘗試為 "+ getitemname(.@item_id) +" 打孔對嗎?";
	mes "好吧, 那請帶給我 ^FF0000"+ .@req_amount[0] +" "+ getitemname(.@item_req[0]) + (.@req_amount[0] > 1 && .@item_req[0] != 999 ? "s":"") +"^000000, "+ (.@item_req[1] != 0 && .@req_amount[1] != 0 ? ("^FF0000"+ .@req_amount[1] +" "+ getitemname(.@item_req[1]) + (.@req_amount[1] > 1 && .@item_req[1] != 999 ? "s":"") + "^000000 ") : "") + "以及 ^FF0000" + (.@zeny_req >= 1000 ? (.@zeny_req /1000 +",000") : .@zeny_req) +",000 zeny^000000 的手續費.";
	mes "對了, 別忘記帶上要打孔的 "+ getitemname(.@item_id) +"!";
	next;
	mes "[世亞布連]";
	if (getiteminfo(.@item_id,5) == 2) // EQP_HAND_R = 2, it's a weapon
	{
		mes "如果你準備了我需要的物品和費用，我會嘗試在你的武器上打孔。";
		mes "但是在那之前,我必須警告你";
		next;
		mes "[世亞布連]";
		mes "如果嘗試打孔到你的武器時失敗了,那麼^FF0000武器^000000 以及 ^FF0000武器上的所有卡片^000000,都將被^FF0000摧毀^000000。";
	}
	else // armor
	{
		mes "如果你準備了我需要的物品和費用，我會嘗試在你的防具上打孔。";
		mes "但是在那之前,我必須警告你";
		next;
		mes "[世亞布連]";
		mes "如果嘗試打孔到你的防具時失敗了,那麼^FF0000防具^000000 以及 ^FF0000防具上的所有卡片^000000,都將被^FF0000摧毀^000000。";
	}
	mes "你還想繼續嘗試打孔嗎?";
	next;
	switch(select("還是繼續打孔:取消"))
	{
		case 1:
			if ((Zeny >= .@zeny_req*1000) && countitem(.@item_req[0]) >= .@req_amount[0] && countitem(getarg(7,512)) >= .@req_amount[1] && countitem(.@item_id) > 0)// if there's no getarg(7) and getarg(8) provided, check itemcount against 0 (we still need a valid item ID though)
			{
				mes "[世亞布連]";
				mes "好吧,我開始工作了!";
				mes "你最好祈禱一個成功的結果。";
				next;
				set .@a, rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3)))
				{
					specialeffect getarg(3) == 51 ? EF_LORD : EF_SANCTUARY; // only S Class items have Lord of Vermillion effect
					mes "[世亞布連]";
					mes "很好,成功了！";
					mes "它看起來非常完美。恭喜你!";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					getitem getarg(1),1;
					next;
					mes "[世亞布連]";
					mes "希望下次在見到你!";
					close;
				}
				else
				{
					specialeffect EF_SUI_EXPLOSION;
					mes "[世亞布連]";
					mes "哇!……我很抱歉,打孔失敗了。";
					mes "呃,我是完全無辜的。";
					mes "這是你的RP,是上帝弄人,天已註定,好嘛?";
					mes "別那麼失望,下次在嘗試一下說不定你的RP會爆發。";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					next;
					mes "[世亞布連]";
					mes "我祝你下次好運!";
					close;
				}
			}
			else
			{
				mes "[世亞布連]";
				mes "你好像缺少一些東西吧。";
				mes "你確信你有需要打孔的裝備,需要的材料和足夠的費用嗎?";
				close;
			}
		case 2:
			mes "[世亞布連]";
			mes "需要一些時間來想想嘛,嗯?";
			mes "好吧,我可以完全明白。";
			mes "記住,如果不冒險的話，生活是不會有樂趣的~";
			close;
	}
}

payon,140,151,5	duplicate(SocketEnchant)	世亞布連#pay	84
lhz_in02,281,35,5	duplicate(SocketEnchant)	世亞布連#lhz	84
prt_in,33,70,5	duplicate(SocketEnchant)	世亞布連#prt	84
morocc,51,41,4	duplicate(SocketEnchant)	世亞布連#moc	84

-	script	Young Man#dummy::SocketMan	-1,{
	mes "[年輕人]";
	mes "我在考慮成為一個打孔師。";
	mes "這是一個新欄位,您可以添加插槽的設備,它將會很冷靜,如果我能在這樣一個有利可圖的職業。";
	next;
	mes "[年輕人]";
	mes "有一個巨大的運氣的成分參與成功添加從我所能理解的插槽。";
	mes "你失敗的時候,但是如果你超級幸運,您可以添加兩個插槽";
	mes "瘋狂嘛.摁?";
	next;
	mes "[年輕人]";
	mes "但是不要太激動。";
	mes ".只有某些裝備可以處理添加兩個額外的插槽。";
	mes "還有更多東西我必須學習之前,我甚至可以說是一個學徒……";
	close;
}

prt_in,31,57,1	duplicate(SocketMan)	年輕人#prt	97
payon,143,143,7	duplicate(SocketMan)	年輕人#pay	97
morocc,60,42,3	duplicate(SocketMan)	年輕人#moc	97
lhz_in02,269,33,5	duplicate(SocketMan)	年輕人#lhz	97
