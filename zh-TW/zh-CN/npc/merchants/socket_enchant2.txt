//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================

//===== rAthena Script =======================================
//= Episode 12 Socket Enchant NPC
//===== By: ================================================== 
//= Gepard
//===== Current Version: ===================================== 
//= 1.1a
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Adds slots to selected weapons and armor.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//= 1.1 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//= 1.1a Added 'disable_items' command. [Euphy]
//============================================================ 

-	script	雷亞布連#dummy::SocketEnchant2	-1,{
	if (checkweight(1201,1) == 0)
	{
		mes "- 抱歉啊 !! -";
		mes "- 你現在東西帶的太多啦. -";
		mes "- 先去減輕一點重量吧. -";
		close;
	}
	mes "[雷亞布連]";
	mes "你好,我叫雷亞布連,";
	mes "我擅長是給武器和裝備打洞.";
	mes "雖然這略有難度,但是我喜歡打洞.";
	mes "我也為做這行業而感到自豪.";
	next;
	mes "[雷亞布連]";
	mes "我已經從哥哥世亞布連學到不少東西了,";
	mes "但是我覺得，我還是有不少缺點的.";
	mes "所以請原諒,比起哥哥來,";
	mes "我的能力並不算好.";
	next;
	mes "[雷亞布連]";
	mes "其實...哥哥不大願意教我，他覺得這樣的話，就會減少這種能力的獨特性.";
	mes "但是,他還是盡他所能的來教我.";
	next;
	mes "[雷亞布連]";
	mes "真的很感謝哥哥,學到了那麼多.";
	mes "我明白自己搞不了那麼多亂七八糟的研究";
	mes "但是,我會把我所能夠做的,發揮到極致.";
	next;
	mes "[雷亞布連]";
	mes "事實上,有一件事情我哥哥囑咐過我不要去做,我也不想讓他難過但是...";
	mes "算了,算了,我閉嘴!";
	next;
	mes "[雷亞布連]";
	mes "唉,話又說回來,有沒有想要打洞的裝備?";
	mes "我的收費,要求材料,以及成功率";
	mes "全都取決於你要在什麼材料上打洞.";
	next;
	switch(select("我想為武器打洞:我想為防具打洞:你好像說了什麼有趣的事情:再聽聽說明:沒事"))
	{
		case 1:
			mes "[雷亞布連]";
			mes "武器嗎？OK..";
			mes "四種等級的武器,有四種不同的成功率.";
			mes "C級的最簡單，而S級的最難.";
			mes "要試試什麼等級的武器呢?";
			next;
			switch(select("C級:B級:A級:S級"))
			{
				case 1:
					mes "[雷亞布連]";
					mes "C級嗎?";
					mes "那麼,請選擇想要打洞的裝備.";
					next;
					switch(select("地靈之書:水靈之書:風靈之書:火靈之書"))
					{
						case 1: callfunc "Func_Socket2",1554,1569,40,66,200,1010,10;
						case 2: callfunc "Func_Socket2",1553,1568,40,66,200,1010,10;
						case 3: callfunc "Func_Socket2",1556,1571,40,66,200,1010,10;
						case 4: callfunc "Func_Socket2",1555,1570,40,66,200,1010,10;
					}
				case 2:
					mes "[雷亞布連]";
					mes "你選擇了一般的B級.";
					mes "想要精煉哪一種武器?";
					next;
					switch(select("獸人斧:彎刀:闇盲之錘"))
					{
						case 1: callfunc "Func_Socket2",1304,1309,40,66,200,984,1;
						case 2: callfunc "Func_Socket2",1113,1114,40,66,200,984,1;
						case 3: callfunc "Func_Socket2",1523,1538,40,61,300,984,1;
					}
				case 3:
					mes "[雷亞布連]";
					mes "A級? 真是...好吧.";
					mes "想要精煉什麼武器?";
					next;
					switch(select("天龍短劍:刺藤拳刃:火靈拳刃:冰靈拳刃:風靈拳刃:黃金之錘:琵琶:女王的辮子:妖怪的槍:加野矛:風靈之劍"))
					{
						case 1:  callfunc "Func_Socket2",13001,13030,40,61,500,984,2;
						case 2:  callfunc "Func_Socket2", 1257, 1276,40,66,500,984,2;
						case 3:  callfunc "Func_Socket2", 1258, 1277,40,66,500,984,2;
						case 4:  callfunc "Func_Socket2", 1256, 1275,40,66,500,984,2;
						case 5:  callfunc "Func_Socket2", 1259, 1278,40,66,500,984,2;
						case 6:  callfunc "Func_Socket2", 1524, 1539,40,61,500,984,2;
						case 7:  callfunc "Func_Socket2", 1918, 1922,40,61,500,984,2;
						case 8:  callfunc "Func_Socket2", 1970, 1976,40,61,500,984,2;
						case 9:  callfunc "Func_Socket2", 1477, 1479,40,56,500,984,2;
						case 10: callfunc "Func_Socket2", 1474, 1480,40,56,500,984,2;
						case 11: callfunc "Func_Socket2", 1167, 1178,40,61,500,984,2;
					}
				case 4:
					mes "[雷亞布連]";
					mes "哈, S級的嗎?! 額...?*臥槽臥槽臥槽~*";
					mes "那麼,你想在哪一種S級別裝備上這麼搞呢?!";
					next;
					switch(select("風靈之矛:破甲短劍:龍牙劍:削刃短劍:妖短劍 泣神怨:G聖錘 十字星:死神之劍"))
					{
						case 1: callfunc "Func_Socket2",1468, 1481,40,51,1000,984,5,999,10;
						case 2: callfunc "Func_Socket2",1225,13032,40,51,1000,984,5,999,10;
						case 3: callfunc "Func_Socket2",1166, 1180,40,51,1000,984,5,999,10;
						case 4: callfunc "Func_Socket2",1224,13031,40,51,1000,984,5,999,10;
						case 5: callfunc "Func_Socket2",1232,13033,40,51,1000,984,5,999,10;
						case 6: callfunc "Func_Socket2",1528, 1540,40,51,1000,984,5,999,10;
						case 7: callfunc "Func_Socket2",1169, 1179,40,51,1000,984,5,999,10;
					}
			}
		case 2:
			mes "[雷亞布連]";
			mes "要對防具打洞呀,好的.";
			mes "四種等級的防具,有四種不同的成功率.";
			mes "C級的最簡單.S級的最難.";
			mes "要試試什麼等級的防具呢?";
			next;
			switch(select("C級:B級:A級:S級"))
			{
				case 1:
					mes "[雷亞布連]";
					mes "C級的喔.";
					mes "那麼想要哪一種防具呢?";
					next;
					switch(select("向日葵頭飾:學士帽:紅蝶髮帶:學生帽"))
					{
						case 1: callfunc "Func_Socket2",2253,5351,40,66,100,999,3;
						case 2: callfunc "Func_Socket2",5012,5347,40,66,100,999,3;
						case 3: callfunc "Func_Socket2",2244,5348,40,66,100,999,5;
						case 4: callfunc "Func_Socket2",5016,5349,40,66,100,999,5;
					}
				case 2:
					mes "[雷亞布連]";
					mes "選擇了B級的裝備哦.";
					mes "那麼想要哪一種裝備呢?";
					next;
					switch(select("骷髏戒指:抗魔鞋"))
					{
						case 1: callfunc "Func_Socket2",2609,2715,40,61,300,999,5;
						case 2: callfunc "Func_Socket2",2409,2432,40,61,300,999,5;
					}
				case 3:
					mes "[雷亞布連]";
					mes "A級? 真是...好吧你說吧.";
					mes "什麼裝備?";
					next;
					switch(select("海盜頭巾:黑色皮革長靴"))
					{
						case 1: callfunc "Func_Socket2",2287,5350,50,61,500,985,1; // 50,61 - it's not a typo
						case 2: callfunc "Func_Socket2",2425,2434,40,51,500,985,1;
						//case 3: callfunc "Func_Socket2",15053,15056,x,x,x,x,x;	// Special Ninja Suit (Needs more info)
					}
				case 4:
					mes "[雷亞布連]";
					mes "S級的嗎?! 額...?*臥槽臥槽臥槽~*";
					mes "那麼,你想在哪種S級裝備上這麼搞呢?!";
					next;
					switch(select("魔法外套:天衣月舞:大天使之盾:襯衫:三角褲"))
					{
						case 1: callfunc "Func_Socket2",2334,2372,40,51,1000,985,1; // Mage Coat has 10% success chance
						case 2: callfunc "Func_Socket2",2327,2373,45,51,1000,985,1; // the rest has 5%
						case 3: callfunc "Func_Socket2",2111,2128,45,51,1000,985,1;
						case 4: callfunc "Func_Socket2",2522,2523,45,51,1000,985,1;
						case 5: callfunc "Func_Socket2",2339,2371,45,51,1000,985,1;
					}
			}
		case 3:
			mes "[雷亞布連]";
			mes "其實...也沒什麼,但是他認為這並不是人類可以隨意涉足的領域.";
			mes "真心不想把我哥哥弄得失望所以,我還是算了吧. ";
			next;
			mes "[雷亞布連]";
			mes "...這活計雖然成功率很高,但是要求很多材料,";
			mes "當然了如果真的去做手續費也超高的. ";
			mes "真心覺得即使我開了,能否人能夠支付得起也是個很大的問題.";
			next;
			mes "[雷亞布連]";
			mes "...額...";
			next;
			mes "[雷亞布連]";
			mes ".............";
			next;
			mes "[雷亞布連]";
			mes ".............";
			next;
			mes "[雷亞布連]";
			mes "好想你蠻感興趣的.";
			mes "好吧,那我就和你說一點點吧. ";
			mes "但是保證,不要洩露秘密.";
			mes "如果我哥哥發現我告訴你,我就完蛋了!";
			next;
			mes "[雷亞布連]";
			mes "這項特殊的服務有 90% 的成功率,";
			mes "但是要^ff0000兩塊黃金^000000和^ff0000兩億 Zeny^000000.";
			mes "我可以為太陽神頭盔打洞.";
			next;
			mes "[雷亞布連]";
			mes "哥哥警告過我，即使在遠古時期,也是不可以在這頂帽子上打洞的. ";
			mes "但是我覺得啊,人類是忍不住想要去探索一下的好奇心理的,";
			mes "特別是對於那些被禁止的東東.";
			mes "啊,要不是不想我哥哥來對我大吼大叫,我早...";
			next;
			switch(select("好厲害,我要使用此服務","結束對話"))
			{
				case 1:
					mes "[雷亞布連]";
					mes ".................";
					next;
					mes "[雷亞布連]";
					mes ".................";
					next;
					mes "[雷亞布連]";
					mes "...什... 什麼? 不要!";
					mes "別用那種眼神盯著我!";
					mes "不要!說了不要!不要就是不要!不!";
					next;
					mes "[雷亞布連]";
					mes "不要,我不!";
					next;
					mes "[雷亞布連]";
					mes "我不能做的...";
					next;
					mes "[雷亞布連]";
					mes "我不能... 做...";
					next;
					mes "[雷亞布連]";
					mes ".................";
					next;
					mes "[雷亞布連]";
					mes "額...";
					next;
					mes "[雷亞布連]";
					mes "那好,就只此一次例外.";
					mes "誰都不要告訴,特別是我哥哥,";
					mes "要承諾喔!";
					next;
					mes "[雷亞布連]";
					mes "那麼給我";
					mes "^FF00002個黃金 和 2,000,000 Zeny 服務費,";
					mes "^FF0000和一頂 太陽神頭盔.";
					mes "^FF0000你知道成功率為 90% 的對吧?^000000";
					mes "乘熱打鐵,快快快!";
					next;
					if ((Zeny > 199999999) && (countitem(969) > 1) && (countitem(5022) > 0))
					{
						mes "[雷亞布連]";
						mes "開始吧.";
						mes "向著神祈禱,祝你好運.";
						next;
						set .@a, rand(1,100);
						if ((.@a > 4) && (.@a < 95))
						{
							specialeffect EF_LORD;
							mes "[雷亞布連]";
							mes "好! 我想傳言中的 90% 成功率並非是空穴來風的!";
							mes "看,完美的打洞!恭喜恭喜啊!";
							delitem 5022,1; //Helm_Of_Sun
							delitem 969,2; //Gold
							set Zeny, Zeny - 200000000;
							getitem 5353,1; //Helm_Of_Sun_
							next;
							mes "[雷亞布連]";
							mes "這是第一次,也是最後一次.";
							mes "不要告訴任何人,明白?";
							close;
						}
						else
						{
							specialeffect EF_SUI_EXPLOSION;
							mes "[雷亞布連]";
							mes "哇! 我的神吶,失敗了!";
							mes "我想,我們撞到厄運了...";
							mes "我們該怎麼辦? ...這就是我不是吃螃蟹的原因啊!";
							delitem 5022,1; //Helm_Of_Sun
							delitem 969,2; //Gold
							set Zeny, Zeny - 200000000;
							next;
							mes "[雷亞布連]";
							mes "額,別再叫我做這事了!再見!";
							close;
						}
					}
					else
					{
						mes "[雷亞布連]";
						mes "額...";
						next;
						mes "[雷亞布連]";
						mes "沒有足夠的材料啊,還來叫我試?.";
						mes "不會再給你做了,即使你帶來材料.再見~";
						close;
					}
				case 2:
					mes "[雷亞布連]";
					mes "誰都不要告訴,明白?";
					close;
			}
		case 4:
			mes "[雷亞布連]";
			mes "還想知道信息嗎?";
			mes "沒什麼特別的要告訴你了,但是有個小提示.";
			next;
			mes "[雷亞布連]";
			mes "打洞員的打洞成功機率,";
			mes "是取決於你武器的精鍊度和打洞員個人的.";
			mes "雖然不大清楚這個是否100%精準,";
			mes "但是我很清楚的看到每個城鎮打洞員成功機率是不同的.";
			next;
			mes "[雷亞布連]";
			mes "舉個例子,一個鎮的打洞員能夠在給高精練裝備打洞的時候發揮出更高的成功機率但是,";
			mes "另一個鎮子上的打洞員給低精練裝備打洞的時候會發揮出更高的成功率.";
			next;
			mes "[雷亞布連]";
			mes "打洞失敗的時候,武器就破損了.";
			mes "我會盡我雖能做到最好,但是打洞的確很複雜.";
			next;
			mes "[雷亞布連]";
			mes "有件重要的事情要記住.";
			mes "^FF0000打洞的時候別帶著兩件名字相同的要打洞物品.";
			mes "^FF0000這樣的話,打洞師父也不知道你到底要打哪一個裝備的洞,";
			mes "^FF0000然後就隨便給你那一件打洞算事.也許有時候貴重的東西就這樣OVER了.^000000";
			next;
			mes "[雷亞布連]";
			mes "希望可以幫到你的忙啊.謝謝.";
			close;
		case 5:
			mes "[雷亞布連]";
			mes "額,好的.";
			close;
	}
}

payon,236,199,3	duplicate(SocketEnchant2)	雷亞布連#pay	86
lighthalzen,96,137,3	duplicate(SocketEnchant2)	雷亞布連#lhz	86
prt_in,22,60,6	duplicate(SocketEnchant2)	雷亞布連#prt	86
moc_ruins,154,86,3	duplicate(SocketEnchant2)	雷亞布連#moc	86

function	script	Func_Socket2	{
	.@item_id = getarg(0);
	.@zeny_req = getarg(4);
	setarray .@item_req[0], getarg(5), getarg(7,0);
	setarray .@req_amount[0], getarg(6), getarg(8,0);

	disable_items;
	mes "[雷亞布連]";
	if (.@item_req[1] != 0 && .@req_amount[1] != 0)
	{
		mes getitemname(.@item_id) +"... 嗯,那就帶給我";
		mes "^FF0000"+ (.@zeny_req >= 1000 ? (.@zeny_req /1000 +",000") : .@zeny_req) +",000 zeny 服務手續費,";
		mes "^FF0000"+ .@req_amount[0] +"個 "+ getitemname(.@item_req[0]) +" 和 "+ .@req_amount[1] +"個 "+ getitemname(.@item_req[1]) +".";
		mes "^FF0000當然, 你還需要一個 "+ getitemname(.@item_id) +".^000000";
	}
	else
	{
		mes getitemname(.@item_id) +"? 嗯,那就帶給我";
		mes "^FF0000"+ (.@zeny_req >= 1000 ? (.@zeny_req/1000 +",000") : .@zeny_req) +",000 zeny 服務手續費 和 "+ .@req_amount[0] +" "+ getitemname(.@item_req[0]) + (.@req_amount[0] > 1 && .@item_req[0] != 99 ? "個":"個") +" 這些東西.";
		mes "^FF0000當然, 別忘記給我帶來 "+ getitemname(.@item_id) +".";
		mes "^FF0000你要把這些東西全部帶齊再來.^000000";
	}
	next;
	mes "[雷亞布連]";
	mes "你都帶來了嗎?";
	mes "我告訴你哦, 如果你打洞失敗了,";
	mes "你將失去所有打洞用到的東西, 包括 "+ (getiteminfo(.@item_id,5) == 2 ? "這件武器" : "這件裝備") +".";// EQP_HAND_R = 2, it's a weapon, otherwise armor
	mes "還要記住, 如果 "+ (getiteminfo(.@item_id,5) == 2 ? "這件武器" : "這件裝備") +" 已經精煉過, 或者插過卡,";
	mes "即使你成功了, 這些精煉值和卡片也會消失的.";
	next;
	switch(select("請幫我打洞吧:這麼危險!我再考慮一下"))
	{
		case 1:
			if ((Zeny >= .@zeny_req*1000) && countitem(.@item_req[0]) >= .@req_amount[0] && countitem(getarg(7,512)) >= .@req_amount[1] && countitem(.@item_id) > 0)// if there's no .@req_amount[0] and .@req_amount[1] provided, check itemcount against 0 (we still need a valid item ID though)
			{
				mes "[雷亞布連]";
				mes "那麼就開始吧!";
				mes "為成功祈禱吧.";
				next;
				set .@a, rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3)))
				{
					specialeffect EF_LORD;
					mes "[雷亞布連]";
					mes "好,成功了.";
					mes "看樣子不錯呢,恭喜!";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					getitem getarg(1),1;
					next;
					mes "[雷亞布連]";
					mes "好啦,下次再見咯!";
					close;
				}
				else
				{
					specialeffect EF_SUI_EXPLOSION;
					mes "[雷亞布連]";
					mes "哇! ...抱歉,失敗了呢.";
					mes "但是,這和我無關!";
					mes "這是被上帝的旨意所左右的呀!";
					mes "別太失望,下次再試試.";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					next;
					mes "[雷亞布連]";
					mes "別太傷心活計,再見了!";
					close;
				}
			}
			else
			{
				mes "[雷亞布連]";
				mes "真傻還是怎麼滴了?";
				mes "你帶來了個鳥啊!做你個[和諧]啊!";
				close;
			}
		case 2:
			mes "[雷亞布連]";
			mes "小心駛得萬年船，那麼下次再見咯.";
			close;
	}
}
