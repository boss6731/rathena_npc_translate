//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================

//===== rAthena Script =======================================
//= Renters
//===== By: ================================================== 
//= kobra_k88, mod by Lupus
//===== Current Version: ===================================== 
//= 2.2
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= Knight and Crusader Peco Peco Breeders, Falcon Breeder scripts
//===== Additional Comments: ================================= 
//= Fully working
//= Added another Falcon Master into the Hunters Guild [Lupus]
//= 1.2: replaced checkoption(x) with checkriding,checkfalcon [Lupus]
//= 1.3: Added support Adv Classes + Baby Class [Lupus]
//= 1.4: Added different prices for normal, advanced and baby classes
//= but you could set them to the same [Lupus]
//= 1.5: Fixed spelling mistakes [Nexon]
//= 1.5a: Small fix (Falcon Taming -> Falcon Mastery) [Komurka]
//= 1.6: Moved the Falcon Master to Hugel [Poki#3]
//= 1.7 Updated to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.8 Added 3rd Job creature NPCs (Dragon/Gryphon) [L0ne_W0lf]
//= 1.9 Enabled Gryphon Renter NPC and added Mado Gear NPC. [Masao]
//= 2.0 Fixed problems with third classes and new mounts. [Euphy]
//= 2.1 Moved some renters to a separate renewal file. [Daegaladh]
//= 2.2 Added warg checks. [Euphy]
//============================================================ 

// PecoPeco Breeder (for Knights)-------------------------------------------
prontera,55,350,5	script	大嘴鳥飼養員(騎士)#knt	105,{
	if(Upper==0) set .@price,2500; //Normal Peco - default price
	if(Upper==1) set .@price,2500; //Armored Peco
	if(Upper==2) set .@price,2500; //Baby Peco

	mes "[大嘴鳥飼養員]";
	if (BaseJob == Job_Knight && Class < Job_Rune_Knight) {
		mes "你好勒，歡迎光臨。";
		mes "榮耀的騎士呀，你想要租用一隻可愛的大嘴鳥嗎？";
		mes "租賃費用是 ^FF0000"+.@price+"^000000 金幣。";
		next;
		switch(select("是的，我想租一隻大嘴鳥:結束對話")) {
		case 1:
			if (zeny < .@price) {
				mes "[大嘴鳥飼養員]";
				mes "你沒有足夠的金錢喲。";
				mes "難道...你破產了？";
				close;
			}
			else if(getskilllv("KN_RIDING") == 0) {
				mes "[大嘴鳥飼養員]";
				mes "很抱歉，你無法使用本服務，因為你根本控制不了大嘴鳥。";
				mes "建議你先學習一下技能列表中的^0000FF騎乘術^000000之後再來找我吧。";
				close;
			}
			else if(checkriding()) {
				mes "[大嘴鳥飼養員]";
				mes "你已經租用了一隻可愛的大嘴鳥了喲。";
				mes "一個人同時只能租用一隻大嘴鳥 ^-^";
				close;
			}
			else if(ismounting()) {
				mes "[大嘴鳥飼養員]";
				mes "喲，你還乘坐著商城坐騎呢，請先從商城坐騎身上下來吧，不然你租了大嘴鳥也沒用呀。";
				close;
			}
			set zeny,zeny-.@price;
			setriding;
			mes "[大嘴鳥飼養員]";
			mes "好滴，這是你的大嘴鳥，請善待它喲。";
			close;
		case 2:
			mes "[大嘴鳥飼養員]";
			mes "噢..我明白了。";
			mes "祝你有個好心情，再見啦。";
			close;
		}
	} else {
		mes "你好，噢...？很抱歉哈。";
		mes "大嘴鳥只能租給^0000FF騎士^000000或^0000FF騎士領主^000000。";
		close;
	}
}

// Grand PecoPeco Breeder (for Crusaders)-------------------------------------------
prontera,232,318,3	script	大嘴鳥飼養員(十字軍)#cru	105,{
	if(Upper==0) set .@price,3500; //Normal Peco - default price
	if(Upper==1) set .@price,3500; //Armored Peco
	if(Upper==2) set .@price,3500; //Baby Peco

	mes "[大嘴鳥飼養員]";
	if (BaseJob == Job_Crusader && Class < Job_Rune_Knight) {
		if(Upper != 1 ) mes "歡迎光臨，十字軍。";
		else mes "歡迎光臨，聖殿十字軍。";
		mes "我們為你準備了特殊的大嘴鳥。";
		mes "租用一隻需要 ^FF0000"+.@price+"^000000 金幣。";
		next;
		switch(select("太好了，租給我一隻大嘴鳥吧:結束對話")) {
		case 1:
			if (zeny < .@price) {
				mes "[大嘴鳥飼養員]";
				mes "你沒有足夠的金錢來支付租金呢。";
				mes "如果你想要租一隻可愛的大嘴鳥的話，請帶來 ^FF0000"+.@price+"^000000 金幣...";
				close;
			}
			else if(getskilllv("KN_RIDING") == 0) {
				mes "[大嘴鳥飼養員]";
				mes "你必須先學會^0000FF騎乘術^000000，然後我才能租給你大嘴鳥。";
				close;
			}
			else if(checkriding()) {
				mes "[大嘴鳥飼養員]";
				mes "你已經租用了一隻可愛的大嘴鳥了喲。";
				mes "一個人同時只能租用一隻大嘴鳥 ^-^";
				close;
			}
			else if(ismounting()) {
				mes "[大嘴鳥飼養員]";
				mes "喲，你還乘坐著商城坐騎呢，請先從商城坐騎身上下來吧，不然你租了大嘴鳥也沒用呀。";
				close;
			}
			set zeny,zeny-.@price;
			setriding;
			mes "[大嘴鳥飼養員]";
			mes "好滴，這是你的大嘴鳥，請善待它喲。";
			close;
		case 2:
			mes "[大嘴鳥飼養員]";
			mes "那麼...回見 ^-^";
			close;
		}
	} else {
		mes "我能為您做些什麼呢？";
		mes "大嘴鳥的租賃要求是十分嚴格的，必須是^0000FF十字軍^000000或者^0000FF聖殿十字軍^000000才能租賃喲。";
		close;
	}
}

// Falcon Master--------------------------------------------------------------
hu_in01,381,304,5	script	獵鷹 管理員#hnt	105,{
	if(Upper==0) set .@price,2500; //Normal Falcon - default price
	if(Upper==1) set .@price,2500; //Scarf Falcon
	if(Upper==2) set .@price,2500; //Baby Falcon

	mes "[獵鷹飼養員]";
	if (BaseJob == Job_Hunter) {
		if (checkoption(Option_Wug) || checkoption(Option_Wugrider)) {
			mes "嘎...";
			mes "你無法租借獵鷹";
			mes "除非你解除招喚的野狼!";
			close;
		}
		mes "你需要一隻獵鷹嗎?";
		mes "只需要一點費用";
		mes "就能租用值得信賴的猛禽";
		mes "租金為 "+.@price+" zeny~";
		next;
		switch(select("我要租獵鷹:取消")) {
		case 1:
			if (checkoption(Option_Wug) || checkoption(Option_Wugrider)) {
				mes "[獵鷹飼養員]";
				mes "嘎...";
				mes "你無法租借獵鷹";
				mes "除非你解除招喚的野狼!";
				close;
			}
			if (zeny < .@price) {
				mes "[獵鷹飼養員]";
				mes "這是什麼?";
				mes "你沒有足夠的";
				mes "金錢嗎?!";
				mes "你最好開始";
				mes "想辦法狩獵賺錢";
				mes "而不是魔物~";
				close;
			}
			else if(getskilllv("HT_FALCON") == 0) {
				mes "[獵鷹飼養員]";
				mes "天啊~";
				mes "請先去學習訓鷹術";
				mes "在來管理獵鷹吧";
				mes "我無法出租給你";
				mes "你目前無法駕馭它";
				mes "明白我的意思吧.";
				close;
			}
			else if(checkfalcon()) {
				mes "[獵鷹飼養員]";
				mes "呃...";
				mes "你已經有一隻獵鷹了";
				mes "往你的頭上看看";
				mes "你的獵鷹正在你頭上盤旋......";
				close;
			}
			set zeny,zeny-.@price;
			setfalcon;
			close;
		case 2:
			mes "[獵鷹飼養員]";
			mes "等一下,";
			mes "你想去哪";
			mes "這些是一流的獵鷹,";
			mes "人格保證! 來吧, 喲~";
			close;
		}
	} else {
		mes "笨蛋!";
		mes "獵鷹只有";
		mes "獵人、神射手、遊俠";
		mes "能租用而已";
		mes "...嘿嘿, 羨慕嗎?";
		close;
	}
}
