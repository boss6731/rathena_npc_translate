//============================================================
//= rAthenaCN 腳本漢化 轉自 [ www.99Max.mE ]
//============================================================


//===== rAthena Script =======================================
//= Bullet Dealers
//===== By ===================================================
//= Playtester, Paradox924X
//===== Version ==============================================
//= 2.0
//===== Compatible With ======================================
//= rAthena Project
//===== Description ==========================================
//= Bullet trader.
//===== Comments =============================================
//= 1.0 First version [Playtester]
//= 1.1 Converted from Aegis [Paradox924X]
//= 1.2 More optimized conversion [Legionaire]
//= 1.2a Removed .GATs [Lupus]
//= 1.3 Fixed [Playtester] Optimized. Got rid of @vars [Lupus]
//= 1.4 Updated to match AEGIS script. [Kisuka]
//= 1.5 Updated to match AEGIS script again. [Masao]
//= 1.6 Moved Izlude duplicate to pre-re/re paths. [Euphy]
//= 2.0 Clean-up. [Capuche]
//============================================================

alberta,118,160,3	script	彈藥經銷商 託尼#alb::bdt	4_M_04,{
	mes "[託尼]";
	if (BaseClass == Job_Gunslinger) {
		mes "我是彈藥經銷商託尼. ";
		mes "當您的子彈不夠的時候就請來";
		mes "找我. 只要給我一些材料, ";
		mes "我就能為您製造您想要的子彈.";
		next;
		switch(select("- 毒子彈:- 火槍子彈:- 雷擊子彈:- 失明子彈:- 冰凍子彈:- 取消")) {
		case 1: callsub S_BulletTrade,937,10,13205;// <item required>, <amount req>, <bullet id>
		case 2: callsub S_BulletTrade,7097,2,13203;
		case 3: callsub S_BulletTrade,7053,3,13204;
		case 4: callsub S_BulletTrade,1024,5,13206;
		case 5: callsub S_BulletTrade,7054,2,13207;
		case 6:
			mes "[託尼]";
			mes "改變主意了嗎?";
			mes "好吧, 如果您還要子彈, ";
			mes "我一直在這裡. ";
			mes "如果您需要更多的子彈";
			mes "可以隨時回來這裡";
			mes "找我, 槍手. ";
			close;
		}
	}
	mes "嘿, 你好我是託尼, 我負責";
	mes "幫槍手分配並製作子彈. ";
	mes "這只是";
	mes "我們工會喜歡的";
	mes "一種形式方式";
	mes "而已.";
	next;
	mes "[託尼]";
	mes "對不起, 如果您";
	mes "是來這裡買子彈的. ";
	mes "我這隻和已經";
	mes "轉職的槍手交易. ";
	close;

S_BulletTrade:
	.@required_id = getarg(0);
	.@required_amount = getarg(1);
	.@bullet_id = getarg(2);

	mes "[託尼]";
	mes "對於一組";
	mes callfunc("F_InsertPlural",30,getitemname(.@bullet_id))+",";
	mes "你需要支付我";
	mes "一個 強化武器金屬-級數二 , ";
	mes "一個 強化武器金屬-級數一 , 以及";
	mes callfunc("F_InsertPlural",.@required_amount,getitemname(.@required_id))+".";
	next;
	mes "[託尼]";
	mes "請記得我一次";
	mes "最多只能給您500組";
	mes "30發的彈藥. 請輸入";
	mes "您想要兌換的";
	mes "彈藥數量. 如果您想要";
	mes "取消, 只需要輸入''0.''";
	next;
	input .@amount;
	mes "[託尼]";
	if (.@amount < 1 || .@amount > 500) {
		mes "嘿!我不能給您那麼多子彈. ";
		mes "如果您真的想要";
		mes "用物品來兌換子彈";
		mes "就請記得";
		mes "不要輸入超過";
		mes "500的數字.";
	}
	else if (countitem(1010) >= .@amount && countitem(1011) >= .@amount && countitem(.@required_id) >= (.@amount*.@required_amount)) {
		if (checkweight(.@bullet_id,.@amount * 30) == 0) {
			mes "呃?你的背包裡好像";
			mes "沒有足夠的空間來存放";
			mes "這些子彈. 等你清理出";
			mes "更多的空間的時候, 再來";
			mes "找我. 你可以把一些東西";
			mes "存放到卡普拉倉庫中.";
		} else {
			mes "太好了, 似乎一切都";
			mes "井井有條的樣子. 讓我用";
			mes "這些材料幫您兌換";
			mes "子彈. 很高興能和您";
			mes "做生意~";
			delitem 1010,.@amount; //Phracon
			delitem 1011,.@amount; //Emveretarcon
			delitem .@required_id,.@amount * .@required_amount;
			getitem .@bullet_id,.@amount * 30;
		}
	} else {
		mes "呼, 看起來您沒有";
		mes "足以製作那麼多子彈的";
		mes "材料. 好吧, ";
		mes "沒關係. 等您集齊了";
		mes "所有需要的材料, ";
		mes "再來找我吧, 好嗎?";
	}
	close;
}
