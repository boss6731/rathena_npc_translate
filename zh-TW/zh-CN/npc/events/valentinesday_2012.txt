//============================================================
//= rAthenaCN [情人節任務_2012]漢化腳本 
//============================================================


//===== rAthena Script =======================================
//= Valentine's Day Event (2012)
//===== By: ==================================================
//= Rikimaru
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= The full Valentine's Day NPCs of the Valentine's Day
//= Event 2012 (account-based).
//===== Additional Comments: =================================
//= 1.0 First version. [Rikimaru]
//= 1.1 Standardized and fixed. [Euphy]
//============================================================

mosk_in,21,246,5	script	粉紅女郎	71,{
	set .@n$,"^0000FF[粉紅女郎]^000000";
	if (#V_QUE12==3) {
		set .@LFlowerID,7864;
		setarray .@SweetsID[0],538,558,539,573,559,560,12062,596,597,12414,12319;
		setarray .@SweetsCost[0],1,2,5,10,10,10,15,15,15,20,20;
		mes .@n$;
		mes "你好 "+strcharinfo(0)+",";
		mes "你想換點什麼嗎?";
		mes "我這裡只能使用情人花";
		mes "才能兌換東西!";
		next;
		if(select("- 是的!","- 不用, 我只是路過.") == 2) close;
		next;
		mes .@n$;
		mes "請選擇需要兌換的物品.";
		mes "括號中顯示了你需要兌換物品所需的情人花數量.";
		for(set .@i,0; .@i < getarraysize(.@SweetsID); set .@i,.@i+1)
			set .@menu$, .@menu$+"^00AA00["+.@SweetsCost[.@i]+"]^000000 "+getitemname(.@SweetsID[.@i])+":";
		next;
		set .@choice, select(.@menu$)-1;
		mes .@n$;
		if (countitem(.@LFlowerID) < .@SweetsCost[.@choice]) {
			mes "你需要更多的情人花才能夠兌換哦!";
			close;
		}
		mes "你確定使用 ^00aa00"+.@SweetsCost[.@choice]+"x 情人花 ^000000 來兌換 ^0055FF"+getitemname(.@SweetsID[.@choice])+"^000000嗎?";
		if(select("- 不, 我改主意了.:- 是的, 兌換!") == 1) close;
		set .@new, .@LFlowerID - .@SweetsCost[.@choice];
		if (.@new < 0) set .@new, 0;
		delitem .@LFlowerID, .@SweetsCost[.@choice];
		getitem .@SweetsID[.@choice],1;
		mes "祝你七夕節快樂!";
		close;
	} else if (#V_QUE12==2) {
		mes .@n$;
		if (countitem(7864) < 15) {
			mes "你需要給我足夠情人花!";
			close;
		}
		mes "感謝你幫我們找回了情人花";
		mes "祝你七夕節快樂!";
		delitem 7864,15;
		next;
		mes .@n$;
		mes "這是一點小小的心意";
		mes "'謝謝'.";
		getexp 500000,400000;
		getitem 617,1;
		getitem 12319,2;
		next;
		mes .@n$;
		mes "如果你能找到更多的情人花";
		mes "你可以帶著它來兌換更多";
		mes "甜蜜的東西.";
		mes "期待與你再次見面!";
		set #V_QUE12,3;
		close;
	} else if (#V_QUE12==1) {
		mes .@n$;
		mes "請記得佩戴 +8 蛋糕帽";
		mes "去找英俊的貝克";
		mes "他就在我身後!";
		close;
	} else if (BaseLevel >= 45) {
		mes .@n$;
		mes "你好 "+strcharinfo(0)+",";
		mes "在這個美麗的七夕情人節裡";
		mes "我們準備了甜蜜的";
		mes "巧克力.";
		next;
		mes .@n$;
		mes "不過我們碰到一個小問題,";
		mes "希望像你這樣熱心,強大的";
		mes "冒險家能夠幫助我們.";
		mes "不過首先, 你得帶來一頂";
		mes "+8 以上的蛋糕帽.";
		mes "然後找我身後英俊的貝克";
		mes "對話, 他會告訴你一切!";
		set #V_QUE12,1;
		close;
	}
	mes .@n$;
	mes "你好 "+strcharinfo(0)+"!";
	close;

OnInit:
	// Unknown item drop rate (currently set at 5%)
	setitemscript 5105,"{ bonus bDex,1; bonus bMaxSP,80; bonus3 bAddMonsterDropItem,7864,RC_DemiHuman,500; }",0;
	end;
}

mosk_in,21,244,3	script	英俊的貝克	410,{
	set .@n$,"^0000FF[英俊的貝克]^000000";
	if(#V_QUE12==3) {
		mes .@n$;
		mes "情人花可以找粉紅女郎兌換物品!";
		close;
	} else if(#V_QUE12==2) {
		mes .@n$;
		mes "記得帶著15朵以上的情人花去找粉紅女郎!";
		close;
	} else if(#V_QUE12==1) { 
		mes .@n$;
		if (getequipid(EQI_HEAD_TOP) == 5024 && getequiprefinerycnt(EQI_HEAD_TOP) >= 8) {
			mes "啊哈, 粉紅女郎已經告訴你要";
			mes "佩戴 +8 蛋糕帽了吧.";
			mes "現在, 我將把你的蛋糕帽";
			mes "變成另一個新的蛋糕帽子.";
			mes "我們開始吧.";
			next;
			mes .@n$;
			delitem 5024,1;
			getitem 5105,1;
			equip 5105;
			mes "現在, 如果你佩戴這頂";
			mes "新的慶典蛋糕帽的話,";
			mes "就有機會";
			mes "在與人型魔物戰鬥時";
			mes "獲得額外的情人花獎勵!";
			next;
			mes .@n$;
			mes "這些怪物偷了我們的情人花,";
			mes "情人花是我們製作七夕節巧克力";
			mes "所需要用到的, 現在";
			mes "請幫我們找回這些情人花";
			mes "並把它們給粉紅女郎!";
			set #V_QUE12,2;
			close;
		} else {
			mes "對不起, 你的+8 蛋糕帽在哪裡?";
			mes "帶著它來找我,";
			mes "還有請記得要裝備在身上.";
			mes "祝你七夕節快樂!";
			close;
		}
	} else {
		mes .@n$;
		mes "你好, "+strcharinfo(0)+"!";
		close;
	}
}